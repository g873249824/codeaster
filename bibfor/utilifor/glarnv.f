      SUBROUTINE GLARNV( IDIST, ISEED, N, X )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILIFOR  DATE 17/02/2003   AUTEUR NICOLAS O.NICOLAS 
C ======================================================================
C COPYRIGHT (C) LAPACK
C ======================================================================
C  -- LAPACK AUXILIARY ROUTINE (VERSION 2.0) --
C     UNIV. OF TENNESSEE, UNIV. OF CALIFORNIA BERKELEY, NAG LTD.,
C     COURANT INSTITUTE, ARGONNE NATIONAL LAB, AND RICE UNIVERSITY
C     SEPTEMBER 30, 1994
C
C  PURPOSE
C  =======
C
C  GLARNV RETURNS A VECTOR OF N RANDOM COMPLEX NUMBERS FROM A UNIFORM OR
C  NORMAL DISTRIBUTION.
C
C  ARGUMENTS
C  =========
C
C  IDIST   (INPUT) INTEGER
C          SPECIFIES THE DISTRIBUTION OF THE RANDOM NUMBERS:
C          = 1:  REAL AND IMAGINARY PARTS EACH UNIFORM (0,1)
C          = 2:  REAL AND IMAGINARY PARTS EACH UNIFORM (-1,1)
C          = 3:  REAL AND IMAGINARY PARTS EACH NORMAL (0,1)
C          = 4:  UNIFORMLY DISTRIBUTED ON THE DISC ABS(Z) < 1
C          = 5:  UNIFORMLY DISTRIBUTED ON THE CIRCLE ABS(Z) = 1
C
C  ISEED   (INPUT/OUTPUT) INTEGER ARRAY, DIMENSION (4)
C          ON ENTRY, THE SEED OF THE RANDOM NUMBER GENERATOR; THE ARRAY
C          ELEMENTS MUST BE BETWEEN 0 AND 4095, AND ISEED(4) MUST BE
C          ODD.
C          ON EXIT, THE SEED IS UPDATED.
C
C  N       (INPUT) INTEGER
C          THE NUMBER OF RANDOM NUMBERS TO BE GENERATED.
C
C  X       (OUTPUT) COMPLEXC16 ARRAY, DIMENSION (N)
C          THE GENERATED RANDOM NUMBERS.
C
C  FURTHER DETAILS
C  ===============
C
C  THIS ROUTINE CALLS THE AUXILIARY ROUTINE FLARUV TO GENERATE RANDOM
C  REAL NUMBERS FROM A UNIFORM (0,1) DISTRIBUTION, IN BATCHES OF UP TO
C  128 USING VECTORISABLE CODE. THE BOX-MULLER METHOD IS USED TO
C  TRANSFORM NUMBERS FROM A UNIFORM TO A NORMAL DISTRIBUTION.
C
C  =====================================================================
C-----------------------------------------------------------------------
C ASTER INFORMATION
C 14/01/2000 TOILETTAGE DU FORTRAN SUIVANT LES REGLES ASTER,
C            REMPLACEMENT DE 1 RETURN PAR GOTO 1000,
C            IMPLICIT NONE.
C-----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE
C      
C     .. SCALAR ARGUMENTS ..
      INTEGER            IDIST, N
C     ..
C     .. ARRAY ARGUMENTS ..
      INTEGER            ISEED( 4 )
      COMPLEX*16         X( * )
C     ..
C     .. PARAMETERS ..
      REAL*8             ZERO, ONE, TWO
      PARAMETER          ( ZERO = 0.0D+0, ONE = 1.0D+0, TWO = 2.0D+0 )
      INTEGER            LV
      PARAMETER          ( LV = 128 )
      REAL*8             TWOPI
      PARAMETER          ( TWOPI = 6.2831853071795864769252867663D+0 )
C     ..
C     .. LOCAL SCALARS ..
      INTEGER            I, IL, IV
C     ..
C     .. LOCAL ARRAYS ..
      REAL*8   U( LV )
C     ..
C     .. EXECUTABLE STATEMENTS ..
C
      DO 60 IV = 1, N, LV / 2
         IL = MIN( LV / 2, N-IV+1 )
C
C        CALL FLARUV TO GENERATE 2CIL REAL NUMBERS FROM A UNIFORM (0,1)
C        DISTRIBUTION (2*IL <= LV)
C
         CALL FLARUV( ISEED, 2*IL, U )
C
         IF( IDIST.EQ.1 ) THEN
C
C           COPY GENERATED NUMBERS
C
            DO 10 I = 1, IL
               X( IV+I-1 ) = DCMPLX( U( 2*I-1 ), U( 2*I ) )
   10       CONTINUE
         ELSE IF( IDIST.EQ.2 ) THEN
C
C           CONVERT GENERATED NUMBERS TO UNIFORM (-1,1) DISTRIBUTION
C
            DO 20 I = 1, IL
               X( IV+I-1 ) = DCMPLX( TWO*U( 2*I-1 )-ONE,
     $                       TWO*U( 2*I )-ONE )
   20       CONTINUE
         ELSE IF( IDIST.EQ.3 ) THEN
C
C           CONVERT GENERATED NUMBERS TO NORMAL (0,1) DISTRIBUTION
C
            DO 30 I = 1, IL
               X( IV+I-1 ) = SQRT( -TWO*LOG( U( 2*I-1 ) ) )*
     $                       EXP( DCMPLX( ZERO, TWOPI*U( 2*I ) ) )
   30       CONTINUE
         ELSE IF( IDIST.EQ.4 ) THEN
C
C           CONVERT GENERATED NUMBERS TO COMPLEX NUMBERS UNIFORMLY
C           DISTRIBUTED ON THE UNIT DISK
C
            DO 40 I = 1, IL
               X( IV+I-1 ) = SQRT( U( 2*I-1 ) )*
     $                       EXP( DCMPLX( ZERO, TWOPI*U( 2*I ) ) )
   40       CONTINUE
         ELSE IF( IDIST.EQ.5 ) THEN
C
C           CONVERT GENERATED NUMBERS TO COMPLEX NUMBERS UNIFORMLY
C           DISTRIBUTED ON THE UNIT CIRCLE
C
            DO 50 I = 1, IL
               X( IV+I-1 ) = EXP( DCMPLX( ZERO, TWOPI*U( 2*I ) ) )
   50       CONTINUE
         END IF
   60 CONTINUE
 1000 CONTINUE
C
C     END OF GLARNV
C
      END
