      REAL*8 FUNCTION GZSUM1( N, CX, INCX )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILIFOR  DATE 17/02/2003   AUTEUR NICOLAS O.NICOLAS 
C ======================================================================
C COPYRIGHT (C) LAPACK
C ======================================================================
C  -- LAPACK AUXILIARY ROUTINE (VERSION 2.0) --
C     UNIV. OF TENNESSEE, UNIV. OF CALIFORNIA BERKELEY, NAG LTD.,
C     COURANT INSTITUTE, ARGONNE NATIONAL LAB, AND RICE UNIVERSITY
C     OCTOBER 31, 1992
C
C  PURPOSE
C  =======
C
C  GZSUM1 TAKES THE SUM OF THE ABSOLUTE VALUES OF A COMPLEX
C  VECTOR AND RETURNS A DOUBLE PRECISION RESULT.
C
C  BASED ON DZASUM FROM THE LEVEL 1 BLAS.
C  THE CHANGE IS TO USE THE 'GENUINE' ABSOLUTE VALUE.
C
C  CONTRIBUTED BY NICK HIGHAM FOR USE WITH GLACON.
C
C  ARGUMENTS
C  =========
C
C  N       (INPUT) INTEGER
C          THE NUMBER OF ELEMENTS IN THE VECTOR CX.
C
C  CX      (INPUT) COMPLEX*16 ARRAY, DIMENSION (N)
C          THE VECTOR WHOSE ELEMENTS WILL BE SUMMED.
C
C  INCX    (INPUT) INTEGER
C          THE SPACING BETWEEN SUCCESSIVE VALUES OF CX.  INCX > 0.
C
C  =====================================================================
C-----------------------------------------------------------------------
C ASTER INFORMATION
C 14/01/2000 TOILETTAGE DU FORTRAN SUIVANT LES REGLES ASTER,
C            REMPLACEMENT DE 1 RETURN PAR GOTO 1000,
C            IMPLICIT NONE.
C-----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE
C
C
C     .. SCALAR ARGUMENTS ..
      INTEGER            INCX, N
C     ..
C     .. ARRAY ARGUMENTS ..
      COMPLEX*16         CX( * )
C     ..
C     .. LOCAL SCALARS ..
      INTEGER            I, NINCX
      REAL*8             STEMP
C     ..
C     .. EXECUTABLE STATEMENTS ..
C
      GZSUM1 = 0.0D0
      STEMP = 0.0D0
      IF( N.LE.0 )
     $   GOTO 1000
      IF( INCX.EQ.1 )
     $   GO TO 20
C
C     CODE FOR INCREMENT NOT EQUAL TO 1
C
      NINCX = N*INCX
      DO 10 I = 1, NINCX, INCX
C
C        NEXT LINE MODIFIED.
C
         STEMP = STEMP + ABS( CX( I ) )
   10 CONTINUE
      GZSUM1 = STEMP
      GOTO 1000
C
C     CODE FOR INCREMENT EQUAL TO 1
C
   20 CONTINUE
      DO 30 I = 1, N
C
C        NEXT LINE MODIFIED.
C
         STEMP = STEMP + ABS( CX( I ) )
   30 CONTINUE
      GZSUM1 = STEMP
 1000 CONTINUE
C
C     END OF GZSUM1
C
      END
