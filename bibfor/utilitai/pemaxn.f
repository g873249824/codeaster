      SUBROUTINE PEMAXN(RESU,NOMCHA,LIEU,NOMLIE,MODELE,
     &                  CHPOST,NBCMP,NOMCMP,NUORD,INST,IOCC)

      IMPLICIT NONE

      INTEGER NBCMP,NUORD,IOCC
      CHARACTER*8 NOMCMP(NBCMP),MODELE,NOMLIE,LIEU
      CHARACTER*19 CHPOST,RESU
      CHARACTER*24 NOMCHA

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 22/02/2011   AUTEUR TARDIEU N.TARDIEU 
C TOLE CRS_1404
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     OPERATEUR   POST_ELEM
C     TRAITEMENT DU MOT CLE-FACTEUR "MINMAX"
C     ROUTINE D'APPEL : PEMIMA
C
C     BUT : EXTRAIRE LE MIN ET LE MAX D'UNE CMP D'UN CHAMNO
C           ET LES STOCKER DANS LA TABLE
C
C     IN  RESU   : NOM DE LA TABLE
C     IN  NOMCHA : NOM SYMBOLIQUE DU CHAMP DU POST-TRAITEMENT
C     IN  LIEU   : LIEU DU POST-TRAITEMENT
C         (LIEU='TOUT'/'GROUP_MA'/'MAILLE')
C     IN  NOMLIE : NOM DU LIEU
C     IN  MODELE : NOM DU MODELE
C     IN  CHPOST  : NOM DU CHAMP DU POST-TRAITEMENT
C     IN  NBCMP   : NOMBRE DE COMPOSANTES
C     IN  NOMCMP  : NOM DES COMPOSANTES
C     IN  NUORD   : NUMERO D'ORDRE
C     IN  INST    : INSTANT
C     IN  IOCC    : NUMERO DE L'OCCURENCE
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16            ZK16
      CHARACTER*24                    ZK24
      CHARACTER*32                            ZK32
      CHARACTER*80                                    ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32     JEXNOM, JEXNUM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER IRET,NBMA,NBMAI,I,JCESV,JCESL,JCESD,JPOIV,JPOIL,JPOID
      INTEGER NUCMP,JCESK,JCMPGD,NCMPM,IBID,JCESC
      INTEGER NBSP,NBPT,ICMP,NBPARA,INDIK8,NBNO
      INTEGER INO,NMIN,NMAX,NPARA,PMAX,PMIN,JINTI,NBCMPM
      REAL*8  VMIN,VMAX,INST,R8MAEM
      COMPLEX*16 CBID
      CHARACTER*8  NOMA,K8B,TYPMCL(2),NOMGD,MOCLES(2),NOMVA,KNMIN,KNMAX
      CHARACTER*16 MOTCLE(2),VALK(2)
      CHARACTER*19 LIGREL,CESOUT
      CHARACTER*24 MESMAI,NOMNOE,NOMMAI
      LOGICAL EXIST
C Tableaux automatiques F90
      REAL*8 MIMA(2*NBCMP+2)
      CHARACTER*16 NOMPAR(4*NBCMP+5),NOMAX(2*NBCMP+3)

      CALL JEMARQ()

      CALL DISMOI('F','NOM_LIGREL',MODELE,'MODELE',IBID,LIGREL,IRET)
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE',IBID,NOMA,IRET)
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8B,IRET)
      CALL DISMOI('F','NB_NO_MAILLA',NOMA,'MAILLAGE',NBNO,K8B,IRET)

      IF(LIEU(1:4).NE.'TOUT') CALL U2MESS('A','UTILITAI3_94')

      NOMNOE = NOMA//'.NOMNOE         '
      NOMPAR(1)  ='CHAMP_GD'
      NOMPAR(2)='NUME_ORDRE'
      NOMPAR(3)='INST'
      NOMPAR(4)=LIEU
      MIMA(1)=INST
      NOMAX(1)=NOMCHA
      NOMAX(2)=NOMLIE

      CALL TBEXIP(RESU,LIEU,EXIST,K8B)
      IF(.NOT.EXIST)THEN
         CALL TBAJPA(RESU,1,NOMPAR(4),'K16')
      ENDIF

C --- CALCULS DES CHAMPS SIMPLES:
      CESOUT='&&PEMAXC_CESOUT'
      CALL CNOCNS ( CHPOST, 'V', CESOUT )
      CALL JEVEUO(CESOUT//'.CNSV','L',JCESV)
      CALL JEVEUO(CESOUT//'.CNSL','L',JCESL)
      CALL JEVEUO(CESOUT//'.CNSD','L',JCESD)
      CALL JEVEUO(CESOUT//'.CNSK','L',JCESK)
      CALL JEVEUO(CESOUT//'.CNSC','L',JCESC)

C --- RECUPERATION DE LA LISTE DES CMPS DU CATALOGUE :
C     (POUR LA GRANDEUR VARI_* , IL FAUT CONSTITUER :(V1,V2,...,VN))
      NOMGD = ZK8(JCESK-1+2)
      CALL JELIRA(CESOUT//'.CNSC','LONMAX',NCMPM,K8B)
      IF (NOMGD(1:5).NE.'VARI_') THEN
         CALL JEVEUO(CESOUT//'.CNSC','L',JCMPGD)
      ELSE
         CALL WKVECT('&&PEMAXC.LIST_CMP','V V K8',NCMPM,JCMPGD)
         DO 25 I = 1,NCMPM
            NOMVA = 'V'
            CALL CODENT(I,'G',NOMVA(2:8))
            ZK8(JCMPGD-1+I) = NOMVA
   25    CONTINUE
      END IF

      NPARA=4*NBCMP
      NBCMPM=ZI(JCESD+1)

      DO 30 I = 1 , NBCMP
         VMIN=R8MAEM()
         VMAX=-R8MAEM()
         ICMP=INDIK8(ZK8(JCESC),NOMCMP(I),1,NBCMPM)
         CALL ASSERT(ICMP.GE.0)
         DO 36 INO=1,NBNO
            IF(ZL(JCESL+(INO-1)*NBCMPM+ICMP-1)) THEN
              IF (VMAX.LT.ZR(JCESV+(INO-1)*NBCMPM+ICMP-1)) THEN
                 VMAX=ZR(JCESV+(INO-1)*NBCMPM+ICMP-1)
                 NMAX=INO
              ENDIF
              IF (VMIN.GT.ZR(JCESV+(INO-1)*NBCMPM+ICMP-1)) THEN
                 VMIN=ZR(JCESV+(INO-1)*NBCMPM+ICMP-1)
                 NMIN=INO
              ENDIF
            ENDIF
 36      CONTINUE
         MIMA(1+2*(I-1)+1)=VMAX
         MIMA(1+2*(I-1)+2)=VMIN
         CALL JENUNO ( JEXNUM(NOMNOE,NMIN) , KNMIN )
         CALL JENUNO ( JEXNUM(NOMNOE,NMAX) , KNMAX )
         NOMAX(2+2*(I-1)+1)=KNMAX
         NOMAX(2+2*(I-1)+2)=KNMIN

         NOMPAR(4+4*(I-1)+1)='MAX_'//NOMCMP(I)
         NOMPAR(4+4*(I-1)+2)='NO_MAX_'//NOMCMP(I)
         NOMPAR(4+4*(I-1)+3)='MIN_'//NOMCMP(I)
         NOMPAR(4+4*(I-1)+4)='NO_MIN_'//NOMCMP(I)

C ---    ON AJOUTE LES PARAMETRES MANQUANTS DANS LA TABLE:
         CALL TBEXIP(RESU,NOMPAR(4+4*(I-1)+1),EXIST,K8B)
         IF(.NOT.EXIST)THEN
            CALL TBAJPA(RESU,1,NOMPAR(4+4*(I-1)+1),'R' )
            CALL TBAJPA(RESU,1,NOMPAR(4+4*(I-1)+2),'K16' )
            CALL TBAJPA(RESU,1,NOMPAR(4+4*(I-1)+3),'R' )
            CALL TBAJPA(RESU,1,NOMPAR(4+4*(I-1)+4),'K16' )
         ENDIF


 30   CONTINUE

C --- ON REMPLIT LA TABLE
      NBPARA=4+NPARA
      CALL TBAJLI(RESU,NBPARA,NOMPAR,NUORD,MIMA,
     &            CBID,NOMAX,0)

      CALL JEDETR('&&PEMAXC_CESOUT')
      CALL JEDETR('&&PEMAXC.LIST_CMP')

      CALL JEDEMA()

      END
