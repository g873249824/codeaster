      SUBROUTINE RECUGD(CAELEM,NOMCMP,VALRES,NBGD,IASSEF,IASSMX)
      IMPLICIT NONE
C
      INTEGER        NBGD,IASSEF,IASSMX
      REAL*8         VALRES(NBGD*IASSEF)
      CHARACTER*8    NOMCMP(NBGD)
      CHARACTER*19   CAELEM
C
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 14/01/2013   AUTEUR FLEJOU J-L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C     PERMET D'EXTRAIRE D'UNE STRUCTURE "CARTE", LES VALEURS DES
C     COMPOSANTES POUR CHAQUE ASSOCIATION.
C-----------------------------------------------------------------------
C IN : CAELEM  : NOM DE LA CARTE.
C IN : NOMCMP  : NOM DES COMPOSANTES DE LA GRANDEUR RECHERCHEE
C                VECTEUR DE LONG. NBGD
C IN  : NBGD   : NOMBRE DE COMPOSANTES RECHERCHEES.
C IN  : IASSEF : NOMBRE D'ASSOCIATIONS DE LA STRUCTURE CARTE.
C IN  : IASSMX : NOMBRE MAX. D'ASSOCIATIONS DE LA STRUCTURE CARTE.
C OUT : VALRES : VALEURS DES COMPOSANTES.
C-----------------------------------------------------------------------
C
      INCLUDE 'jeveux.h'
      CHARACTER*32   JEXNOM
C
      INTEGER        ICARD,ICARV,ICMP,ICODE,INDIK8,NBEC,IER
      INTEGER        II,IRANG,IRANV,JJ,LL,NBCMP
C
      LOGICAL        EXISDG
C
      CHARACTER*1    K1BID
      CHARACTER*8    K8BID
      CHARACTER*24   CARAV,CARAD
      CHARACTER*32   KEXNOM
C
C-----------------------------------------------------------------------
      CALL JEMARQ()
C
      CARAV = CAELEM(1:19)//'.VALE'
      CARAD = CAELEM(1:19)//'.DESC'
      CALL JEVEUO(CARAV,'L',ICARV)
      CALL JEVEUO(CARAD,'L',ICARD)
C
      KEXNOM = JEXNOM('&CATA.GD.NOMCMP','CAGEPO')
      CALL JELIRA(KEXNOM,'LONMAX',NBCMP,K1BID)
      CALL JEVEUO(KEXNOM,'L',ICMP)
C     NOMBRE D'ENTIERS CODES DANS LA CARTE
      CALL DISMOI('F','NB_EC','CAGEPO','GRANDEUR',NBEC,K8BID,IER)
C     TOUTES LES COMPOSANTES DOIVENT ETRE DANS LA GRANDEUR
      DO 200 JJ=1,NBGD
         IRANG = INDIK8( ZK8(ICMP) , NOMCMP(JJ) , 1 , NBCMP )
         IF (IRANG .EQ. 0) THEN
            CALL U2MESK('E','UTILITAI4_8',1,NOMCMP(JJ))
         ENDIF
200    CONTINUE
C
      DO 10 II=1,IASSEF
         ICODE = ZI(ICARD-1+3+2*IASSMX+NBEC*(II-1)+1)
C
         DO 20 JJ=1,NBGD
C           RANG DANS LA GRANDEUR
            IRANG = INDIK8( ZK8(ICMP) , NOMCMP(JJ) , 1 , NBCMP )
C           RANG DANS LA CARTE
            IRANV = 0
            DO 40 LL = 1,IRANG
               IF ( EXISDG(ICODE,LL) ) IRANV = IRANV + 1
40          CONTINUE
C           ON MET A ZERO SI INEXISTANT
            IF (IRANV.EQ.0) THEN
               VALRES(NBGD*(II-1)+JJ) = 0.0D0
            ELSE
               VALRES(NBGD*(II-1)+JJ) = ZR(ICARV-1+NBCMP*(II-1)+IRANV)
            ENDIF
20       CONTINUE
10    CONTINUE
C
      CALL JEDEMA()
      END
