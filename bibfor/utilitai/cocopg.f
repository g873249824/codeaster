      SUBROUTINE COCOPG(OBIN,OBOUT,NBOBJ,LONNEW,BASE)
      IMPLICIT REAL*8 (A-H,O-Z)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 27/06/2005   AUTEUR D6BHHJP J.P.LEFEBVRE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     --
C     ARGUMENTS:
C     ----------
      CHARACTER*(*) OBIN,OBOUT,BASE
      INTEGER NBOBJ,LONNEW
C ----------------------------------------------------------------------
C     BUT : AGRANDIR UNE COLLECTION
C       1)   ON PEUT AGRANDIR UNE COLLECTION EN AUGMENTANT LE NOMBRE DE
C            SES OBJETS  : NBOBJ > NMAXOC
C       2)   ON PEUT AGRANDIR UNE COLLECTION CONTIGUE EN AUGMENTANT
C            SON 'LONT'  : LONNEW > LONT
C     IN:
C       OBIN   : NOM D'UNE COLLECTION  JEVEUX  A RECOPIER
C                DANS OBOUT QUI AURA PLUS D'ELEMENTS QUE OBIN
C       NBOBJ  : NOMBRE D'OBJETS VOULU POUR LA NOUVELLE COLLECTION
C       LONNEW : POUR UNE COLLECTION CONTIGUE, VALEUR DU NOUVEAU  "LONT"
C       BASE   : 'G','V','L' BASE DE CREATION DE OBOUT
C     OUT:
C       OBOUT  : NOM D'UNE COLLECTION JEVEUX (CIBLE)
C
C ----------------------------------------------------------------------
C     VARIABLES LOCALES:
C     ------------------
      CHARACTER*24 OB1,OB2,NOPTLG
      CHARACTER*1 BAS2
      CHARACTER*4 GENR,TYPE,DOCU
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR,JEXR8
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI ,LONG,LON1,LON2,LONT,LTYP
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*2 ACCE2
      CHARACTER*8 ZK8,STOCKA,NOM8,MODLO2
      CHARACTER*8 K8BID
      CHARACTER*16 ZK16,KBID,NOM16
      CHARACTER*24 ZK24,NOM24
      CHARACTER*40 ACCES,MODELO
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
C --------------- FIN COMMUNS NORMALISES  JEVEUX  --------------------
C
C
C
      CALL JEMARQ()
      BAS2 = BASE
      OB1 = OBIN
      OB2 = OBOUT

      CALL JELIRA(OB1,'NMAXOC',NMAXOC,KBID)

      CALL JELIRA(OB1,'GENR',IBID,GENR)
      CALL JELIRA(OB1,'TYPE',IBID,TYPE)
      CALL JELIRA(OB1,'LTYP',LTYP,KBID)
      IF (TYPE(1:1).EQ.'K')  CALL CODENT(LTYP,'G',TYPE(2:4))
      CALL JELIRA(OB1,'NUTIOC',NUTIOC,KBID)
      CALL JELIRA(OB1,'ACCES',IBID,ACCES)
      CALL JELIRA(OB1,'STOCKAGE',IBID,STOCKA)
      CALL JELIRA(OB1,'MODELONG',IBID,MODELO)
C
      IF (ACCES(1:2).EQ.'R8') THEN
         CALL UTMESS('F','COCOPG','ACCES "R8" INTERDIT ICI.')
      END IF
C
      IF (ACCES(1:2).EQ.'NO') THEN
         ACCE2='NO'
      ELSE
         ACCE2='NU'
      END IF
C
      IF  (MODELO(1:8).EQ.'VARIABLE') THEN
         MODLO2='VARIABLE'
         NOPTLG=MODELO(10:33)
      ELSE
         MODLO2='CONSTANT'
         NOPTLG=' '
      END IF


C     -- CREATION :
C     -------------
      IF (NOPTLG(1:1).EQ.' ') THEN
         CALL JECREC(OB2,BAS2(1:1)//' '//GENR(1:1)//' '//TYPE(1:4) ,
     +         ACCE2,STOCKA,MODLO2,NBOBJ)
      ELSE
         CALL JECREC(OB2,BAS2(1:1)//' '//GENR(1:1)//' '//TYPE(1:4) ,
     +         ACCE2,STOCKA,NOPTLG,NBOBJ)
      END IF


C     -- DOCU :
C     ---------
      CALL JELIRA(OB1,'DOCU',IBID,DOCU)
C
C     POUR NE PAS PLANTER JEVEUX ...
      IF (DOCU.NE.'    ')  CALL JEECRA(OB2,'DOCU',IBID,DOCU)


C     -- LONT :
C     -----------------------------
      IF (STOCKA.EQ.'CONTIG') THEN
         CALL JELIRA(OB1,'LONT',LONT,KBID)
         CALL JEECRA(OB2,'LONT',LONNEW,KBID)
      END IF

C     -- LONMAX  DANS LE CAS CONSTANT,DISPERSE:
C     -------------------------------------------------------
      IF ((MODLO2(1:8).EQ.'CONSTANT').AND.(STOCKA.EQ.'DISPERSE')
     +               .AND.(NOPTLG(1:1).EQ.' ')) THEN
         IF (GENR(1:1).EQ.'E') THEN
         ELSE IF (GENR(1:1).EQ.'V') THEN
            CALL JELIRA(OB1,'LONMAX',LON1,K8BID)
            IF (LON1.GT.0) CALL JEECRA(OB2,'LONMAX',LON1,K8BID)
         END IF
      END IF
C
C     -- BOUCLE SUR LES ELEMENTS DE LA COLLECTION :
C     ---------------------------------------------
      DO 1, IOC =1,NUTIOC
C
C        -- ON FAIT LE JECROC :
C        ----------------------
         IF (ACCES(1:2).EQ.'NU') THEN
            CALL JECROC(JEXNUM(OB2,IOC))
         ELSE IF (ACCES(1:2).EQ.'NO') THEN
            CALL JENUNO(JEXNUM(OB1,IOC),NOM24)
            CALL JECROC(JEXNOM(OB2,NOM24))
         ELSE
            CALL UTMESS('F','COCOPG','ACCES INTERDIT')
         END IF
C
C        -- ON ECRIT LES ATTRIBUTS PARTICULIERS AU GENRE :
C           ET ON RECOPIE LES VALEURS :
C        -------------------------------------------------
         IF (GENR(1:1).EQ.'E') THEN
            CALL JEVEUO(JEXNUM(OB1,IOC),'L',IAD1)
            CALL JEVEUO(JEXNUM(OB2,IOC),'E',IAD2)
            CALL JACOPO(1,TYPE,IAD1,IAD2)
         ELSE IF (GENR(1:1).EQ.'V') THEN
            CALL JELIRA(JEXNUM(OB1,IOC),'LONMAX',LON1,K8BID)
           IF((NOPTLG(1:1).EQ.' ').AND.(MODLO2(1:8).EQ.'VARIABLE'))THEN
               CALL JEECRA(JEXNUM(OB2,IOC),'LONMAX',LON1,K8BID)
            END IF
            CALL JELIRA(JEXNUM(OB1,IOC),'LONUTI',LONG,K8BID)
            CALL JEECRA(JEXNUM(OB2,IOC),'LONUTI',LONG,K8BID)
            IF (LON1.EQ.0) GO TO 1
            CALL JEVEUO(JEXNUM(OB1,IOC),'L',IAD1)
            CALL JEVEUO(JEXNUM(OB2,IOC),'E',IAD2)
            CALL JACOPO(LON1,TYPE,IAD1,IAD2)
         ELSE
            CALL UTMESS('F','COCOPG','GENRE : '//GENR(1:1)
     +       //' NON PREVU.')
         END IF
C
 1    CONTINUE
C
 9999 CONTINUE
      CALL JEDEMA()
      END
