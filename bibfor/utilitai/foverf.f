      SUBROUTINE FOVERF(V,NC,IER)
      IMPLICIT REAL*8 (A-H,O-Z)
      INTEGER         NC,IER,I,ISENS,ILARGE,NIV,IFM
      REAL*8          V(NC)
      CHARACTER*16 NOMCMD, TYPFON
      CHARACTER*19 NOMFON
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 18/12/2007   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     VERIFICATION DU CARACTERE CROISSANT OU DECROISSANT
C     DES VALEURS DANS V
C     ------------------------------------------------------------------
C IN     : V   : VECTEUR DES VALEURS
C IN     : NC  : NOMBRE DE POINTS
C IN/OUT : IER :
C  EN ENTREE :   0 : SI ON VEUT SIMPLEMENT RECUPERER LE SENS
C             <> 0 : DECLENCHE UNE ERREUR SI LE SENS N'EST PAS VERIFIE
C  EN SORTIE : LE SENS DE LA LISTE DES PARAMETRES
C  CODE DU SENS : 2 : STRICTEMENT CROISSANT
C                 1 : CROISSANT AU SENS LARGE
C                 0 : NON MONOTONE
C                -1 : DECROISSANT AU SENS LARGE
C                -2 : STRICTEMENT DECROISSANT
C  EX : SI EN ENTREE IER=1 ET QUE LA LISTE N'EST PAS CROISSANTE, ERREUR
C  REM : S'IL N'Y A QU'UNE VALEUR, ON RETOURNE LA MEME VALEUR
C     ------------------------------------------------------------------
C
C     ISENS (TRAVAIL) : 1 SI CROIS, -1 SI DECROIS,
C                      99 SI ON NE SAIT PAS, 0 NON MONOTONE
C     ILARGE : 0 SI AU SENS LARGE, 1 SI STRICT
      CALL INFNIV(IFM,NIV)
C
      IF(NC.GT.1)THEN
        ISENS=99
        ILARGE=1
        I=2
        IF(V(I).EQ.V(I-1))THEN
          ILARGE=0
        ELSEIF(V(I).GT.V(I-1))THEN
          ISENS=1
        ELSE
          ISENS=-1
        ENDIF
C
        DO 10 I=3,NC
          IF(V(I).EQ.V(I-1))THEN
            ILARGE=0
            IF (NIV.GE.2) THEN
               WRITE(IFM,1000) I,V(I)
            ENDIF
          ELSEIF(V(I).GT.V(I-1))THEN
            IF(ISENS.EQ.99.OR.ISENS.EQ.1)THEN
              ISENS=1
            ELSE
              ISENS=0
              IF (NIV.GE.2) THEN
                 WRITE(IFM,1001) I,V(I-1),V(I)
              ENDIF
            ENDIF
          ELSE
            IF(ISENS.EQ.99.OR.ISENS.EQ.-1)THEN
              ISENS=-1
            ELSE
              ISENS=0
              IF (NIV.GE.2) THEN
                 WRITE(IFM,1002) I,V(I-1),V(I)
              ENDIF
            ENDIF
          ENDIF
C         POUR SORTIE ANTICIPEE DE LA BOUCLE
          IF(ISENS.EQ.0) GOTO 11
10      CONTINUE
11      CONTINUE
        IF(ISENS.EQ.99)THEN
C         AU CAS OU ON NE SAIT TOUJOURS PAS
          ISENS=1
          ILARGE=0
        ENDIF
        ISENS=ISENS+(ISENS*ILARGE)
C       ON RENVOIT LE SENS OU UN MESSAGE D'ERREUR
        IF(IER.EQ.0)THEN
          IER=ISENS
        ELSEIF(IER.NE.ISENS)THEN
          CALL GETRES(NOMFON,TYPFON,NOMCMD)
          IF(IER.EQ.2)THEN
C            PARAMETRES NON STRICTEMENT CROISSANTS
            CALL U2MESK('F', 'FONCT0_44', 1, NOMFON)
          ELSEIF(IER.EQ.1)THEN
C           PARAMETRES NON CROISSANTS
            CALL U2MESK('F', 'FONCT0_45', 1, NOMFON)
          ELSEIF(IER.EQ.-1)THEN
C           PARAMETRES NON DECROISSANTS
            CALL U2MESK('F', 'FONCT0_46', 1, NOMFON)
          ELSEIF(IER.EQ.-2)THEN
C           PARAMETRES NON STRICTEMENT DECROISSANTS
            CALL U2MESK('F', 'FONCT0_47', 1, NOMFON)
          ENDIF
          CALL ASSERT(.FALSE.)
        ENDIF
      ELSE
C        UNE SEULE VALEUR, ON RETOURNE CROISSANT STRICT SI IER=0
         IF(IER.EQ.0)THEN
           ISENS=2
         ELSE
C        ON RETOURNE CE QU'ON A DEMANDE
           ISENS=IER
         ENDIF
      ENDIF
      IER=ISENS
C
 1000 FORMAT('EGALITE       I=',I6,'   VALEUR(I)   :',1PE16.9)
 1001 FORMAT('CROISSANT     I=',I6,'   VALEUR(I-1) :',1PE16.9,
     +       '   VALEUR(I)   :',1PE16.9)
 1002 FORMAT('DECROISSANT   I=',I6,'   VALEUR(I-1) :',1PE16.9,
     +       '   VALEUR(I)   :',1PE16.9)
C
      END
