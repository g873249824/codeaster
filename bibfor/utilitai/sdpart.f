      SUBROUTINE SDPART(NBSD  ,NBSDP0,SDLOC )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 11/10/2011   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C-----------------------------------------------------------------------
C    - FONCTION REALISEE : REPARTITION DE SOUS-DOMAINES PAR PROCESSEUR
C
C ARGUMENTS D'APPELS
C IN  NBSD   : (I) : NOMBRE DE SOUS-DOMAINES A REPARTIR
C IN  NBSDP0 : (I) : NOMBRE DE SOUS-DOMAINES A DONNER AU PROCESSEUR 0
C OUT SDLOC  : (I) : TABLEAU DE TAILLE NBSD
C                    SDLOC (I) = 1 SI LE SOUS-DOMAINE I EST TRAITE
C                                  LOCALEMENT
C----------------------------------------------------------------------
C RESPONSABLE DESOZA T.DESOZA
C CORPS DU PROGRAMME
      IMPLICIT NONE
C DECLARATION PARAMETRES D'APPELS
      INTEGER      SDLOC (NBSD),NBSD  ,NBSDP0

C DECLARATION VARIABLES LOCALES
      INTEGER      NBPROC,RANG,I
      INTEGER      NBSDPP,SDREST,NPDEB,NSDDEB,NSDFIN
      INTEGER      IPROC,IPROC1,DECAL
C----------------------------------------------------------------------
C
C --- INITIALISATIONS
      CALL MPICM0(RANG  ,NBPROC)
C
C --- EN SEQUENTIEL ON GAGNE DU TEMPS
      IF (NBPROC.EQ.1) THEN
        DO 1 I = 1,NBSD
          SDLOC (I) = 1
    1   CONTINUE
        GOTO 999
      ENDIF

      DO 10 I = 1,NBSD
        SDLOC (I) = 0
   10 CONTINUE
C
C --- PAS DE TRAITEMENT PARTICULIER DU PROC. 0
      IF (NBSDP0.EQ.0) THEN
        NBSDPP = NBSD/NBPROC
        SDREST = NBSD-(NBPROC*NBSDPP)
        NPDEB  = 0
      ELSE
C
C --- DELESTAGE DU PROC. 0
        IF (RANG.EQ.0) THEN
          DO 100 I = 1,NBSDP0
            SDLOC (I) = 1
  100     CONTINUE
        ENDIF
C
C ----- RESTE REPARTI ENTRE LES PROC. RESTANTS
        NBSDPP = (NBSD-NBSDP0)/(NBPROC-1)
        SDREST = (NBSD-NBSDP0)-((NBPROC-1)*NBSDPP)
        NPDEB  = 1
      ENDIF
C
      DO 110 IPROC = NPDEB,NBPROC-1
          IF (IPROC.EQ.RANG) THEN
C --------- INDICE RELATIF DU PROCESSEUR A EQUILIBRER
            IPROC1 = IPROC-NPDEB
C --------- BORNES DES SDS A LUI ATTRIBUER
            NSDDEB = 1+NBSDP0+ IPROC1   *NBSDPP
            NSDFIN =   NBSDP0+(IPROC1+1)*NBSDPP
C --------- REPARTITION DES SD RESTANTS (AU PLUS NBPROC-1 OU -2)
C --------- PARMI LES PROC. DE NUMERO (1 OU 2) A NBPROC-1
            IF (IPROC1.GT.SDREST) THEN
C ----------- TOUS LES SD RESTANTS ONT ETE TRAITES
              DECAL = SDREST
            ELSE
              IF (IPROC1.EQ.0) THEN
C ------------- LE PROC. 0 OU 1 NE RECOIVENT PAS DE SD. SUPPLEMENTAIRES
                DECAL  = 0
              ELSE
C ------------- LE PROC. IPROC1 RECOIT 1 SD SUPPLEMENTAIRE
                DECAL  = IPROC1-1
                NSDFIN = NSDFIN+1
              ENDIF
            ENDIF
C --------- ATTRIBUTION DES SD AUX PROC.
            DO 120 I = NSDDEB,NSDFIN
              SDLOC (DECAL+I) = 1
  120       CONTINUE
          ENDIF
  110   CONTINUE

  999 CONTINUE

      END
