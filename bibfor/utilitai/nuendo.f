      SUBROUTINE NUENDO(NUMEDD,SDNUEN)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXNOM
      CHARACTER*24 NUMEDD
      CHARACTER*24 SDNUEN
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (INITIALISATION)
C
C CREATION DE LA SD POUR REPERAGE DDL ENDOMMAGEMENT AUX NOEUDS
C
C ----------------------------------------------------------------------
C
C
C IN  NUMEDD : NOM DE LA NUMEROTATION
C IN  SDNUEN : NOM DE LA S.D. DDL ENDOMMAGEMENT AUX NOEUDS
C
C
C
C
      CHARACTER*8   NOCMP, NOMGD, MODELE, NOMA, K8BID
      CHARACTER*16  NOMTE
      CHARACTER*19  LIGRMO
      CHARACTER*24  NOLILI,NOLIEL
      LOGICAL       EXISDG
      INTEGER       NEC,NBNOEU,NCMPMX
      INTEGER       NLILI,NBNO,NEQ,NBNOC
      INTEGER       IER,IBID,ICO
      INTEGER       IMA,INO,IDAMG,INDIK8,I,K,INOC,IVAL,IADG
      INTEGER       ITRAV,ICONEX,NBELEM
      INTEGER       IANCMP,IANUEQ,IAPRNO
      INTEGER       IFM,NIV
      INTEGER       NBGR,IGR,TE,TYPELE,NBELGR,LIEL,IEL,NBGREL
      INTEGER       JNUEN
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- MODELE ASSOCIE AU NUME_DDL
C
      CALL DISMOI('F','NOM_MODELE',NUMEDD,'NUME_DDL',IBID  ,MODELE,IER)
C
C --- NOM DU MAILLAGE
C
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE'  ,IBID  ,NOMA  ,IER)
C
C --- NOMBRE DE NOEUDS DU MAILLAGE
C
      CALL DISMOI('F','NB_NO_MAILLA',NOMA,'MAILLAGE',NBNOEU,K8BID ,IER)
C
C --- LIGREL DU MODELE
C
      LIGRMO = MODELE//'.MODELE'
C
C --- CREATION D'UN VECTEUR DESTINE A CONTENIR LES NUMEROS DES NOEUDS
C
      CALL WKVECT('&&NUENDO.NOEUDS','V V I',NBNOEU,ITRAV)
C
C --- REPERAGE DES NOEUDS SUR ELEMENTS AVEC ENDOMMAGEMENT AUX NOEUDS
C
      NBGR   = NBGREL(LIGRMO)
      NOLIEL = LIGRMO//'.LIEL'
      DO 140 IGR = 1,NBGR
        TE     = TYPELE(LIGRMO,IGR)
        CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',TE),NOMTE)
        NBELGR = NBELEM(LIGRMO,IGR)
        CALL JEVEUO(JEXNUM(NOLIEL,IGR),'L',LIEL)
        IF (NOMTE.EQ.'MNDPTR6'      .OR.
     &      NOMTE.EQ.'MNDPQS8'      .OR.
     &      NOMTE.EQ.'MNAXTR6'      .OR.
     &      NOMTE.EQ.'MNAXQS8'      .OR.
     &      NOMTE.EQ.'MNVG_HEXA20'  .OR.
     &      NOMTE.EQ.'MNVG_TETRA10' .OR.
     &      NOMTE.EQ.'MNVG_PENTA15') THEN
          DO 130 IEL = 1,NBELGR
            IMA = ZI(LIEL-1+IEL)
            CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',IMA),'L',ICONEX)
            CALL JELIRA(JEXNUM(NOMA//'.CONNEX',IMA),'LONMAX',
     &                  NBNO,K8BID)
            DO 20 INO = 1, NBNO
              ZI(ITRAV+ZI(ICONEX+INO-1)-1) = 1
  20        CONTINUE
 130      CONTINUE
        ENDIF
 140  CONTINUE
C
C --- NOMBRE DE NOEUDS
C
      NBNOC = 0
      DO 30 INO = 1, NBNOEU
        IF (ZI(ITRAV+INO-1).EQ.1) THEN
          NBNOC = NBNOC + 1
        ENDIF
  30  CONTINUE
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE> ... CREATION SD DDLS EN'//
     &                ' ENDOMMAGEMENT AUX NOEUDS: ',NBNOC
      ENDIF
C
C --- RECUPERATION DU NOMBRE D'INCONNUES DU MODELE
C
      CALL JELIRA(NUMEDD(1:14)//'.NUME.NUEQ','LONUTI',NEQ,K8BID)
      IF (NBNOC.GT.0) THEN
        CALL WKVECT(SDNUEN,'V V I',NEQ,JNUEN)
      ELSE
        GOTO 9999
      ENDIF
C
C --- NOMBRE DE COMPOSANTES ASSOCIEES A LA GRANDEUR DEPL_R
C
      NOMGD  = 'DEPL_R'
      CALL DISMOI('F','NB_EC',NOMGD,'GRANDEUR',NEC,K8BID,IER)
      CALL JELIRA(JEXNOM('&CATA.GD.NOMCMP',NOMGD),'LONMAX',NCMPMX,K8BID)
      CALL JEVEUO(JEXNOM('&CATA.GD.NOMCMP',NOMGD),'L',IANCMP)
      NOCMP  = 'DAMG'
C
C --- LOCALISATION DE DAMG DANS LA LISTE DES DDLS ASSOCIES
C --- A LA GRANDEUR DEPL_R
C
      IDAMG = INDIK8(ZK8(IANCMP),NOCMP,1,NCMPMX)
      CALL ASSERT(IDAMG.NE.0)
C
C --- RECUPERATION DU .PRNO ASSOCIE AU MAILLAGE
C
      CALL JELIRA(NUMEDD(1:14)//'.NUME.PRNO','NMAXOC',NLILI,K8BID)
      K = 0
      DO 40 I = 1, NLILI
        CALL JENUNO(JEXNUM(NUMEDD(1:14)//'.NUME.LILI',I),NOLILI)
        IF (NOLILI(1:8).NE.'&MAILLA ') GOTO 40
        K = I
  40  CONTINUE
      CALL ASSERT (K.NE.0)
C
      CALL JEVEUO(JEXNUM(NUMEDD(1:14)//'.NUME.PRNO',K),'L',IAPRNO)
C
C --- TABLEAU DES NUMEROS D'EQUATIONS
C
      CALL JEVEUO(NUMEDD(1:14)//'.NUME.NUEQ','L',IANUEQ)
C
C --- AFFECTATION DU TABLEAU DES NUMEROS DES INCONNUES
C
      INOC = 0
      DO 50 INO = 1, NBNOEU
        IF (ZI(ITRAV+INO-1).EQ.0) GOTO 50
        INOC = INOC + 1
        IVAL = ZI(IAPRNO+(INO-1)*(NEC+2)+1-1)
        IADG = IAPRNO+(INO-1)*(NEC+2)+3-1

        IF (EXISDG(ZI(IADG),IDAMG)) THEN

          ICO = 0
          DO 70 I = 1, IDAMG-1
            IF (EXISDG(ZI(IADG),I)) ICO = ICO + 1
  70      CONTINUE
          ZI(JNUEN+IVAL+ICO-1) = 1
        ENDIF
  50  CONTINUE
C
C --- MENAGE
      CALL JEDETR('&&NUENDO.NOEUDS')
C
9999  CONTINUE
C
      CALL JEDEMA()
      END
