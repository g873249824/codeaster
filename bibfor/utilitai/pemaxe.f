      SUBROUTINE PEMAXE(RESU,NOMCHA,LIEU,NOMLIE,MODELE,
     &                  CHPOST,NBCMP,NOMCMP,NUORD,INST,IOCC)

      IMPLICIT NONE

      INCLUDE 'jeveux.h'
      INTEGER NBCMP,NUORD,IOCC
      CHARACTER*8 NOMCMP(NBCMP),MODELE,NOMLIE,LIEU
      CHARACTER*19 CHPOST,RESU
      CHARACTER*24 NOMCHA

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C TOLE CRS_1404
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     OPERATEUR   POST_ELEM
C     TRAITEMENT DU MOT CLE-FACTEUR "MINMAX"
C     ROUTINE D'APPEL : PEMIMA
C
C     BUT : EXTRAIRE LE MIN ET LE MAX D'UNE CMP D'UN CHAMELEM
C           ET LES STOCKER DANS LA TABLE
C
C     IN  RESU   : NOM DE LA TABLE
C     IN  NOMCHA : NOM SYMBOLIQUE DU CHAMP DU POST-TRAITEMENT
C     IN  LIEU   : LIEU DU POST-TRAITEMENT
C         (LIEU='TOUT'/'GROUP_MA'/'MAILLE')
C     IN  NOMLIE : NOM DU LIEU
C     IN  MODELE : NOM DU MODELE
C     IN  CHPOST  : NOM DU CHAMP DU POST-TRAITEMENT
C     IN  NBCMP   : NOMBRE DE COMPOSANTES
C     IN  NOMCMP  : NOM DES COMPOSANTES
C     IN  NUORD   : NUMERO D'ORDRE
C     IN  INST    : INSTANT
C     IN  IOCC    : NUMERO DE L'OCCURENCE
C     ------------------------------------------------------------------

      INTEGER IRET,NBMA,NBMAI,I,JCESV,JCESL,JCESD
      INTEGER NUCMP,JCESK,JCMPGD,NCMPM,IBID,IAD,INDMA
      INTEGER JMESMA,IPT,NBSP,NBPT,ICMP,IMA,NBPARA,INDIK8,NBNO
      INTEGER NMIN,NMAX,NPARA,PMAX,PMIN
      REAL*8  VMIN,VMAX,INST,R8MAEM
      COMPLEX*16 CBID
      CHARACTER*8  NOMA,K8B,TYPMCL(2),NOMGD,NOMVA,KNMIN,KNMAX
      CHARACTER*16 MOTCLE(2)
      CHARACTER*19 LIGREL,CESOUT
      CHARACTER*24 MESMAI,NOMMAI
      LOGICAL EXIST
C Tableaux automatiques F90
      REAL*8 MIMA(2*NBCMP+2)
      CHARACTER*16 NOMPAR(6*NBCMP+5),MAMAX(2*NBCMP+3)
      INTEGER PTMAX(1+2*NBCMP)

      CALL JEMARQ()

      CALL DISMOI('F','NOM_LIGREL',MODELE,'MODELE',IBID,LIGREL,IRET)
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE',IBID,NOMA,IRET)
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8B,IRET)
      CALL DISMOI('F','NB_NO_MAILLA',NOMA,'MAILLAGE',NBNO,K8B,IRET)

      NOMMAI = NOMA//'.NOMMAI         '

C --- CREATION D'UN TABLEAU D'INDICES POUR REPERER
C     LES MAILLES DU POST TRAITEMENT
      CALL WKVECT('&&PEMAXC_IND.MAILLE','V V I',NBMA,INDMA)
      IF(LIEU(1:4).NE.'TOUT')THEN
         MESMAI    = '&&PEMAXC_NUM.MAILLE'
         MOTCLE(1) = 'GROUP_MA'
         TYPMCL(1) = 'GROUP_MA'
         CALL RELIEM(' ',NOMA,'NU_MAILLE','MINMAX', IOCC, 1,
     &               MOTCLE, TYPMCL, MESMAI, NBMAI )
         CALL JEVEUO(MESMAI,'L',JMESMA)
         DO 5 I=1,NBMA
           ZI(INDMA+I-1)=0
 5       CONTINUE
         DO 10 I=1,NBMAI
           ZI(INDMA+ZI(JMESMA+I-1)-1)=1
 10      CONTINUE
      ELSE
         DO 15 I=1,NBMA
           ZI(INDMA+I-1)=1
 15      CONTINUE
      ENDIF

      NOMPAR(1)  ='CHAMP_GD'
      NOMPAR(2)='NUME_ORDRE'
      NOMPAR(3)='INST'
      NOMPAR(4)=LIEU
      MIMA(1)=INST
      MAMAX(1)=NOMCHA
      MAMAX(2)=NOMLIE

      CALL TBEXIP(RESU,LIEU,EXIST,K8B)
      IF(.NOT.EXIST)THEN
         CALL TBAJPA(RESU,1,NOMPAR(4),'K16')
      ENDIF

C --- CALCULS DES CHAMPS SIMPLES:
      CESOUT='&&PEMAXC_CESOUT'

      CALL CELCES ( CHPOST, 'V', CESOUT )
      CALL JEVEUO(CESOUT//'.CESV','L',JCESV)
      CALL JEVEUO(CESOUT//'.CESL','L',JCESL)
      CALL JEVEUO(CESOUT//'.CESD','L',JCESD)
      CALL JEVEUO(CESOUT//'.CESK','L',JCESK)


C --- RECUPERATION DE LA LISTE DES CMPS DU CATALOGUE :
C     (POUR LA GRANDEUR VARI_* , IL FAUT CONSTITUER :(V1,V2,...,VN))
      NOMGD = ZK8(JCESK-1+2)
      CALL JELIRA(CESOUT//'.CESC','LONMAX',NCMPM,K8B)
      IF (NOMGD(1:5).NE.'VARI_') THEN
        CALL JEVEUO(CESOUT//'.CESC','L',JCMPGD)
      ELSE
        CALL WKVECT('&&PEMAXC.LIST_CMP','V V K8',NCMPM,JCMPGD)
        DO 25 I = 1,NCMPM
          NOMVA = 'V'
          CALL CODENT(I,'G',NOMVA(2:8))
          ZK8(JCMPGD-1+I) = NOMVA
   25   CONTINUE
      END IF

      DO 30 ICMP = 1 , NBCMP
         NUCMP=INDIK8(ZK8(JCMPGD),NOMCMP(ICMP),1,NCMPM)
         VMIN=R8MAEM()
         VMAX=-R8MAEM()

         DO 35 IMA=1,NBMA
            IF(ZI(INDMA+IMA-1).NE.1)GOTO 35
            NBPT=ZI(JCESD-1+5+4*(IMA-1)+1)
            NBSP=ZI(JCESD-1+5+4*(IMA-1)+2)
            CALL ASSERT(NBSP.EQ.1)
            DO 40 IPT=1,NBPT
               CALL CESEXI('C',JCESD,JCESL,IMA,IPT,1,NUCMP,IAD)
               IF(IAD.GT.0)THEN
                 IF (VMAX.LT.ZR(JCESV-1+IAD)) THEN
                    VMAX=ZR(JCESV-1+IAD)
                    NMAX=IMA
                    PMAX=IPT
                 ENDIF
                 IF (VMIN.GT.ZR(JCESV-1+IAD)) THEN
                    VMIN=ZR(JCESV-1+IAD)
                    NMIN=IMA
                    PMIN=IPT
                 ENDIF
               ENDIF
 40         CONTINUE
 35      CONTINUE

         NOMPAR(4+6*(ICMP-1)+1)='MAX_'//NOMCMP(ICMP)
         NOMPAR(4+6*(ICMP-1)+2)='MA_MAX_'//NOMCMP(ICMP)
         NOMPAR(4+6*(ICMP-1)+3)='PT_MAX_'//NOMCMP(ICMP)
         NOMPAR(4+6*(ICMP-1)+4)='MIN_'//NOMCMP(ICMP)
         NOMPAR(4+6*(ICMP-1)+5)='MA_MIN_'//NOMCMP(ICMP)
         NOMPAR(4+6*(ICMP-1)+6)='PT_MIN_'//NOMCMP(ICMP)

         MIMA(1+2*(ICMP-1)+1)=VMAX
         MIMA(1+2*(ICMP-1)+2)=VMIN

         CALL JENUNO ( JEXNUM(NOMMAI,NMIN) , KNMIN )
         CALL JENUNO ( JEXNUM(NOMMAI,NMAX) , KNMAX )

         MAMAX(2+2*(ICMP-1)+1)=KNMAX
         MAMAX(2+2*(ICMP-1)+2)=KNMIN
         PTMAX(1+2*(ICMP-1)+1)=PMAX
         PTMAX(1+2*(ICMP-1)+2)=PMIN

         CALL TBEXIP(RESU,NOMPAR(4+6*(ICMP-1)+1),EXIST,K8B)
         IF(.NOT.EXIST)THEN
            CALL TBAJPA(RESU,1,NOMPAR(4+6*(ICMP-1)+1),'R')
            CALL TBAJPA(RESU,1,NOMPAR(4+6*(ICMP-1)+2),'K16')
            CALL TBAJPA(RESU,1,NOMPAR(4+6*(ICMP-1)+3),'I')
            CALL TBAJPA(RESU,1,NOMPAR(4+6*(ICMP-1)+4),'R')
            CALL TBAJPA(RESU,1,NOMPAR(4+6*(ICMP-1)+5),'K16')
            CALL TBAJPA(RESU,1,NOMPAR(4+6*(ICMP-1)+6),'I')
         ENDIF
 30   CONTINUE

      NPARA=6*NBCMP
      PTMAX(1)=NUORD

C --- ON REMPLIT LA TABLE
      NBPARA=4+NPARA
      CALL TBAJLI(RESU,NBPARA,NOMPAR,PTMAX,MIMA,
     &            CBID,MAMAX,0)

      CALL JEDETR('&&PEMAXC_IND.MAILLE')
      CALL JEDETR('&&PEMAXC_CESOUT')
      CALL JEDETR('&&PEMAXC.LIST_CMP')

      CALL JEDEMA()

      END
