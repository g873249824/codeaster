      SUBROUTINE RSUTRG(NOMSD,IORDR,IRANG)
      IMPLICIT NONE
      INTEGER IORDR,IRANG
      CHARACTER*(*) NOMSD
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 20/07/2010   AUTEUR MAHFOUZ D.MAHFOUZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE VABHHTS J.PELLET
C      CORRESPONDANCE NUMERO D'ORDRE UTILISATEUR (IORDR) AVEC LE
C      NUMERO DE RANGEMENT (IRANG).
C ----------------------------------------------------------------------
C IN  : NOMSD  : NOM DE LA STRUCTURE "RESULTAT"
C IN  : IORDR  : NUMERO D'ORDRE UTILISATEUR.
C OUT : IRANG  : NUMERO DE RANGEMENT.
C                = 0 , LE NUMERO D'ORDRE N'EXISTE PAS
C ----------------------------------------------------------------------
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR,JEXR8
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C ---------------- FIN COMMUNS NORMALISES  JEVEUX  --------------------
      CHARACTER*19 NOMD2
      CHARACTER*1 K1BID
      INTEGER NBORDR,JORDR,I,DEBUT,MILIEU,FIN,DIFF,MAXIT
C ----------------------------------------------------------------------

      CALL JEMARQ()
      IRANG = 0
      NOMD2 = NOMSD

C     --- RECUPERATION DU .ORDR ---
      CALL JELIRA(NOMD2//'.ORDR','LONUTI',NBORDR,K1BID)
      IF (NBORDR.EQ.0) GO TO 20
      CALL JEVEUO(NOMD2//'.ORDR','L',JORDR)

C     --- ON REGARDE SI ZI=I POUR EVITER UNE BOUCLE DE CALCUL ---
      IF ((IORDR.GE.1) .AND. (IORDR.LE.NBORDR)) THEN
        IF (ZI(JORDR-1+IORDR).EQ.IORDR) THEN
          IRANG = IORDR
          GO TO 20
        END IF
      END IF

      IF ((IORDR.GE.0) .AND. (IORDR.LE.NBORDR-1)) THEN
        IF (ZI(JORDR-1+IORDR+1).EQ.IORDR) THEN
          IRANG = IORDR + 1
          GO TO 20
        END IF
      END IF


C     --- RECHERCHE DU NUMERO DE RANGEMENT ---
      IF ( NBORDR.EQ.1 ) THEN
        IF ( ZI(JORDR).EQ.IORDR ) THEN
          IRANG = 1
        ENDIF
      ELSE
        DEBUT = 0
        FIN   = NBORDR-1
        MAXIT = 1+LOG(1.D0*NBORDR+1.D0)/LOG(2.D0)
        DO 10 I = 1,MAXIT
          DIFF = (FIN-DEBUT)/2
          MILIEU = DEBUT+DIFF
          IF ( ZI(JORDR+MILIEU).EQ.IORDR ) THEN
            IRANG = MILIEU+1
            GO TO 20
          ELSEIF ( ZI(JORDR+MILIEU).GT.IORDR ) THEN
            FIN = MILIEU-1
          ELSE
            DEBUT = MILIEU+1
          ENDIF
          IF ( DEBUT.GE.FIN ) THEN
            DIFF = (FIN-DEBUT)/2
            MILIEU = DEBUT+DIFF
            IF ( ZI(JORDR+MILIEU).EQ.IORDR ) IRANG = MILIEU+1
            GO TO 20
          ENDIF
   10   CONTINUE
      ENDIF

   20 CONTINUE
      CALL JEDEMA()
      END
