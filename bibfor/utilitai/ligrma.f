      SUBROUTINE LIGRMA(MA,LISTGR)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 18/12/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

      IMPLICIT NONE

      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      CHARACTER*8   MA
      CHARACTER*24  LISTGR
C
C   POUR CHAQUE MAILLE :
C   CREER LA LISTE DES GROUPES DE MAILLES CONTENANT CETTE MAILLE
C
C   IN
C       MA      : MAILLAGE
C
C   OUT
C       LISTGR  : LISTE DES GROUPES DE MAILLES

      INTEGER       IBID,NBMA,IRET,NBGMA,I,IAGMA,N,II,IMA,JLONG,JLIST
      INTEGER       NBMAT,JCPT
      CHARACTER*8   KBID
      CHARACTER*24  LONG,VCPT


      CALL JEMARQ()

      CALL DISMOI('F','NB_MA_MAILLA',MA,'MAILLAGE',NBMA,KBID,IBID)

C     CREATION DU VECTEUR CONTENANT LE NOMBRE DE GROUPES PAR MAILLE
      LONG = '&&LIGRMA.LONG'
      CALL WKVECT(LONG,'V V I',NBMA,JLONG)

C     CREATION DU VECTEUR COMPTEUR
      VCPT = '&&LIGRMA.VCPT'
      CALL WKVECT(VCPT,'V V I',NBMA,JCPT)

      CALL JEEXIN(MA//'.GROUPEMA',IRET)
      NBGMA = 0
      NBMAT = 0
      IF (IRET.GT.0) CALL JELIRA(MA//'.GROUPEMA','NUTIOC',
     &                           NBGMA,KBID)

C     ON PARCOURT LES GROUPES DE MAILLES ET ON REMPLIT L'OBJET LONG
      DO 10,I=1,NBGMA
        CALL JEVEUO(JEXNUM(MA//'.GROUPEMA',I),'L',IAGMA)
        CALL JELIRA(JEXNUM(MA//'.GROUPEMA',I),'LONUTI',N,KBID)
        DO 11, II=1,N
          IMA=ZI(IAGMA-1+II)
          ZI(JLONG-1+IMA)=ZI(JLONG-1+IMA)+1
          NBMAT = NBMAT +1
 11    CONTINUE
 10   CONTINUE

C     CREATION DE LA COLLECTION CONTINUE A NBMA LIGNES DE LONGUEUR
C     TOTALE EGALE A NBMAT
      CALL JECREC(LISTGR,'V V I','NU','CONTIG','VARIABLE',NBMA)
      CALL JEECRA(LISTGR,'LONT',NBMAT,KBID)
      DO 20 IMA=1,NBMA
        CALL JEECRA(JEXNUM(LISTGR,IMA),'LONMAX',ZI(JLONG-1+IMA),KBID)
        CALL JECROC(JEXNUM(LISTGR,IMA))
 20   CONTINUE

C     REMPLISSAGE DE LISTGR AVEC LES NUMEROS DES GROUPES
      DO 30,I=1,NBGMA
        CALL JEVEUO(JEXNUM(MA//'.GROUPEMA',I),'L',IAGMA)
        CALL JELIRA(JEXNUM(MA//'.GROUPEMA',I),'LONUTI',N,KBID)
        DO 31, II=1,N
          IMA=ZI(IAGMA-1+II)
          CALL JEVEUO(JEXNUM(LISTGR,IMA),'E',JLIST)
C         ON AJOUTE LE GROUPE I A LA LISTE DES GROUPES POUR CETTE MAILLE
          ZI(JCPT-1+IMA) = ZI(JCPT-1+IMA) + 1
          ZI(JLIST-1+ZI(JCPT-1+IMA)) = I
 31     CONTINUE
 30   CONTINUE

      CALL JEDETR(LONG)
      CALL JEDETR(VCPT)

      CALL JEDEMA()
      END
