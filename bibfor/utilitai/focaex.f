      SUBROUTINE FOCAEX(NOMFON,PARTIE, SORTIE, BASE )
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*(*) NOMFON, PARTIE,SORTIE
      CHARACTER*1   BASE
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 08/03/2000   AUTEUR CIBHHPD P.DAVID 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C     MEDISIS:  COMBINAISON LINEAIRE DE FONCTIONS
C                            OU DE NAPPE DE MEME VALEUR DE PARAMETRES
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      CHARACTER*24 PROL , VALE
      CHARACTER*8  NOPARA, NORESU, INTERP, PROLGD
      CHARACTER*1  K1BID
C     ----------------------------------------------------------------
C
C     --- DETERMINATION DE LA LISTE DES INSTANTS PRODUIT ---
      CALL JEMARQ()
      NBINST = 0
      VALE(20:24) = '.VALE'
      PROL(20:24) = '.PROL'
      VALE(1:19) = NOMFON
      CALL JELIRA(VALE,'LONUTI',NBINST,K1BID)
      NBINST=NBINST/3
      CALL JEVEUO(VALE,'L',LVAR)
C
C        --- CREATION DE L'OBJET DES INSTANTS ET DES VALEURS ---
      VALE(1:19) = SORTIE
      CALL WKVECT(VALE,BASE//' V R',2*NBINST,LRES)
      DO 10 I = 0, NBINST-1
         ZR(LRES+I) = ZR(LVAR+I)
  10  CONTINUE
      IF (PARTIE.EQ.'REEL') THEN
        DO 21 I=0,NBINST-1
          ZR(LRES+NBINST+I)=ZR(LVAR+NBINST+2*I)
  21    CONTINUE
      ELSE IF (PARTIE.EQ.'IMAG') THEN
        DO 22 I=0,NBINST-1
          ZR(LRES+NBINST+I)=ZR(LVAR+NBINST+2*I+1)
  22    CONTINUE
      ELSE IF (PARTIE.EQ.'MODULE') THEN
        DO 23 I=0,NBINST-1
          ZR(LRES+NBINST+I)=ABS(DCMPLX(ZR(LVAR+NBINST+2*I),
     .                                 ZR(LVAR+NBINST+2*I+1)))
  23    CONTINUE
      ELSE IF (PARTIE.EQ.'PHASE') THEN
        COEF=R8DGRD()
        DO 24 I=0,NBINST-1
          IF (ZR(LVAR+NBINST+2*I  ).EQ.0.AND.
     .        ZR(LVAR+NBINST+2*I+1).EQ.0) THEN
            CALL UTMESS('F',' ','CALCUL DE PHASE IMPOSSIBLE')
          ENDIF
          ZR(LRES+NBINST+I)=ATAN2(ZR(LVAR+NBINST+2*I+1),
     .                            ZR(LVAR+NBINST+2*I))/COEF
  24    CONTINUE
      ENDIF
C
C
C     FIN DU REMPLISSAGE
      VALE(1:19) = SORTIE
C
      CALL FOCOAT ( NOMFON, 1, NOPARA, NORESU, INTERP, PROLGD)
C
C     --- CREATION DU .PROL ---
      PROL(1:19) = SORTIE
      CALL WKVECT(PROL,BASE//' V K8',5,LPROS)
      ZK8(LPROS  ) = 'FONCTION'
      ZK8(LPROS+1) = INTERP
      ZK8(LPROS+2) = NOPARA
      ZK8(LPROS+3) = NORESU
      ZK8(LPROS+4) = PROLGD
C
      CALL JEDEMA()
      END
