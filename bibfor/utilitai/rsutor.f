      SUBROUTINE RSUTOR(NOMSD,CHAMP,NOMSYM,IORDR,IRET)
      IMPLICIT NONE
      INTEGER IORDR,IRET
      CHARACTER*(*) NOMSD,CHAMP,NOMSYM
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 10/10/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE VABHHTS J.PELLET
C      RECUPERATION DU NUMERO D'ORDRE ET DU NOM SYMBOLIQUE
C      CORRESPONDANT A UN NOM DE CHAMP D'UNE STRUCTURE DE DONNEES NOMSD.
C
C ATTENTION : CETTE ROUTINE N'EST PAS ADAPTEE A UNE SD RESULTAT QUI
C             STOCKE PLUSIEURS FOIS LE MEME NOM DE CHAMP DANS
C             PLUSIEURS CASES DE L'OBJET .TACH
C             ON RETOURNERA TOUJOURS LE 1ER CHAMP TROUVE
C REMARQUE :
C   POUR GAGNER DU TEMPS, ON REPART DU DERNIER NUMERO D'ORDRE TROUVE
C   CAR EN GENERAL, ON BOUCLE SUR DES NUMEROS D'ORDRE CROISSANTS
C ----------------------------------------------------------------------
C IN  : NOMSD  : NOM DE LA STRUCTURE "RESULTAT"
C IN  : CHAMP  : NOM DU CHAMP A RECHERCHER
C OUT : NOMSYM : NOM SYMBOLIQUE
C OUT : IORDR  : NUMERO D'ORDRE
C OUT : IRET   :  / 1 : CHAMP A ETE TROUVE
C                 / 0 : CHAMP N'A PAS ETET TROUVE
C ----------------------------------------------------------------------
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR,JEXR8
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C ---------------- FIN COMMUNS NORMALISES  JEVEUX  --------------------
      CHARACTER*19 NOMD2,NOCH19
      CHARACTER*24 NOMCH
      CHARACTER*1 K1BID
      INTEGER J, JORDR,NBORDR,JTACH,I,NBCHAM,KORDAV,IDEBU
      SAVE KORDAV
      DATA KORDAV /0/
C ----------------------------------------------------------------------

      CALL JEMARQ()
      IRET = 0

      NOMD2 = NOMSD
      NOCH19 = CHAMP

      CALL JELIRA(NOMD2//'.ORDR','LONMAX',NBORDR,K1BID)
      CALL JEVEUO(NOMD2//'.ORDR','L',JORDR)
      CALL JELIRA(NOMD2//'.DESC','NOMMAX',NBCHAM,K1BID)
      CALL JEVEUO(NOMD2//'.TACH','L',JTACH)

C     PEUT-ON COMMENCER A KORDAV ?
      IF (KORDAV.GT.0 .AND. KORDAV.LE.NBORDR) THEN
        IDEBU=KORDAV
      ELSE
        IDEBU=1
      ENDIF


C     -- ON CHERCHE A PARTIR DE IDEBU :
      DO 20 I = 1,NBCHAM
        DO 10 J = IDEBU,NBORDR
          NOMCH = ZK24(JTACH-1+J+(I-1)*NBORDR)
          IF (NOMCH(1:19).EQ.NOCH19) GOTO 9998
   10   CONTINUE
   20 CONTINUE


C     -- SI ON N'A PAS TROUVE, ON REPART DE 1 :
      DO 21 I = 1,NBCHAM
        DO 11 J = 1,IDEBU-1
          NOMCH = ZK24(JTACH-1+J+(I-1)*NBORDR)
          IF (NOMCH(1:19).EQ.NOCH19) GOTO 9998
   11   CONTINUE
   21 CONTINUE
      GOTO 9999


 9998 CONTINUE
      IRET = 1
      CALL JENUNO(JEXNUM(NOMD2//'.DESC',I),NOMSYM)
      IORDR   = ZI(JORDR-1+J)
      KORDAV=J

 9999 CONTINUE
      CALL JEDEMA()
      END
