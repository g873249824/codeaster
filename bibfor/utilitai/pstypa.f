      SUBROUTINE PSTYPA ( NBPASE, INPSCO, NOSIMP, NOPASE, TYPEPS )
C
C     PARAMETRES SENSIBLES - TYPE DE SENSIBILITE - ARCHIVAGE
C     *          *           ***                   *
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 17/06/2002   AUTEUR GNICOLAS G.NICOLAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C ----------------------------------------------------------------------
C     ARCHIVE LE TYPE DE SENSIBILITE CORRESPONDANT A UN PARAMETRE
C     DANS LA STRUCTURE INPSCO
C ----------------------------------------------------------------------
C IN  NBPASE  : NOMBRE DE PARAMETRES SENSIBLES
C IN  INPSCO  : STRUCTURE CONTENANT LA LISTE DES NOMS
C               VOIR LA DEFINITION DANS SEGICO
C IN  NOSIMP  : NOM SIMPLE IMPACTE PAR LA DERIVATION
C IN  NOPASE  : NOM DU PARAMETRE DE SENSIBILITE
C IN  TYPEPS  : TYPE DE SENSIBILITE POUR CE PARAMETRE
C
C ----------------------------------------------------------------------
C
      IMPLICIT   NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NBPASE
C
      CHARACTER*(*) NOSIMP, NOPASE, TYPEPS
      CHARACTER*(*) INPSCO
C
C 0.2. ==> COMMUNS
C
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'PSTYPA' )
C
      INTEGER NBPSCO
C
      INTEGER LXLGUT
C
      INTEGER ADPSCO, ADRAUX, NRPASE
      INTEGER IAUX, JAUX
      INTEGER ADMOCL, ADVALE
      INTEGER ADTSEK, ADTSKK, ADTSEI, ADTSII
      INTEGER NBMOCL, NBCONT
C
      CHARACTER*4 SAUX04
      CHARACTER*8 SAUX08
      CHARACTER*24 SAUX24
      CHARACTER*24 BLAN24
      CHARACTER*24 STTSEK, STTSEI
      CHARACTER*24 LIMOCL, LIVALE
C
      LOGICAL LOGAUX
C
C                    123456789012345678901234
      DATA BLAN24 / '                        ' /
C
C====
C 1. ECRITURE/LECTURE AU BON ENDROIT ...
C====
C
C 1.1. ==> CARACTERISTIQUES DE LA STRUCTURE DE SAUVEGARDE
C
      CALL SEGICO ( 2,
     >              SAUX08, IAUX, SAUX24, SAUX24,
     >              INPSCO, NBPSCO, JAUX )
C
      CALL SEGICO ( 3,
     >              SAUX08, IAUX, SAUX24, SAUX24,
     >              INPSCO, ADPSCO, JAUX )
C
C 1.2. ==> REPERAGE DU NUMERO DE PARAMETRE SENSIBLE
C
      DO 12 , IAUX = 1 , NBPASE
C
        ADRAUX = ADPSCO + (NBPSCO+1)*IAUX
        IF ( NOPASE.EQ.ZK24(ADRAUX+1) ) THEN
          NRPASE = IAUX
          GOTO 20
        ENDIF
C
   12 CONTINUE
C
      CALL UTMESS ( 'A', NOMPRO,
     >'CE PARAMETRE DE SENSIBILITE EST INTROUVABLE : '//NOPASE(1:8) )
      CALL UTMESS ( 'F', NOMPRO, 'ERREUR DE PROGRAMMATION' )
C
C====
C 2. ARCHIVAGE
C====
C
   20 CONTINUE
C
C 2.1. ==> LES MOTS-CLES ET VALEURS ASSOCIES A CE PARAMETRE SENSIBLE
C
C               12   345678   9012345
      LIMOCL = '&&'//NOMPRO//'_LIMOCL'
      LIVALE = '&&'//NOMPRO//'_LIVALE'
C
      CALL PSREMC ( NOSIMP, NOPASE, NBMOCL, LIMOCL, LIVALE, IAUX )
C
C 2.2. ==> LE NOM DES STRUCTURES QUI MEMORISENT LE TYPE DE SENSIBILITE
C          POUR CE PARAMETRE ET LA COMMANDE EN COURS
C
C 2.2.1. ==> LA BASE DU NOM DES STRUCTURES
C            ELLE EST CREEE AU PREMIER COUP, ET RECUPEREE ENSUITE
C
      IF ( ZK24(ADRAUX+2).EQ.BLAN24 ) THEN
C
        CALL CODENT ( NRPASE, 'D0', SAUX04 )
        IAUX = LXLGUT(INPSCO)
        SAUX24 = BLAN24
        SAUX24(1:IAUX+4) = INPSCO(1:IAUX)//SAUX04
C                                          1234
        ZK24(ADRAUX+2) = SAUX24
        LOGAUX = .TRUE.
C
      ELSE
C
        SAUX24 = ZK24(ADRAUX+2)
        LOGAUX = .FALSE.
C
      ENDIF
C
C 2.2.2. ==> LES NOMS COMPLETS
C
      IAUX = LXLGUT(SAUX24)
C
      STTSEI = SAUX24
      STTSEI(IAUX+1:IAUX+2) = '.I'
C
      STTSEK = SAUX24
      STTSEK(IAUX+1:IAUX+4) = '.K80'
C
C 2.3. ==> ALLOCATION
C 2.3.1. ==> AU PREMIER COUP, ON CREE LES DEUX STRUCTURES
C
      IF ( LOGAUX ) THEN
C
        NBCONT = 1
        CALL WKVECT ( STTSEI,'V V I',NBCONT,ADTSEI )
        CALL JEECRA ( STTSEI,'LONUTI',NBCONT,SAUX08 )
        ADTSII = ADTSEI
C
        CALL WKVECT ( STTSEK,'V V K80',2*NBMOCL+1,ADTSEK )
        CALL JEECRA ( STTSEK,'LONUTI',2*NBMOCL+1,SAUX08 )
        ADTSKK = ADTSEK - 1
C
C 2.3.2. ==> AUX PASSAGES SUIVANTS, ON ALLONGE LES STRUCTURES
C
      ELSE
C
        CALL JELIRA ( STTSEI, 'LONUTI', NBCONT, SAUX08 )
        NBCONT = NBCONT + 1
        CALL JUVECA ( STTSEI,NBCONT )
        CALL JEECRA ( STTSEI,'LONUTI',NBCONT,SAUX08 )
        CALL JEVEUO ( STTSEI, 'E', ADTSEI )
        ADTSII = ADTSEI + NBCONT - 1
C
        CALL JELIRA ( STTSEK, 'LONUTI', IAUX, SAUX08 )
        IAUX = IAUX + 2*NBMOCL + 1
        CALL JUVECA ( STTSEK,IAUX )
        CALL JEECRA ( STTSEK,'LONUTI',IAUX,SAUX08 )
        CALL JEVEUO ( STTSEK, 'E', ADTSEK )
        ADTSKK = ADTSEK + IAUX - 2*NBMOCL - 1
C
      ENDIF
C
C 2.4. ==> ON REMPLIT LES STRUCTURES POUR CETTE CONTRIBUTUION
C
C 2.4.1. ==> LE NOMBRE DE MOTS-CLES ASSOCIES A CE PARAMETRE
C
      ZI(ADTSII) = NBMOCL
C
C 2.4.2. ==> LE TYPE DE PARAMETRE SENSIBLE
C
      IAUX = LXLGUT(TYPEPS)
      ADTSKK = ADTSKK + 1
      ZK80(ADTSKK)(1:IAUX) = TYPEPS(1:IAUX)
C
C 2.4.3. ==> LES NBMOCL COUPLES (MOT-CLE,VALEUR)
C
      IF ( NBMOCL.GT.0 ) THEN
C
        CALL JEVEUO ( LIMOCL, 'L', ADMOCL )
        CALL JEVEUO ( LIVALE, 'L', ADVALE )
        DO 24 , IAUX = 1 , NBMOCL
          ADTSKK = ADTSKK + 1
          ZK80(ADTSKK) = ZK80(ADMOCL+IAUX-1)
          ADTSKK = ADTSKK + 1
          ZK80(ADTSKK) = ZK80(ADVALE+IAUX-1)
   24   CONTINUE
C
      ENDIF
C
C 2.5. ==> MENAGE
C
      CALL JEDETR ( LIMOCL )
      CALL JEDETR ( LIVALE )
C
      END
