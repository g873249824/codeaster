      SUBROUTINE CHPOND(CHIN,CESOUT,CESPOI)

      IMPLICIT NONE

      CHARACTER*19 CHIN,CESOUT,CESPOI
C    -------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 13/03/2006   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C     OPERATEUR   POST_ELEM
C     TRAITEMENT DU MOT CLE-FACTEUR "INTEGRALE"
C     ROUTINE D'APPEL : PEECAL
C
C     BUT : CALCULE UN CHAMP ELGA PONDERE DU POIDS DES PG 
C          (POIDS*JACOBIEN)
C
C     IN  CHIN      : CHAMP A PONDERER   (CHAM_ELEM   /ELGA)
C     OUT CESOUT    : CHIN + PONDERATION (CHAM_ELEM_S /ELGA)
C     IN/OUT CESPOI : PONDERATION        (CHAM_ELEM_S /ELGA)
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16            ZK16
      CHARACTER*24                    ZK24
      CHARACTER*32                            ZK32
      CHARACTER*80                                    ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32     JEXNOM, JEXNUM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER IBID,IRET,NBCHIN,NBMA,NBPT,NBSP,NBCMP,JOUTV,JOUTL,JOUTD
      INTEGER I,IAD1,IAD2,IAD3,ISP,IMA,ICMP,IPT,JCHSV,JCHSL,JCHSD
      INTEGER JPOIV,JPOID,JPOIL,JPOIC,JCH2,JCH1,IRET1,IRET2
      REAL*8 POIDS
      PARAMETER(NBCHIN=1)
      CHARACTER*8 MODELE,LPAIN(NBCHIN),LPAOUT(1),NOMA,K8B
      CHARACTER*19 CHINS
      CHARACTER*24 LIGREL,CHGEOM,LCHIN(NBCHIN),LCHOUT(1),VEFCH1,VEFCH2
      LOGICAL EXIGEO

      CALL JEMARQ()

      CALL DISMOI('F','NOM_LIGREL',CHIN,'CHAM_ELEM',IBID,LIGREL,IBID)


      CALL DISMOI('F','NOM_MODELE',LIGREL,'LIGREL',IBID,MODELE,IBID)
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE',IBID,NOMA,IRET)
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8B,IRET)


C --- CALCUL DU CHAMP CESPOI
C    (UNIQUEMENT AU PREMIER NUMERO D'ORDRE RENCONTRE)

      CALL JEEXIN(CESPOI//'.CESV',IRET)

      IF(IRET.EQ.0)THEN

        CALL MEGEOM(MODELE,' ',EXIGEO,CHGEOM)
        LCHIN(1)=CHGEOM(1:19)
        LPAIN(1)='PGEOMER'
        LCHOUT(1)='&&PEECAL_CHOUT'
        LPAOUT(1)='PCOORPG'

        CALL CALCUL('S','COOR_ELGA',LIGREL,NBCHIN,LCHIN,LPAIN,1,LCHOUT,
     &               LPAOUT,'G')

C       --- VERIFICATION SUR LES CHAMPS CESPOI ET CHIN :
C       (MEME FAMILLE DE PG & MEME ELEMENT DE REFERENCE)
        VEFCH1='&&PEECAL_CHIN'
        VEFCH2='&&PEECAL_LCHOUT'
        CALL CELFPG(CHIN,VEFCH1,1,IRET1)
        CALL CELFPG(LCHOUT,VEFCH2,1,IRET2)
        CALL JEVEUO(VEFCH1,'L',JCH1)
        CALL JEVEUO(VEFCH2,'L',JCH2)
        DO 5 IMA=1,NBMA
          CALL ASSERT(ZK16(JCH1+IMA-1).EQ.ZK16(JCH2+IMA-1))
 5      CONTINUE
        CALL JEDETR(VEFCH1)
        CALL JEDETR(VEFCH2)

        CALL CELCES(LCHOUT, 'V', CESPOI )
        CALL CESRED(CESPOI,0,IBID,1,'W','V',CESPOI)

      ENDIF

C --- CREATION ET RECUPERATION DES POINTEURS  
    
      CALL JEVEUO(CESPOI//'.CESV','L',JPOIV)
      CALL JEVEUO(CESPOI//'.CESL','L',JPOIL)
      CALL JEVEUO(CESPOI//'.CESD','L',JPOID)
      CALL JEVEUO(CESPOI//'.CESC','L',JPOIC)

      CHINS='&&CHPOND_CHINS' 
      CALL CELCES ( CHIN, 'V', CHINS )
      CALL JEVEUO(CHINS//'.CESV','L',JCHSV)
      CALL JEVEUO(CHINS//'.CESL','L',JCHSL)
      CALL JEVEUO(CHINS//'.CESD','L',JCHSD)

C --- CREATION ET REMPLISSAGE DES CHAMPS OUT

      CALL COPISD('CHAM_ELEM_S','V',CHINS,CESOUT)

      CALL JEVEUO(CESOUT//'.CESV','E',JOUTV)
      CALL JEVEUO(CESOUT//'.CESL','E',JOUTL)
      CALL JEVEUO(CESOUT//'.CESD','E',JOUTD)

      DO 10 IMA=1,NBMA
        NBPT =ZI(JCHSD-1+5+4*(IMA-1)+1)
        NBSP =ZI(JCHSD-1+5+4*(IMA-1)+2)
        NBCMP=ZI(JCHSD-1+5+4*(IMA-1)+3)
        DO 20 IPT=1,NBPT
          CALL CESEXI('C',JPOID,JPOIL,IMA,IPT,1,1,IAD2)
          IF(IAD2.GT.0) THEN
            POIDS=ZR(JPOIV-1+IAD2)
          ENDIF
          DO 30 ISP=1,NBSP
            DO 40 ICMP=1,NBCMP
               CALL CESEXI('C',JCHSD,JCHSL,IMA,IPT,ISP,ICMP,IAD1)
               IF(IAD1.GT.0) THEN
                 CALL CESEXI('C',JOUTD,JOUTL,IMA,IPT,ISP,ICMP,IAD3)
                 ZR(JOUTV-1+IAD3)=ZR(JCHSV-1+IAD1)*POIDS
               ENDIF
 40         CONTINUE
 30       CONTINUE
 20     CONTINUE
 10   CONTINUE

      CALL JEDEMA()

      END
