      SUBROUTINE MAROTA (TETAG,   DROT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C FONCTION: CALCULE LA MATRICE DE ROTATION 'DROT' CORRESPONDANT AU
C           VECTEUR-ROTATION 'TETAG'
C
C     IN  : TETAG     : VECTEUR D'ORDRE 3
C
C     OUT : DROT      : MATRICE ORTHOGONALE D'ORDRE 3
C ------------------------------------------------------------------
      IMPLICIT NONE
      REAL*8 TETAG(3),DROT(3,3),AMAT1(3,3),AMAT2(3,3)
C
C-----------------------------------------------------------------------
      INTEGER I ,J 
      REAL*8 COEF ,COEF1 ,COEF2 ,DDOT ,EPSIL ,R8PREM ,TETA1 
      REAL*8 TETA2 ,UN ,ZERO 
C-----------------------------------------------------------------------
      ZERO  = 0.D0
      EPSIL = R8PREM( )**4
      UN    = 1.D0
      TETA2=DDOT(3,TETAG,1,TETAG,1)
      IF (ABS(TETA2).LT.EPSIL) GO TO 30
      TETA1 = SQRT (TETA2)
      COEF = UN / TETA1
      CALL ANTISY (TETAG,COEF,   AMAT1)
      COEF = UN / TETA2
      DO 12 J=1,3
      DO 11 I=1,3
      AMAT2(I,J) = COEF * TETAG(I) * TETAG(J)
   11 CONTINUE
      AMAT2(J,J) = AMAT2(J,J) - UN
   12 CONTINUE
      COEF1 = SIN(TETA1)
      COEF2 = UN - COS(TETA1)
      DO 22 J=1,3
      DO 21 I=1,3
      DROT(I,J) = COEF1*AMAT1(I,J) + COEF2*AMAT2(I,J)
   21 CONTINUE
      DROT(J,J) = DROT(J,J) + UN
   22 CONTINUE
      GO TO 40
C*** TETAG EST TRES PETIT ET LA MATRICE DROT EST PRATIQUEMENT L'UNITE
   30 CONTINUE
      DO 32 J=1,3
      DO 31 I=1,3
      DROT(I,J) = ZERO
   31 CONTINUE
      DROT(J,J) = UN
   32 CONTINUE
   40 CONTINUE
      GOTO 9999
 9999 CONTINUE
      END
