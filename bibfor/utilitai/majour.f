      SUBROUTINE MAJOUR(NEQ   ,REAROT,INDRO ,CHAINI,CHADEL,
     &                  COEF  ,CHAMAJ)
C      
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 19/12/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT NONE
      LOGICAL       REAROT
      INTEGER       NEQ
      INTEGER       INDRO(*)
      REAL*8        CHAINI(*),CHADEL(*),CHAMAJ(*),COEF      
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME - UTILITAIRE - STATIQUE)
C
C MET A JOUR LES CHAM_NO DES DEPLACEMENTS 
C      
C ----------------------------------------------------------------------
C 
C
C CHAMAJ = CHAINI + COEF*CHADEL.
C   POUR LES TRANSLATIONS ET LES PETITES ROTATIONS, ON APPLIQUE
C   LA FORMULE PRECEDENTE A LA LETTRE.
C   POUR LES GRANDES ROTATIONS, LE VECTEUR-ROTATION DE CHAMAJ
C   EST CELUI DU PRODUIT DE LA ROTATION DEFINIE DANS CHAINI PAR
C   COEF FOIS L'INCREMENT DE ROTATION DEFINI DANS CHADEL.
C
C IN  NEQ    : LONGUEUR DES CHAM_NO
C IN  REAROT : TRUE  S'IL Y A DES DDL DE GRDE ROTATION
C                       FALSE SINON
C IN  INDRO  : VECTEUR DONNANT LE TYPE DES DDL:
C                 0: TRANSLATION OU PETITE ROTATION
C                 1: GRANDE ROTATION
C IN  CHAINI : CHAM_NO DONNE
C IN  CHADEL : CHAM_NO DONNE
C IN  COEF   : REEL DONNE
C OUT CHAMAJ : CHAM_NO MIS A JOUR
C      
C ----------------------------------------------------------------------
C
      INTEGER IRAN(3),I,ICOMP,IC
      REAL*8 TETA(3),DELTET(3),QUATER(4),DELQUA(4)
C      
C ----------------------------------------------------------------------
C
      IF (.NOT.REAROT) THEN
        DO 10 I=1,NEQ
          CHAMAJ(I) = CHAINI(I) + COEF*CHADEL(I)
10      CONTINUE
      ELSE
        ICOMP = 0
        DO 20 I=1,NEQ
          IF (INDRO(I).EQ.0) THEN
            CHAMAJ(I)     = CHAINI(I) + COEF*CHADEL(I)
          ELSE IF (INDRO(I).EQ.1) THEN
            ICOMP         = ICOMP + 1
            IRAN(ICOMP)   = I
            TETA  (ICOMP) = CHAINI(I)
            DELTET(ICOMP) = COEF*CHADEL(I)
            IF (ICOMP.EQ.3) THEN
              ICOMP = 0
              CALL VROQUA(TETA  ,QUATER)
              CALL VROQUA(DELTET,DELQUA)
              CALL PROQUA(DELQUA,QUATER)
              CALL QUAVRO(TETA  ,QUATER)
              DO 15 IC=1,3
                CHAMAJ(IRAN(IC)) = TETA (IC)
15            CONTINUE
            ENDIF
          ELSE
            CALL ASSERT(.FALSE.)
          ENDIF
20      CONTINUE
      ENDIF
      END
