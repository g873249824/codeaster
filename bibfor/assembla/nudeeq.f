      SUBROUTINE NUDEEQ(BASE,NU14,NEQ,GDS,IDDLAG)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ASSEMBLA  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)

C     ARGUMENTS:
C     ----------
      CHARACTER*14 NU14
      CHARACTER*2 BASE
      INTEGER NEQ,GDS,IDDLAG
C ----------------------------------------------------------------------
C     BUT : CREER LES OBJETS .DEEQ ET .DELG DANS UN PROF_CHNO.

C     IN:
C     BASE    : BASE(1:1) : BASE POUR CREER LE NUME_DDL
C                    (SAUF LE PROF_CHNO)
C             : BASE(2:2) : BASE POUR CREER LE PROF_CHNO
C     NU14 : NOM D'UN NUME_DDL
C     NEQ    : NOMBRE D'EQUATIONS (OU DE DDL) DU PROF_CHNO
C     GDS    : NUMERO DE LA GRANDEUR SIMPLE ASSOCIEE AU CHAMP.
C     IDDLAG : ADRESSE DE L'OBJET DSCLAG CREE LOCALEMENT DANS NUEFFE

C     OUT:
C     NU14 EST COMPLETE DE L'OBJET ".NUME.DEEQ" V(I) DIM=2*NEQ
C         (CET OBJET EST DETRUIT S'IL EXISTE DEJA).
C     V((IDDL-1)*2+1)--> SI LE NOEUD SUPPORT DE L'EQUA. IDDL EST PHYS.:
C                           +NUMERO DU NOEUD
C                        SI LE NOEUD SUPPORT DE L'EQUA. IDDL EST UN
C                        LAGRANGE DE BLOCAGE :
C                           +NUMERO DU NOEUD PHYS. BLOQUE
C                        SI LE NOEUD SUPPORT DE L'EQUA. IDDL EST UN
C                        LAGRANGE DE LIAISON :
C                            0
C     V((IDDL-1)*2+2)--> SI LE NOEUD SUPPORT DE L'EQUA. IDDL EST PHYS.:
C                           + NUM. DANS L'ORDRE DU CATAL. DES GRAND.
C                           DE LA CMP CORRESPONDANT A L'EQUATION IDDL.
C                        SI LE NOEUD SUPPORT DE L'EQUA. IDDL EST UN
C                        LAGRANGE DE BLOCAGE :
C                           - NUM. DANS L'ORDRE DU CATAL. DES GRAND.
C                           DE LA CMP CORRESPONDANT AU BLOCAGE.
C                        SI LE NOEUD SUPPORT DE L'EQUA. IDDL EST UN
C                        LAGRANGE DE LIAISON :
C                            0
C     NU14 EST COMPLETE DE L'OBJET ".NUME.DELG" V(I) DIM=NEQ
C         (CET OBJET EST DETRUIT S'IL EXISTE DEJA).
C     V( IDDL ) --> 0  SI LE NOEUD SUPPORT DE L'EQUATION IDDL N'EST PAS
C                         UN NOEUD DE LAGRANGE
C                   -1 SI LE NOEUD SUPPORT DE L'EQUATION IDDL EST UN
C                         "1ER" NOEUD DE LAGRANGE
C                   -2 SI LE NOEUD SUPPORT DE L'EQUATION IDDL EST UN
C                         "2EME" NOEUD DE LAGRANGE
C ----------------------------------------------------------------------

C     FONCTIONS EXTERNES:
C     -------------------
      LOGICAL EXISDG
      INTEGER NBEC

C     VARIABLES LOCALES:
C     ------------------

C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      CHARACTER*32 JEXNUM
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8,K8BID,MA,NONO,NOCMP
      CHARACTER*16 ZK16
      CHARACTER*19 NUMEQA
      CHARACTER*24 ZK24
      CHARACTER*24 VALK(2)
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      CHARACTER*1 K1BID
C ---------------- FIN COMMUNS NORMALISES  JEVEUX  --------------------



      CALL JEMARQ()
      NUMEQA=NU14//'.NUME'
      CALL DISMOI('F','NOM_MAILLA',NUMEQA,'NUME_EQUA',IBID,MA,IED)
      CALL DISMOI('F','NB_NO_MAILLA',MA,'MAILLAGE',NBNM,K8BID,IED)
      CALL DISMOI('F','NB_NL_MAILLA',MA,'MAILLAGE',NBNL,K8BID,IED)
      IF (NBNL.GT.0) CALL JEVEUO(MA//'.TYPL','L',JTYPL)


C     - ALLOCATION DE ".DEEQ":
      CALL JEDETR(NUMEQA//'.DEEQ')
      CALL WKVECT(NUMEQA//'.DEEQ',BASE(2:2)//' V I',2*NEQ,JDEEQ)

C     - ALLOCATION DE ".DELG":
      CALL JEDETR(NUMEQA//'.DELG')
      CALL WKVECT(NUMEQA//'.DELG',BASE(1:1)//' V I',NEQ,JDELG)


C     - ADRESSE DE ".NUEQ":
      CALL JEVEUO(NUMEQA//'.NUEQ','L',JNUEQ)

      CALL JELIRA(JEXNUM('&CATA.GD.NOMCMP',GDS),'LONMAX',NCMPMX,K1BID)
      NEC = NBEC(GDS)
      IF (NCMPMX.EQ.0) CALL U2MESS('F','ASSEMBLA_24')
      IF (NEC.EQ.0) CALL U2MESS('F','ASSEMBLA_25')
      NBLAG = 0


      CALL JELIRA(NUMEQA//'.PRNO','NMAXOC',NBLIGR,K1BID)
      DO 30 I = 1,NBLIGR
        CALL JELIRA(JEXNUM(NUMEQA//'.PRNO',I),'LONMAX',L,K1BID)
        IF (L.GT.0) THEN
          CALL JEVEUO(JEXNUM(NUMEQA//'.PRNO',I),'L',JPRNO)
C---- NBNO : SI I=1 --> NOMBRE DE NOEUDS DU MAILLAGE
C            SI I>1 --> NOMBRE DE NOEUDS SUPPLEMENTAIRES DU LIGREL I
          NBNO = L/ (NEC+2)
          IF ((I.EQ.1) .AND. (NBNO.NE. (NBNM+NBNL))) CALL U2MESS('F','CA
     &LCULEL_2')

          DO 20 J = 1,NBNO
C--- J : SI I=1 --> NUMERO DU NOEUD DU MAILLAGE
C        SI I>1 --> NUMERO DU NOEUD SUPPLEMENTAIRE DU
C                   LIGREL I (CHANGE DE SIGNE).
            IDDL = ZI(JPRNO-1+ (J-1)* (NEC+2)+1) - 1
            IADG = JPRNO - 1 + (J-1)* (NEC+2) + 3
            DO 10 K = 1,NCMPMX
              IF (EXISDG(ZI(IADG),K)) THEN
                IDDL = IDDL + 1
                IEQ = ZI(JNUEQ-1+IDDL)

                IF (I.EQ.1) THEN
                  CALL ASSERT((NBNL.LE.0) .OR. (IEQ.EQ.IDDL))
                  ZI(JDEEQ-1+2* (IEQ-1)+1) = J
                  ZI(JDEEQ-1+2* (IEQ-1)+2) = K
                  ZI(JDELG-1+IEQ) = 0
                ELSE
                  ILAG = NBLAG + J
                  NOB = ZI(IDDLAG+ (ILAG-1)*3)
                  NDDLB = ZI(IDDLAG+ (ILAG-1)*3+1)
                  ZI(JDEEQ-1+2* (IEQ-1)+1) = NOB
                  ZI(JDEEQ-1+2* (IEQ-1)+2) = NDDLB
                  ZI(JDELG-1+IEQ) = -ZI(IDDLAG+ (ILAG-1)*3+2)
                END IF

              END IF
   10       CONTINUE
   20     CONTINUE
          IF (I.GT.1) NBLAG = NBLAG + NBNO
        END IF
   30 CONTINUE


C     -- ON VERIFIE QUE LES DDLS BLOQUES NE SONT PAS BLOQUES
C        PLUSIEURS FOIS (ON NE REGARDE QUE LES 10 1ERES CMPS):
C     -------------------------------------------------------
      CALL WKVECT('&&NUEFFE.LNOBLOQ','V V I',NBNM*10,JLBLQ)
      DO 40,IEQ = 1,NEQ
        NUNO = ZI(JDEEQ-1+2* (IEQ-1)+1)
        NUCMP = ZI(JDEEQ-1+2* (IEQ-1)+2)
        IF ((NUNO.GT.0) .AND. (NUCMP.LT.0) .AND. (NUCMP.GE.-10)) THEN
          NUCMP = -NUCMP
          ZI(JLBLQ-1+ (NUNO-1)*10+NUCMP) = ZI(JLBLQ-1+ (NUNO-1)*10+
     &      NUCMP) + 1
        END IF
   40 CONTINUE

      IER = 0
      DO 60,NUNO = 1,NBNM
        DO 50,NUCMP = 1,10
          IF (ZI(JLBLQ-1+ (NUNO-1)*10+NUCMP).GT.2) THEN
            IER = IER + 1
            CALL JENUNO(JEXNUM(MA//'.NOMNOE',NUNO),NONO)
            CALL JEVEUO(JEXNUM('&CATA.GD.NOMCMP',GDS),'L',JNCMP)
            NOCMP = ZK8(JNCMP-1+NUCMP)
             VALK(1) = NONO
             VALK(2) = NOCMP
             CALL U2MESK('E','ASSEMBLA_26', 2 ,VALK)
          END IF
   50   CONTINUE
   60 CONTINUE
      CALL ASSERT(IER.LE.0)
      CALL JEDETR('&&NUEFFE.LNOBLOQ')


      CALL JEDEMA()
      END
