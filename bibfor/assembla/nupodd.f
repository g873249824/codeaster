      SUBROUTINE NUPODD(NU,BASE,RANG,NBPROC)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*14 NU
      CHARACTER*2  BASE
      INTEGER RANG,NBPROC
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ASSEMBLA  DATE 21/01/2013   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE SELLENET N.SELLENET
C ----------------------------------------------------------------------
C  NUME_DDL - CREATION DES TABLEAUX DE POSSESSION DES DDL
C  --                                  --             --
C ----------------------------------------------------------------------
C
C   CETTE ROUTINE CREE LE VECTEUR .NUML.PDDL QUI PRECISE SI UN
C    DDL LOCAL EST POSSEDE PAR LE PROC COURANT
C   RAPPEL : PAR CONVENTION, QUAND UN DDL EST PARTAGE ENTRE DEUX
C            PROCESSEURS, C'EST LE PROCESSEUR DE PLUS BAS NIVEAU
C            QUI POSSEDERA CE DDL
C
C IN  :
C   NU      K14  NOM DU NUME_DDL
C   BASE    K2   BASE(1:1) : BASE POUR CREER LE NUME_DDL
C                    (SAUF LE PROF_CHNO)
C                BASE(2:2) : BASE POUR CREER LE PROF_CHNO
C
      INTEGER      IBID,IER,NBMA,NBNOMA,JPRTK,JNUMSD
      INTEGER      NLILI,ILI,IGR,NEL,IEL,NUMA,JNEQU,JPDDL,NBNO,INO
      INTEGER      NUNO,IDDL,NDDL,DDL1G,JNUGL,NUMPRO,CURPRO,K1,N1
      INTEGER      DDL1L,ILIB,NEQL,JCONX1,JCONX2,IDPRN1,IDPRN2
      INTEGER      JADLI,JADNE,NEC
C
      CHARACTER*8  NOMA,K8B,MO,PARTIT
      CHARACTER*19 LIGRMO,NOMLIG
      CHARACTER*24 KBID
      CHARACTER*32 JEXNUM,JEXATR
C
      LOGICAL      LDIST,LDGREL
C----------------------------------------------------------------------
      INTEGER ZZPRNO,NUNOEL,L,IGREL,J
      INTEGER ZZNGEL,ZZNELG,ZZLIEL,ZZNSUP,ZZNEMA
C
C---- FONCTION D ACCES AUX ELEMENTS DES CHAMPS PRNO DES S.D. LIGREL
C     REPERTORIEES DANS LE CHAMP LILI DE NUME_DDL ET A LEURS ADRESSES
C     ZZPRNO(ILI,NUNOEL,1) = NUMERO DE L'EQUATION ASSOCIEES AU 1ER DDL
C                            DU NOEUD NUNOEL DANS LA NUMEROTATION LOCALE
C                            AU LIGREL ILI DE .LILI
C     ZZPRNO(ILI,NUNOEL,2) = NOMBRE DE DDL PORTES PAR LE NOEUD NUNOEL
C     ZZPRNO(ILI,NUNOEL,2+1) = 1ER CODE
C     ZZPRNO(ILI,NUNOEL,2+NEC) = NEC IEME CODE
C
C      IZZPRN(ILI,NUNOEL,L) = (IDPRN1-1+ZI(IDPRN2+ILI-1)+
C     &                       (NUNOEL-1)* (NEC+2)+L-1)
      ZZPRNO(ILI,NUNOEL,L)=ZI(IDPRN1-1+ZI(IDPRN2+ILI-1)+
     &                     (NUNOEL-1)*(NEC+2)+L-1)
C
C---- NBRE DE GROUPES D'ELEMENTS (DE LIEL) DU LIGREL ILI
C
      ZZNGEL(ILI)=ZI(JADLI+3*(ILI-1))
C
C---- NBRE D ELEMENTS DU LIEL IGREL DU LIGREL ILI DU REPERTOIRE TEMP.
C     .MATAS.LILI(DIM DU VECTEUR D'ENTIERS .LILI(ILI).LIEL(IGREL) )

      ZZNELG(ILI,IGREL)=ZI(ZI(JADLI+3*(ILI-1)+2)+IGREL)-
     &                  ZI(ZI(JADLI+3*(ILI-1)+2)+IGREL-1)-1
C
C---- FONCTION D ACCES AUX ELEMENTS DES CHAMPS LIEL DES S.D. LIGREL
C     REPERTORIEES DANS LE REPERTOIRE TEMPORAIRE .MATAS.LILI
C     ZZLIEL(ILI,IGREL,J) =
C      SI LA JIEME MAILLE DU LIEL IGREL DU LIGREL ILI EST:
C          -UNE MAILLE DU MAILLAGE : SON NUMERO DANS LE MAILLAGE
C          -UNE MAILLE TARDIVE : -POINTEUR DANS LE CHAMP .NEMA
C
      ZZLIEL(ILI,IGREL,J)=ZI(ZI(JADLI+3*(ILI-1)+1)-1+
     &                    ZI(ZI(JADLI+3*(ILI-1)+2)+IGREL-1)+J-1)
C
C---- NBRE DE NOEUDS DE LA MAILLE TARDIVE IEL ( .NEMA(IEL))
C     DU LIGREL ILI REPERTOIRE .LILI
C     (DIM DU VECTEUR D'ENTIERS .LILI(ILI).NEMA(IEL) )
C
      ZZNSUP(ILI,IEL)=ZI(ZI(JADNE+3*(ILI-1)+2)+IEL)-
     &                ZI(ZI(JADNE+3*(ILI-1)+2)+IEL-1)-1
C
C---- FONCTION D ACCES AUX ELEMENTS DES CHAMPS NEMA DES S.D. LIGREL
C     REPERTORIEES DANS LE REPERTOIRE TEMPO. .MATAS.LILI
C
      ZZNEMA(ILI,IEL,J)=ZI(ZI(JADNE+3*(ILI-1)+1)-1+
     &                  ZI(ZI(JADNE+3*(ILI-1)+2)+IEL-1)+J-1)
C
      CALL JEMARQ()
C
C---- RECHERCHE DU MAILLAGE ET DU NOMBRE DE MAILLES ET DE NOEUDS
      CALL DISMOI('F','NOM_MAILLA',NU,'NUME_DDL',IBID,NOMA,IER)
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8B,IER)
      CALL DISMOI('F','NB_NO_MAILLA',NOMA,'MAILLAGE',NBNOMA,K8B,IER)
C
C---- ON RAMENE EN MEMOIRE LES OBJETS DU .NUME :
C     CALCUL DE NEQG, NLILI
      CALL JEVEUO(NU//'     .ADNE','L',JADNE)
      CALL JEVEUO(NU//'     .ADLI','L',JADLI)
      CALL JEVEUO(NU//'.NUME.PRNO','L',IDPRN1)
      CALL JEVEUO(JEXATR(NU//'.NUME.PRNO','LONCUM'),'L',IDPRN2)
      CALL JELIRA(NU//'.NUME.PRNO','NMAXOC',NLILI,KBID)
      CALL JELIRA(JEXNUM(NU//'.NUME.PRNO',1),'LONMAX',N1,KBID)
C
C     -- CALCUL DU NOMBRE D'ENTIERS CODES :
      NEC=N1/NBNOMA-2
C
C---- RECHERCHE DU TABLEAU PARTITION
      CALL DISMOI('F','NOM_MODELE',NU,'NUME_DDL',IBID,MO,IER)
      CALL DISMOI('F','NOM_LIGREL',MO,'MODELE',IBID,LIGRMO,IER)
      CALL DISMOI('F','PARTITION',LIGRMO,'LIGREL',IBID,PARTIT,IER)
      LDIST=.FALSE.
      LDGREL=.FALSE.
      CALL MPICM0(RANG,NBPROC)
      IF (PARTIT.NE.' ') THEN
        CALL ASSERT(NBPROC.GT.1)
        LDIST=.TRUE.
        CALL JEVEUO(PARTIT//'.PRTK','L',JPRTK)
        LDGREL=ZK24(JPRTK-1+1).EQ.'GROUP_ELEM'
        IF (LDGREL) THEN
          JNUMSD=1
        ELSE
          CALL JEVEUO(PARTIT//'.NUPROC.MAILLE','L',JNUMSD)
        ENDIF
      ENDIF
      CALL ASSERT(LDIST)
C
C---- LECTURE DE LA CONNECTIVITE
      CALL JEVEUO(NOMA//'.CONNEX','L',JCONX1)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',JCONX2)
C
      CALL JEVEUO(NU//'.NUML.NEQU','L',JNEQU)
      NEQL=ZI(JNEQU)
      CALL WKVECT(NU//'.NUML.PDDL',BASE(1:1)//' V I',NEQL,JPDDL)
      CALL JEVEUO(NU//'.NUML.NUGL','L',JNUGL)
      DO 10 IDDL=0,NEQL-1
        ZI(JPDDL+IDDL)=NBPROC+1
   10 CONTINUE
C
C---- CREATION DU TABLEAU DE POSSESSION DES DDL LOCAUX
      DO 100 ILI=2,NLILI
        CALL JENUNO(JEXNUM(NU//'.NUME.LILI',ILI),NOMLIG)
        IF (ILI.EQ.2) CALL ASSERT(NOMLIG.EQ.LIGRMO)
        DO 90 IGR=1,ZZNGEL(ILI)
          NEL=ZZNELG(ILI,IGR)
          DO 80 IEL=1,NEL
            NUMA=ZZLIEL(ILI,IGR,IEL)
            CALL ASSERT(NUMA.NE.0)
C
            IF (NUMA.GT.0) THEN
              IF (LDGREL) THEN
                NUMPRO=MOD(IGR,NBPROC)
              ELSE
                NUMPRO=ZI(JNUMSD-1+NUMA)
              ENDIF
C             -- MAILLE DU MAILLAGE :
              NBNO=ZI(JCONX2+NUMA)-ZI(JCONX2+NUMA-1)
              DO 40 INO=1,NBNO
                NUNO=ZI(JCONX1-1+ZI(JCONX2+NUMA-1)+INO-1)
C
                DDL1G=ZZPRNO(1,NUNO,1)
                NDDL=ZZPRNO(1,NUNO,2)
                DDL1L=ZI(JNUGL+DDL1G-1)
                IF ( DDL1L.EQ.0 ) GOTO 40
                CURPRO=ZI(JPDDL+DDL1L-1)
C
                IF ( NUMPRO.LT.CURPRO ) THEN
                  CURPRO=NUMPRO
                ELSE
                  GOTO 40
                ENDIF
C
                DO 30 IDDL=1,NDDL
                  ZI(JPDDL+DDL1L+IDDL-2)=CURPRO
   30           CONTINUE
   40         CONTINUE
C
            ELSE
              IF (LDGREL) THEN
                NUMPRO=MOD(IGR,NBPROC)
              ELSE
                NUMPRO=0
              ENDIF
C             -- MAILLE TARDIVE :
              NUMA=-NUMA
              NBNO=ZZNSUP(ILI,NUMA)
              DO 70 K1=1,NBNO
                NUNO=ZZNEMA(ILI,NUMA,K1)
                IF (NUNO.LT.0) THEN
                  NUNO=-NUNO
                  ILIB=ILI
                ELSE
                  ILIB=1
                ENDIF
                DDL1G=ZZPRNO(ILIB,NUNO,1)
                NDDL=ZZPRNO(ILIB,NUNO,2)
                DDL1L=ZI(JNUGL+DDL1G-1)
                CURPRO=ZI(JPDDL+DDL1L-1)
C
                IF ( NUMPRO.LT.CURPRO ) THEN
                  CURPRO=NUMPRO
                ELSE
                  GOTO 70
                ENDIF
C
                DO 60 IDDL=1,NDDL
                  DDL1L=ZI(JNUGL+DDL1G+IDDL-2)
                  IF ( DDL1L.EQ.0 ) GOTO 60
                  ZI(JPDDL+DDL1L-1)=CURPRO
   60           CONTINUE
   70         CONTINUE
            ENDIF
   80     CONTINUE
   90   CONTINUE
  100 CONTINUE
C
C---- DETERMINATION DU GRAPH DE COMMUNICATION ET DES RACCORDS
      CALL NUGRCO(NU,BASE)
C
C---- CREATION D'UNE NUMEROTATION POUR PETSC
      CALL NURENU(NU,BASE)
C
      CALL JEDEMA()
C
      END
