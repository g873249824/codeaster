      SUBROUTINE NUMERO(NUPOSS,MODELZ,INFCHZ,SOLVEU,BASE,NU)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ASSEMBLA  DATE 05/05/2004   AUTEUR BOITEAU O.BOITEAU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ----------------------------------------------------------------------
C IN  K14  NUPOSS  : NOM D'UN NUME_DDL CANDIDAT (OU ' ')
C                    SI NUPOSS != ' ', ON  REGARDE SI LE PROF_CHNO
C                    DE NUPOSS EST CONVENABLE.
C                    (POUR EVITER DE CREER SYTEMATIQUEMENT 1 PROF_CHNO)
C IN  K8   MODELE  : NOM DU MODELE
C IN  K19  INFCHA  : NOM DE L'OBJET DE TYPE INFCHA
C IN  K19  SOLVEU  : NOM DE L'OBJET DE TYPE SOLVEUR
C IN  K2   BASE    : BASE(1:1) : BASE POUR CREER LE NUME_DDL
C                    (SAUF LE NUME_EQUA)
C                  : BASE(2:2) : BASE POUR CREER LE NUME_EQUA
C VAR/JXOUT K14 NU : NOM DU NUME_DDL.
C                    SI NUPOSS !=' ', NU PEUT ETRE MODIFIE (NU=NUPOSS)
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C       20/11/03 (OB): AJOUT DES NUME_DDLS LOCAUX POUR SOLVEUR FETI.
C----------------------------------------------------------------------
C RESPONSABLE VABHHTS J.PELLET
C CORPS DU PROGRAMME
      IMPLICIT NONE
      
C DECLARATION PARAMETRES D'APPELS      
      CHARACTER*(*) MODELZ,SOLVEU,INFCHZ
      CHARACTER*(*) NU,NUPOSS
      CHARACTER*2 BASE
      
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
      CHARACTER*32 JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------

C DECLARATION VARIABLES LOCALES
      INTEGER      NCHAR,NBLIG,IRET,JCHAR,JLLIGR,K,JTYPCH,ISLVK,IDIME,
     &             I,ILIMA,NBMA,NBSD,IFM,NIV,ILISMA,IBID,ISOLFS,IREFE,
     &             IFETN,NEQUA,NBPB,NCHARF,L
      CHARACTER*1  K1
      CHARACTER*3  VERIF
      CHARACTER*8  MOLOC,NOMCHA,K8BID,METHOD,NOMSD,MODELE
      CHARACTER*16 PHENO,NOMSOL
      CHARACTER*19 KCHARG,INFCHA,LIGRSD
      CHARACTER*24 LCHARG,LLIGR,NOMLIG,SDFETI,NOMSDA,LISMAI,K24STO,
     &             NOMFE2,KSOLVF

C RECUPERATION ET MAJ DU NIVEAU D'IMPRESSION
      CALL INFNIV(IFM,NIV) 

C-----------------------------------------------------------------------
C CONSTRUCTION D'UN OBJET JEVEUX CONTENANT LA LISTE DES CHARGES ET
C LE NOM DU MODELE DE CALCUL
C-----------------------------------------------------------------------
      CALL JEMARQ()
      INFCHA = INFCHZ
      MODELE = MODELZ
      LCHARG = INFCHA//'.LCHA'
      NCHAR = 0
      CALL JEEXIN(LCHARG,IRET)
      IF (IRET.NE.0) THEN
        CALL JELIRA(LCHARG,'LONMAX',NCHAR,K8BID)
        CALL JEVEUO(LCHARG,'L',JCHAR)
      END IF
      LLIGR = '&&NUMERO.LISTE_LIGREL'
      CALL WKVECT(LLIGR,'V V K24',NCHAR+1,JLLIGR)
      NBLIG = 0
      CALL JEEXIN(MODELE//'.MODELE    .NBNO',IRET)
      IF (IRET.GT.0) THEN
        K24STO = MODELE(1:8)//'.MODELE'      
        ZK24(JLLIGR) = K24STO
        NBLIG = NBLIG + 1
      END IF
      DO 10 K = 1,NCHAR
        NOMCHA = ZK24(JCHAR+K-1)
        CALL JEVEUO(NOMCHA(1:8)//'.TYPE','L',JTYPCH)
        NOMLIG = NOMCHA(1:8)//'.CH'//ZK8(JTYPCH) (1:2)//'.LIGRE.LIEL'
        CALL JEEXIN(NOMLIG,IRET)
        IF (IRET.GT.0) THEN
          ZK24(JLLIGR+NBLIG) = NOMLIG(1:19)
          NBLIG = NBLIG + 1
        END IF
   10 CONTINUE
      CALL JEECRA(LLIGR,'LONUTI',NBLIG,K8BID)

C SOLVEUR FETI ?
      CALL JEVEUO(SOLVEU(1:19)//'.SLVK','L',ISLVK)
      METHOD=ZK24(ISLVK)
        
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
      IF (METHOD(1:4).EQ.'FETI') THEN
      
C STRUCTURE DE DONNEES DE TYPE SD_FETI
        SDFETI=' '
        SDFETI(1:8)=ZK24(ISLVK+5)
        VERIF=ZK24(ISLVK+2)
        
C VERIFICATION COHERENCE SD_FETI AVEC PARAMETRAGE OPERATEUR
        CALL JEVEUO(SDFETI(1:19)//'.REFE','L',IREFE)
        CALL JELIRA(SDFETI(1:19)//'.REFE','LONMAX',NCHARF,K8BID)
        NCHARF=NCHARF-1   
        NBPB=0
        IF (ZK8(IREFE).NE.MODELE) NBPB=NBPB+1
        DO 17 K=1,NCHAR
          NOMCHA=ZK24(JCHAR+K-1)
          DO 15 L=1,NCHARF
            IF (NOMCHA.EQ.ZK8(IREFE+L)) GOTO 17
   15     CONTINUE
          NBPB=NBPB+1
   17   CONTINUE
        IF (VERIF.EQ.'OUI') THEN
          K1='F'
        ELSE
          K1='A'
        ENDIF
        IF (NBPB.NE.0) THEN
          CALL UTDEBM(K1,'NUMERO','INCOHERENCE ENTRE SD_FETI ET ')
          CALL UTIMPI('L','LE PARAMETRAGE DE L''OPERATEUR',0,IBID)
          CALL UTIMPI('L','NOMBRE D''INCOHERENCE(S): ',1,NBPB)
          CALL UTFINM()                   
        ENDIF
C RECHERCHE DU PHENOMENE POUR LES NOUVEAUX LIGRELS DE SOUS-DOMAINE
C CF DISMPH.F
        CALL DISMOI('F','PHENOMENE',MODELE,'MODELE',IBID,PHENO,IRET)
        MOLOC=' '
        IF (PHENO(1:9).EQ.'MECANIQUE') THEN
          MOLOC='DDL_MECA'
        ELSE IF (PHENO(1:9).EQ.'THERMIQUE') THEN
          MOLOC='DDL_THER'
        ELSE IF (PHENO(1:9).EQ.'ACOUSTIQU') THEN        
          MOLOC='DDL_ACOU'
        ELSE
          CALL UTMESS('F','NUMERO',
     &       'PHENOMENE NON PREVU DANS LE MOLOC DE NUMER2 POUR DD')
        ENDIF
        IF (NIV.GE.3) THEN
          WRITE(IFM,*)
          WRITE (IFM,*)'<FETI/NUMERO> PHENOMENE ',PHENO,MOLOC  
          WRITE(IFM,*)
        ENDIF
        
C VERIFICATION DE L'ABSENCE DE MAILLES TARDIVES DANS LES LIGRELS DE
C CHARGE
        DO 20 I=2,NBLIG
          KCHARG=ZK24(JLLIGR+I-1)(1:19)
          CALL JEEXIN(KCHARG//'.LIEL',IRET)
          IF (IRET.GT.0)
     &      CALL UTMESS('F','NUMERO',
     &     'LA CHARGE '//KCHARG(1:8)//' CONTIENT UN LIGREL TARDIF'//
     &     ' POUR L''INSTANT, ILS SONT PROSCRITS AVEC FETI')
   20   CONTINUE
   
C PREPARATION BOUCLE SUR LES SOUS-DOMAINES   
        CALL JEVEUO(SDFETI(1:19)//'.DIME','L',IDIME)
        NBSD=ZI(IDIME)
        NOMSDA=SDFETI(1:19)//'.FETA'
C ADRESSE DANS L'OBJET JEVEUX SOLVEUR.FETS DES NOMS DES OBJETS
C JEVEUX REPRESENTANT LES SOLVEURS LOCAUX               
        CALL JEVEUO(SOLVEU(1:19)//'.FETS','L',ISOLFS)   

C CONSTITUTION OBJET STOCKAGE.FETS
        CALL WKVECT(NU(1:14)//'.FETN',BASE(1:1)//' V K24',NBSD,IFETN)
                        
C BOUCLE SUR LES SOUS-DOMAINES --------------------------------------
        DO 30 I=1,NBSD
        
          CALL JEVEUO(JEXNUM(NOMSDA,I),'L',ILIMA)
          CALL JELIRA(JEXNUM(NOMSDA,I),'LONMAX',NBMA,K8BID)
          
C OBJET TEMPORAIRE CONTENANT LES NOMS DES MAILLES DU SD I
          CALL JENUNO(JEXNUM(NOMSDA,I),NOMSD)
          LISMAI = '&&NUMERO.MODELE.LMA'          
          CALL WKVECT (LISMAI,'V V K8',NBMA,ILISMA)
          
C REMPLISSAGE DE L'OBJET LISMAI
          CALL FETCRL(ZI(ILIMA),NBMA,ZK24(JLLIGR),NBLIG,ZK8(ILISMA))

C MONITORING
          IF (NIV.GE.3) THEN
            WRITE(IFM,*)
            WRITE(IFM,*)'DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD'
            WRITE(IFM,*)'<FETI/NUMERO> NUMERO DE SOUS-DOMAINE: ',I
            WRITE(IFM,*)'<FETI/NUMERO> REMPLISSAGE OBJET JEVEUX ',
     &        LISMAI(1:19)
            WRITE(IFM,*)     
          ENDIF

C CREATION DU LIGREL TEMPORAIRE ASSOCIE AU SOUS-DOMAINE
C LES 16 PREMIERS CHARACTERES SONT OBLIGEATOIRES COMPTE-TENU DES
C PRE-REQUIS DES ROUTINES DE CONSTRUCTION DU NUM_DDL.
          LIGRSD = MODELE(1:8)//'.MODELE.'//NOMSD(1:3)  
          
          CALL EXLIM1(LISMAI,NBMA,MODELE,'V',LIGRSD)
          CALL JEDETR(LISMAI)

C MONITORING
          IF (NIV.GE.3)
     &      WRITE (IFM,*)'<FETI/NUMERO> REMPLISSAGE OBJET JEVEUX ',
     &        LIGRSD(1:19)
          IF (NIV.GE.4) 
     & CALL UTIMSD('MESSAGE',2,.FALSE.,.TRUE.,LIGRSD(1:19),1,'V')

C --------------------------------------------------------------
C CREATION ET REMPLISSAGE DE LA SD NUME_DDL "ESCLAVE" LIEE A
C CHAQUE SOUS-DOMAINE
C --------------------------------------------------------------
C CREATION DU NUME_DDL ASSOCIE AU SOUS-DOMAINE
C ON TRONQUE NU CAR NOMSD EST UN K8 ET NOMFE2 EST BLOQUE A 14
C PAR NUEFFE.F
          NOMFE2 = NU(1:5)//'.'//NOMSD(1:8)
          ZK24(JLLIGR)=LIGRSD
          KSOLVF = ZK24(ISOLFS+I-1)
          CALL NUMER2(' ',NBLIG,ZK24(JLLIGR),MOLOC,KSOLVF,BASE,
     &      NOMFE2,NEQUA)
          CALL DETRSD('LIGREL',LIGRSD)
C REMPLISSAGE OBJET NU.FETN
          ZK24(IFETN+I-1)=NOMFE2(1:19)
          
C MONITORING
          IF (NIV.GE.3) THEN
            WRITE(IFM,*)
            WRITE(IFM,*)'<FETI/NUMERO> NOMBRE D''EQUATIONS ',NEQUA
            WRITE(IFM,*)'<FETI/NUMERO> REMPLISSAGE OBJET JEVEUX ',
     &        NOMFE2(1:14)
            WRITE(IFM,*)            
          ENDIF 
          IF (NIV.GE.4) 
     &      CALL UTIMSD('MESSAGE',2,.FALSE.,.TRUE.,
     &      NOMFE2(1:14),1,' ')
     
C REMISE A JOUR DU LIGREL DE MODELE GLOBALE
          ZK24(JLLIGR) = K24STO
   30   CONTINUE


C FIN DE IF METHOD='FETI'               
      ENDIF
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------

C --------------------------------------------------------------
C CREATION ET REMPLISSAGE DE LA SD NUME_DDL "MAITRE"
C --------------------------------------------------------------
      CALL NUMER2(NUPOSS,NBLIG,ZK24(JLLIGR),' ',SOLVEU,BASE,NU,NEQUA)
      CALL JEDETR(LLIGR)

C MONITORING
          IF (NIV.GE.3) THEN
            WRITE(IFM,*)          
            WRITE(IFM,*)'<FETI/NUMERO> DOMAINE GLOBAL'
            WRITE(IFM,*)'<FETI/NUMERO> NOMBRE D''EQUATIONS ',NEQUA
            WRITE(IFM,*)'<FETI/NUMERO> REMPLISSAGE OBJET JEVEUX ',
     &        NU(1:14)
          ENDIF   
          IF (NIV.GE.4) 
     &      CALL UTIMSD('MESSAGE',2,.FALSE.,.TRUE.,NU(1:14),1,' ')
          IF (NIV.GE.3) THEN 
            WRITE(IFM,*)'DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD'        
            WRITE(IFM,*)
          ENDIF       
      CALL JEDEMA()
      END
