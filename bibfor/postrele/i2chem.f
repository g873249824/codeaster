      SUBROUTINE I2CHEM(NOMAIL,NBPARM)
      IMPLICIT NONE
      INTEGER NBPARM
      CHARACTER*8 NOMAIL
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF POSTRELE  DATE 19/10/2010   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C     OPERATEUR INTE_MAIL_2D, MOT CLE FACTEUR "DEFI_CHEMIN"

C ----------------------------------------------------------------------
C     ----------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM
C     ------------------------------------------------------------------
      INTEGER N1,N2,NBTNOE,IFM,NIV,IER,NBTM,OCC,IM,IG,JGRM1,JGRM2
      INTEGER EXISTE,NUMM,JTYPM,NBM,JGRMA,JMAIL,IN,MI
      INTEGER LIBR1,ALSTOT,JNOE,NIG,NID,KMAIL1,KMAIL2
      INTEGER ASTRCT,JMAIL1,JMAIL2,JMAIL3,APTSTR,NBCNX,KCHM,SGCOUR
      INTEGER KTYPCB,KNOMMA,CHM,DEBCHM,FINCHM,IND,IATYMA
      INTEGER NUMSE,NUMM1,NUMM2,IBID,NUMNO,IRET,IDEB,TROUVE
      INTEGER VALI(7)
      REAL*8 EPSI
      LOGICAL OUVERT
      CHARACTER*1 K1B
      CHARACTER*8 K8B,NOMCRB,TYPM,NOMGR,NOMMA,NOMSE,NOMM1,NOMM2,NOEUD
      CHARACTER*16 TYPCRB,OPERA
      CHARACTER*24 CONEC,TYPE,NOMMAI,NOMNOE
      CHARACTER*24 GRPMAI,NCHMIN,NMAIL1,NMAIL2,NTPCRB,NNOMMA
      CHARACTER*24 VALK(7)
C     ------------------------------------------------------------------
      CALL JEMARQ()

      CALL INFNIV(IFM,NIV)

      CALL GETRES(NOMCRB,TYPCRB,OPERA)
      CALL GETVR8(' ','PRECISION',0,1,1,EPSI,N2)

C----------------------------------------------------------------------

C                     D E F I _ C H E M I N

C----------------------------------------------------------------------
      CONEC = NOMAIL//'.CONNEX         '
      TYPE = NOMAIL//'.TYPMAIL        '
      NOMMAI = NOMAIL//'.NOMMAI         '
      NOMNOE = NOMAIL//'.NOMNOE         '
      GRPMAI = NOMAIL//'.GROUPEMA       '
      CALL DISMOI('F','NB_NO_MAILLA',NOMAIL,'MAILLAGE',NBTNOE,K8B,IER)

      IER = 0
      NBTM = 0
      DO 40,OCC = 1,NBPARM,1
        CALL GETVTX('DEFI_CHEMIN','MAILLE',OCC,1,0,K8B,N1)
        CALL GETVTX('DEFI_CHEMIN','GROUP_MA',OCC,1,0,K8B,N2)
        IF (N1.NE.0) THEN
          N1 = -N1
          CALL WKVECT('&&OP0050.MAILLE','V V K8',N1,JMAIL)
          CALL GETVTX('DEFI_CHEMIN','MAILLE',OCC,1,N1,ZK8(JMAIL),N1)
          DO 10,IM = 1,N1,1
            NOMMA = ZK8(JMAIL+IM-1)
            CALL JEEXIN(JEXNOM(NOMMAI,NOMMA),EXISTE)
            IF (EXISTE.EQ.0) THEN
              IER = IER + 1
              VALK (1) = 'DEFI_CHEMIN'
              VALK (2) = NOMMA
              CALL U2MESG('E', 'INTEMAIL_12',2,VALK,1,OCC,0,0.D0)
            ELSE
              CALL JENONU(JEXNOM(NOMAIL//'.NOMMAI',NOMMA),IBID)
              CALL JEVEUO(TYPE,'L',IATYMA)
              JTYPM = IATYMA - 1 + IBID
              CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(JTYPM)),TYPM)
              IF (TYPM.NE.'SEG2' .AND. TYPM.NE.'SEG3') THEN
                IER = IER + 1
                VALK (1) = 'DEFI_CHEMIN'
                VALK (2) = NOMMA
                CALL U2MESG('E', 'INTEMAIL_15',2,VALK,1,OCC,0,0.D0)
              END IF
            END IF
   10     CONTINUE
          NBTM = NBTM + N1
          CALL JEDETR('&&OP0050.MAILLE')
        END IF
        IF (N2.NE.0) THEN
          N2 = -N2
          CALL WKVECT('&&OP0050.GROUP_MA','V V K8',N2,JGRMA)
          CALL GETVTX('DEFI_CHEMIN','GROUP_MA',OCC,1,N2,ZK8(JGRMA),N2)
          DO 30,IG = 1,N2,1
            NOMGR = ZK8(JGRMA+IG-1)
            CALL JENONU(JEXNOM(GRPMAI,NOMGR),EXISTE)
            IF (EXISTE.EQ.0) THEN
              IER = IER + 1
              VALK (1) = 'DEFI_CHEMIN'
              VALK (2) = NOMGR
              CALL U2MESG('E', 'INTEMAIL_16',2,VALK,1,OCC,0,0.D0)
            ELSE
              CALL JELIRA(JEXNOM(GRPMAI,NOMGR),'LONUTI',NBM,K1B)
              CALL JEVEUO(JEXNOM(GRPMAI,NOMGR),'L',JGRM1)
              DO 20,IM = 1,NBM,1
                CALL JEVEUO(TYPE,'L',IATYMA)
                JTYPM = IATYMA - 1 + ZI(JGRM1+IM-1)
                CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(JTYPM)),TYPM)
                IF (TYPM.NE.'SEG2' .AND. TYPM.NE.'SEG3') THEN
                  IER = IER + 1
                  VALK (1) = 'DEFI_CHEMIN'
                  VALK (2) = NOMGR
                  CALL U2MESG('E', 'INTEMAIL_13',2,VALK,1,OCC,0,0.D0)
                END IF
   20         CONTINUE
              NBTM = NBTM + NBM
            END IF
   30     CONTINUE
          CALL JEDETR('&&OP0050.GROUP_MA')
        END IF
   40 CONTINUE

      IF (IER.GT.0) CALL U2MESS('F', 'INTEMAIL_14')

      CALL WKVECT('&INTLISTOTAL','V V I',NBTM,ALSTOT)
      LIBR1 = 1
      DO 80,OCC = 1,NBPARM,1
        CALL GETVTX('DEFI_CHEMIN','MAILLE',OCC,1,0,K8B,N1)
        CALL GETVTX('DEFI_CHEMIN','GROUP_MA',OCC,1,0,K8B,N2)
        IF (N1.NE.0) THEN
          N1 = -N1
          CALL WKVECT('&&OP0050.MAILLE','V V K8',N1,JMAIL3)
          CALL GETVTX('DEFI_CHEMIN','MAILLE',OCC,1,N1,ZK8(JMAIL3),N2)
          DO 50,IM = 1,N1,1
            CALL JENONU(JEXNOM(NOMMAI,ZK8(JMAIL3+IM-1)),NUMM)
            CALL I2RDLI(NUMM,ZI(ALSTOT),LIBR1)
   50     CONTINUE
          CALL JEDETR('&&OP0050.MAILLE')
        ELSE
          N2 = -N2
          CALL WKVECT('&&OP0050.GROUP_MA','V V K8',N2,JGRMA)
          CALL GETVTX('DEFI_CHEMIN','GROUP_MA',OCC,1,N2,ZK8(JGRMA),N2)
          DO 70,IG = 1,N2,1
            NOMGR = ZK8(JGRMA+IG-1)
            CALL JELIRA(JEXNOM(GRPMAI,NOMGR),'LONUTI',NBM,K1B)
            CALL JEVEUO(JEXNOM(GRPMAI,NOMGR),'L',JGRM2)
            DO 60,IM = 1,NBM,1
              NUMM = ZI(JGRM2+IM-1)
              CALL I2RDLI(NUMM,ZI(ALSTOT),LIBR1)
   60       CONTINUE
   70     CONTINUE
          CALL JEDETR('&&OP0050.GROUP_MA')
        END IF
   80 CONTINUE
      NBTM = LIBR1 - 1

C     --- ON VERIFIE QU'EN 1 NOEUD ON A AU PLUS 2 MAILLES ---

      CALL WKVECT('&&OP0050.VERI_MAIL','V V I',NBTNOE,JNOE)
      DO 90 IM = 1,NBTM
        MI = ZI(ALSTOT+IM-1)
        CALL I2EXTF(MI,1,CONEC(1:15),TYPE(1:16),NIG,NID)
        ZI(JNOE+NIG-1) = ZI(JNOE+NIG-1) + 1
        ZI(JNOE+NID-1) = ZI(JNOE+NID-1) + 1
   90 CONTINUE
      OUVERT = .FALSE.
      DO 100 IN = 1,NBTNOE
        IF (ZI(JNOE+IN-1).EQ.1) OUVERT = .TRUE.
  100 CONTINUE
      DO 110 IN = 1,NBTNOE
        IF (ZI(JNOE+IN-1).GT.2) THEN
          CALL JENUNO(JEXNUM(NOMNOE,IN),NOMSE)
          VALK (1) = NOMSE
          VALI (1) = ZI(JNOE+IN-1)
          CALL U2MESG('F', 'INTEMAIL_17',1,VALK,1,VALI,0,0.D0)
        END IF
  110 CONTINUE
      CALL JEDETR('&&OP0050.VERI_MAIL')

C     --- CHOIX DE L'ABSCISSE CURVILIGNE 0. ---

      NUMNO = 0
      CALL GETVTX(' ','NOEUD_ORIG',1,1,0,NOEUD,N1)
      IF (N1.NE.0) THEN
        CALL GETVTX(' ','NOEUD_ORIG',1,1,1,NOEUD,N1)
        CALL JENONU(JEXNOM(NOMNOE,NOEUD),NUMNO)
      ELSE
        CALL GETVTX(' ','GROUP_NO_ORIG',1,1,0,NOEUD,N1)
        IF (N1.NE.0) THEN
          CALL GETVTX(' ','GROUP_NO_ORIG',1,1,1,NOEUD,N1)
          CALL UTNONO(' ',NOMAIL,'NOEUD',NOEUD,K8B,IRET)
          IF (IRET.EQ.10) THEN
            CALL U2MESK('F','INTEMAIL_31',1,NOEUD)
          ELSE IF (IRET.EQ.1) THEN
            VALK (1) = NOEUD
            VALK (2) = K8B
            CALL U2MESK('A','INTEMAIL_18',2,VALK)
          END IF
          CALL JENONU(JEXNOM(NOMNOE,K8B),NUMNO)
        END IF
      END IF

      IF (NUMNO.NE.0) THEN
        TROUVE = 0
        DO 120 IM = 1,NBTM
          MI = ZI(ALSTOT+IM-1)
          CALL I2EXTF(MI,1,CONEC(1:15),TYPE(1:16),NIG,NID)
          IF (NIG.EQ.NUMNO) THEN
            TROUVE = TROUVE + 1
            IDEB = IM
          END IF
          IF (NID.EQ.NUMNO) THEN
            TROUVE = TROUVE + 1
            IDEB = IM
          END IF
  120   CONTINUE
        IF (OUVERT .AND. TROUVE.NE.1) THEN
          CALL U2MESS('F','INTEMAIL_1')
        END IF
        IF (TROUVE.EQ.0) THEN
          CALL U2MESS('F','INTEMAIL_2')
        END IF
        CALL WKVECT('&&OP0050.VERI_MAIL','V V I',NBTM,JNOE)
        MI = 0
        DO 130 IM = IDEB,NBTM
          MI = MI + 1
          ZI(JNOE+MI-1) = ZI(ALSTOT+IM-1)
  130   CONTINUE
        DO 140 IM = 1,IDEB - 1
          MI = MI + 1
          ZI(JNOE+MI-1) = ZI(ALSTOT+IM-1)
  140   CONTINUE
        DO 150 IM = 1,NBTM
          ZI(ALSTOT+IM-1) = ZI(JNOE+IM-1)
  150   CONTINUE
        CALL JEDETR('&&OP0050.VERI_MAIL')
      END IF

C        /* CALCUL DES COMPOSSANTES CONNEXES (I.E. CHEMINS) */
C        /*       ET REPERAGE DANS LE MAILLAGE              */

      CALL WKVECT('&INTSTRUCT','V V I',2*NBTM,ASTRCT)
      CALL WKVECT('&INTMAIL1','V V I',NBTM,JMAIL1)
      CALL WKVECT('&INTMAIL2','V V I',NBTM,JMAIL2)
      CALL WKVECT('&INTPTSTRUCT','V V I',NBTM+1,APTSTR)
      CALL I2IMAM(CONEC,TYPE,ZI(ALSTOT),NBTM,ZI(ASTRCT),ZI(APTSTR),
     &            NBCNX,ZI(JMAIL1),ZI(JMAIL2))

C         /* CREATION DES CHAMPS DU CONCEPT PRODUIT */

      NCHMIN = NOMCRB//'.CHEMIN'
      NMAIL1 = NOMCRB//'.MAIL1'
      NMAIL2 = NOMCRB//'.MAIL2'
      NTPCRB = NOMCRB//'.TYPCOURBE'
      NNOMMA = NOMCRB//'.NOMMAIL'

      N1 = ZI(APTSTR+ (NBCNX+1)-1) - 1
      CALL JECREC(NCHMIN,'G V I','NU','CONTIG','VARIABLE',NBCNX)
      CALL JEECRA(NCHMIN,'LONT',N1,' ')

      CALL JECREC(NMAIL1,'G V I','NU','CONTIG','VARIABLE',NBCNX)
      CALL JEECRA(NMAIL1,'LONT',N1,' ')

      CALL JECREC(NMAIL2,'G V I','NU','CONTIG','VARIABLE',NBCNX)
      CALL JEECRA(NMAIL2,'LONT',N1,' ')

      CALL WKVECT(NTPCRB,'G V K8',1,KTYPCB)
      ZK8(KTYPCB) = 'LISTMAIL'

      CALL WKVECT(NNOMMA,'G V K8',1,KNOMMA)
      ZK8(KNOMMA) = NOMAIL

      DO 170,CHM = 1,NBCNX,1
        DEBCHM = ZI(APTSTR+CHM-1)
        FINCHM = ZI(APTSTR+CHM) - 1
        N1 = FINCHM - DEBCHM + 1
        CALL JECROC(JEXNUM(NCHMIN,CHM))
        CALL JEECRA(JEXNUM(NCHMIN,CHM),'LONMAX',N1,' ')
        CALL JEVEUO(JEXNUM(NCHMIN,CHM),'E',KCHM)
        CALL JECROC(JEXNUM(NMAIL1,CHM))
        CALL JEECRA(JEXNUM(NMAIL1,CHM),'LONMAX',N1,' ')
        CALL JEVEUO(JEXNUM(NMAIL1,CHM),'E',KMAIL1)
        CALL JECROC(JEXNUM(NMAIL2,CHM))
        CALL JEECRA(JEXNUM(NMAIL2,CHM),'LONMAX',N1,' ')
        CALL JEVEUO(JEXNUM(NMAIL2,CHM),'E',KMAIL2)
        DO 160,SGCOUR = DEBCHM,FINCHM,1
          IND = ZI(ASTRCT+SGCOUR-1)
          IF (IND.NE.0) THEN
            NUMSE = ZI(ALSTOT+IND-1)
            CALL JENUNO(JEXNUM(NOMMAI,NUMSE),NOMSE)
            ZI(KCHM+SGCOUR-DEBCHM) = ZI(ALSTOT+IND-1)
            ZI(KMAIL1+SGCOUR-DEBCHM) = ZI(JMAIL1+IND-1)
            ZI(KMAIL2+SGCOUR-DEBCHM) = ZI(JMAIL2+IND-1)
          ELSE
            ZI(KCHM+SGCOUR-DEBCHM) = 0
            ZI(KMAIL1+SGCOUR-DEBCHM) = 0
            ZI(KMAIL2+SGCOUR-DEBCHM) = 0
          END IF
  160   CONTINUE
  170 CONTINUE
      CALL JEDETR('&INTSTRUCT')
      CALL JEDETR('&INTMAIL1')
      CALL JEDETR('&INTMAIL2')
      CALL JEDETR('&INTPTSTRUCT')
      CALL JEDETR('&INTLISTOTAL')

      IF (NIV.GE.2) THEN
        WRITE (IFM,1000)
        WRITE (IFM,1010) NBCNX
        DO 190,CHM = 1,NBCNX,1
          CALL JELIRA(JEXNUM(NCHMIN,CHM),'LONMAX',NBM,K1B)
          CALL JEVEUO(JEXNUM(NCHMIN,CHM),'L',KCHM)
          CALL JEVEUO(JEXNUM(NMAIL1,CHM),'L',KMAIL1)
          CALL JEVEUO(JEXNUM(NMAIL2,CHM),'L',KMAIL2)
          NUMSE = ZI(KCHM+NBM-1)
          IF (NUMSE.EQ.0) THEN
            WRITE (IFM,1020) CHM
          ELSE
            WRITE (IFM,1030) CHM
          END IF
          WRITE (IFM,1040)
          DO 180,SGCOUR = 1,NBM,1
            NUMSE = ZI(KCHM+SGCOUR-1)
            IF (NUMSE.EQ.0) GO TO 180
            CALL JENUNO(JEXNUM(NOMMAI,NUMSE),NOMSE)
            NUMM1 = ZI(KMAIL1+SGCOUR-1)
            NUMM2 = ZI(KMAIL2+SGCOUR-1)
            IF (NUMM1.EQ.0 .AND. NUMM2.EQ.0) THEN
              WRITE (IFM,1050) NOMSE
            ELSE IF (NUMM1.EQ.0 .AND. NUMM2.NE.0) THEN
              CALL JENUNO(JEXNUM(NOMMAI,NUMM2),NOMM2)
              WRITE (IFM,1052) NOMSE,NOMM2
            ELSE IF (NUMM1.NE.0 .AND. NUMM2.NE.0) THEN
              CALL JENUNO(JEXNUM(NOMMAI,NUMM1),NOMM1)
              CALL JENUNO(JEXNUM(NOMMAI,NUMM2),NOMM2)
              WRITE (IFM,1054) NOMSE,NOMM1,NOMM2
            ELSE IF (NUMM1.NE.0 .AND. NUMM2.EQ.0) THEN
              CALL JENUNO(JEXNUM(NOMMAI,NUMM1),NOMM1)
              WRITE (IFM,1052) NOMSE,NOMM1
            END IF
  180     CONTINUE
  190   CONTINUE
      END IF

 1000 FORMAT (/,1X,'COURBE DEFINIE A PARTIR D''UNE LISTE DE MAILLES')
 1010 FORMAT (3X,'COURBE DEFINIE PAR ',I2,' CHEMIN(S)')
 1020 FORMAT (5X,'CHEMIN NUMERO ',I2,' DE TYPE "ARC OUVERT"')
 1030 FORMAT (5X,'CHEMIN NUMERO ',I2,' DE TYPE "CYCLE"')
C 1040 FORMAT(9X,'MAILLE  ENCADREE PAR MAILLE_1  MAILLE_2')
 1040 FORMAT (9X,'MAILLES DEFINISSANT LE CHEMIN:')
 1050 FORMAT (10X,A8)
 1052 FORMAT (10X,A8,14X,A8)
 1054 FORMAT (10X,A8,14X,A8,2X,A8)

      CALL JEDEMA()
      END
