      SUBROUTINE PRCCM2 ( NOMMAT, NBORDR, PARA, SM )
      IMPLICIT   NONE
      INTEGER             NBORDR
      REAL*8              PARA, SM
      CHARACTER*(*)       NOMMAT
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF POSTRELE  DATE 01/05/2002   AUTEUR CIBHHBC R.FERNANDES 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C     POST_RCCM: CALCUL DU KE, SALT, NADM ET DOMMAGE
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16             ZK16
      CHARACTER*24                      ZK24
      CHARACTER*32                               ZK32
      CHARACTER*80                                        ZK80
      COMMON  /KVARJE/ ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
C     ----- FIN COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER      I, IDKEO, IDKEE, IDSATO, IDSATE, IDNADO, IDNADE,
     +             IDUSAO, IDUSAE, NBCYCL, IDNBC1, IDNBC3, IDSNOM,
     +             IDSNEM, IDSPO, IDSPE
      REAL*8       SNOMAX, SNEMAX, SPO, SPE, KEO, KEE, SALTO, SALTE,
     +             NADMO, NADME
C     ------------------------------------------------------------------
C
C
      CALL JEMARQ()

C --- CREATION D'UN TABLEAU DE TRAVAIL DESTINE A CONTENIR LE
C --- COEFFICIENT DE CONCENTRATION ELASTOPLASTIQUE A L'ORIGINE
C --- DU CHEMIN :
C     ---------
      CALL WKVECT('&&OP0165.KEO','V V R',NBORDR,IDKEO)
C
C --- CREATION D'UN TABLEAU DE TRAVAIL DESTINE A CONTENIR LE
C --- COEFFICIENT DE CONCENTRATION ELASTOPLASTIQUE A L'EXTREMITE
C --- DU CHEMIN :
C     ---------
      CALL WKVECT('&&OP0165.KEE','V V R',NBORDR,IDKEE)
C
C --- CREATION D'UN TABLEAU DE TRAVAIL DESTINE A CONTENIR LA
C --- CONTRAINTE EQUIVALENTE ALTERNEE A L'ORIGINE DU CHEMIN :
C     -----------------------------------------------------
      CALL WKVECT('&&OP0165.SALTO','V V R',NBORDR,IDSATO)
C
C --- CREATION D'UN TABLEAU DE TRAVAIL DESTINE A CONTENIR LA
C --- CONTRAINTE EQUIVALENTE ALTERNEE A L'EXTREMITE DU CHEMIN :
C     -------------------------------------------------------
      CALL WKVECT('&&OP0165.SALTE','V V R',NBORDR,IDSATE)
C
C --- CREATION D'UN TABLEAU DE TRAVAIL DESTINE A CONTENIR LE
C --- NOMBRE DE CYCLES ADMISSIBLE A L'ORIGINE DU CHEMIN :
C     -------------------------------------------------
      CALL WKVECT('&&OP0165.NADMO','V V R',NBORDR,IDNADO)
C
C --- CREATION D'UN TABLEAU DE TRAVAIL DESTINE A CONTENIR LE
C --- NOMBRE DE CYCLES ADMISSIBLE A L'EXTREMITE DU CHEMIN :
C     ---------------------------------------------------
      CALL WKVECT('&&OP0165.NADME','V V R',NBORDR,IDNADE)
C
C --- CREATION D'UN TABLEAU DE TRAVAIL DESTINE A CONTENIR LE
C --- FACTEUR D'USAGE A L'ORIGINE DU CHEMIN :
C     -------------------------------------
      CALL WKVECT('&&OP0165.USAGEO','V V R',NBORDR,IDUSAO)
C
C --- CREATION D'UN TABLEAU DE TRAVAIL DESTINE A CONTENIR LE
C --- FACTEUR D'USAGE A L'EXTREMITE DU CHEMIN :
C     ---------------------------------------
      CALL WKVECT('&&OP0165.USAGEE','V V R',NBORDR,IDUSAE)
C
      CALL JEVEUO ( '&&OP0165.SNOMAX' , 'L', IDSNOM )
      CALL JEVEUO ( '&&OP0165.SNEMAX' , 'L', IDSNEM )
      CALL JEVEUO ( '&&OP0165.SPO'    , 'L', IDSPO  )
      CALL JEVEUO ( '&&OP0165.SPE'    , 'L', IDSPE  )
      CALL JEVEUO ( '&&OP0165.NBCYCL1', 'L', IDNBC1 )
      CALL JEVEUO ( '&&OP0165.NBCYCL3', 'L', IDNBC3 )
C
      DO 10 I = 1 , NBORDR
         SNOMAX = ZR(IDSNOM+I-1)
         SNEMAX = ZR(IDSNEM+I-1)
         SPO    = ZR(IDSPO +I-1)
         SPE    = ZR(IDSPE +I-1)
C
C ---    CALCUL DU COEFFICIENT DE CONCENTRATION ELASTO-PLASTIQUE KE
C ---    AUX ORIGINES ET EXTREMITES DU CHEMIN
C ---    CALCUL DE LA CONTRAINTE EQUIVALENTE ALTERNEE SALT
C ---    AUX ORIGINES ET EXTREMITES DU CHEMIN
C ---    CALCUL DU NOMBRE DE CYCLES ADMISSIBLE NADM EN UTILISANT
C ---    LA COURBE DE WOHLER AUX ORIGINES ET EXTREMITES DU CHEMIN
C        --------------------------------------------------------
         CALL PRCCM3(NOMMAT, PARA, SM, SNOMAX, SPO, KEO, SALTO, NADMO)
         CALL PRCCM3(NOMMAT, PARA, SM, SNEMAX, SPE, KEE, SALTE, NADME)
C
         ZR(IDKEO +I-1) = KEO
         ZR(IDKEE +I-1) = KEE
         ZR(IDSATO+I-1) = SALTO
         ZR(IDSATE+I-1) = SALTE
         ZR(IDNADO+I-1) = NADMO
         ZR(IDNADE+I-1) = NADME
C
C ---    CALCUL DES FACTEURS D'USAGE AUX EXTREMITES DU CHEMIN
C ---    CORRESPONDANT A LA COMBINAISON DES 2 INSTANTS CONSIDERES
C ---    DU TRANSITOIRE :
C        --------------
         NBCYCL = MIN(ZI(IDNBC1+I-1),ZI(IDNBC3+I-1))
C
         ZR(IDUSAO+I-1) = DBLE(NBCYCL) / NADMO
         ZR(IDUSAE+I-1) = DBLE(NBCYCL) / NADME
C
 10   CONTINUE
C
      CALL JEDEMA()
      END
