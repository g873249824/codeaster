      SUBROUTINE XMELET (NOMTE ,TYPMAI,TYPMAE,TYPMAM,TYPMAC,
     &                  NDIM  ,NDDL  ,NNE   ,NNM   ,NNC   ,
     &                  LMALIN,NNES  ,NDDLSE,NSINGE,NSINGM)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 05/10/2010   AUTEUR MAHFOUZ D.MAHFOUZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C    
      IMPLICIT NONE
      CHARACTER*16 NOMTE
      CHARACTER*8  TYPMAI,TYPMAE,TYPMAM,TYPMAC
      INTEGER      NDIM,NDDL,NDDLSE,NNE,NNM,NNC,NNES
      INTEGER      NSINGE,NSINGM
      LOGICAL      LMALIN
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE XFEMGG - CALCUL ELEM.)
C
C RETOURNE QUELQUES INFOS SUR LES ELEMENTS DE CONTACT FORMES ENTRE
C DEUX ELEMENTS  X-FEM
C
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C
C
C IN  NOMTE  : NOM DU TE DE L'ELEMENT DE CONTACT EN JEU
C OUT TYPMAI : NOM DE LA MAILLE ESCLAVE D'ORIGINE
C OUT TYPMAE : NOM DE LA MAILLE ESCLAVE
C OUT TYPMAM : NOM DE LA MAILLE MAITRE
C OUT TYPMAC : NOM DE LA MAILLE DE CONTACT
C OUT NDIM   : DIMENSION DE LA MAILLE DE CONTACT
C OUT NDDL   : NOMBRE TOTAL DE DEGRES DE LIBERTE DE LA MAILLE DE CONTACT
C OUT NNE    : NOMBRE DE NOEUDS DE LA MAILLE ESCLAVE
C OUT NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C OUT NNC    : NOMBRE DE NOEUDS DE LA MAILLE DE CONTACT
C OUT LMALIN : INDIQUE SI LA MAILLE EST LINEAIRE
C OUT NNES   : NOMBRE DE NOEUDS SOMMETS DE LA MAILLE ESCLAVE
C OUT NSINGE : NOMBRE DE FONCTIONS SINGULIERE ESCLAVES
C OUT NSINGM : NOMBRE DE FONCTIONS SINGULIERE MAITRES
C OUT NDDLSE : NOMBRE DE DDLS D'UN NOEUD SOMMET ESCLAVE
C
C ----------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16             ZK16
      CHARACTER*24                      ZK24
      CHARACTER*32                               ZK32
      CHARACTER*80                                        ZK80
      COMMON  /KVARJE/ ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------


      CHARACTER*8 ELREFP, ENRE, ENRM, TYPMA
      CHARACTER*8 LIELRF(10)
      INTEGER NTROU, ILIE, NDIMD, NNOD, NNOSD, IBID , IER
      INTEGER IADZI,IAZK24
      LOGICAL ISMALI

      CALL ELREF1(ELREFP)
      IF (ELREFP .EQ. 'HE8') TYPMAI = 'HEXA8'
      IF (ELREFP .EQ. 'PE6') TYPMAI = 'PENTA6'
      IF (ELREFP .EQ. 'TE4') TYPMAI = 'TETRA4'
      IF (ELREFP .EQ. 'QU4') TYPMAI = 'QUAD4'
      IF (ELREFP .EQ. 'QU8') TYPMAI = 'QUAD8'
      IF (ELREFP .EQ. 'TR3') TYPMAI = 'TRIA3'
      IF (ELREFP .EQ. 'TR6') TYPMAI = 'TRIA6'
     
      CALL TEATTR (NOMTE,'S','XFEM_E',ENRE,IER)
      CALL TEATTR (NOMTE,'S','XFEM_M',ENRM,IER)
C
C --- NOMBRE DE FONCTIONS SINGULIERES    
C
      IF (ENRE.EQ.'H') THEN
        NSINGE = 0
      ELSEIF ((ENRE.EQ.'C').OR.(ENRE.EQ.'T')) THEN
        NSINGE = 1
      ELSE
        CALL U2MESK('F','DVP_4',1,NOMTE)
      ENDIF
C       
      IF (ENRM.EQ.'C') THEN
        NSINGM = 1
      ELSEIF ((ENRM.EQ.'H').OR.(ENRM.EQ.'T')) THEN
        NSINGM = 0
      ELSE
        CALL U2MESK('F','DVP_4',1,NOMTE)
      ENDIF

      CALL ELREF2(NOMTE,10,LIELRF,NTROU)
       
      DO 190 ILIE = 1, NTROU
        CALL ELREF4(LIELRF(ILIE),'NOEU',NDIMD,NNOD,NNOSD,IBID,
     &               IBID,IBID,IBID,IBID)
        IF ( ILIE.EQ.1 ) THEN
         NDIM = NDIMD
         NNE  = NNOD
         NNES = NNOSD
         TYPMAE = LIELRF(ILIE)
         LMALIN = ISMALI(TYPMAE)
        ENDIF

        IF ( ILIE.EQ.2 .AND. NTROU.EQ.3 ) THEN
         NNM  = NNOD
         TYPMAM = LIELRF(ILIE)
        ENDIF

        IF ( ILIE.EQ.2 .AND. NTROU.EQ.2 ) THEN 
         NNM  = NNE
         TYPMAM = TYPMAE
         NNC  = NNOD
         TYPMAC = LIELRF(ILIE)
        ENDIF

        IF ( ILIE.EQ.3 .AND. NTROU.EQ.3 ) THEN
         NNC  = NNOD
         TYPMAC = LIELRF(ILIE)
        ENDIF
 190  CONTINUE

      CALL TECAEL(IADZI,IAZK24)
      TYPMA=ZK24(IAZK24-1+3+ZI(IADZI-1+2)+3)
      IF (TYPMA(1:2).EQ.TYPMA(4:5)) THEN
        TYPMAE = TYPMAM
      ENDIF

      IF (ENRE.EQ.'T') THEN
        NNM  = 0
        TYPMAM = '  '
      ENDIF     
C
C --- NOMBRE DE DDLS D'UN NOEUD SOMMET ESCLAVE    
C
      IF (ENRE.EQ.'T') THEN
        NDDLSE  = 2*NDIM
      ELSE
        NDDLSE  = NDIM *(3+NSINGE)
      ENDIF
C
C --- CALCUL DU NOMBRE TOTAL DE DDL
C
      IF (ENRE.EQ.'T') THEN
        NDDL   = NDDLSE*NNE
      ELSE
        IF (LMALIN) THEN
          NDDL = NDIM * (NNE*(3+NSINGE) + NNM*(2+NSINGM))
        ELSE
          NDDL = NDIM * (NNE + 4*NNM)
        ENDIF
      ENDIF
      
      END
