      SUBROUTINE FSTAT0(NBPT,FN,OFFSET,FNMOYT,FNMOYC,FNRMST,FNRMSC,
     &                  FNMAX,FNMIN,FMAXMO,FMINMO,NBMAXR,NBMINR)
C***********************************************************************
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 12/01/2000   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C CETTE ROUTINE EST EN FAIT L'ANCIENNE FSTAT RENOMMEE FSTAT0

C       MOYENNAGE STATISTIQUE DES FORCES AMV



      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 FN(*),FNMOYT,FNMOYC,FNETYP,FNRMSC,FNRMST,FNMAX,FNMIN
      REAL*8 SMINR,SMAXR,OFFSET,FMAXMO,FMINMO,SFN2,SFN,SFND


C       ARGUMENTS:
C       ----------------------------------------
C       IN:
C            NBPT         NB DE POINTS DU TABLEAU A ANALYSER
C            FN           TABLEAU A ANALYSER
C            OFFSET       VALEUR DE SEUIL DE DETECTION DES VALEURS

C       OUT:
C            FNMOY        VALEUR MOYENNE ( COMPTAGE AU DESSUS DU SEUIL )
C            FNETYP       ECART TYPE ( VALEUR DITE PARFOIS RMS )
C            FNRMS        SQRT DE LA MOYENNE DES CARRES ( RMS POUR DES F
C                         REDRESSEES )
C            FNMAX        VALEUR MAXIMUM ABSOLU DU TABLEAU
C            FNMIN        VALEUR MINIMUM ABSOLU DE LA FONCTION
C            FMAXMO       MOYENNE DES MAXIMAS RELATIFS DE LA FONCTION
C            FMINMO       MOYENNE DES MINIMAS RELATIFS DE LA FONCTION



C       VARIABLES UTILISEES
C       ----------------------------------------
C       SFN SOMME DES FORCES DE CHOC
C       SFN2 SOMME DES CARRES DES FORCES DE CHOC
C       NBCOUNT NOMBRE DE VALEURS DU TABLEAU > SEUIL
C       NBMAXR  NOMBRE DE MAXIMAS RELATIFS RENCONTRES
C       NBMINR  NOMBRE DE MINIMAS RELATIFS RENCONTRES
C       SMAXR   SOMME DES MAXIMAS RELATIFS
C       SMINR   SOMME DES MINIMAS RELATIFS

      SFN = 0.D0
      SFN2 = 0.D0
      FNMAX = -10.D20
      FNMIN = -FNMAX
      NCOUNT = 0
      NBMINR = 0
      NBMAXR = 0
      SMAXR = 0.0D0
      SMINR = 0.0D0

      DO 10 I = 1,NBPT

        IF ((ABS(FN(I))).GT.OFFSET) THEN
          NCOUNT = NCOUNT + 1
          SFN = SFN + FN(I)

C           RECHERCHE DES EXTREMAS ABSOLUS

          IF (FN(I).GT.FNMAX) FNMAX = FN(I)
          IF (FN(I).LT.FNMIN) FNMIN = FN(I)

C           RECHERCHE DES EXTREMAS RELATIFS

          IF ((I.GT.1) .AND. (I.LT.NBPT)) THEN

            IF ((FN(I).GT.FN(I-1)) .AND. (FN(I).GT.FN(I+1))) THEN
              SMAXR = SMAXR + FN(I)
              NBMAXR = NBMAXR + 1
            END IF

            IF ((FN(I).LT.FN(I-1)) .AND. (FN(I).LT.FN(I+1))) THEN
              SMINR = SMINR + FN(I)
              NBMINR = NBMINR + 1
            END IF

          END IF

        END IF

   10 CONTINUE

      IF (NCOUNT.NE.0) THEN
        FNMOYC = SFN/DBLE(NCOUNT)
        FNMOYT = SFN/DBLE(NBPT)

      ELSE
        FNMOYC = 0.D0
        FNMOYT = SFN/DBLE(NBPT)
      END IF

      IF (NBMINR.NE.0) THEN
        FMINMO = SMINR/DBLE(NBMINR)

      ELSE
        FMINMO = 0.D0
      END IF

      IF (NBMAXR.NE.0) THEN
        FMAXMO = SMAXR/DBLE(NBMAXR)

      ELSE
        FMAXMO = 0.D0
      END IF

      DO 20 I = 1,NBPT
        IF (ABS(FN(I)).GT.OFFSET) THEN
          SFN2 = SFN2 + FN(I)**2
        END IF

   20 CONTINUE

      IF (NCOUNT.NE.0) THEN
        FNRMSC = SQRT(SFN2/DBLE(NCOUNT))
        FNRMST = SQRT(SFN2/DBLE(NBPT))

      ELSE
        FNRMSC = 0.D0
        FNRMST = SQRT(SFN2/DBLE(NBPT))

        FNMIN = 0.D0
        FNMAX = 0.D0
      END IF

      GO TO 30

   30 CONTINUE
      END
