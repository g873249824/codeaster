        SUBROUTINE INSISO (LCS, KPIC, R0, KRUP, KAPA, HP, TAU)
        IMPLICIT REAL*8 (A-H,O-Z)
C       ------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 27/03/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C       ------------------------------------------------------------
C   NADAI_BETON  :   LOI ECROUISSAGE ISOTROPE TAU(KAPA) A KAPA DONNE
C
C       IN  :  LCS    = RESISTANCE EN COMPRESSION UNIAXIALE
C              KAPA   = DEFORMATION PLASTIQUE CUMULEE
C              KPIC   = DEFORMATION AU PIC COURBE TAU(KAPA)
C              R0     = SEUIL INITIAL : TAU0/LCS
C       OUT :  INSISO = TAU(KAPA)
C              HP     = MODULE PLASTIQUE (DTAU/DKAPA)
C       ------------------------------------------------------------
        INTEGER    NDT , NDI
        REAL*8     KAPA, HP, TAU, R0, LCS, KPIC
        REAL*8     RAP, UN, KPIC2, KRUP
C       ------------------------------------------------------------
        COMMON /TDIM/   NDT , NDI
C
        IF (KAPA.LT.0.D0)  KAPA=0.D0
C
C - ETAT ELASTIQUE
C
        IF( KAPA .EQ. 0.D0 ) THEN
         TAU = (R0+1.D-20)*LCS
         HP = 1.D+30*(LCS/KPIC)
         GOTO 9999
        ENDIF
C
C - CONSTANTES
C
        UN = 1.D0
        KPIC2 = KPIC*KPIC
        RAP = ((KAPA-KPIC)/KPIC)**2
C
C - COMPORTEMENT PRE-PIC  (ECROUISSAGE POSITIF: HP > 0)
C
        IF( KAPA .GT. 0.D0 .AND. KAPA .LE. KPIC ) THEN
         TAU = LCS*(R0+(UN-R0)*SQRT(UN-RAP+1.D-10))
         HP  = LCS*(UN-R0)*(KPIC-KAPA)/(KPIC2*SQRT(UN-RAP+1.D-7))
C
C - COMPORTEMENT POST-PIC (ECROUISSAGE NEGATIF: HP < 0)
C
        ELSE IF ( KAPA .GT. KPIC .AND. KAPA .LE. KRUP ) THEN
         TAU = LCS/(KPIC-KRUP)*(0.95D0*KAPA + 0.05D0*KPIC - KRUP)
         HP = (0.95D0*LCS)/(KPIC-KRUP)
C
C - COMPORTEMENT POST-PIC (MATERIAU RUINE: HP=0)
C
        ELSE IF (KAPA .GT. KRUP ) THEN
          TAU =  0.05D0*LCS
          HP  =  0.D0
C
        ENDIF
C
 9999   CONTINUE
        END
