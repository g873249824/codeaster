      SUBROUTINE MEDEHY(MODELE,NCHAR,LCHAR,MATE,EXITIM,TIME,CHHYDR,
     &                  CHSECH,CHSREF)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 06/03/2006   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
      INTEGER NCHAR
      CHARACTER*24 CHHYDR,CHSECH,CHSREF
      CHARACTER*(*) MODELE,LCHAR(*),MATE
      REAL*8 TIME
      LOGICAL EXITIM 
C ----------------------------------------------------------------------
C     DETERMINATION DES CHAMPS DE SECHAGE ET D HYDRATATION

C IN  MODELE  : NOM DU MODELE
C IN  NCHAR   : NOMBRE DE CHARGES
C IN  LCHAR   : LISTE DES CHARGES
C IN  MATE    : CHAMP DE MATERIAUX
C IN  EXITIM  : VRAI SI L'INSTANT EST DONNE
C IN  TIME    : INSTANT DE LA DETERMINATION
C OUT CHHYDR  : NOM DU CHAMP D'HYDRATATION
C               S'IL N'Y A PAS D'HYDRATATION ON REND UN CHAMP NUL
C OUT CHSECH  : NOM DU CHAMP DE SECHAGE
C               S'IL N'Y A PAS DE SECHAGE ON REND UN CHAMP NUL
C OUT CHSREF  : NOM DU SECHAGE DE REFERENCE - NUL SI N'EXISTE PAS
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------

      CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------

      INTEGER IBID,IERD,N1,N2
      INTEGER IRET,IRET2,NUMCH1,NUMCH2,JHYDR,JSECH
      CHARACTER*8 HYDR,SECH,K8BID,NOMO,NOMA,REPKH,REPKS
      CHARACTER*8 LPAIN(1),LPAOUT(1)
      CHARACTER*1 BASE

      CHARACTER*16 TYSD,TABTYP(4)
      CHARACTER*19 CH19,HYMOD,HYDTEM
      CHARACTER*24 NOM24,LIGRMO,CHMATE
      CHARACTER*24 LCHIN(1),LCHOUT(1),CHGEOM,OPTION
      COMPLEX*16 CBID
      LOGICAL     EXISRF
      CALL JEMARQ()
      LIGRMO = MODELE(1:8)//'.MODELE'
      BASE = 'V'
      HYDTEM='&&MEDEHY.HYD'
      HYMOD='&&MEDEHY.HYDMOD'

      IF (MODELE(1:1).NE.' ') THEN
        NOMO = MODELE
      ELSE IF (NCHAR.LE.0) THEN
        CALL UTMESS('F','MEDEHY','IL FAUT UN MODELE OU DES CHARGES.')
      ELSE
        CALL DISMOI('F','NOM_MODELE',LCHAR(1),'CHARGE',IBID,NOMO,IERD)
      END IF

      CALL DISMOI('F','NOM_MAILLA',NOMO,'MODELE',IBID,NOMA,IERD)
      CHMATE = MATE//'.CHAMP_MAT'

C     --- DEPENDANCE HYDRATATION SECHAGE ---

      IF (MATE.NE.' ') THEN
        CALL DISMOI('F','ELAS_F_HYDR',CHMATE,'CHAM_MATER',IBID,REPKH,
     &              IERD)
        CALL DISMOI('F','ELAS_F_SECH',CHMATE,'CHAM_MATER',IBID,REPKS,
     &              IERD)
      END IF

C CHAMP HYDRIQUE
C --------------

      IRET = 0
      DO 10 ICHA = 1,NCHAR
        CALL JEEXIN(LCHAR(ICHA) (1:8)//'.CHME.EVOL.HYDR',IRET)
        IF (IRET.NE.0) THEN
          CALL JEVEUO(LCHAR(ICHA) (1:8)//'.CHME.EVOL.HYDR','L',JHYDR)
          HYDR = ZK8(JHYDR)
          GO TO 20
        END IF
   10 CONTINUE
   20 CONTINUE

      IF (REPKH.EQ.'OUI' .AND. IRET.EQ.0) CALL UTMESS('F','MEDEHY',
     &    'LE MATERIAU DEPEND DE L''HYDRATATION ET IL N''Y A PAS '//
     &    'DE CHAMP D''HYDRATATION, MORALITE LE CALCUL EST IMPOSSIBLE.')


      CHHYDR = '&&CHHYDR.CH_TEMP_R         '

      IF (IRET.GT.0) THEN

C ----- SI LE CHAMP CHHYDR EXISTE DEJA , ON LE DETRUIT:

        CALL DETRSD('CHAMP_GD',CHHYDR)

        CALL GETTCO(HYDR,TYSD)
        IF (TYSD(1:9).EQ.'EVOL_THER') THEN
          CALL DISMOI('F','NB_CHAMP_UTI',HYDR,'RESULTAT',NBCHAM,K8BID,
     &                IERD)
          IF (NBCHAM.GT.0) THEN
            IF ( .NOT.EXITIM) THEN
              CALL UTMESS('I',' METEMP ',
     &                    'L''INSTANT DU CALCUL EST PRIS '//
     &                    ' ARBITRAIREMENT A 0.0 ')
              TIME2 = 0.0D0
              IF (NBCHAM.GT.1) THEN
                CALL UTMESS('F',' MEDEHY ',
     &                      ' ON N''ACCEPTE UN INSTANT ARBITRAIRE'//
     &                      ' QUE SI LE CONCEPT TEMPERATURE N''A'//
     &                      ' QU''1 CHAMP.')
              END IF
            ELSE
              TIME2 = TIME
            END IF

C --------- RECUPERATION DU CHAMP HYDRIQUE DANS CHHYDR

            CALL RSINCH(HYDR,'HYDR_ELNO_ELGA','INST',TIME2,HYDTEM,
     &                 'CONSTANT','CONSTANT',1,BASE,ICORET)
            IF (ICORET.GE.10) THEN
              CALL UTDEBM('F','MEDEHY_01','INTERPOLATION HYDRATATION:')
              CALL UTIMPK('L','EVOL_THER:',1,HYDR)
              CALL UTIMPR('S','INSTANT:',1,TIME2)
              CALL UTIMPI('L','ICORET:',1,ICORET)
              CALL UTFINM()
            END IF
            CALL ALCHML(LIGRMO,'FULL_MECA','PHYDRMR','V',HYMOD,IRET,' ')
            CALL CHPCHD(HYDTEM,'ELGA',HYMOD,'NON',BASE,CHHYDR)
            CALL DETRSD('CHAMP',HYDTEM)
            CALL DETRSD('CHAMP',HYMOD)
          ELSE
            CALL UTMESS('F','MEDEHY_02',' LE CONCEPT EVOL_THER : '//
     &                  CHHYDR//' NE CONTIENT AUCUN CHAMP D HYDRATATION'
     &                  )
          END IF
        ELSE
          CALL UTMESS('F','MEDEHY_03','ERREUR DE TYPE SUR LA CHARGE '//
     &                'HYDRIQUE '//HYDR)
        END IF
      ELSE

C IL N'Y A PAS DE CHARGES HYDRIQUES
C CREATION D'UN CHAMELEM CONSTANTE AFFECTEE A 0.D0
C LE NOM DU CHAMP HYDRIQUE EST BIDONIFIE

        CHHYDR(19:22) = '.BID'
        OPTION = 'TOU_INI_ELGA'
        CHGEOM = NOMA//'.COORDO'
        LPAIN(1) = 'PGEOMER'
        LCHIN(1) = CHGEOM
        LPAOUT(1) = 'PHYDR_R'
        LCHOUT(1) = CHHYDR
        CALL CALCUL('S',OPTION,LIGRMO,1,LCHIN,LPAIN,1,LCHOUT,LPAOUT,'V')
      END IF

C CHAMP SECHAGE
C -------------

      IRET2 = 0
      DO 30 ICHA = 1,NCHAR
        CALL JEEXIN(LCHAR(ICHA) (1:8)//'.CHME.EVOL.SECH',IRET2)
        IF (IRET2.NE.0) THEN
          CALL JEVEUO(LCHAR(ICHA) (1:8)//'.CHME.EVOL.SECH','L',JSECH)
          SECH = ZK8(JSECH)
          GO TO 40
        END IF
   30 CONTINUE
   40 CONTINUE

      IF (REPKS.EQ.'OUI' .AND. IRET2.EQ.0) CALL UTMESS('F','MEDEHY',
     &    'LE MATERIAU DEPEND DU SECHAGE ET IL N''Y A PAS '//
     &    'DE CHAMP SECHAGE, MORALITE LE CALCUL EST IMPOSSIBLE.')


      CHSECH = '&&CHSECH.CH_TEMP_R         '
C    LECTURE DU SECHAGE DE REFERENCE      
      CALL MESREF(MATE,NOMA,EXISRF,CHSREF)
       IF (REPKS.EQ.'OUI' .AND.( .NOT.EXISRF)) CALL UTMESS('F','MEDEHY',
     &    'VOUS UTILISEZ UN CHAMP DE SECHAGE SANS AVOIR DEFINI '//
     &    'DE SECHAGE DE REFERENCE.')

      IF (IRET2.GT.0) THEN

C ----- SI LE CHAMP CHSECH EXISTE DEJA , ON LE DETRUIT:

        CALL DETRSD('CHAMP_GD',CHSECH)

        CALL GETTCO(SECH,TYSD)
        IF (TYSD(1:9).EQ.'EVOL_THER') THEN
          CALL DISMOI('F','NB_CHAMP_UTI',SECH,'RESULTAT',NBCHAM,K8BID,
     &                IERD)
          IF (NBCHAM.GT.0) THEN
            IF (.NOT. (EXITIM)) THEN
              CALL UTMESS('I',' METEMP ',
     &                    'L''INSTANT DU CALCUL EST PRIS '//
     &                    ' ARBITRAIREMENT A 0.0 ')
              TIME2 = 0.0D0
              IF (NBCHAM.GT.1) THEN
                CALL UTMESS('F',' MEDEHY ',
     &                      ' ON N''ACCEPTE UN INSTANT ARBITRAIRE'//
     &                      ' QUE SI LE CONCEPT TEMPERATURE N''A'//
     &                      ' QU''1 CHAMP.')
              END IF
            ELSE
              TIME2 = TIME
            END IF

C --------- RECUPERATION DU CHAMP SECHAGE DANS SECH

            CALL RSINCH(SECH,'TEMP','INST',TIME2,CHSECH,'CONSTANT',
     &                  'CONSTANT',1,BASE,ICORET)
            IF (ICORET.GE.10) THEN
              CALL UTDEBM('F','MEDEHY_04','INTERPOLATION SECHAGE:')
              CALL UTIMPK('L','EVOL_THER:',1,SECH)
              CALL UTIMPR('S','INSTANT:',1,TIME2)
              CALL UTIMPI('L','ICORET:',1,ICORET)
              CALL UTFINM()
            END IF
          ELSE
            CALL UTMESS('F','MEDEHY_05',' LE CONCEPT EVOL_THER : '//
     &                  SECH//' NE CONTIENT AUCUN CHAMP SECHAGE')
          END IF

         ELSE IF ((TYSD(1:5).EQ.'CHAM_') .OR.
     +            (TYSD(1:6).EQ.'CARTE_')) THEN
C           ----------------------------------------------
            TABTYP(1)='CARTE#TEMP_F'
            TABTYP(2)='CARTE#TEMP_R'
            TABTYP(3)='NOEU#TEMP_R'
            TABTYP(4)='ELXX#TEMP_R'
            CALL CHPVE2('F',SECH,4,TABTYP,IRET)
            CH19 = SECH(1:8)
            CALL COPISD('CHAMP_GD','V',CH19,CHSECH(1:19))
            CALL JEDETR(CHSECH(1:19)//'.TITR')

        ELSE
          CALL UTMESS('F','MEDEHY_06','ERREUR DE TYPE SUR LA CHARGE '//
     &                'SECHAGE '//SECH)
        END IF
      ELSE

C IL N'Y A PAS DE CHARGES SECHAGES
C CREATION D'UNE CARTE CONSTANTE AFFECTEE A 0.D0
C LE NOM DU CHAMP DE SECHAGE EST BIDONIFIE

        CHSECH(19:22) = '.BID'
        CALL MECACT('V',CHSECH,'MODELE',LIGRMO,'TEMP_R',1,'TEMP',IBID,
     &              0.0D0,CBID,K8BID)
      END IF
   50 CONTINUE
C FIN ------------------------------------------------------------------
      CALL JEDEMA()
      END
