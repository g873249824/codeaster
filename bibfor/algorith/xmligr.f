      SUBROUTINE XMLIGR(NOMA  ,NOMO  ,RESOCO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 27/06/2011   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT     NONE
      CHARACTER*8  NOMA,NOMO
      CHARACTER*24 RESOCO
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE XFEM - CREATION OBJETS - LIGREL)
C
C CREATION DU LIGREL POUR LES ELEMENTS TARDIFS DE CONTACT
C
C ----------------------------------------------------------------------
C
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  NOMO   : NOM DU MODELE
C IN  RESOCO : SD POUR LA RESOLUTION DE CONTACT
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM,JEXATR
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      INTEGER NBTYP
      PARAMETER (NBTYP=30)
      INTEGER ICO,JCO,NBGREL,IPC,NBPC,K,INO,IBID
      INTEGER JNOMA,JTYMAI,JMAIL,IACNX1,ILCNX1
      INTEGER NUMMAM,NUMMAE,JNBNO,LONG,JAD,ITYTE,JTYNMA
      INTEGER NNDEL,NUMTYP,COMPT(NBTYP),JTABF,CFMMVD,ZTABF
      INTEGER      JNOSDC,IFM,NIV
      INTEGER IMOD,IATT(2),IMAIL(2),NNO(2),NDIM
      CHARACTER*24 TABFIN,NOSDCO
      CHARACTER*19 LIGRXF
      CHARACTER*16 NTYELE,NTYELM,NOMTE,NOMTM,NOMTE2(NBTYP),MAIL3(2,4)
      CHARACTER*8  K8BID
      CHARACTER*4  MODE(3)
      CHARACTER*3  MAIL2(2,4)
      CHARACTER*2  MAIL(2,4)
      CHARACTER*1  ATTR(7)
C
      DATA (MODE(K),K=1,3) /'MECP','MEDP','ME3D'/
      DATA (ATTR(K),K=1,7) /'H','C','T','2','3','4','H'/
      DATA (MAIL(1,K),K=1,4) /'T3','Q4','T6','Q8'/
      DATA (MAIL(2,K),K=1,4) /'T4','P6','H8',' '/
      DATA (MAIL2(1,K),K=1,4) /'TR3','QU4','TR6','QU8'/
      DATA (MAIL2(2,K),K=1,4) /'TE4','PE6','HE8',' '/
      DATA (MAIL3(1,K),K=1,4) /'TRIA3','QUAD4','TRIA6','QUAD8'/
      DATA (MAIL3(2,K),K=1,4) /'TETRA4','PENTA6','HEXA8',' '/

C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> CREATION DU LIGREL DES'//
     &        ' ELEMENTS DE CONTACT'
      ENDIF
C
C --- ACCES OBJETS
C
      TABFIN = RESOCO(1:14)//'.TABFIN'
      NOSDCO = RESOCO(1:14)//'.NOSDCO'
      CALL JEVEUO(TABFIN,'E',JTABF )
      CALL JEVEUO(NOSDCO,'L',JNOSDC)
      ZTABF  = CFMMVD('ZTABF')
      CALL JEVEUO(NOMA//'.TYPMAIL','L',JTYMAI)
      CALL JEVEUO(NOMO//'.MAILLE','L',JMAIL)
C
C --- INITIALISATIONS
C
      CALL DISMOI('F','DIM_GEOM',NOMA,'MAILLAGE',NDIM,K8BID,IBID)
      DO 10,K = 1,NBTYP
        COMPT(K) = 0
   10 CONTINUE
      NBPC   = NINT(ZR(JTABF-1+1))
      LIGRXF = ZK24(JNOSDC+3-1)(1:19)
C
C --- DESTRUCTION DU LIGREL S'IL EXISTE
C
      CALL DETRSD('LIGREL',LIGRXF)
C
C --- CREATION DE .NOMA
C
      CALL WKVECT(LIGRXF//'.LGRF','V V K8',2,JNOMA)
      ZK8(JNOMA-1+1) = NOMA
      CALL JEVEUO(NOMA//'.CONNEX','L',IACNX1)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',ILCNX1)
C
C --- ON COMPTE LE NOMBRE DE NOEUDS A STOCKER AU TOTAL
C
      LONG = NBPC
      DO 20,IPC = 1,NBPC
        NUMMAE = NINT(ZR(JTABF+ZTABF*(IPC-1)+1))
        NUMMAM = NINT(ZR(JTABF+ZTABF*(IPC-1)+2))
        CALL XMELEL(NDIM,JMAIL,JTYMAI,NUMMAE,NUMMAM,
     &              IMOD,IATT,IMAIL,NNO)
        LONG = LONG+NNO(1)+NNO(2)
   20 CONTINUE
C
C --- PAS DE NOEUDS TARDIFS
C
      CALL WKVECT(LIGRXF//'.NBNO','V V I',1,JNBNO)
      ZI(JNBNO-1+1) = 0
C
C --- VECTEUR DE TYPES DE MAILLE DU LIGREL
C
      CALL WKVECT('&&XMLIGR.TYPNEMA','V V I',NBPC,JTYNMA)
C
C --- CREATION DE L'OBJET .NEMA
C
      CALL JECREC(LIGRXF//'.NEMA','V V I','NU','CONTIG','VARIABLE',NBPC)
      CALL JEECRA(LIGRXF//'.NEMA','LONT',LONG,K8BID)
      NBGREL = 0
      DO 50,IPC = 1,NBPC
        NUMMAE = NINT(ZR(JTABF+ZTABF*(IPC-1)+1))
        NUMMAM = NINT(ZR(JTABF+ZTABF*(IPC-1)+2))
        CALL XMELEL(NDIM,JMAIL,JTYMAI,NUMMAE,NUMMAM,
     &              IMOD,IATT,IMAIL,NNO)
C
C ----- CREATION DE L'ELEMENT DE CONTACT DANS LE LIGREL
C
        CALL JECROC(JEXNUM(LIGRXF//'.NEMA',IPC))
        NNDEL = NNO(1)+NNO(2)
        CALL JEECRA(JEXNUM(LIGRXF//'.NEMA',IPC),'LONMAX',NNDEL+1,K8BID)
        CALL JEVEUO(JEXNUM(LIGRXF//'.NEMA',IPC),'E',JAD)
        IF (IATT(1).NE.3) THEN
          NOMTM = MAIL2(NDIM-1,IMAIL(1))//MAIL2(NDIM-1,IMAIL(2))
        ELSE
          NOMTM = MAIL3(NDIM-1,IMAIL(1))
        ENDIF
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM',NOMTM),NUMTYP)
        ZI(JAD-1+NNDEL+1) = NUMTYP
C
C ----- RECOPIE DES NUMEROS DE NOEUDS DE LA MAILLE ESCLAVE
C
        DO 30,INO = 1,NNO(1)
          ZI(JAD-1+INO) = ZI(IACNX1+ZI(ILCNX1-1+NUMMAE)-2+INO)
   30   CONTINUE
C
C ----- RECOPIE DES NUMEROS DE NOEUDS DE LA MAILLE MAITRE
C
        DO 40,INO = 1,NNO(2)
          ZI(JAD-1+NNO(1)+INO) = ZI(IACNX1+ZI(ILCNX1-1+NUMMAM)-2+INO)
   40   CONTINUE
C
C --- TYPE D'ÉLÉMENT TARDIF
C
        NOMTE=MODE(IMOD)//MAIL(NDIM-1,IMAIL(1))//ATTR(IATT(1))
        IF (IATT(1).NE.3) THEN
          NOMTE=NOMTE(1:7)//MAIL(NDIM-1,IMAIL(2))//ATTR(IATT(2))//'_XH'
        ELSE
          NOMTE=NOMTE(1:7)//'_XH'
        ENDIF
        DO 60 K = 1,NBGREL
          IF (NOMTE.EQ.NOMTE2(K)) THEN
            COMPT(K) = COMPT(K)+1
            ZI(JTYNMA-1+IPC)=K
            GOTO 50
          ENDIF
   60   CONTINUE
        NBGREL = NBGREL+1
        NOMTE2(NBGREL) = NOMTE
        COMPT(NBGREL) = 1
        ZI(JTYNMA-1+IPC)=NBGREL
   50 CONTINUE
      CALL ASSERT(NBGREL.NE.0)
C
C --- CREATION DE L'OBJET .LIEL
C
      CALL JECREC(LIGRXF//'.LIEL','V V I','NU','CONTIG','VARIABLE',
     &            NBGREL)

      LONG = NBGREL + NBPC
      CALL JEECRA(LIGRXF//'.LIEL','LONT',LONG,K8BID)
      ICO = 0
      DO 90,K = 1,NBGREL
        ICO = ICO + 1
        CALL JECROC(JEXNUM(LIGRXF//'.LIEL',ICO))
        CALL JEECRA(JEXNUM(LIGRXF//'.LIEL',ICO),'LONMAX',COMPT(K)+1,
     &              K8BID)
        CALL JEVEUO(JEXNUM(LIGRXF//'.LIEL',ICO),'E',JAD)

        CALL JENONU(JEXNOM('&CATA.TE.NOMTE',NOMTE2(K)),ITYTE)
        ZI(JAD-1+COMPT(K)+1) = ITYTE

        JCO = 0
        DO 80,IPC = 1,NBPC
          IF (ZI(JTYNMA-1+IPC).EQ.K) THEN
            JCO = JCO + 1
            ZI(JAD-1+JCO) = -IPC
          ENDIF
   80   CONTINUE
   90 CONTINUE
C
C --- IMPRESSIONS
C
      IF (NIV.GE.2) THEN
        CALL MMIMP2(IFM,NOMA,LIGRXF,JTABF)
      ENDIF
C
C --- INITIALISATION DU LIGREL
C
      CALL JEECRA(LIGRXF//'.LGRF','DOCU',IBID,'MECA')
      CALL INITEL(LIGRXF)
C
C --- MENAGE
C
      CALL JEDETR('&&XMLIGR.TYPNEMA')
C
      CALL JEDEMA()
      END
