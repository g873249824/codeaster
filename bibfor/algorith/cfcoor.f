      SUBROUTINE CFCOOR(NOMA  ,NEWGEO,NUMMAM,KSI1  ,KSI2  ,
     &                  COORDP)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 01/04/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8  NOMA
      CHARACTER*24 NEWGEO
      INTEGER      NUMMAM
      REAL*8       KSI1
      REAL*8       KSI2
      REAL*8       COORDP(3)
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE DISCRETE - APPARIEMENT)
C
C CALCUL DES COORDONNEES DE LA PROJECTION DU NOEUD ESCLAVE
C SUR LA MAILLE MAITRE 
C
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : COORDONNES DE TOUS LES NOEUDS
C IN  NUMMAM : NUMERO ABSOLU DE LA MAILLE MAITRE DANS LE MAILLAGE
C IN  KSI1   : COORDONNEE PARAMETRIQUE KSI DU PROJETE
C IN  KSI2   : COORDONNEE PARAMETRIQUE ETA DU PROJETE
C OUT COORDP : COORDONNEES DE LA PROJECTION DU NOEUD ESCLAVE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      JCOOR,JDES
      INTEGER      NNO,INO,NO(9),IDIM,NDIM
      REAL*8       COORMA(27)
      CHARACTER*8  ALIAS,FFORME
C
C-----------------------------------------------------------------------
C
      CALL JEMARQ()
C
      CALL JEVEUO(NEWGEO(1:19)//'.VALE','L',JCOOR)
      CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',NUMMAM),'L',JDES)
C
C --- INITIALISATIONS
C
      DO 10 IDIM = 1,3
        COORDP(IDIM) = 0.D0
   10 CONTINUE
      FFORME = 'STANDARD'
C
C --- INFOS SUR LA MAILLE MAITRE
C
      CALL MMELTY(NOMA  ,NUMMAM,ALIAS,NNO  ,NDIM  )
      if (NNO.EQ.9) THEN
        NNO = 8
        ALIAS = 'QU8'
      endif      
C
C --- NUMEROS ABSOLUS DES NOEUDS DE LA MAILLE MAITRE
C
      DO 23 INO = 1,NNO
        NO(INO) = ZI(JDES+INO-1)
   23 CONTINUE
C
C --- COORDONNEES DES NOEUDS DE LA MAILLE MAITRE
C
      DO 20 INO = 1,NNO
        COORMA(3*(INO-1)+1) = ZR(JCOOR+3*(NO(INO)-1))
        COORMA(3*(INO-1)+2) = ZR(JCOOR+3*(NO(INO)-1)+1)
        COORMA(3*(INO-1)+3) = ZR(JCOOR+3*(NO(INO)-1)+2)
   20 CONTINUE
C
C --- COORDONNEES DU PROJETE
C
      CALL MMCOOR(ALIAS ,NNO   ,NDIM  ,COORMA,FFORME,
     &            KSI1  ,KSI2  ,COORDP)
C
      CALL JEDEMA()
      END
