      SUBROUTINE XTMAFI(NOMA,NDIM,FISS,NFISS,LISMAI,MESMAI,NBMA)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/03/2013   AUTEUR CUVILLIE M.CUVILLIEZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRS_1404
C RESPONSABLE GENIAUT S.GENIAUT
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      INTEGER       NFISS,NBMA,NDIM
      CHARACTER*8   NOMA,FISS(NFISS)
      CHARACTER*24  LISMAI,MESMAI
C
C ----------------------------------------------------------------------
C
C ROUTINE XFEM
C
C  CREATION DE LA LISTE TOTALE DES NOMS DES MAILLES FISSUREES
C  (MAILLES HEAV + CTIP + HECT) POUR LES FISSURES CONTENUES
C  DANS LA LISTE FISS
C  SI NDIM = 0 : ON LES PREND TOUTES
C  SI NDIM NON NUL : ON NE PREND QUE LES MAILLES DE DIMENSION NDIM

C ----------------------------------------------------------------------
C
C IN     NOMA   : NOM DU MAILLAGE
C IN     NDIM   : DIMENSION DES MAILLES A LISTER
C IN     FISS   : LISTE DES NOMS DES SD FISS_XFEM
C IN     NFISS  : LONGUEUR DE FISS
C IN/OUT LISMAI : NOM DE LA LISTE CREEE CONTENANT LES NUMEROS DE MAILLES
C IN/OUT MESMAI : NOM DE LA LISTE CREEE CONTENANT LES NOMS DES MAILLES
C OUT    NBMA   : LONGUEUR DE MESMAI
C
C
C
C
      INTEGER         IFISS,KK,JGRP,NMAENR,I,IMA,JMA,CPT,IRET
      INTEGER         JTMDIM,JTYPMA,NDIME,JMAD,JMAI,MXSTAC
      CHARACTER*8     NOMAIL,K8BID
      CHARACTER*24    NOMMAI,GRP(NFISS,3)
C
      PARAMETER (MXSTAC=100)
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C     VERIF QUE LES TABLEAUX LOCAUX DYNAMIQUES NE SONT PAS TROP GRANDS
C     (VOIR CRS 1404)
      CALL ASSERT(NFISS.LE.MXSTAC)
C
      NOMMAI = NOMA//'.NOMMAI'
      CALL JEVEUO('&CATA.TM.TMDIM','L',JTMDIM)
      CALL JEVEUO(NOMA//'.TYPMAIL','L',JTYPMA)

C     DIMENTIONNEMENT GROSSIER DE LA LISTE
      CPT = 0
      DO 10 IFISS = 1,NFISS
        GRP(IFISS,1) = FISS(IFISS)//'.MAILFISS.HEAV'
        GRP(IFISS,2) = FISS(IFISS)//'.MAILFISS.CTIP'
        GRP(IFISS,3) = FISS(IFISS)//'.MAILFISS.HECT'
        DO 20 KK = 1,3
          CALL JEEXIN(GRP(IFISS,KK),IRET)
          IF (IRET.EQ.0) GOTO 20
          CALL JELIRA(GRP(IFISS,KK),'LONMAX',NMAENR,K8BID)
          CPT = CPT + NMAENR
 20     CONTINUE
 10   CONTINUE

C     CREATION DE LA LISTE TEMPORAIRE
      CALL WKVECT('&&XTMAFI.TEMP','V V K8',CPT,JMA)
C     CREATION DE LA LISTE TEMPORAIRE
      CALL WKVECT('&&XTMAFI.TEMI','V V I',CPT,JMAI)

C     REMPLISSAGE DE LA LISTE
      NBMA = 0
      DO 100 IFISS = 1,NFISS


C       BOUCLE SUR LES 3 GROUPES : HEAV, CTIP ET HECT
        DO 110 KK = 1,3

          CALL JEEXIN(GRP(IFISS,KK),IRET)
          IF (IRET.NE.0) THEN
            CALL JEVEUO(GRP(IFISS,KK),'L',JGRP)
            CALL JELIRA(GRP(IFISS,KK),'LONMAX',NMAENR,K8BID)

C           BOUCLE SUR LES MAILLES DE CHAQUE GROUPE
            DO 120 I = 1,NMAENR
              IMA = ZI(JGRP-1+I)
C             NDIME : DIMENSION TOPOLOGIQUE DE LA MAILLE
              NDIME= ZI(JTMDIM-1+ZI(JTYPMA-1+IMA))
              IF ((NDIM.EQ.NDIME).OR.(NDIM.EQ.0)) THEN
                CALL JENUNO(JEXNUM(NOMMAI,IMA),NOMAIL)
                NBMA =NBMA + 1
                ZK8(JMA-1+NBMA) = NOMAIL
                ZI(JMAI-1+NBMA) = IMA
              ENDIF
 120        CONTINUE

          ENDIF

 110    CONTINUE

 100  CONTINUE

C     VERIFICATION
      CALL ASSERT(NBMA.LE.CPT)
      CALL ASSERT(NBMA.GE.1)

C     CREATION DE LA LISTE DEFINITIVE
      CALL WKVECT(MESMAI,'V V K8',NBMA,JMAD)
      DO 200 I = 1,NBMA
        ZK8(JMAD-1+I) = ZK8(JMA-1+I)
 200  CONTINUE
C
C     CREATION DE LA LISTE DEFINITIVE
      CALL WKVECT(LISMAI,'V V I',NBMA,JMAD)
      DO 201 I = 1,NBMA
        ZI(JMAD-1+I) = ZI(JMAI-1+I)
 201  CONTINUE
C
      CALL JEDETR ( '&&XTMAFI.TEMP' )
      CALL JEDETR ( '&&XTMAFI.TEMI' )

      CALL JEDEMA()
      END
