      SUBROUTINE CALCTR(MODGEN,NBSST,IDESC,ROT,TRAN)
      IMPLICIT REAL*8 (A-H,O-Z)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 05/07/2005   AUTEUR CIBHHPD L.SALMONA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C***********************************************************************
C  C. VARE     DATE 03/10/94
C-----------------------------------------------------------------------
C  BUT:  < CALCUL DES TRANSLATIONS DU MODELE_GENE >
C
C  CALCULER LE VECTEUR DES TRANSLATIONS ASSOCIE A CHAQUE SOUS-STRUCTURE
C  DU MODELE GENERALISE
C
C-----------------------------------------------------------------------
C
C MODGEN  /I/ : NOM K8 DU MODELE GENERALISE
C NBSST   /I/ : ENTIER DESIGNANT LE NOMBRE DE SOUS-STRUCTURES
C IDESC   /I/ : VECTEUR DES NUMEROS DES SOUS-STRUCTURES LIAISONNEES
C ROT     /I/ : MATRICE DE ROTATION
C TRAN    /O/ : MATRICE DE TRANSLATION
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
      CHARACTER*32 JEXNUM,JEXNOM
C
C----------  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
C   PARAMETRE REPRESENTANT LE NOMBRE MAX DE COMPOSANTES DE LA GRANDEUR
C   SOUS-JACENTE TRAITEE
C
      PARAMETER  (NBCMPM=10)
      CHARACTER*8 MODGEN,LINT,LINT0,K8BID
      CHARACTER*8 SST1,SST2,INTF1,INTF2,SST,SST0,INTF,INTF0
      CHARACTER*8 MAIL,MAIL0
      REAL*8      X(3),XR(3),X0(3),X0R(3),ROT(3,NBSST),TRAN(3,NBSST)
      INTEGER     IDESC(3,NBSST)
      REAL*8      MATROT(NBCMPM,NBCMPM),ZERO
      REAL*8      MATBUF(NBCMPM,NBCMPM),MATTMP(NBCMPM,NBCMPM)
C
C-----------------------------------------------------------------------
      DATA ZERO /0.0D+00/
C-----------------------------------------------------------------------
C
      CALL JEMARQ()
      DO 30 I=1,NBSST
        NUSST=IDESC(1,I)
        NULIA=IDESC(2,I)
        NUCO=IDESC(3,I)
        IF (NULIA.NE.0) THEN
          DO 40 J=1,NBSST
            IF (IDESC(1,J).EQ.NUCO) NUASCO=J
40        CONTINUE
          CALL JEVEUO(JEXNUM(MODGEN//'      .MODG.LIDF',NULIA),'L',
     &                LLDEFL)
          SST1 =ZK8(LLDEFL)
          INTF1=ZK8(LLDEFL+1)
          SST2 =ZK8(LLDEFL+2)
          INTF2=ZK8(LLDEFL+3)
C
          CALL JENONU(JEXNOM(MODGEN//'      .MODG.SSNO',SST1),NU1)
          CALL JENONU(JEXNOM(MODGEN//'      .MODG.SSNO',SST2),NU2)
C
          IF(NU2.EQ.NUSST) THEN
            SST  =SST2
            INTF =INTF2
            SST0 =SST1
            INTF0=INTF1
          ELSE
            SST  =SST1
            INTF =INTF1
            SST0 =SST2
            INTF0=INTF2
          ENDIF
C
C-----RECUPERATION MAILLAGE ET INTERFACE AMONT SOUS-STRUCTURE 1
C
          CALL MGUTDM(MODGEN,SST,IBID,'NOM_MAILLAGE',IBID,MAIL)
          CALL MGUTDM(MODGEN,SST,IBID,'NOM_LIST_INTERF',IBID,LINT)
C
C-----ROTATION
C
          CALL INTET0(ROT(1,I),MATTMP,3)
          CALL INTET0(ROT(2,I),MATROT,2)
          CALL R8INIR(NBCMPM*NBCMPM,ZERO,MATBUF,1)
          CALL PMPPR(MATTMP,NBCMPM,NBCMPM,1,MATROT,NBCMPM,NBCMPM,1,
     &               MATBUF,NBCMPM,NBCMPM)
          CALL INTET0(ROT(3,I),MATTMP,1)
          CALL R8INIR(NBCMPM*NBCMPM,ZERO,MATROT,1)
          CALL PMPPR(MATBUF,NBCMPM,NBCMPM,1,MATTMP,NBCMPM,NBCMPM,1,
     &               MATROT,NBCMPM,NBCMPM)
C
C-----MOYENNE DES COORDONNEES DES NOEUDS DE L'INTERFACE 1
C
          CALL JENONU(JEXNOM(LINT//'.IDC_NOMS',INTF),IBID)
          CALL JEVEUO(JEXNUM(LINT//'.IDC_LINO',IBID),'L',LLINT)
          CALL JELIRA(JEXNUM(LINT//'.IDC_LINO',IBID),'LONMAX',
     &                NBNO,K8BID)
          CALL JEVEUO(LINT//'.IDC_DEFO','L',LLDESC)
          CALL JEVEUO(MAIL//'.COORDO    .VALE','L',LLCOO)
          XR(1)=0.D0
          XR(2)=0.D0
          XR(3)=0.D0
          DO 70 II = 1,NBNO
             INU=ZI(LLINT-1+II)
             NUNO=ZI(LLDESC+INU-1)
             DO 50 K=1,3
               X(K)=ZR(LLCOO+(NUNO-1)*3+K-1)
50           CONTINUE
C
             DO 60 K=1,3
             DO 60 L=1,3
               XR(K)=XR(K)+MATROT(K,L)*X(L)
60           CONTINUE
70        CONTINUE
          IF (NBNO.NE.0) THEN
             XR(1) = XR(1)/NBNO
             XR(2) = XR(2)/NBNO
             XR(3) = XR(3)/NBNO
          ENDIF
C
C-----RECUPERATION MAILLAGE ET INTERFACE AMONT SOUS-STRUCTURE 2
C
          CALL MGUTDM(MODGEN,SST0,IBID,'NOM_MAILLAGE',IBID,MAIL0)
          CALL MGUTDM(MODGEN,SST0,IBID,'NOM_LIST_INTERF',IBID,LINT0)
C
C-----ROTATION
C
          CALL INTET0(ROT(1,NUASCO),MATTMP,3)
          CALL INTET0(ROT(2,NUASCO),MATROT,2)
          CALL R8INIR(NBCMPM*NBCMPM,ZERO,MATBUF,1)
          CALL PMPPR(MATTMP,NBCMPM,NBCMPM,1,MATROT,NBCMPM,NBCMPM,1,
     &               MATBUF,NBCMPM,NBCMPM)
          CALL R8INIR(NBCMPM*NBCMPM,ZERO,MATROT,1)
          CALL INTET0(ROT(3,NUASCO),MATTMP,1)
          CALL PMPPR(MATBUF,NBCMPM,NBCMPM,1,MATTMP,NBCMPM,NBCMPM,1,
     &               MATROT,NBCMPM,NBCMPM)
C
C-----MOYENNE DES COORDONNEES DES NOEUDS DE L'INTERFACE 2
C
          CALL JENONU(JEXNOM(LINT0//'.IDC_NOMS',INTF0),IBID)
          CALL JEVEUO(JEXNUM(LINT0//'.IDC_LINO',IBID),'L',LLINT)
          CALL JELIRA(JEXNUM(LINT0//'.IDC_LINO',IBID),'LONMAX',
     &                NBNO,K8BID)
          CALL JEVEUO(LINT0//'.IDC_DEFO','L',LLDESC)
          CALL JEVEUO(MAIL0//'.COORDO    .VALE','L',LLCOO)
          X0R(1)=0.D0
          X0R(2)=0.D0
          X0R(3)=0.D0
          DO 100 II = 1,NBNO
             INU0=ZI(LLINT-1+II)
             NUNO0=ZI(LLDESC+INU0-1)
             DO 80 K=1,3
               X0(K)=ZR(LLCOO+(NUNO0-1)*3+K-1)
80           CONTINUE
C
             DO 90 K=1,3
             DO 90 L=1,3
                 X0R(K)=X0R(K)+MATROT(K,L)*X0(L)
90           CONTINUE
100       CONTINUE
          IF (NBNO.NE.0) THEN
             X0R(1) = X0R(1)/NBNO
             X0R(2) = X0R(2)/NBNO
             X0R(3) = X0R(3)/NBNO
          ENDIF
C
C-----CALCUL DE LA MATRICE DE TRANSLATION
C
          DO 110 K=1,3
            TRAN(K,I)=X0R(K)-XR(K)+TRAN(K,NUASCO)
110       CONTINUE
        ENDIF
30    CONTINUE
C
 9999 CONTINUE
      CALL JEDEMA()
      END
