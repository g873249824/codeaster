      SUBROUTINE MMMJEU(NDIM  ,JEUSUP,NORM  ,GEOME ,GEOMM ,
     &                  DDEPLE,DDEPLM,MPROJT,JEU   ,DJEU  ,
     &                  DJEUT ,IRESOG,TAU1  ,TAU2  ,GENE11,
     &                  GENE21)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/03/2013   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INTEGER NDIM
      REAL*8  JEUSUP
      REAL*8  NORM(3),TAU1(3),TAU2(3)
      REAL*8  GEOMM(3),GEOME(3)
      REAL*8  DDEPLE(3),DDEPLM(3)
      REAL*8  MPROJT(3,3)
      REAL*8  JEU,DJEU(3),DJEUT(3)
      INTEGER IRESOG
      REAL*8  GENE11(3,3),GENE21(3,3)
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - UTILITAIRE)
C
C CALCUL DES JEUX
C
C ----------------------------------------------------------------------
C
C
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  JEUSUP : JEU SUPPLEMENTAIRE PAR DIST_ESCL/DIST_MAIT
C IN  NORM   : VALEUR DE LA NORMALE
C IN  GEOME  : COORDONNEES ACTUALISEES DU POINT DE CONTACT
C IN  GEOMM  : COORDONNEES ACTUALISEES DU PROJETE DU POINT DE CONTACT
C IN  DDEPLE : INCREMENT DEPDEL DU DEPL. DU POINT DE CONTACT
C IN  DDEPLM : INCREMENT DEPDEL DU DEPL. DU PROJETE DU POINT DE CONTACT
C IN  MPROJT : MATRICE DE PROJECTION TANGENTE
C OUT JEU    : JEU NORMAL ACTUALISE
C OUT DJEU   : INCREMENT DEPDEL DU JEU
C OUT DJEUT  : INCREMENT DEPDEL DU JEU TANGENT
C IN  IRESOG : ALGO. DE RESOLUTION POUR LA GEOMETRIE
C              0 - POINT FIXE
C              1 - NEWTON
C IN  TAU1   : PREMIER VECTEUR TANGENT
C IN  TAU2   : SECOND VECTEUR TANGENT
C OUT GENE11 : MATRICE
C OUT GENE21 : MATRICE
C
C ----------------------------------------------------------------------
C
      INTEGER IDIM,I,J
C
C ----------------------------------------------------------------------
C
      DO 1 IDIM = 1,3
        DJEU(IDIM)  = 0.D0
        DJEUT(IDIM) = 0.D0
   1  CONTINUE
      JEU = 0.D0
C
C --- CALCUL DE L'INCREMENT DE JEU
C
      DO 5 IDIM = 1,3
        DJEU(IDIM) = DDEPLE(IDIM) - DDEPLM(IDIM)
   5  CONTINUE
C
C --- CALCUL DU JEU TOTAL
C
      JEU    = JEUSUP
      DO 10 IDIM = 1,NDIM
        JEU   = JEU + (GEOME(IDIM)+DDEPLE(IDIM)-
     &                 GEOMM(IDIM)-DDEPLM(IDIM))*NORM(IDIM)
 10   CONTINUE
C
C --- PROJECTION DE L'INCREMENT DE JEU SUR LE PLAN TANGENT
C
      DO 20 I=1,NDIM
        DO 25 J=1,NDIM
          DJEUT(I) = MPROJT(I,J)*DJEU(J)+DJEUT(I)
 25     CONTINUE
 20   CONTINUE
C
C --- MATRICE GENE11 ET GENE21
C
      IF (IRESOG.EQ.1) THEN
         IF ((1.D0-JEU**2) .NE. 0.0D0) THEN
           DO 24 I = 1 , NDIM
             DO 26 J =1 , NDIM
              GENE11(I,J) = GENE11(I,J)*JEU/(1.D0-JEU**2)+
     &                      TAU1(I)*TAU1(J)/(1.D0-JEU**2)

              GENE21(I,J) = GENE21(I,J)*JEU/(1.D0-JEU**2)+
     &                      TAU2(I)*TAU1(J)/(1.D0-JEU**2)
   26        CONTINUE
   24      CONTINUE
         ELSE
           CALL MATINI( 3, 3,0.D0,GENE11)
           CALL MATINI( 3, 3,0.D0,GENE21)
         ENDIF
      ELSE
         CALL MATINI( 3, 3,0.D0,GENE11)
         CALL MATINI( 3, 3,0.D0,GENE21)
      ENDIF

      END
