        SUBROUTINE LCDPEQ(VIND, VINF,COMP,NBCOMM,CPMONO,NMAT,NVI,SIG,
     &  DETOT,EPSD,MATERF)

        IMPLICIT NONE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 06/02/2012   AUTEUR PROIX J-M.PROIX 
C TOLE CRS_1404
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     DEFORMATION PLASTIQUE EQUIVALENTE CUMULEE MACROSCOPIQUE
C     POUR LE MONOCRISTAL
C     IN  VIND   :  VARIABLES INTERNES A T
C     IN  VINF   :  VARIABLES INTERNES A T+DT
C          COMP   :  NOM MODELE DE COMPORTEMENT
C          NBCOMM :  INDICES DES COEF MATERIAU
C          CPMONO :  NOMS DES LOIS MATERIAU PAR FAMILLE
C          NMAT   :  DIMENSION MATER
C          VIND   :  VARIABLES INTERNES A T
C          SIG    :  CONTRAINTES A T
C          DETOT  :  INCREMENT DE  DEFORMATION TOTALE OU DF
C          EPSD   :  DEFORMATION TOTALE A T OU F A T
C     VAR  NVI    :  NOMBRE DE VARIABLES INTERNES
C          VINF   :  VARIABLES INTERNES A T+DT
C          MATERF :  COEF MATERIAU
C     ----------------------------------------------------------------
      INTEGER NVI,NMAT,NBCOMM(NMAT,3),NBPHAS,I,IPHAS,INDFV,NUVI,NS
      REAL*8  VIND(NVI),VINF(NVI),DVIN(NVI),SIG(6),GRANB(6)
      REAL*8  EPSEQ,E,NU,FV,SIGG(6)
      REAL*8  ID(3,3),F(3,3),FPM(3,3),FP(3,3),FE(3,3),DETP,LCNRTE
      REAL*8  DETOT(*),EPSD(*),PK2(6),DEVI(6),ENDOC
      REAL*8 MATERF(NMAT,2)
      CHARACTER*16 LOI,CPMONO(5*NMAT+1),LOCA,COMP(*)
      DATA    ID/1.D0,0.D0,0.D0, 0.D0,1.D0,0.D0, 0.D0,0.D0,1.D0/    

      LOI  = COMP(1)
      IF (LOI(1:8).EQ.'MONOCRIS')  THEN
         NVI = NVI +3
         IF (COMP(3)(1:4).EQ.'SIMO') THEN
            NVI=NVI+9
         ENDIF
      ENDIF

C --    DEBUT TRAITEMENT DE VENDOCHAB --
C --    CALCUL DES CONTRAINTES SUIVANT QUE LE MATERIAU EST
C --    ENDOMMAGE OU PAS

      IF (LOI(1:8).EQ.'MONOCRIS') THEN      
  
         IF (COMP(3)(1:5).NE.'PETIT') THEN
C           ICI CONTRAIREMENT A LCMMON, NVI EST LE NOMBRE TOTAL DE V.I
            NS=(NVI-27)/3
            CALL DCOPY(9,VINF(6+3*NS+1),1,FP,1)
            CALL MATINV('S',3,FP,FPM,DETP)
            CALL PMAT(3,DETOT,EPSD,F)
            CALL PMAT(3,F,FPM,FE)
C           CALCUL DES CONTRAINTES DE KIRCHOFF
            CALL DCOPY(6,SIG,1,PK2,1)
            CALL DSCAL(3,SQRT(2.D0),PK2(4),1)
            CALL PK2SIG(3,FE,1.D0,PK2,SIG,1)
C           LES RACINE(2) ATTENDUES PAR NMCOMP :-)       
            CALL DSCAL(3,SQRT(2.D0),SIG(4),1)
            CALL DAXPY(9,-1.D0,ID,1,FE,1)
            CALL DCOPY(9,FE,1,VINF(6+3*NS+10),1)
            CALL LCGRLA(FP,DEVI)
            CALL DCOPY(6,DEVI,1,VINF,1)
            CALL DSCAL(3,SQRT(2.D0),DEVI(4),1) 
            CALL DAXPY(9,-1.D0,ID,1,FP,1)
            CALL DCOPY(9,FP,1,VINF(6+3*NS+1),1)
            EPSEQ = LCNRTE(DEVI)
         ELSE
C           V.I. 1 A 6 REPRÈSENTE LA DEFORMATION VISCOPLASTIQUE MACRO
            EPSEQ=0
            DO 10 I=1,6
                DVIN(I)=VINF(I)-VIND(I)
                EPSEQ=EPSEQ+DVIN(I)*DVIN(I)
10          CONTINUE
            EPSEQ = SQRT ( 2.0D0/3.0D0* EPSEQ )
         ENDIF
         VINF (NVI-1) = VIND (NVI-1) + EPSEQ
         
      ELSEIF (LOI(1:8).EQ.'POLYCRIS') THEN

C        V.I. 1 A 6 REPRÈSENTE LA DEFORMATION VISCOPLASTIQUE MACRO
         EPSEQ=0
         DO 20 I=1,6
             DVIN(I)=VINF(I)-VIND(I)
             EPSEQ=EPSEQ+DVIN(I)*DVIN(I)
20       CONTINUE
         EPSEQ = SQRT ( 2.0D0/3.0D0* EPSEQ )
         VINF (7) = VIND (7) + EPSEQ
C        LOCALISATION
C        RECUPERATION DU NOMBRE DE PHASES
         NBPHAS=NBCOMM(1,1)
         LOCA=CPMONO(1)
C        CALCUL DE  B
         DO 53 I=1,6
            GRANB(I)=0.D0
53       CONTINUE
         DO 54 I=1,6
         DO 54 IPHAS=1,NBPHAS
            INDFV=NBCOMM(1+IPHAS,3)
            FV=MATERF(INDFV,2)
            GRANB(I)=GRANB(I)+FV*VINF(7+6*(IPHAS-1)+I)
54       CONTINUE
         NUVI=NVI-6*NBPHAS-1
         DO 1 IPHAS=1,NBPHAS
          INDFV=NBCOMM(1+IPHAS,3)
C         RECUPERER L'ORIENTATION DE LA PHASE ET LA PROPORTION
          FV=MATERF(INDFV,2)
          E =MATERF(1,1)
          NU=MATERF(2,1)
          CALL LCLOCA(MATERF(1,2),E,NU,NMAT,NBCOMM,NBPHAS,SIG,VINF,
     &            IPHAS,GRANB,LOCA,SIGG)
            DO 2 I=1,6
               VINF(NUVI+6*(IPHAS-1)+I)=SIGG(I)
   2        CONTINUE
   1     CONTINUE
      ENDIF

      IF (EPSEQ.EQ.0.D0) THEN
         VINF (NVI) = 0.D0
      ELSE
         VINF (NVI) = 1.D0
      ENDIF

      IF (LOI(1:9).EQ.'VENDOCHAB') THEN
C --    DEBUT TRAITEMENT DE VENDOCHAB --
C --    CALCUL DE DSDE SUIVANT QUE LE MATERIAU EST ENDOMMAGE OU PAS
        ENDOC=(1.0D0-VINF(9))
        MATERF(1,1)=MATERF(1,1)*ENDOC
      ENDIF
      
      END
