        SUBROUTINE LCMMRE ( TYPMOD, NMAT, MATERD, MATERF,
     &      COMP,NBCOMM, CPMONO, PGL,NFS,NSG,TOUTMS,HSR, NR, NVI,VIND,
     &       ITMAX, TOLER,TIMED,TIMEF,YD,YF,DEPS,DY,R,IRET)
        IMPLICIT NONE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/03/2012   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE JMBHH01 J.M.PROIX
C TOLE CRP_21 CRS_1404
C       ----------------------------------------------------------------
C     MONOCRISTAL  : CALCUL DES RESIDUS DU SYSTEME NL A RESOUDRE = R(DY)
C                    CF. R5.03.11
C                    DY =  DSIG (DBETA PAR SYSTEME)
C                    Y  =  SIG   (BETA  PAR SYSTEME)
C                    R  = ( R1  R2   )
C                    ATTENTION IL FAUT CALCULER -R
C
C     IN  TYPMOD :  TYPE DE MODELISATION
C         NMAT   :  DIMENSION MATER
C         MATERD :  COEFFICIENTS MATERIAU A T
C         MATERF :  COEFFICIENTS MATERIAU A T+DT
C         COMP   :  NOM COMPORTEMENT
C         NBCOMM :  INCIDES DES COEF MATERIAU
C         CPMONO :  NOM DES COMPORTEMENTS
C         PGL    :  MATRICE DE PASSAGE
C         TOUTMS :  TENSEURS D'ORIENTATION
C         HSR    :  MATRICE D'INTERACTION
C         NR     :  DIMENSION DECLAREE DRDY
C         NVI    :  NOMBRE DE VARIABLES INTERNES
C         VIND   :  VARIABLES INTERNES A L'INSTANT PRECEDENT
C         ITMAX  :  ITER_INTE_MAXI
C         TOLER  :  RESI_INTE_RELA
C         TIMED  :  ISTANT PRECEDENT
C         TIMEF  :  INSTANT ACTUEL
C         YD     :  VARIABLES A T       = ( SIGD BETAD )
C         YF     :  VARIABLES A T + DT  = ( SIGF BETAF )
C         DEPS   :  INCREMENT DE DEFORMATION OU GRADIENT DF
C         DY     :  SOLUTION  =  ( DSIG DBETA )
C         NR     :  DIMENSION DECLAREE DRDY
C     OUT R      :  RESIDU DU SYSTEME NL A T + DT
C         IRET   :  CODE RETOUR
C     ----------------------------------------------------------------
      INTEGER NDT , NDI , NMAT, NR, NVI, NSFV, IRET
      INTEGER NBFSYS,IFA,NBSYS,IS,ITMAX, NFS, NSG
      INTEGER NBCOMM(NMAT,3),NSFA,IFL,NUECOU
C
      REAL*8 DDOT,DKOOH(6,6),FKOOH(6,6),TIMED, TIMEF
      REAL*8 SIGF(6), SIGD(6), MSNS(3,3),PGL(3,3),DGAMM1
      REAL*8 DEPS(*),DEPSE(6), DEVI(6),DT
      REAL*8 EPSED(6) , EPSGL(6), H1SIGF(6),VIND(*)
      REAL*8 MATERD(NMAT*2) ,MATERF(NMAT*2),EPSEF(6)
      REAL*8 MUS(6),NG(3),TAUS,DGAMMA,DALPHA,DP,RP,DEPSDT
      REAL*8 R(NR),DY(NR),YD(NR),YF(NR),TOLER,FE(3,3)
      REAL*8 TOUTMS(NFS,NSG,6), HSR(NSG,NSG),Q(3,3),LG(3)
      REAL*8 GAMSNS(3,3),FP(3,3),DEPST(6)
      REAL*8 CRIT,SGNS,EXPBP(NSG)
      CHARACTER*8  TYPMOD
      CHARACTER*16 CPMONO(5*NMAT+1),COMP(*),NOMFAM
C     ----------------------------------------------------------------
      COMMON /TDIM/   NDT , NDI
      COMMON /DEPS6/DEPSDT
C     ----------------------------------------------------------------
C
      DT=TIMEF-TIMED
C     INVERSE DE L'OPERATEUR D'ELASTICITE DE HOOKE
      IF (MATERF(NMAT).EQ.0) THEN
         CALL LCOPIL  ( 'ISOTROPE' , TYPMOD , MATERD(1) , DKOOH )
         CALL LCOPIL  ( 'ISOTROPE' , TYPMOD , MATERF(1) , FKOOH )
      ELSEIF (MATERF(NMAT).EQ.1) THEN
         CALL LCOPIL  ( 'ORTHOTRO' , TYPMOD , MATERD(1) , DKOOH )
         CALL LCOPIL  ( 'ORTHOTRO' , TYPMOD , MATERF(1) , FKOOH )
      ENDIF

      CALL R8INIR(9,0.D0,GAMSNS,1)
      CALL LCEQVN(NDT,YF(1),SIGF)
      CALL R8INIR(6,0.D0,DEVI,1)
      
C     POUR DD_CC      
      IF (COMP(3)(1:5).NE.'PETIT') THEN
         CALL LCGRLA (DEPS,DEPST)
         CALL DSCAL(3,SQRT(2.D0),DEPST(4),1)
      ELSE
         CALL DCOPY(6,DEPS,1,DEPST,1)
      ENDIF
      DEPSDT=SQRT(DDOT(6,DEPST,1,DEPST,1)/1.5D0)/DT
      
      NBFSYS=NBCOMM(NMAT,2)
      IRET=0
      

C     NSFA : debut de la famille IFA dans DY et YD, YF
      NSFA=6
C     NSFV : debut de la famille IFA dans les variables internes
      NSFV=6

      DO 6 IFA=1,NBFSYS
      
         IFL=NBCOMM(IFA,1)           
         NUECOU=NINT(MATERF(NMAT+IFL))
         NOMFAM=CPMONO(5*(IFA-1)+1)       

         CALL LCMMSG(NOMFAM,NBSYS,0,PGL,MUS,NG,LG,0,Q)

         DO 7 IS=1,NBSYS
C           CALCUL DE LA SCISSION REDUITE
            CALL CALTAU(COMP,IFA,IS,SIGF,FKOOH,NFS,NSG,TOUTMS,
     &                  TAUS,MUS,MSNS)
C           CALCUL DE L'ECOULEMENT SUIVANT LE COMPORTEMENT            
            CALL LCMMLC(NMAT,NBCOMM,CPMONO,NFS,NSG,HSR,NSFV,NSFA,IFA,
     &      NBSYS,IS,DT,NVI,VIND,YD,DY,ITMAX,TOLER,MATERF,EXPBP,TAUS,
     &                  DALPHA,DGAMMA,DP,CRIT,SGNS,RP,IRET)

            IF (IRET.GT.0) THEN
               GOTO 9999
            ENDIF

            IF ((NUECOU.GE.4).AND.(NUECOU.LE.7)) THEN
C           POUR LES LOIS DD_* ALPHA représente la variable principale
               R(NSFA+IS)=-(DY(NSFA+IS)-DALPHA)
            ELSE
               DGAMM1=DY(NSFA+IS)
               R(NSFA+IS)=-(DGAMM1-DGAMMA)
            ENDIF

            IF (COMP(3)(1:5).EQ.'PETIT') THEN
               CALL DAXPY(6,DGAMMA,MUS,1,DEVI,1)
            ELSE
               CALL DAXPY(9,DGAMMA,MSNS,1,GAMSNS,1)
            ENDIF
  7     CONTINUE

        NSFA=NSFA+NBSYS
        NSFV=NSFV+NBSYS*3

  6   CONTINUE

      IF (COMP(3)(1:5).NE.'PETIT') THEN
         CALL CALCFE(NR,NDT,NVI,VIND,DEPS,GAMSNS,FE,FP,IRET)       
         IF (IRET.GT.0) THEN
            GOTO 9999
         ENDIF
         CALL LCGRLA ( FE,EPSGL)
         CALL LCPRMV ( FKOOH,   SIGF  , H1SIGF )
         CALL LCDIVE ( EPSGL,   H1SIGF  , R(1) )
      ELSE      
         CALL LCEQVN ( NDT , YD(1)       , SIGD)
         CALL LCPRMV ( DKOOH,   SIGD  , EPSED )
         CALL LCDIVE ( DEPS ,   DEVI  , DEPSE )
         CALL LCSOVE ( EPSED,   DEPSE , EPSEF )
C LA PREMIERE EQUATION EST  (HF-1)SIGF -(HD-1)SIGD -(DEPS-DEPSP)=0
         CALL LCPRMV ( FKOOH,   SIGF  , H1SIGF )
         CALL LCDIVE ( EPSEF,   H1SIGF  , R(1) )
      ENDIF
      
9999  CONTINUE
      END
