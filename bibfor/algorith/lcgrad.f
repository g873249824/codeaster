      SUBROUTINE LCGRAD(RESI,RIGI,NDIM,NDIMSI,NEPS,SIGMA,APG,LAG,GRAD,
     &                  ALDC,R,C,KTG,SIG,DSIDEP)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/03/2012   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
      IMPLICIT NONE
      
      LOGICAL RESI,RIGI
      INTEGER NDIM,NDIMSI,NEPS
      REAL*8 SIGMA(6),KTG(6,6,4),APG,LAG,GRAD(NDIM),ALDC,R,C
      REAL*8 SIG(NEPS),DSIDEP(NEPS,NEPS)
C ----------------------------------------------------------------------
C  GRAD_VARI : CALCUL DES TERMES COMMUNS AU LAGRANGIEN AUGMENTE
C ----------------------------------------------------------------------
C IN  RESI   .TRUE. SI CALCUL DES CONTRAINTES
C IN  RIGI   .TRUE. SI CALCUL DE LA RIGIDITE
C IN  NDIM   DIMENSION DE L'ESPACE
C IN  NDIMSI DIMENSION DES DEFORMATIONS ET DES CONTRAINTES MECANIQUES
C IN  NEPS   DIMENSION DES DEFORMATIONS ET DES CONTRAINTES
C IN  SIGMA  CONTRAINTES MECANIQUES
C IN  APG    VARIABLE NON LOCALE NODALE INTERPOLEE AU POINT DE GAUSS
C IN  LAG    MULTIPLICATEUR DE LAGRANGE
C IN  GRAD   GRADIENT DE LA VARIABLE NON LOCALE
C IN  ALDC   VARIABLE NON LOCALE ISSUE DE LA LOI DE COMPORTEMENT
C IN  R      COEFFICIENT D'AUGMENTATION DU LAGRANGIEN AUGMENTE
C IN  C      COEFFICIENT DE PONDERATION DU TERME NON LOCAL
C IN  KTG    MATRICES TANGENTES PAR BOUT
C             KTG(6,6,1) DSIGMA/DEPS
C             KTG(6,1,2) DSIGMA/DPHI
C             KTG(6,1,3) DALDC/DEPS
C             KTG(1,1,4) DALDC/DPHI
C OUT SIG    CONTRAINTES GENERALISEES
C OUT DSIDEP MATRICE TANGENTE GENERALISEE
C ----------------------------------------------------------------------
      INTEGER I,J
C ----------------------------------------------------------------------


C -- CALCUL DES CONTRAINTES GENERALISEES

      IF (RESI) THEN
        DO 10 I = 1,NDIMSI
          SIG(I) = SIGMA(I)
 10     CONTINUE
        SIG(NDIMSI+1) = LAG + R*(APG-ALDC)
        SIG(NDIMSI+2) = APG-ALDC
        DO 20 I = 1,NDIM
          SIG(NDIMSI+2+I) = C*GRAD(I)
 20     CONTINUE
      END IF


C -- CALCUL DE LA MATRICE TANGENTE GENERALISEE

      IF (RIGI) THEN
        CALL R8INIR(NEPS*NEPS, 0.D0, DSIDEP,1)

C      SIG - EPS
        DO 30 I = 1,NDIMSI
          DO 40 J = 1,NDIMSI
            DSIDEP(I,J) = KTG(I,J,1)
 40       CONTINUE
 30     CONTINUE

C      SIG - A ET SIG - MU
        DO 50 I = 1,NDIMSI
          DSIDEP(I,NDIMSI+1) = R*KTG(I,1,2)
          DSIDEP(I,NDIMSI+2) = KTG(I,1,2)
 50     CONTINUE
 
C      SIGA - EPS ET SIGMU - EPS
        DO 60 I = 1,NDIMSI
          DSIDEP(NDIMSI+1,I) = -R*KTG(I,1,3)
          DSIDEP(NDIMSI+2,I) = -KTG(I,1,3)
 60     CONTINUE
 
C      SIGA - A
        DSIDEP(NDIMSI+1,NDIMSI+1) = (1-R*KTG(1,1,4))*R

C      SIGA - MU ET SIGMU - A
        DSIDEP(NDIMSI+1,NDIMSI+2) = 1-R*KTG(1,1,4)
        DSIDEP(NDIMSI+2,NDIMSI+1) = 1-R*KTG(1,1,4)

C      SIGMU - MU
        DSIDEP(NDIMSI+2,NDIMSI+2) = - KTG(1,1,4)

C      SIGG - GRAD
        DO 70 I = 1,NDIM
          DSIDEP(NDIMSI+2+I,NDIMSI+2+I) = C
 70     CONTINUE
 
      END IF
      
      END
