      SUBROUTINE DTCYCL(VECDT,NITNEW,ICYCL,DTMIN)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/05/2000   AUTEUR KXBADNG T.KESTENS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C DESCRIPTION : PROCEDURE DE DETECTION D'ITERATIONS CYCLIQUES
C -----------   DANS NEWTON
C
C               APPELANT : NEWTON
C
C IN     : VECDT  : REAL*8 , VECTEUR DE DIMENSION NITMAX+1 = 151
C                   VECTEUR DE TRAVAIL CONTENANT LES PAS DE TEMPS
C                   AJUSTES PAR NEWTON A CHAQUE ITERATION
C IN     : NITNEW : INTEGER , SCALAIRE
C                   NOMBRE D'ITERATIONS DEJA EXECUTEES DANS NEWTON
C
C OUT    : ICYCL  : INTEGER , SCALAIRE
C                   INDICATEUR D'ITERATIONS CYCLIQUES
C                   ICYCL = 0 : PAS D'ITERATIONS CYCLIQUES
C                   ICYCL = 1 : ITERATIONS CYCLIQUES
C OUT    : DTMIN  : REAL*8 , SCALAIRE
C                   VALEUR MINIMALE DU PAS DE TEMPS AU COURS DES
C                   ITERATIONS CYCLIQUES
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
C ARGUMENTS
C ---------
      REAL*8     VECDT(*)
      INTEGER    NITNEW, ICYCL
      REAL*8     DTMIN
C
C VARIABLES LOCALES
C -----------------
      INTEGER    IDT, IDT1, IDT2
      REAL*8     DTCOUR, DTREF, EPSDTC
C
C FONCTIONS INTRINSEQUES
C ----------------------
C     INTRINSIC  ABS
C
C-------------------   DEBUT DU CODE EXECUTABLE    ---------------------
C
      ICYCL = 0
C.... N.B. EPSDTC EST DEFINIE EN COHERENCE AVEC NEWTON
      EPSDTC = 1.0D-04
C
      DO 10 IDT1 = 0, NITNEW-1
         DTREF = VECDT(IDT1+1)
         DO 11 IDT2 = IDT1+1, NITNEW
            DTCOUR = VECDT(IDT2+1)
            IF ( (ABS(DTCOUR-DTREF)/DTREF).LT.EPSDTC ) THEN
               ICYCL = 1
               GO TO 20
            ENDIF
  11     CONTINUE
  10  CONTINUE
C
  20  CONTINUE
      IF ( ICYCL.EQ.1 ) THEN
         DTMIN = DTREF
         IF ( (IDT2-IDT1).GT.1 ) THEN
            DO 30 IDT = IDT1+1, IDT2-1
               IF ( VECDT(IDT+1).LT.DTMIN ) DTMIN = VECDT(IDT+1)
  30        CONTINUE
         ENDIF
      ENDIF
C
C --- FIN DE DTCYCL.
      END
