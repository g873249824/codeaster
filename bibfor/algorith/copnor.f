      SUBROUTINE COPNOR(NOMA  ,DEFICO,FFORME,POSMAI,KSI1  ,
     &                  KSI2  ,TAU1  ,TAU2)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 01/04/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INTEGER       POSMAI         
      REAL*8        TAU1(3),TAU2(3)
      CHARACTER*8   NOMA,FFORME
      CHARACTER*24  DEFICO
      REAL*8        KSI1,KSI2
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - APPARIEMENT)
C
C CALCUL DES VECTEURS TANGENTS LOCAUX EN UN POINT
C ON PROCEDE PAR INTERPOLATION LINEAIRE DES VECTEURS TANGENTS 
C AUX NOEUDS DE LA MAILLE 
C      
C ----------------------------------------------------------------------
C
C NB: CONFUSION NOEUD/POINT DE CONTACT CAR LES OPTIONS FONCTIONNENT
C QUE POUR INTEGRATION='NOEUD'
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  FFORME : TYPE DES FONCTIONS DE FORME
C               'CONTINUE' POUR ELTS DE CONTACT
C               'STANDARD' POUR ELTS STANDARDS
C IN  POSMAI : MAILLE QUI RECOIT LA PROJECTION
C IN  KSI1   : COORDONNEE X DU POINT PROJETE
C IN  KSI2   : COORDONNEE Y DU POINT PROJETE
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C OUT TAU1   : PREMIERE TANGENTE LOCALE AU POINT PROJETE
C OUT TAU2   : SECONDE TANGENTE LOCALE AU POINT ROJETE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      REAL*8       ZERO
      PARAMETER  ( ZERO   =  0.0D0  )   
      CHARACTER*24 CONTMA,NOMACO,PNOMA,TANINI     
      INTEGER      JMACO ,JNOMA,JPONO,JTGINI
      INTEGER      JDEC,POSNO,NUMMAI
      INTEGER      INO,IDIM
      INTEGER      NDIM,NNO
      REAL*8       VECTA1(27),VECTA2(27)
      CHARACTER*8  ALIAS
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- RECUPERATION DE QUELQUES DONNEES
C     
      CONTMA = DEFICO(1:16)//'.MAILCO'
      NOMACO = DEFICO(1:16)//'.NOMACO'
      PNOMA  = DEFICO(1:16)//'.PNOMACO' 
      TANINI = DEFICO(1:16)//'.TANINI'
      CALL JEVEUO(TANINI,'L',JTGINI)       
      CALL JEVEUO(CONTMA,'L',JMACO)
      CALL JEVEUO(NOMACO,'L',JNOMA)
      CALL JEVEUO(PNOMA, 'L',JPONO)
C
C --- INITIALISATIONS
C         
      DO 40 IDIM = 1,3
        TAU1(IDIM) = ZERO
        TAU2(IDIM) = ZERO
   40 CONTINUE                
C
C --- MAILLE COURANTE
C
      NUMMAI = ZI(JMACO+POSMAI-1)
      CALL MMELTY(NOMA  ,NUMMAI,ALIAS ,NNO   ,NDIM  )
      IF (NNO.GT.9) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
C      
C --- VECTEURS TANGENTS LOCAUX AUX NOEUDS DE LA MAILLE
C
      JDEC = ZI(JPONO+POSMAI-1)
      DO 10 INO = 1,NNO
        POSNO  = ZI(JNOMA+JDEC+INO-1)
        DO 11 IDIM = 1,3
          VECTA1(3*(INO-1)+IDIM) = ZR(JTGINI+6*(POSNO -1)+IDIM-1)   
          VECTA2(3*(INO-1)+IDIM) = ZR(JTGINI+6*(POSNO -1)+3+IDIM-1)
 11     CONTINUE                     
   10 CONTINUE
C
C --- VALEUR DES TANGENTES EN CE POINT PAR LISSAGE A PARTIR VALEURS 
C --- NODALES
C
      CALL MMCOOR(ALIAS ,NNO   ,NDIM  ,VECTA1,FFORME,
     &            KSI1  ,KSI2  ,TAU1  )
      IF (NDIM.EQ.3) THEN
        CALL MMCOOR(ALIAS ,NNO   ,NDIM  ,VECTA2,FFORME,
     &              KSI1  ,KSI2  ,TAU2  )    
      ENDIF 
C
      CALL JEDEMA()
      END
