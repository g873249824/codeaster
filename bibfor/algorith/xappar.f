      SUBROUTINE XAPPAR(PREMIE,NOMA,MODELE,DEFICO)
      
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/10/2007   AUTEUR NISTOR I.NISTOR 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
      IMPLICIT NONE
      LOGICAL      PREMIE
      CHARACTER*8  NOMA,MODELE
      CHARACTER*24 DEFICO
C
C ----------------------------------------------------------------------
C ROUTINE APPELLEE PAR : NMIBLE
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C
C REALISE L'APPARIEMENT ENTRE SURFACE ESCLAVE ET SURFACE MAITRE POUR 
C LE CONTACT METHODE CONTINUE.
C
C METHODE : POUR CHAQUE POINT DE CONTACT (SUR UNE MAILLE ESCLAVE ET
C AVEC UN SCHEMA D'INTEGRATION DONNE), ON RECHERCHE LE NOEUD MAITRE LE
C PLUS PROCHE ET ON PROJETTE SUR LES MAILLES QUI L'ENTOURE
C
C STOCKAGE DES POINTS  DE CONTACT DES SURFACES  ESCLAVES ET APPARIEMENT
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  PREMIE : VAUT .TRUE. SI PREMIER INSTANT DE STAT/DYNA_NON_LINE
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  NEWGEO : NOUVELLE GEOMETRIE (AVEC DEPLACEMENT)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      IFM,NIV,IRET,IBID,I,J,K,INI,IAD,NNO,NIVERR,TYCO
      INTEGER      NDIM,ZTABF,CFMMVD,ZMESX,NTMA,NTPC,NFIS,IFACE,IFACM
      INTEGER      JMAESC,JTABF,JNFIS,IMA,NBN,IZONE,JMOFIS,JECPD,JCMCF
      INTEGER      POSMA,JCESV1,JCESV2,JCESL1,JCESL2,JCESD1,JCESD2,JDIM
      INTEGER      MMAIT,AMAIT,NMAIT,PMAIT,ZMAIT,POSMIN,ITEMAX
      REAL*8       GEOM(3),XPG,YPG,HPG,TN(9),VEC1(3),DR(2,9)
      REAL*8       T1MIN(3),T2MIN(3),XIMIN,YIMIN,LAMBDA,R8BID
      REAL*8       TOLEOU,EPSMAX
      REAL*8       DDR(3,9),PINT(4),TAU1(3),TAU2(3),NMIN(3),JEUMIN
      CHARACTER*8  NOMFIS,ALIAS
      CHARACTER*19 CHS1,CHS2
      CHARACTER*24 MAESCL,TABFIN, NDIMCO, K24BID,K24BLA
      LOGICAL      PROJIN,CTCINI,LBID,LGLISS
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)      
C      
C --- RECUPERATION DE QUELQUES DONNEES      
C
      MAESCL = DEFICO(1:16) // '.MAESCL'
      TABFIN = DEFICO(1:16) // '.TABFIN'
      
      CHS1 = '&&XAPPAR.CHS1'
      CHS2 = '&&XAPPAR.CHS2'
      ALIAS= 'SG2'   

      DO 9 I=1,3
        GEOM(I)=0.D0
  9   CONTINUE

      NDIMCO = DEFICO(1:16) // '.NDIMCO'
      CALL JEVEUO(NDIMCO,'L',JDIM)
      NDIM=ZI(JDIM)
C      NNO=2
C---------------------------------------------------

      CALL JEVEUO(MAESCL,'L',JMAESC)
      CALL JEVEUO(TABFIN,'E',JTABF)
C
      ZTABF = CFMMVD('ZTABF')  
      ZMESX = CFMMVD('ZMESX')    
C
C --- INFOS GENERIQUES POUR L'ALGORITHME D'APPARIEMENT
      CALL MMINFP(0,DEFICO,K24BLA,'PROJ_NEWT_ITER',
     &            ITEMAX,R8BID,K24BID,LBID)
      CALL MMINFP(0,DEFICO,K24BLA,'PROJ_NEWT_EPSI',
     &            IBID,EPSMAX,K24BID,LBID)   
      CALL MMINFP(0,DEFICO,K24BLA,'TOLE_PROJ_EXT',
     &            IBID,TOLEOU,K24BID,LBID)     
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> APPARIEMENT' 
      ENDIF   

C----ON RECUPERE LES CHAMPS DES GEOMETRIE ESCLAVE ET MAITRE
      CALL CELCES(MODELE//'.TOPOFAC.GE','V',CHS1)
      CALL CELCES(MODELE//'.TOPOFAC.GM','V',CHS2)
      CALL JEVEUO(CHS1//'.CESD','L',JCESD1)
      CALL JEVEUO(CHS2//'.CESD','L',JCESD2)
      CALL JEVEUO(CHS1//'.CESV','L',JCESV1)
      CALL JEVEUO(CHS2//'.CESV','L',JCESV2)
      CALL JEVEUO(CHS1//'.CESL','L',JCESL1)
      CALL JEVEUO(CHS2//'.CESL','L',JCESL2)

      NTMA = ZI(JMAESC)           
      NTPC=0

      DO 100 IMA=1,NTMA

        POSMA  = ZI(JMAESC+ZMESX*(IMA-1)+1)
        IZONE  = ZI(JMAESC+ZMESX*(IMA-1)+2)
        NBN    = ZI(JMAESC+ZMESX*(IMA-1)+3)

        IF (NBN.EQ.0) GO TO 100

        CALL MMINFP(IZONE,DEFICO,K24BLA,'INTEGRATION',
     &              TYCO,R8BID,K24BID,LBID)
        CALL MMINFP(IZONE,DEFICO,K24BLA,'CONTACT_INIT',
     &              IBID,R8BID,K24BID,CTCINI)
        CALL MMINFP(IZONE,DEFICO,K24BLA,'SEUIL_INIT',
     &              IBID,LAMBDA,K24BID,LBID)
        CALL MMINFP(IZONE,DEFICO,K24BLA,'GLISSIERE',
     &              IBID,R8BID,K24BID,LGLISS)
        LAMBDA = -ABS(LAMBDA)

        CALL JEVEUO(DEFICO(1:16)//'.ECPDON','L',JECPD)
        CALL JEVEUO(DEFICO(1:16)//'.CARACF','L',JCMCF)

C     BOUCLE SUR LES FACETTES DE CONTACT (A INTRODUIRE)
C     POUR L'INSTANT (CAS 2D) ON A UNE SEULE FACETTE
        IFACE=1

C-----BOUCLE SUR LES POINTS DE CONTACT
        DO 110 INI=1,NBN

C-----COORDONNEES DANS ELEMENT DE REFERENCE ET POIDS DU POINT DE CONTACT
C-----Attention! Le type de maille de contact, 'SG2' est introduite
C-----en dur ici pour le cas 2D!

         CALL MMGAUS(ALIAS,TYCO,INI,XPG,YPG,HPG)

C --- CALCUL DES COORDONNEES REELLES DU POINT DE CONTACT 
         CALL XCOPCO(CHS1,ALIAS,POSMA,XPG,YPG,GEOM)

C --- RECHERCHE DU POINT D'INTERSECTION SUR LE COTE MAÎTRE
C     LE PLUS PROCHE DU POINT DE CONTACT
         CALL XMREPT(MODELE,NDIM,GEOM,JMAESC,ZMAIT,
     &               MMAIT,AMAIT,NMAIT,PMAIT)

C----- PROJECTION DU POINT DE CONTACT SUR LA FACETTE DE CONTACT 
C----- LA PLUS PROCHE
         CALL XMREMA(MODELE,NOMA,NDIM,DEFICO,IZONE,MMAIT,
     &               PMAIT,AMAIT,NMAIT,ZMAIT,GEOM,JMAESC,
     &               POSMIN,JEUMIN,NMIN,T1MIN,T2MIN,XIMIN,
     &               YIMIN,PROJIN)

C----ON TROUVE LA FACETTE MAITRE DONT LE PROJETE APPARTIENT (EN
C--- 3D CA VA CHANGER!!!)
         IFACM=1

C --- STOCKAGE DES VALEURS DANS LA CARTE (VOIR XMCART) 
C
C-----NUMERO DE LA MAILLE ESCLAVE
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+1)  = POSMA
C-----NUMERO DE LA MAILLE MAITRE
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+2)  = POSMIN
C-----COORDONNEES GEOMETRIQUES DU POINT DE CONTACT	  
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+3)  = XPG
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+12) = YPG
C-----COORDONNEES PARAMETRIQUES DU PROJETE
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+4)  = XIMIN
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+5)  = YIMIN
C-----VECTEURS TANGENTS DANS LE POINT DE CONTACT 
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+6)  = T1MIN(1)
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+7)  = T1MIN(2)
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+8)  = T1MIN(3)
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+9)  = T2MIN(1)
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+10) = T2MIN(2)
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+11) = T2MIN(3)
C------NUMERO DE LA ZONE DE FISSURE  
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+15) = IZONE
C------POIDS DU POINT DU CONTACT
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+16) = HPG
C------NUMERO LOCAL DE LA MAILLE DE CONTACT, CELUI DE MAESCL	  
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+23) = IMA
C------NUMERO DE LA FACETTE ESCLAVE
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+25) = IFACE
C------NUMERO DE LA FACETTE MAITRE
         ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+26) = IFACM

C --- CONTACT_INIT (POS. 13) ET MEMOIRE DE CONTACT (POS. 28)
C --- LA MEMOIRE DE CONTACT EST INITIALISEE AVEC CONTACT_INI
C-----ET SERT POUR LE CAS DU CONTACT GLISSIERE
C 
         IF (PREMIE) THEN
           IF (CTCINI) THEN
             ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+13) = 1.D0
             ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+28) = 1.D0
           ELSE
             ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+13) = 0.D0
             ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+28) = 0.D0
           END IF
         ENDIF  
C
C ----- ON RENSEIGNE LE CONTACT GLISSIERE SI DECLARE
        IF (PREMIE) THEN
           IF (LGLISS) THEN
             ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+29) = 1.D0
           ELSE
             ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+29) = 0.D0
           END IF
         ENDIF 
  
C --- SEUIL_INIT
C 
         IF (PREMIE) THEN
           ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+14) = LAMBDA
         ENDIF  
C
C --- NOEUDS EXCLUS PAR PROJECTION HORS ZONE
C
         IF (.NOT. PROJIN) THEN
           ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+22) = 1.D0
         ELSE
           ZR(JTABF+ZTABF*NTPC+ZTABF*(INI-1)+22) = 0.D0
         ENDIF

 110    CONTINUE
         NTPC = NTPC + NBN

C      FIN DE LA BOUCLE DUR LES FACETTES DE CONTACT (A INTRODUIRE) 

 100  CONTINUE

      ZR(JTABF-1+1) = NTPC

      CALL JEDEMA()
      END
