      SUBROUTINE MMTANR(NOMA  ,NDIMG ,NEQ   ,DEFICO,RESOCO,
     &                  IZONE ,LPIVAU,LFROTT,POSMAE,POSMAM,
     &                  NUMMAM,TYCO  ,IPC   ,KSI1  ,KSI2  ,
     &                  TAU1M ,TAU2M ,EXNOEC,EXNOEF,NBEXFR,
     &                  NDEXFR,TAU1  ,TAU2  ,ALIAS)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 21/10/2008   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C TOLE CRP_21
C
      IMPLICIT NONE
      LOGICAL       LPIVAU,LFROTT
      CHARACTER*8   NOMA,ALIAS
      INTEGER       NEQ,NDIMG
      INTEGER       POSMAM,POSMAE,IZONE,NUMMAM
      REAL*8        KSI1,KSI2   
      CHARACTER*24  DEFICO,RESOCO
      REAL*8        TAU1M(3),TAU2M(3)
      REAL*8        TAU1(3),TAU2(3)
      INTEGER       IPC
      INTEGER       NBEXFR,NDEXFR(3)         
      LOGICAL       EXNOEC,EXNOEF
      INTEGER       TYCO
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - APPARIEMENT)
C
C MODIFIE LES VECTEURS TANGENTS LOCAUX SUIVANT OPTIONS
C      
C ----------------------------------------------------------------------
C
C
C IN  LPIVAU : SI MECANISME AUTO DETECTION DES REDONDANCES ACTIVE
C IN  LFROTT : FROTTEMENT
C IN  NOMA   : NOM DU MAILLAGE
C IN  METHOD : TYPE DE METHODE DE CONTACT (CONTINUE OU DISCRET)
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT 
C IN  RESOCO : SD POUR LA RESOLUTION DE CONTACT
C IN  NDIMG  : DIMENSION DE L'ESPACE
C IN  NEQ    : NOMBRE D'EQUATIONS DU SYSTEME
C IN  IZONE  : ZONE DE CONTACT ACTIVE
C IN  POSMAM : MAILLE MAITRE QUI RECOIT LA PROJECTION
C IN  NUMMAM : NUMERO ABSOLU MAILLE MAITRE QUI RECOIT LA PROJECTION
C IN  POSMAE : MAILLE ESCLAVE CONTENANT LE POINT DE CONTACT
C IN  TYCO   : TYPE D'INTEGRATION
C IN  IPC    : NUMERO DU POINT DE CONTACT (DEPEND SCHEMA INTEGRATION)
C IN  EXNOEF : VAUT .TRUE. SI LE NOEUD DOIT ETRE EXCLU DE 
C              LA SURFACE DE CONTACT SUIVANT UNE DIRECTION (FROTTEMENT)
C I/O NBEXFR : NOMBRE DE NOEUDS A EXCLURE SUR LA MAILLE FROTTEMENT
C I/O NDEXFR : NUMERO DES NOEUDS EXCLUS SUR LA MAILLE FROTTEMENT
C IN  KSI1   : PREMIERE COORDONNEE PARAMETRIQUE PT CONTACT PROJETE 
C              SUR MAILLE MAITRE
C IN  KSI2   : SECONDE COORDONNEE PARAMETRIQUE PT CONTACT PROJETE 
C              SUR MAILLE MAITRE
C IN  TAU1M  : PREMIERE TANGENTE SUR LA MAILLE MAITRE AU POINT ESCLAVE
C              PROJETE
C IN  TAU2M  : SECONDE TANGENTE SUR LA MAILLE MAITRE AU POINT ESCLAVE
C              PROJETE
C OUT EXNOEC : VAUT .TRUE. SI LE NOEUD DOIT ETRE EXCLU COMPLETEMENT
C OUT TAU1   : PREMIERE TANGENTE LOCALE AU POINT ESCLAVE PROJETE
C OUT TAU2   : SECONDE TANGENTE LOCALE AU POINT ESCLAVE PROJETE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      VALRET,NUMENM
      REAL*8       NORM(3),R8PREM,NOOR 
      CHARACTER*24 NOMACO
      INTEGER      JNOMA
      INTEGER      POSNOE,NBNOE,JDECNO 
      CHARACTER*4  TYPENM        
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ() 
C
C --- INITIALISATIONS
C         
      IF (NUMMAM.LE.0) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
      NUMENM = NUMMAM  
      TYPENM = 'MAIL'    
C      
C --- RECUPERATION DE QUELQUES DONNEES      
C
      NOMACO = DEFICO(1:16)//'.NOMACO'  
      CALL JEVEUO(NOMACO,'L',JNOMA)      
C
C --- NOEUD ESCLAVE SI INTEGRATION AUX NOEUDS
C
      IF (TYCO.EQ.1) THEN
C       GLUTE POUR QUAD8 POUR LEQUEL NBNOE=8<NBPC=9
        IF (ALIAS.EQ.'QU8') THEN
          POSNOE = -1
        ELSE
          CALL CFNBEN(NOMA  ,DEFICO,POSMAE,'MAIL',NBNOE ,
     &                JDECNO)
          IF (IPC.LE.NBNOE) THEN
            POSNOE = ZI(JNOMA+JDECNO+IPC-1)
          ELSE
            CALL ASSERT(.FALSE.)
          ENDIF
        ENDIF
      ELSE
        POSNOE = 0  
      ENDIF      
C
C --- CHOIX DE LA NORMALE
C
      CALL CFTANR(NOMA  ,NDIMG ,DEFICO,IZONE ,POSNOE,
     &            TYPENM,POSMAM,NUMENM,KSI1  ,KSI2  ,
     &            TAU1M ,TAU2M ,TAU1  ,TAU2  )              
C                 
C --- ON REDEFINI TANGENTE AVEC SANS_GROUP_NO_FR
C 
      IF (LFROTT) THEN 
        CALL MMTGEX(NOMA  ,NDIMG ,DEFICO,IZONE ,POSMAE,
     &              IPC   ,EXNOEF,NBEXFR,NDEXFR,TAU1  ,
     &              TAU2  ,VALRET)
        IF (VALRET.NE.0) THEN
          CALL ASSERT(.FALSE.)
        ENDIF  
      ENDIF   
C
C --- ON TESTE SI NOEUD EST IMPLIQUE DANS DES RELATIONS
C --- DE TYPE LAGRANGE/LIAISON_DDL
C      
      IF (LPIVAU) THEN 
        CALL MMNORM(NDIMG ,TAU1  ,TAU2  ,NORM  ,NOOR)
        IF (NOOR.LE.R8PREM()) THEN
          CALL ASSERT(.FALSE.)
        ENDIF                        
        CALL REDCEX(NEQ   ,NOMA  ,DEFICO,RESOCO,NORM  ,
     &              IPC   ,POSMAE,EXNOEC)
      END IF
C
      CALL JEDEMA() 
C
      END
