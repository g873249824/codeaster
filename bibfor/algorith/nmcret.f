      SUBROUTINE NMCRET(SDERRO,TYPCOD,VALI  )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 02/10/2012   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*24 SDERRO
      CHARACTER*3  TYPCOD
      INTEGER      VALI
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (SD ERREUR)
C
C GENERE UN EVENEMENT A PARTIR D'UN CODE RETOUR
C
C ----------------------------------------------------------------------
C
C
C IN  SDERRO : SD GESTION DES ERREURS
C IN  TYPCOD : TYPE DU CODE RETOUR
C             'LDC' - INTEG. DU COMPORTEMENT
C                -1 : PAS D'INTEGRATION DU COMPORTEMENT
C                 0 : CAS DU FONCTIONNEMENT NORMAL
C                 1 : ECHEC DE L'INTEGRATION DE LA LDC
C                 2 : ERREUR SUR LA NON VERIF. DE CRITERES PHYSIQUES
C                 3 : SIZZ PAS NUL POUR C_PLAN DEBORST
C             'PIL' - PILOTAGE
C                -1 : PAS DE CALCUL DU PILOTAGE
C                 0 : CAS DU FONCTIONNEMENT NORMAL
C                 1 : PAS DE SOLUTION
C                 2 : BORNE ATTEINTE -> FIN DU CALCUL
C             'FAC' - FACTORISATION
C                -1 : PAS DE FACTORISATION
C                 0 : CAS DU FONCTIONNEMENT NORMAL
C                 1 : MATRICE SINGULIERE
C                 2 : ERREUR LORS DE LA FACTORISATION
C                 3 : ON NE SAIT PAS SI SINGULIERE
C             'RES' - RESOLUTION
C                -1 : PAS DE RESOLUTION
C                 0 : CAS DU FONCTIONNEMENT NORMAL
C                 1 : NOMBRE MAXIMUM D'ITERATIONS ATTEINT
C             'CTC' - CONTACT DISCRET
C                -1 : PAS DE CALCUL DU CONTACT DISCRET
C                 0 : CAS DU FONCTIONNEMENT NORMAL
C                 1 : NOMBRE MAXI D'ITERATIONS
C                 2 : MATRICE SINGULIERE
C IN  VALI   : VALEUR DU CODE RETOUR
C
C
C
C
      INTEGER      IEVEN,ZEVEN
      CHARACTER*24 ERRECN,ERRECV,ERRENO
      INTEGER      JEECON,JEECOV,JEENOM
      CHARACTER*24 ERRINF
      INTEGER      JEINF
      INTEGER      VCRET
      CHARACTER*8  NCRET
      CHARACTER*9  NEVEN
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- L'OPERATION N'A PAS EU LIEU -> ON SORT
C
      IF (VALI.EQ.-1) GOTO 999
C
C --- ACCES SD
C
      ERRINF = SDERRO(1:19)//'.INFO'
      CALL JEVEUO(ERRINF,'L',JEINF)
      ZEVEN  = ZI(JEINF-1+1)
C
      ERRENO = SDERRO(1:19)//'.ENOM'
      ERRECV = SDERRO(1:19)//'.ECOV'
      ERRECN = SDERRO(1:19)//'.ECON'
      CALL JEVEUO(ERRENO,'L',JEENOM)
      CALL JEVEUO(ERRECV,'L',JEECOV)
      CALL JEVEUO(ERRECN,'L',JEECON)
C
C --- RECHERCHE EVENEMENT ATTACHE A CE CODE-RETOUR
C
      DO 15 IEVEN = 1,ZEVEN
C
C ----- NOM DE L'EVENEMENT
C
        NEVEN  = ZK16(JEENOM-1+IEVEN)(1:9)
C
C ----- NOM DU CODE RETOUR
C
        NCRET  = ZK8 (JEECON-1+IEVEN)
C
C ----- VALEUR DU CODE RETOUR
C
        VCRET  = ZI  (JEECOV-1+IEVEN)
C
C ----- ACTIVATION DE L'EVENEMENT
C
        IF (NCRET.EQ.TYPCOD) THEN
          IF (VCRET.EQ.VALI) THEN
            CALL NMCREL(SDERRO,NEVEN,.TRUE. )
          ELSE
            CALL NMCREL(SDERRO,NEVEN,.FALSE.)
          ENDIF
        ENDIF
 15   CONTINUE
C
 999  CONTINUE
C
      CALL JEDEMA()
      END
