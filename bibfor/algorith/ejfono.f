      SUBROUTINE EJFONO(NDIM,NDDL,AXI,NNO1,NNO2,NPG,IPG,WREF,VFF1,VFF2,
     &                  IDF2,DFFR2,GEOM,IU,IP,SIGM,VECT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 15/04/2013   AUTEUR LAVERNE J.LAVERNE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRS_1404
       IMPLICIT NONE
       LOGICAL AXI
       INTEGER NDIM,IDF2,IPG,NDDL,NNO1,NNO2,NPG,IU(3,16),IP(4)
       REAL*8  VFF1(NNO1,NPG),VFF2(NNO2,NPG),GEOM(NDIM,NNO2),WREF(NPG)
       REAL*8  DFFR2(NDIM-1,NNO2,NPG)
       REAL*8  VECT(NDDL),SIGM(2*NDIM-1,NPG)
C ----------------------------------------------------------------------
C      OPTION FORC_NODA LES JOINTS QUADRA ET HYME
C ----------------------------------------------------------------------
C IN  NDIM    : DIMENSION DES ELEMENTS 
C IN  NDDL    : NOMBRE DE DDL
C IN  AXI     : .TRUE. SI AXISYMETRIE
C IN  NNO1    : NOMBRE DE NOEUDS (FAMILLE U)
C IN  VFF1    : VALEUR DES FONCTIONS DE FORME (FAMILLE U)
C IN  NNO2    : NOMBRE DE NOEUDS (FAMILLE P)
C IN  VFF2    : VALEUR DES FONCTIONS DE FORME (FAMILLE P)
C IN  DFFR2   : DERIVEES DES FONCTIONS DE FORME DE REFERENCE (FAMILLE P)
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  WREF    : POIDS DES POINTS DE GAUSS DE REFERENCE 
C IN  GEOM    : COORDONNEES DES NOEUDS
C IN  IU      : DECALAGE D'INDICE POUR ACCEDER AUX DDL DE DEPLACEMENT
C IN  IP      : DECALAGE D'INDICE POUR ACCEDER AUX DDL DE PRESSION
C OUT VECT    : FORCES INTERIEURES DE REFERENCE
C ----------------------------------------------------------------------
      INTEGER KPG,N,I,J,KK
      REAL*8  WG,B(2*NDIM-1,NDIM+1,2*NNO1+NNO2),TEMP,ROT(NDIM*NDIM)
C ----------------------------------------------------------------------

      CALL R8INIR(NDDL , 0.D0, VECT,1)
      
      DO 1000 KPG=1,NPG

        CALL EJCINE(NDIM,AXI,NNO1,NNO2,VFF1(1,KPG),VFF2(1,KPG),
     &              WREF(KPG),DFFR2(1,1,KPG),GEOM,WG,KPG,IPG,IDF2,ROT,B)


C       VECTEUR FINT : U
        DO 300 N=1,2*NNO1
          DO 301 I=1,NDIM
        
            KK = IU(I,N)
            TEMP = 0.D0
            DO 320 J = 1,NDIM
              TEMP = TEMP + B(J,I,N)*SIGM(J,KPG)
 320        CONTINUE
  
            VECT(KK) = VECT(KK) + WG*TEMP
          
 301      CONTINUE
 300    CONTINUE

C       VECTEUR FINT : P
        DO 302 N=1,NNO2
        
          KK = IP(N)
          TEMP = 0.D0
          DO 321 I = NDIM+1,2*NDIM-1
            TEMP = TEMP + B(I,NDIM+1,2*NNO1+N)*SIGM(I,KPG)
 321      CONTINUE
 
          VECT(KK) = VECT(KK) + WG*TEMP
                      
 302    CONTINUE

 1000 CONTINUE
      END
