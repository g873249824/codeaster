      SUBROUTINE TTPRSM(NDIM,DEPLE,DEPLM,
     &                  TYALGF,COEFFA,COEFFS,COEFFP,
     &                  NORM,TAU1,TAU2,INADH,MPROJ,
     &                  RESE,NRESE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/02/2007   AUTEUR TORKHANI M.TORKHANI 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

      IMPLICIT NONE
      INTEGER NDIM
      REAL*8  DEPLE(6)
      REAL*8  DEPLM(6)
      REAL*8  COEFFA,COEFFS,COEFFP
      REAL*8  TAU1(3),TAU2(3),NORM(3)
      INTEGER INADH,TYALGF
      REAL*8  MPROJ(3,3)
      REAL*8  RESE(3)
      REAL*8  NRESE
C
C ----------------------------------------------------------------------
C ROUTINE APPELLEE PAR : TE0364/TE0365
C ----------------------------------------------------------------------
C
C  ON TESTE L'ETAT D'ADHERENCE DU POINT DE CONTACT PC
C  CALCUL DE P_B(0,1)(LAMDBA+ RHO C [[DELTA X]])
C
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  DEPLM  : LE CHAMP DE DEPLACEMENT DU PT MAITRE
C IN  DEPLE  : LE CHAMP DE DEPLACEMENT DU PT ESCLAVE
C IN  COEFFA : COEFFICIENT DE REGULARISATION DE FROTTEMENT
C IN  COEFFS : COEFFICIENT DE STABILISATION DE FROTTEMENT
C IN  COEFFP : COEFFICIENT DE D'AUGMENTATION DE FROTTEMENT
C IN  TYALGF : TYPE D'ALGORITHME DE FROTTEMENT
C IN  NORM   : NORMALE AU POINT DE CONTACT
C IN  TAU1   : PREMIER VECTEUR TANGENT
C IN  TAU2   : SECOND VECTEUR TANGENT
C OUT MPROJ  : MATRICE DE L'OPERATEUR DE PROJECTION
C OUT INADH  : INDICE D'ADHERENCE
C               1: ADHERENT
C               0: GLISSANT
C OUT RESE   : X SI ADHERENT X/||X|| SINON
C OUT NRESE  : RACINE DE LA NORME DE RESE
C
C ----------------------------------------------------------------------
C
      INTEGER I,J,K
      REAL*8  GEOMI(3),BIDON(3)
C
C ----------------------------------------------------------------------
C
C --- INITIALISATIONS
C
      NRESE = 0.D0
      DO 10 I = 1,3
        RESE(I)  = 0.D0
        BIDON(I) = 0.D0
        DO 11 J = 1,3
          MPROJ(I,J) = 0.D0
   11   CONTINUE
   10 CONTINUE
C
C --- CALCUL DE LA MATRICE DE L'OPERATEUR DE PROJECTION
C
      DO 320 I = 1,NDIM
        DO 310 J = 1,NDIM
          MPROJ(I,J) = -1.D0*NORM(I)*NORM(J)
  310   CONTINUE
  320 CONTINUE
      DO 330 I = 1,NDIM
        MPROJ(I,I) = 1 + MPROJ(I,I)
  330 CONTINUE
C
C --- CALCUL DE LA "VITESSE"[[DELTA X]]
C
      DO 12 I = 1,NDIM
        GEOMI(I) = DEPLE(I) - DEPLM(I)
   12 CONTINUE
      DO 21 I=1,NDIM
        DO 22  K=1,NDIM
          BIDON(I) = MPROJ(I,K)*GEOMI(K)+BIDON(I)
22      CONTINUE
21    CONTINUE

      IF (TYALGF .NE. 1) THEN
        COEFFA = COEFFS
      END IF
      
      IF (NDIM.EQ.2) THEN
        DO 30 I = 1,2
          RESE(I) = DEPLE(NDIM+1+1)*TAU1(I)+COEFFA* BIDON(I)
   30   CONTINUE
      ELSE IF (NDIM.EQ.3) THEN
        DO 31 I = 1,3
          RESE(I) = DEPLE(NDIM+1+1)*TAU1(I)+DEPLE(NDIM+3)*TAU2(I)
     &              +COEFFA*BIDON(I)
   31   CONTINUE
      ELSE
         CALL U2MESS('F','ALGORITH11_4')
      END IF
C
C -- CALCUL DU COEF D'ADHERENCE
C
      DO 40 I = 1,3
        NRESE = RESE(I)*RESE(I) + NRESE
 40   CONTINUE
      NRESE = SQRT(NRESE)
C
C --- ON TESTE SI    NRESE < 1 OU NON
C --- SI OUI ADHERENCE
C
      IF (NRESE.LE.1.D0) THEN
        INADH = 1
      ELSE
        INADH = 0
      END IF
      
      IF (INADH.EQ.1 .AND. TYALGF .EQ. 2) THEN
        DO 43 I = 1,3
          RESE(I)  = 0.D0
   43   CONTINUE
        IF (NDIM.EQ.2) THEN
          DO 41 I = 1,2
            RESE(I) = DEPLE(NDIM+1+1)*TAU1(I)
   41     CONTINUE
        ELSE IF (NDIM.EQ.3) THEN
          DO 42 I = 1,3
            RESE(I) = DEPLE(NDIM+1+1)*TAU1(I)+DEPLE(NDIM+3)*
     &                TAU2(I)
   42     CONTINUE
        ELSE
          CALL U2MESS('F','ALGORITH11_4')
        END IF 
      END IF
      
   50 CONTINUE

      END
