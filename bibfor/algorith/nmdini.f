      SUBROUTINE NMDINI(MOTFAC,IOCC  ,PROVLI,INSTIN,LINSEI,
     &                  TOLE  ,NBINST,LINSTI,NUMINI)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE 
      INCLUDE 'jeveux.h'
      CHARACTER*16 MOTFAC
      CHARACTER*19 PROVLI
      REAL*8       TOLE,INSTIN
      LOGICAL      LINSEI,LINSTI
      INTEGER      NUMINI,NBINST,IOCC
C
C ----------------------------------------------------------------------
C
C ROUTINE *_NON_LINE (STRUCTURES DE DONNES - DISCRETISATION)
C
C DETERMINATION DU NUMERO D'ORDRE INITIAL
C
C ----------------------------------------------------------------------
C
C
C IN  MOTFAC : MOT-CLEF FACTEUR POUR INFO SUR INCREMENT
C IN  IOCC   : OCCURRENCE DU MOT-CLEF FACTEUR MOTFAC
C IN  PROVLI : NOM DE LA LISTE D'INSTANT PROVISOIRE
C IN  TOLE   : TOLERANCE POUR RECHERCHE DANS LISTE D'INSTANTS
C IN  LINSEI : .TRUE. SI L'INSTANT DE L'ETAT INITIAL EXISTE
C IN  INSTIN : INSTANT INITIAL QUAND ETAT_INIT
C IN  NBINST : NOMBRE D'INSTANTS DANS LA LISTE
C OUT LINSTI : .TRUE. SI L'INSTANT INITIAL N'EXISTAIT PAS 
C              DANS LA LISTE D'INSTANTS -> ON A PRIS LE PLUS PROCHE
C OUT NUMINI : NUMERO D'ORDRE INITIAL
C
C
C
C
      INTEGER      N1,N2,I
      REAL*8       INST,INS,DT,DTMIN
      INTEGER      JINST
      INTEGER      IARG
C      
C ----------------------------------------------------------------------
C      
      CALL JEMARQ() 
C
C --- INITIALISATIONS
C
      NUMINI = 0
      LINSTI = .FALSE.
C
C --- ACCES LISTE D'INSTANTS PROVISOIRE
C
      CALL JEVEUO(PROVLI,'L',JINST)
C
C --- LECTURE MOTS-CLEFS
C
      CALL GETVIS(MOTFAC,'NUME_INST_INIT',IOCC,IARG,1,NUMINI,N1)
      CALL GETVR8(MOTFAC,'INST_INIT'     ,IOCC,IARG,1,INST  ,N2)
C
C --- PAS D'OCCURRENCE DES MOTS-CLES -> NUMERO INITIAL
C
      IF ((N1+N2 .EQ. 0).AND.(.NOT.LINSEI)) THEN     
        NUMINI = 0
C
C --- MOTS-CLES INST_INIT OU INSTANT DEFINI PAR ETAT_INIT
C
      ELSE IF (N1 .EQ. 0) THEN
        IF (N2 .EQ. 0) THEN
C
C ------- INSTANT DEFINI PAR ETAT_INIT
C
          INST   = INSTIN
          CALL UTACLI(INST  ,ZR(JINST),NBINST,TOLE  ,NUMINI)
C
C ------- SI INST NON PRESENT DANS LA LISTE D'INSTANT
C ------- ON CHERCHE L INSTANT LE PLUS PROCHE AVANT L'INSTANT CHERCHE
C
          IF (NUMINI.LT.0) THEN
            LINSTI = .TRUE.
            DTMIN  = INST - ZR(JINST)
            INS    = ZR(JINST)
            DO 40 I=1,NBINST-1
              DT     = INST - ZR(JINST+I)
              IF (DT.LE.0.D0) THEN
                GOTO 45
              ENDIF
              IF (DT.LT.DTMIN) THEN
                DTMIN  = DT
                INS    = ZR(JINST+I)
              ENDIF
 40         CONTINUE
 45         CONTINUE
            INST   = INS
          ENDIF
        ENDIF
C
        CALL UTACLI(INST  ,ZR(JINST),NBINST,TOLE  ,NUMINI)
        IF (NUMINI .LT. 0) THEN
          CALL U2MESR('F','DISCRETISATION_89',1,INST  )
        ENDIF
      ENDIF
C
C --- VERIFICATIONS
C
      CALL ASSERT(NUMINI.GE.0)
      CALL ASSERT(NUMINI.LE.NBINST)        
C
      CALL JEDEMA()

      END
