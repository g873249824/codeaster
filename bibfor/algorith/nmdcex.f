      SUBROUTINE NMDCEX(SDDISC,ITERAT,DELTAT,LENIVO,DEPART,
     &                  NBRPAS,RATIO ,RETOUR)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 03/11/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*19 SDDISC
      INTEGER      ITERAT,DEPART,RETOUR
      INTEGER      NBRPAS,LENIVO
      REAL*8       RATIO,DELTAT
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME - DECOUPE)
C
C DECOUPE PAR EXTRAPOLATION SUR INTERVALLE [DEBUT,ITERAT]
C
C ----------------------------------------------------------------------
C
C
C IN  SDDISC : SD DISCRETISATION
C IN  ITERAT : NUMERO D'ITERATION DE NEWTON
C IN  DELTAT : INCREMENT DE TEMPS
C IN  LENIVO : NIVEAU DE SOUS-DECOUPE COURANT
C IN  DEPART : NUMERO ITERATION POUR CALCUL EXTRAPOLATION
C OUT NBRPAS : NOMBRE DE PAS A CREER
C OUT RATIO  : RATIO DE CREATION DES PAS
C OUT RETOUR : CODE RETOUR ALGORITHME EXTRAPOLATION
C                0 - ON DECOUPE
C                3 - ON AUTORISE DES ITERATIONS EN PLUS
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C 
      REAL*8       ZERO,UN,DEUX
      PARAMETER   (ZERO=0.0D0, UN=1.0D0, DEUX=2.0D0)
C
      CHARACTER*24 TPSERR
      INTEGER      JERRE
      INTEGER      I,NBVAL,REGRES
      REAL*8       ERRREL,ERRMAX,R8PREM
      REAL*8       GLBREL,GLBMAX
      REAL*8       CIBLE,CIBLEN
      REAL*8       SX,SY,SX2,SYX
      REAL*8       XDET,XA0,XA1,XX,XXBB,XN
      REAL*8       MINITE,MAXITE
      REAL*8       VALRM(4)
      INTEGER      VALIM(2)
      CHARACTER*40 VALKM(1)      
C      
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      RETOUR = 0
C
C --- ACCES SD LISTE D'INSTANTS
C    
      TPSERR = SDDISC(1:19)//'.ERRE'  
      CALL JEVEUO(TPSERR,'E',JERRE )
C
C --- LECTURE DES INFOS SUR LES CONVERGENCES : CF NMCRSU
C
      NBVAL  = NINT(ZR(JERRE))
      MINITE = ZR(JERRE + 2)
      MAXITE = ZR(JERRE + 3)
      GLBREL = ZR(JERRE + 4)
      GLBMAX = ZR(JERRE + 5)           
C      
C --- REGRESSION SUR GLOB_RELA OU GLOB_MAXI ?
C
      REGRES = NINT(ZR(JERRE + 6)) 
      ERRREL = ZR(JERRE + NBVAL + ITERAT*2 )
      ERRMAX = ZR(JERRE + NBVAL + ITERAT*2 + 1 )
C      
C --- SI REGRES=3 ON DOIT FAIRE LA REGRESSION SUR LES 2, MAIS ON
C --- COMMENCE PAR LA FAIRE SUR GLOB_RELA
C --- SI ERRREL > GLBREL ON MET REGRES=1 POUR FAIRE LA REGRESSION
C ---  SUR GLOB_RELA
C --- SI ERRMAX > GLBMAX ON MET REGRES=2 POUR FAIRE LA REGRESSION
C ---  SUR GLOB_MAXI
C
      IF  ( REGRES .EQ. 3 ) THEN
        REGRES = 0
        IF      ( ERRREL .GT. GLBREL ) THEN
          REGRES = 1
        ELSE IF ( ERRMAX .GT. GLBMAX ) THEN
          REGRES = 2
        ENDIF
      ENDIF      
      IF      ( REGRES .EQ. 1 ) THEN
        CIBLE = GLBREL
        XX    = LOG(ERRREL)
      ELSE IF ( REGRES .EQ. 2 ) THEN
        CIBLE = GLBMAX
        XX    = LOG(ERRMAX)
      ENDIF
C            
C --- LES CRITERES D'ERREUR SONT OK, MAIS PAS DETECTE DANS LE
C --- STAT_NON_LINE. CELA PEUT SE PRODUIRE QUAND IL Y A DU CONTACT
C --- AVEC DE LA REACTUALISATION GEOMETRIQUE. ON SORT SANS FAIRE
C --- DE SUBDIVISION
C
      IF ( REGRES .EQ. 0 ) THEN
        VALRM(1) = ERRREL
        VALRM(2) = GLBREL
        VALRM(3) = ERRMAX
        VALRM(4) = GLBMAX
        VALIM(1) = ITERAT
        CALL U2MESG('I','SUBDIVISE_9',0,VALKM,1,VALIM,4,VALRM)
        RETOUR = 0
        GOTO 999
      ENDIF      
C
C --- CALCUL DE LA REGRESSION
C --- ON DONNE UN POIDS DOUBLE AU 3 DERNIERS POINTS CELA REVIENT
C --- A AJOUTER DES POINTS => MEILLEURE EXTRAPOLATION
C
      SX  = ZERO
      SY  = ZERO
      SX2 = ZERO
      SYX = ZERO
      XN  = ZERO
      DO 110 I = DEPART, ITERAT
        IF ( I .GT. (ITERAT - 3) ) THEN
           XN   = XN  + DEUX
           SX   = SX  + DEUX*XX
           SY   = SY  + DEUX*I
           SX2  = SX2 + DEUX*(XX**2)
           SYX  = SYX + DEUX*XX*I
        ELSE
           XN   = XN  + UN
           SX   = SX  + XX
           SY   = SY  + I
           SX2  = SX2 + XX**2
           SYX  = SYX + XX*I
        ENDIF
110   CONTINUE
      XDET   = -SX**2 + SX2*XN
      XA0    =  SX2*SY - SX*SYX
      XA1    = -(SX*SY) + SYX*XN
C
C --- NOMBRE D'ITERATIONS TROUVEES
C      
      CIBLEN = (XA0 + XA1*LOG(CIBLE) )/XDET

C        LA METHODE TROUVE UN NOMBRE D'ITERATION < LIMITE ITERATION
C        CELA SE PRODUIT SI ON DONNE ITER_GLOB_MAXI ET ITER_GLOB_ELAS.
C        SI ITER_GLOB_ELAS EST < ITER_GLOB_MAXI ALORS L'EXTRAPOLATION
C        QUI EST FAITE LORS DE ITER_GLOB_ELAS PEUT DONNER UN NOMBRE
C        ENTRE CES 2 VALEURS.
C        ON COMPARE PAR RAPPORT AU MIN DES 2 VALEURS ITER_GLOB
C        IL FAUT FAIRE ATTENTION AUX ARRONDIS PAR SECURITE AJOUT 20%

      IF ( (CIBLEN*1.20D0) .LT. MINITE ) THEN
        RATIO    = 24.0D0/((3.0D0*NBRPAS+UN)**2 - UN)
        VALIM(1) = NBRPAS
        VALIM(2) = LENIVO
        VALRM(1) = RATIO
        VALRM(2) = DELTAT
        VALRM(3) = CIBLEN
        VALRM(4) = ZR(JERRE+2) 
        IF ( REGRES .EQ. 1 ) THEN
          VALKM(1) = 'RESI_GLOB_RELA'
        ELSE
          VALKM(1) = 'RESI_GLOB_MAXI'
        ENDIF               
        CALL U2MESG('I','SUBDIVISE_11',1,VALKM,2,VALIM,4,VALRM)
        GOTO 999
      ENDIF

      VALIM(1) = CIBLEN
      VALIM(2) = NINT(MAXITE)
      CALL U2MESI('I','SUBDIVISE_16',2,VALIM)
      IF ( CIBLEN .LT. MAXITE ) THEN
        IF ( CIBLEN .LE. ITERAT ) THEN
          VALIM(1) = ITERAT
          CALL U2MESI('I','SUBDIVISE_17',1,VALIM)
        ENDIF
        RETOUR = 3
      ELSE
C       CALCUL DU RATIO POUR ATTEINDRE LA CIBLE
  
        IF (XA1.LE.R8PREM()) THEN
          RATIO    = 24.0D0/((3.0D0*NBRPAS+UN)**2 - UN)
          RETOUR   = 0
          VALIM(1) = NBRPAS
          VALIM(2) = LENIVO
          VALRM(1) = RATIO
          VALRM(2) = DELTAT
          VALRM(3) = CIBLEN
          VALRM(4) = ZR(JERRE+2) 
          IF ( REGRES .EQ. 1 ) THEN
            VALKM(1) = 'RESI_GLOB_RELA'
          ELSE
            VALKM(1) = 'RESI_GLOB_MAXI'
          ENDIF               
          CALL U2MESG('I','SUBDIVISE_11',1,VALKM,2,VALIM,4,VALRM)
          GOTO 999
        ENDIF 
        IF ( (CIBLEN-MAXITE) .LE. (-10.0D0*XA1/XDET) ) THEN
          RATIO = EXP( (CIBLEN-MAXITE)*XDET/XA1 )
        ELSE
          RATIO = EXP( -10.0D0 )
        ENDIF
        RATIO = 0.48485D0*RATIO
        XXBB = ( -UN + (UN+24.0D0/RATIO)**0.5D0 )/3.0D0
        IF ( XXBB .LT. 2.0D0 ) THEN
          NBRPAS = 2
          RATIO  = 0.50D0
        ELSE
          NBRPAS = NINT( XXBB )
        ENDIF
        VALIM(1) = NBRPAS
        VALIM(2) = LENIVO
        VALRM(1) = RATIO
        VALRM(2) = DELTAT
        CALL U2MESG('I','SUBDIVISE_18',0,VALKM,2,VALIM,2,VALRM)
      ENDIF
C         
  999 CONTINUE   
C
      CALL JEDEMA()
      END
