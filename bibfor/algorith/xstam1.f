      SUBROUTINE XSTAM1(NOMO,NOMA,NBMA,NMAFIS,MAFIS,STANO,
     &                  MAFON,MAEN1,MAEN2,MAEN3,
     &                  NMAFON,NMAEN1,NMAEN2,NMAEN3 )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 14/10/2008   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C
      IMPLICIT NONE
      INTEGER       NMAFIS,NMAFON,NMAEN1,NMAEN2,NMAEN3,NBMA
      INTEGER       NCOUCH,STANO(*),MAFIS(NMAFIS)
      INTEGER       MAFON(NMAFIS),MAEN1(NBMA),MAEN2(NBMA),MAEN3(NBMA)
      CHARACTER*8   NOMO,NOMA
C      
C ----------------------------------------------------------------------
C
C ROUTINE XFEM
C
C CALCUL DU STATUT DES MAILLES SUIVANT LE STATUT DES NOEUDS EN COURS
C
C ----------------------------------------------------------------------
C
C IN  NOMO   : NOM DU MODELE
C IN  NOMA   : NOM DU MAILLAGE      
C IN  NBMA   : NOMBRE DE MAILLES DU MAILLAGE
C IN  NMAFIS : NOMBRE DE MAILLES DE LA ZONE FISSURE
C IN  MAFIS  : VECTUER DES MAILLES DE LA ZONE FISSURE
C IN  STANO  : VECTEUR STATUT DES NOEUDS

C OUT  NMAFON : NOMBRE DE MAILLES CONTENANT LE FOND DE FISSURE
C OUT  NMAEN1 : NOMBRE DE MAILLES 'HEAVISIDE'
C OUT  NMAEN2 : NOMBRE DE MAILLES 'CRACKTIP'
C OUT  NMAEN3 : NOMBRE DE MAILLES 'HEAVISIDE-CRACKTIP'
C OUT  MAFON  : VECTEUR DES MAILLES 'CONTENANT LE FOND DE FISSURE
C OUT  MAEN1  : VECTEUR DES MAILLES 'HEAVISIDE'
C OUT  MAEN2  : VECTEUR DES MAILLES 'CRACKTIP'
C OUT  MAEN3  : VECTEUR DES MAILLES 'HEAVISIDE-CRACKTIP'
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32  JEXATR,JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER       JMOMA,JMA,NUNO,JCONX1,JCONX2
      INTEGER       I,IM1,IM2,IM3,IMA,ITYPMA,IN,IMAE,J
      INTEGER       EM,EM1,EM2,NMAABS,NBNOTT(3),NNO,EN
      INTEGER       NBNOE,INO
      CHARACTER*8   TYPMA,K8B
      CHARACTER*19  MAI
      
      CALL JEMARQ()

      I=0
      IM1=0
      IM2=0
      IM3=0

      MAI=NOMA//'.TYPMAIL'
      CALL JEVEUO(MAI,'L',JMA)
      CALL JEVEUO(NOMO//'.MAILLE','L',JMOMA)
      CALL JEVEUO(NOMA//'.CONNEX','L',JCONX1)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',JCONX2)


C     BOUCLE SUR LES MAILLES DU MAILLAGE
      DO 310 IMA=1,NBMA

        NMAABS=IMA

        ITYPMA=ZI(JMA-1+NMAABS)
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYPMA),TYPMA)
        IF (TYPMA(1:3).EQ.'POI') GOTO 310

C       ON ZAPPE LES MAILLES SANS MODELE
        IF (ZI(JMOMA-1+NMAABS).EQ.0) GOTO 310

        EM=0
        EM1=0
        EM2=0
        CALL PANBNO(ITYPMA,NBNOTT)
        NNO  = NBNOTT(1) + NBNOTT(2) + NBNOTT(3)

C       BOUCLE SUR LES NOEUDS DE LA MAILLE
        DO 311 IN=1,NNO
          NUNO = ZI(JCONX1-1+ZI(JCONX2+NMAABS-1)+IN-1)
          EN   = STANO(NUNO)
          IF (EN.EQ.1.OR.EN.EQ.3) EM1=EM1+1
          IF (EN.EQ.2.OR.EN.EQ.3) EM2=EM2+1
 311    CONTINUE
        IF (EM1.GE.1) EM=1
        IF (EM2.GE.1) EM=2
        IF (EM1.GE.1.AND.EM2.GE.1) EM=3

        IF (EM2.EQ.NNO.AND.TYPMA(1:3).NE.'SEG') THEN

C         MAILLE RETENUE POUR MAFOND (TS LS NOEUDS SONT 'CARRÉS')
C         SOUS RÉSERVE QUE CE SOIT UNE MAILLE DE MAFIS
C         ET PAS UNE MAILLE SEG
          DO 312 IMAE=1,NMAFIS
            IF (NMAABS .EQ. MAFIS(IMAE)) THEN
              I=I+1
              CALL ASSERT(I.LE.NMAFIS)
              MAFON(I)=NMAABS
C             ON SORT DE LA BOUCLE 312
              GOTO 313
            ENDIF
 312      CONTINUE
 313      CONTINUE
        ENDIF

C       ON RÉCUPÈRE LES NUMEROS DES MAILLES ENRICHIES
        IF (EM.EQ.1)   THEN
          IM1=IM1+1
          CALL ASSERT(IM1.LE.NBMA)
          MAEN1(IM1)=NMAABS
        ELSEIF (EM.EQ.2)   THEN
          IM2=IM2+1
          CALL ASSERT(IM2.LE.NBMA)
          MAEN2(IM2)=NMAABS
        ELSEIF (EM.EQ.3)   THEN
          IM3=IM3+1
          CALL ASSERT(IM3.LE.NBMA)
          MAEN3(IM3)=NMAABS
        ENDIF
 310  CONTINUE

      NMAFON=I
      NMAEN1=IM1
      NMAEN2=IM2
      NMAEN3=IM3

      CALL JEDEMA()
      END
