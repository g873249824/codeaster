      SUBROUTINE CHOR2C(LISCHA,VECELE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C
C ======================================================================
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*19 LISCHA,VECELE
C
C ----------------------------------------------------------------------
C
C TRANSFORMATION D'UNE COLLECTION DE CHAM_NO A VALEURS REELLES EN
C UNE COLLECTION DE CHAM_NO A VALEURS COMPLEXES AVEC PARTIE
C IMAGINAIRE NULLE
C
C ----------------------------------------------------------------------
C
C I/O VECELE : NOM DU VECT_ELEM
C
C
C
C
      CHARACTER*24 VACHAR
      INTEGER      JVACHA
      CHARACTER*19 CHAMNO
      INTEGER      JCN
      CHARACTER*24 RESUEL
      CHARACTER*8  K8BID,TYPECH,TYPSCA
      INTEGER      IRET,KVALE,IBID,ICHAR
      INTEGER      IVEC,NBVEC,NBVDIM,IVALE,NBVALE,JVEC
      CHARACTER*4  TYRESL
      CHARACTER*1  TYPCHN
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- VERIFICATION DU VACHAR
C
      VACHAR = VECELE//'.CHNO'
      CALL JEEXIN(VACHAR,IRET)
      CALL ASSERT(IRET.NE.0)
      CALL JELIRA(VACHAR,'LONMAX',NBVEC,K8BID)
      CALL ASSERT(NBVEC.NE.0)
C
C --- DIMENSIONNEMENT SD DE SAUVEGARDE
C
      CALL JEVEUO(VACHAR,'L',JVACHA)
      CHAMNO = ZK24(JVACHA+1-1)(1:19)
      CALL JEVEUO(VECELE//'.RELR','L',JVEC)
      CALL JELIRA(CHAMNO//'.VALE','LONMAX',NBVDIM,K8BID)
      CALL WKVECT('&&CHOR2C.COPIE_TRAVAIL','V V R',NBVDIM,KVALE)
C
C --- BOUCLES SUR LES CHAMNO
C
      DO 10 IVEC = 1,NBVEC
C
C ----- NOM DU RESU_ELEM
C
        RESUEL = ZK24(JVEC-1+IVEC)
C
C ----- NOM DU CHAMNO
C
        CHAMNO = ZK24(JVACHA+IVEC-1)(1:19)
C
C ----- RECUPERATION DU NUMERO DE LA CHARGE DU RESU_ELEM
C
        CALL CORICH('L',RESUEL,IBID,ICHAR)
        CALL ASSERT((ICHAR.NE.0).AND.(ICHAR.GE.-2))
C
C ----- TYPE DU CHARGEMENT
C
        CALL DISMOI('F','TYPE_CHAMP',RESUEL,'CHAMP',IBID,
     &              TYRESL,IBID)
        IF (TYRESL.EQ.'RESL') THEN
          CALL DISMOI('F','TYPE_SCA',RESUEL,'RESUELEM',IBID,
     &                TYPSCA,IBID)
          IF (TYPSCA.EQ.'R') THEN
            TYPCHN = 'R'
          ELSEIF (TYPSCA.EQ.'C') THEN
            TYPCHN = 'C'
          ELSE
            CALL ASSERT(.FALSE.)
          ENDIF
        ELSEIF (TYRESL.EQ.'NOEU') THEN
          CALL LISLTC(LISCHA,ICHAR ,TYPECH)
          TYPCHN = 'R'
          IF (TYPECH.EQ.'COMP') TYPCHN = 'C'
        ELSE

        ENDIF
C
C ----- CONVERSION
C
        IF (TYPCHN.EQ.'R') THEN
          CALL JEVEUO(CHAMNO//'.VALE','L',JCN)
          CALL JELIRA(CHAMNO//'.VALE','LONMAX',NBVALE,K8BID)
          IF (NBVDIM.NE.NBVALE) CALL ASSERT(.FALSE.)
C
C ------- SAUVEGARDE DES VALEURS
C
          CALL DCOPY(NBVALE,ZR(JCN),1,ZR(KVALE),1)
C
C ------- DESTRUCTION CHAMNO A VALEURS REELLES
C
          CALL JEDETR(CHAMNO//'.VALE')
C
C ------- CREATION CHAMNO A VALEURS COMPLEXES
C
          CALL WKVECT(CHAMNO//'.VALE','V V C',NBVALE,JCN)
          DO 20 IVALE=1,NBVALE
            ZC(JCN+IVALE-1) = DCMPLX(ZR(KVALE+IVALE-1),0.D0)
20        CONTINUE
C
C ------- CHANGEMENT DE LA REFERENCE A LA GRANDEUR
C
          CALL SDCHGD(CHAMNO,'C')
        ENDIF
10    CONTINUE
C
      CALL JEDETR('&&CHOR2C.COPIE_TRAVAIL')
      CALL JEDEMA()
      END
