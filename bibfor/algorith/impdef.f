      SUBROUTINE IMPDEF(ZDEF  ,FONACT,MODE  ,INFCMP,NBSUIV,
     &                  JIMCOL,NBCOL)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 19/12/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INTEGER      ZDEF
      LOGICAL      FONACT(*)
      INTEGER      INFCMP(4)
      INTEGER      NBSUIV
      INTEGER      JIMCOL
      INTEGER      NBCOL
      CHARACTER*8  MODE
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (AFFICHAGE)
C
C DEFINITION DES COLONNES ACTIVEES POUR LES CAS STANDARD ET MINIMUM
C
C ----------------------------------------------------------------------
C
C
C IN  ZDEF   : NOMBRE MAXI DE COLONNES DISPONIBLES POUR L'AFFICHAGE
C IN  FONACT : FONCTIONNALITES ACTIVEES (VOIR NMFONC)
C IN  INFCMP : VAUT 1 SI ON DOIT AFFICHER DES INFOS SUR LE NOEUD OU
C              LES RESIDUS SONT EVALUES
C            INFCMP(1):  RESI_RELA
C            INFCMP(2):  RESI_MAXI
C            INFCMP(3):  RESI_REFE
C            INFCMP(4):  CTCD_GEOM
C IN  NBSUIV : NOMBRE DE SUIVIS EN TEMPS REEL
C IN  MODE   : MODE DE DEFINITION DES COLONNES
C            'STANDARD': TOUTES LES COLONNES ATTACHEES A DES
C                          FONCTIONNALITES ACTIVEES
C                         + DETAILS SUR LES RESIDUS (INFCMP)
C                         + SUIVIS EN TEMPS REEL (NBSUIV)
C            'MINIMUM' : ITERATION DE NEWTON
C                          RESIDUS PRINCIPAUX RESI_RELA ET RESI_MAXI
C IN  JIMCOL : ADRESSE JEVEUX DU VECTEUR CONTENANT LE CODE DE LA
C              COLONNE ACTIVEE
C OUT NBCOL  : NOMBRE DE COLONNES ACTIVEES
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      ISUI,ICOL
      LOGICAL      ISFONC,LRELI,LPILO,LCTCD,LCTCC
      LOGICAL      LBORST,LREFE,LFETI
      CHARACTER*16 NOMSUI
      CHARACTER*1  FOR1
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- FONCTIONNALITES ACTIVEES
C
      LRELI  = ISFONC(FONACT,'RECH_LINE')
      LPILO  = ISFONC(FONACT,'PILOTAGE')
      LCTCC  = ISFONC(FONACT,'CONT_CONTINU')
      LCTCD  = ISFONC(FONACT,'CONT_DISCRET')
      LREFE  = ISFONC(FONACT,'RESI_REFE')
      LBORST = ISFONC(FONACT,'DEBORST')
      LFETI  = ISFONC(FONACT,'FETI')      
C
C --- MODE STANDARD
C
      IF (MODE.EQ.'STANDARD') THEN
        ICOL = 1
C
C --- CONTACT CONTINU
C
        IF (LCTCC) THEN
          CALL IMPDEA(ZDEF,'CTCC_GEOM',JIMCOL,ICOL)
          IF (FONACT(10)) THEN
            CALL IMPDEA(ZDEF,'CTCC_FROT',JIMCOL,ICOL)
          ENDIF
          CALL IMPDEA(ZDEF,'CTCC_CONT',JIMCOL,ICOL)
        ENDIF
C
C --- ITERATIONS DE NEWTON
C
        CALL IMPDEA(ZDEF,'ITER_NEWT',JIMCOL,ICOL)
C
C --- RESI_GLOB_RELA
C
        CALL IMPDEA(ZDEF,'RESI_RELA',JIMCOL,ICOL)
        IF (INFCMP(1).EQ.1) THEN
          CALL IMPDEA(ZDEF,'RELA_NOEU',JIMCOL,ICOL)
        ENDIF
C
C --- RESI_GLOB_MAXI
C
        CALL IMPDEA(ZDEF,'RESI_MAXI',JIMCOL,ICOL)
        IF (INFCMP(2).EQ.1) THEN
          CALL IMPDEA(ZDEF,'MAXI_NOEU',JIMCOL,ICOL)
        ENDIF
C
C --- RESIDU PAR REFERENCE
C
        IF (LREFE) THEN
          CALL IMPDEA(ZDEF,'RESI_REFE',JIMCOL,ICOL)
          IF (INFCMP(3).EQ.1) THEN
            CALL IMPDEA(ZDEF,'REFE_NOEU',JIMCOL,ICOL)
          ENDIF
        ENDIF
C
C --- METHODE DE BORST
C
        IF (LBORST) THEN
          CALL IMPDEA(ZDEF,'ITER_DEBO',JIMCOL,ICOL)
        ENDIF
C
C --- RECHERCHE LINEAIRE
C
        IF (LRELI) THEN
          CALL IMPDEA(ZDEF,'RELI_ITER',JIMCOL,ICOL)
          CALL IMPDEA(ZDEF,'RELI_COEF',JIMCOL,ICOL)
        ENDIF
C
C --- PILOTAGE
C
        IF (LPILO) THEN
          CALL IMPDEA(ZDEF,'PILO_PARA',JIMCOL,ICOL)
        ENDIF
C
C --- OPTION D'ASSEMBLAGE
C
        CALL IMPDEA(ZDEF,'MATR_ASSE',JIMCOL,ICOL)
C
C --- CONTACT DISCRET
C
        IF (LCTCD) THEN
          CALL IMPDEA(ZDEF,'CTCD_ITER',JIMCOL,ICOL)
          CALL IMPDEA(ZDEF,'CTCD_INFO',JIMCOL,ICOL)
          CALL IMPDEA(ZDEF,'CTCD_GEOM',JIMCOL,ICOL)
          IF (INFCMP(4).EQ.1) THEN
            CALL IMPDEA(ZDEF,'CTCD_NOEU',JIMCOL,ICOL)
          ENDIF
        ENDIF
C
C --- FETI
C
        IF (LFETI) CALL IMPDEA(ZDEF,'ITER_FETI',JIMCOL,ICOL)
C
C --- SUIVIS EN TEMPS REEL
C
        IF (NBSUIV.GT.4) THEN
          CALL U2MESS('F','MECANONLINE4_3')
        ENDIF

        DO 10 ISUI = 1, NBSUIV
          WRITE (FOR1,'(I1)') ISUI
          NOMSUI = 'SUIV_'//FOR1
          CALL IMPDEA(ZDEF,NOMSUI(1:9),JIMCOL,ICOL)
   10   CONTINUE
C
C ---
C --- MODE MINIMUM
C ---
C
      ELSE IF (MODE.EQ.'MINIMUM ') THEN
        ICOL = 1
C
C --- ITERATIONS DE NEWTON
C
        CALL IMPDEA(ZDEF,'ITER_NEWT',JIMCOL,ICOL)
C
C --- RESI_GLOB_RELA
C
        CALL IMPDEA(ZDEF,'RESI_RELA',JIMCOL,ICOL)
        IF (INFCMP(1).EQ.1) THEN
          CALL IMPDEA(ZDEF,'RELA_NOEU',JIMCOL,ICOL)
        ENDIF
C
C --- RESI_GLOB_MAXI
C
        CALL IMPDEA(ZDEF,'RESI_MAXI',JIMCOL,ICOL)
        IF (INFCMP(2).EQ.1) THEN
          CALL IMPDEA(ZDEF,'MAXI_NOEU',JIMCOL,ICOL)
        ENDIF
C
C --- FETI
C
        IF (LFETI) CALL IMPDEA(ZDEF,'ITER_FETI',JIMCOL,ICOL)

      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
C
      NBCOL = ICOL - 1
C
      CALL JEDEMA()

      END
