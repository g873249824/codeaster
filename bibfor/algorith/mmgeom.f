      SUBROUTINE MMGEOM(NBDM,NDIM,NNE,NNM,ICOMPL,
     &                  IGEOM,IDEPL,IDEPM,IVITM,IVITP,IACCM,
     &                  FFPC,FFPR,GEOME,GEOMM,
     &                  DEPLE,DEPLM,DEPLME,DEPLMM,
     &                  VITME,ACCME,VITMM,ACCMM,
     &                  IFORM,VITPE,VITPM)
      IMPLICIT NONE
      INTEGER NBDM,NDIM,NNE,NNM,ICOMPL,IFORM
      INTEGER IGEOM,IDEPL,IDEPM,IVITM,IVITP,IACCM
      REAL*8  FFPC(9),FFPR(9),GEOMM(3),GEOME(3),DEPLE(6),DEPLME(6)
      REAL*8  DEPLM(3),DEPLMM(3),ACCME(6),VITME(6),ACCMM(6),VITMM(6)
      REAL*8  VITPE(6),VITPM(3)
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 03/11/2008   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C ROUTINE APPELLEE PAR : TE0364/TE0365
C ----------------------------------------------------------------------
C
C MISE A JOUR:
C  * DE LA GEOMETRIE POINT DE CONTACT/SON PROJETE
C  * DES DEPLACEMENTS POINT DE CONTACT/SON PROJETE
C  * DES VITESSES/ACC. POINT DE CONTACT/SON PROJETE SI COMPLIANCE
C
C IN  NBDM   : NB DE DDL DE LA MAILLE ESCLAVE
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  NNE    : NOMBRE DE NOEUDS DE LA MAILLE ESCLAVE
C IN  NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C IN  ICOMPL : INDICATEUR SI COMPLIANCE (CALCUL DES VITESSES/ACC.)
C IN  IGEOM  : ADRESSE JEVEUX POUR CHAMP DE GEOMETRIE A L'INSTANT
C              COURANT
C IN  IDEPL  : ADRESSE JEVEUX POUR CHAMP DE DEPLACEMENTS A L'INSTANT
C              COURANT
C IN  IDEPM  : ADRESSE JEVEUX POUR CHAMP DE DEPLACEMENTS A L'INSTANT
C              PRECEDENT
C IN  IVITM  : ADRESSE JEVEUX POUR CHAMP DE VITESSES A L'INSTANT
C              PRECEDENT
C IN  IACCM  : ADRESSE JEVEUX POUR CHAMP D'ACCELERATIONS A L'INSTANT
C               PRECEDENT
C IN  FFPC   : DERIVEES SECONDES DES FONCTIONS DE FORME AU POINT
C              DE CONTACT
C IN  FFPR   : DERIVEES SECONDES DES FONCTIONS DE FORME AU PROJETE
C              DU POINT DE CONTACT
C OUT GEOME  : TABLEAU DES COORDONNEES DU POINT DE CONTACT
C OUT GEOMM  : TABLEAU DES COORDONNEES DU PROJETE DU POINT DE CONTACT
C OUT DEPLE  : DEPLACEMENTS DU POINT DE CONTACT
C OUT DEPLM  : DEPLACEMENTS DU PROJETE DU POINT DE CONTACT
C OUT DEPLME : DEPLACEMENTS PRECEDENTS DU POINT DE CONTACT
C OUT DEPLMM : DEPLACEMENTS PRECEDENTS DU PROJETE DU POINT DE CONTACT
C OUT VITME  : VITESSE DU POINT DE CONTACT
C OUT ACCME  : ACCELERATION DU POINT DU CONTACT
C OUT VITMM  : VITESSE DU PROJETE DU POINT DE CONTACT
C OUT ACCMM  : ACCELERATION DU PROJETE DU POINT DU CONTACT
C
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER   I, J
C
C ----------------------------------------------------------------------
C
      DO 9 I = 1,NDIM
        GEOME(I)  = 0.D0
        GEOMM(I)  = 0.D0
        DEPLM(I)  = 0.D0
        DEPLMM(I) = 0.D0
        VITPM(I)  = 0.D0
 9    CONTINUE
      DO 10 I = 1,2*NDIM
        DEPLE(I)  = 0.D0
        DEPLME(I) = 0.D0
        VITME(I)  = 0.D0 
        ACCME(I)  = 0.D0 
        VITMM(I)  = 0.D0
        ACCMM(I)  = 0.D0
        VITPE(I)  = 0.D0
 10   CONTINUE
C
      DO 11 I = 1,NDIM
        DO 12 J = 1,NNE
          GEOME(I) = GEOME(I) + 
     &               FFPC(J)*ZR(IGEOM+(J-1)*NDIM+I-1)
12      CONTINUE
11    CONTINUE
      DO 21 I = 1,NDIM
        DO 22 J = 1,NNM
          GEOMM(I) = GEOMM(I) +
     &               FFPR(J)*ZR(IGEOM+NNE*NDIM+(J-1)*NDIM+I-1)
22      CONTINUE
21    CONTINUE
C
C --- POUR LES NOEUDS ESCLAVES
C
      DO 31 I = 1,NBDM
        DO 32 J = 1,NNE
          DEPLE(I)  = DEPLE(I)  +
     &                FFPC(J)*ZR(IDEPL+(J-1)*NBDM+I-1)
          DEPLME(I) = DEPLME(I) +
     &                FFPC(J)*ZR(IDEPM+(J-1)*NBDM+I-1)
          IF (IFORM.EQ.2) THEN
            VITPE(I)  = VITPE(I) +
     &                  FFPC(J)*ZR(IVITP+(J-1)*NBDM+I-1)
          ENDIF
          IF (ICOMPL.EQ.1) THEN 
            VITME(I)  = VITME(I) +
     &                  FFPC(J)*ZR(IVITM+(J-1)*NBDM+I-1)
            ACCME(I)  = ACCME(I) +
     &                  FFPC(J)*ZR(IACCM+(J-1)*NBDM+I-1) 
          ENDIF       
32      CONTINUE
31    CONTINUE
C
C --- POUR LES NOEUDS MAITRES
C
      DO 41 I = 1,NDIM
        DO 42 J = 1,NNM
          DEPLM(I)  = DEPLM(I)  + 
     &                FFPR(J)*ZR(IDEPL+NNE*NBDM+(J-1)*NDIM+I-1)
          DEPLMM(I) = DEPLMM(I) +
     &                FFPR(J)*ZR(IDEPM+NNE*NBDM+(J-1)*NDIM+I-1)
          IF (IFORM.EQ.2) THEN
            VITPM(I)  = VITPM(I) +
     &                  FFPR(J)*ZR(IVITP+NNE*NBDM+(J-1)*NDIM+I-1)
          ENDIF
          IF (ICOMPL.EQ.1) THEN      
            VITMM(I)  = VITMM(I) +
     &                  FFPR(J)*ZR(IVITM+NNE*NBDM+(J-1)*NDIM+I-1)
            ACCMM(I)  = ACCMM(I) +
     &                  FFPR(J)*ZR(IACCM+NNE*NBDM+(J-1)*NDIM+I-1)
          ENDIF       
42      CONTINUE
41    CONTINUE
C
      END
