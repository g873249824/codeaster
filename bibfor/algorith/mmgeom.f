      SUBROUTINE MMGEOM(IRESOG,NDIM  ,NNE   ,NNM   ,FFE   ,
     &                  FFM   ,DDFFM ,GEOMAE,GEOMAM,TAU1  ,
     &                  TAU2  ,NORM  ,MPROJN,MPROJT,GEOME ,
     &                  GEOMM ,MPRT1N,MPRT2N,GENE11,GENE21)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 25/06/2012   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE     'jeveux.h'
      INTEGER      NDIM,NNE,NNM
      INTEGER      IRESOG
      REAL*8       FFE(9),FFM(9),DDFFM(3,9)
      REAL*8       NORM(3),TAU1(3),TAU2(3)
      REAL*8       MPROJN(3,3),MPROJT(3,3)
      REAL*8       MPRT1N(3,3),MPRT2N(3,3)
      REAL*8       GEOMM(3),GEOME(3)
      REAL*8       GEOMAE(9,3),GEOMAM(9,3)
      REAL*8       GENE11(3,3),GENE21(3,3)
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - UTILITAIRE)
C
C CALCUL DES INCREMENTS - GEOMETRIE
C
C ----------------------------------------------------------------------
C
C
C DEPDEL - INCREMENT DE DEPLACEMENT DEPUIS DEBUT DU PAS DE TEMPS
C DEPMOI - DEPLACEMENT DEBUT DU PAS DE TEMPS
C GEOMAx - GEOMETRIE ACTUALISEE GEOM_INIT + DEPMOI
C
C
C IN  IRESOG : ALGO. DE RESOLUTION POUR LA GEOMETRIE
C              0 - POINT FIXE
C              1 - NEWTON
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  NNE    : NOMBRE DE NOEUDS DE LA MAILLE ESCLAVE
C IN  NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C IN  FFE    : FONCTIONS DE FORMES DEPL. ESCL.
C IN  FFM    : FONCTIONS DE FORMES DEPL. MAIT.
C IN  DDFFM  : DERIVEES SECONDES DES FONCTIONS DE FORME MAITRES
C IN  GEOMAE : GEOMETRIE ACTUALISEE SUR NOEUDS ESCLAVES
C IN  GEOMAM : GEOMETRIE ACTUALISEE SUR NOEUDS MAITRES
C IN  TAU1   : PREMIER VECTEUR TANGENT
C IN  TAU2   : SECOND VECTEUR TANGENT
C OUT NORM   : NORMALE INTERIEURE
C OUT MPROJN : MATRICE DE PROJECTION NORMALE
C OUT MPROJT : MATRICE DE PROJECTION TANGENTE
C OUT MPRONY : MATRICE DE PROJECTION (GEOME-GEOMM)/NORMALE
C OUT MPRT1N : MATRICE DE PROJECTION TANGENTE1/NORMALE
C OUT MPRT2N : MATRICE DE PROJECTION TANGENTE2/NORMALE
C OUT GEOME  : COORDONNEES ACTUALISEES DU POINT DE CONTACT
C OUT GEOMM  : COORDONNEES ACTUALISEES DU PROJETE DU POINT DE CONTACT
C OUT GENE11 : MATRICE
C OUT GENE21 : MATRICE
C
C ----------------------------------------------------------------------
C
      INTEGER   IDIM,INOE,INOM
C
C ----------------------------------------------------------------------
C

C
C --- INITIALISATIONS
C
      CALL VECINI(3,0.D0,GEOME)
      CALL VECINI(3,0.D0,GEOMM)
C
C --- COORDONNEES ACTUALISEES DU POINT DE CONTACT
C
      DO 11 IDIM = 1,NDIM
        DO 12 INOE = 1,NNE
          GEOME(IDIM) = GEOME(IDIM) + FFE(INOE)*GEOMAE(INOE,IDIM)
12      CONTINUE
11    CONTINUE
C
C --- COORDONNEES ACTUALISEES DE LA PROJECTION DU POINT DE CONTACT
C
      DO 21 IDIM = 1,NDIM
        DO 22 INOM = 1,NNM
          GEOMM(IDIM) = GEOMM(IDIM) + FFM(INOM)*GEOMAM(INOM,IDIM)
22      CONTINUE
21    CONTINUE
C
C --- CALCUL DE LA NORMALE ET DES MATRICES DE PROJECTION
C
      CALL MMCALN(NDIM  ,TAU1  ,TAU2  ,NORM  ,MPROJN,
     &            MPROJT)
C
C --- MATRICES DE PROJECTION POUR NEWTON GENERALISE
C
      IF (IRESOG.EQ.1) THEN
        CALL MMCALG(NDIM  ,NNM   ,DDFFM ,GEOMAM,TAU1  ,
     &              TAU2  ,NORM  ,MPRT1N,MPRT2N,GENE11,
     &              GENE21)
      ENDIF
C

      END
