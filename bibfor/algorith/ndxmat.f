      SUBROUTINE NDXMAT(FONACT,LISCHA,SOLVEU,NUMEDD,SDDYNA,
     &                  SDDISC,MEELEM,MEASSE,MATASS)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*19  MATASS
      CHARACTER*19  SDDYNA,SDDISC
      INTEGER       FONACT(*)
      CHARACTER*19  MEELEM(*),MEASSE(*)
      CHARACTER*24  NUMEDD
      CHARACTER*19  LISCHA,SOLVEU
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME - CALCUL)
C
C ASSEMBLAGE DE LA MATRICE GLOBALE - CAS EXPLICITE
C
C ----------------------------------------------------------------------
C
C
C IN  SDDYNA : SD DYNAMIQUE
C IN  SDDISC : SD DISC_INST
C IN  NUMEDD : NOM DE LA NUMEROTATION MECANIQUE
C IN  LISCHA : SD L_CHARGE
C IN  SOLVEU : NOM DU SOLVEUR DE NEWTON
C IN  MEASSE : VARIABLE CHAPEAU POUR NOM DES MATR_ASSE
C IN  MEELEM : VARIABLE CHAPEAU POUR NOM DES MATR_ELEM
C OUT MATASS : MATRICE ASSEMBLEE RESULTANTE
C
C
C
C
      LOGICAL      ISFONC,NDYNLO
      LOGICAL      LSUIV,LSHIMA,LPREM
      REAL*8       NDYNRE,COEMAS,COESHI
      CHARACTER*8  NOMDDL
      REAL*8       COEMAT
      CHARACTER*24 LIMAT
      CHARACTER*4  TYPCST
      REAL*8       COEMAM(2)
      CHARACTER*24 LIMAM(2)
      CHARACTER*4  TYPCSM(2)
      INTEGER      NBMAT,IRET
      CHARACTER*19 RIGID,MASSE
      INTEGER      IFM,NIV
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE><CALC> CALCUL MATRICE GLOBALE'
      ENDIF
C
C --- DECOMPACTION DES VARIABLES CHAPEAUX
C
      CALL NMCHEX(MEASSE,'MEASSE','MERIGI',RIGID)
      CALL NMCHEX(MEASSE,'MEASSE','MEMASS',MASSE)
      NOMDDL = ' '
C
C --- FONCTIONNALITES ACTIVEES
C
      LSUIV  = ISFONC(FONACT,'FORCE_SUIVEUSE')
      LSHIMA = NDYNLO(SDDYNA,'COEF_MASS_SHIFT')
C
C --- PREMIER PAS DE TEMPS ?
C
      CALL DIBCLE(SDDISC,'PREMIE','L',IRET  )
      LPREM  = IRET.EQ.0
C
C --- SUPPRESSION ANCIENNE MATRICE ASSEMBLEE
C
      CALL DETRSD('MATR_ASSE',MATASS)
C
C --- COEFFICIENTS POUR MATRICES
C
      COEMAS = NDYNRE(SDDYNA,'COEF_MATR_MASS')
      COESHI = NDYNRE(SDDYNA,'COEF_MASS_SHIFT')
C
C --- DECALAGE DE LA MATRICE MASSE (COEF_MASS_SHIFT)
C
      IF (LSHIMA.AND.LPREM) THEN
        TYPCSM(1) = 'R'
        TYPCSM(2) = 'R'
        COEMAM(1) = 1.D0
        COEMAM(2) = COESHI
        LIMAM(1)  = MASSE
        LIMAM(2)  = RIGID
        NBMAT     = 2
        CALL MTCMBL(NBMAT ,TYPCSM,COEMAM,LIMAM,MASSE,
     &              NOMDDL,' '   ,'ELIM=')
      ENDIF
C
C --- MATRICES ET COEFFICIENTS
C
      TYPCST = 'R'
      LIMAT  = MASSE
      NBMAT  = 1
      COEMAT = COEMAS
C
C --- DEFINITION DE LA STRUCTURE DE LA MATRICE
C
      CALL MTDEFS(MATASS,MASSE ,'V','R')      
C
C --- ASSEMBLAGE
C
      CALL MTCMBL(NBMAT ,TYPCST,COEMAT,LIMAT,MATASS,
     &            NOMDDL,' '   ,'ELIM=')
C
C --- PRISE EN COMPTE DE LA MATRICE TANGENTE DES FORCES SUIVEUSES
C
      IF (LSUIV) THEN
        CALL ASCOMA(MEELEM,NUMEDD,SOLVEU,LISCHA,MATASS)
      ENDIF
C
      CALL JEDEMA()
      END
