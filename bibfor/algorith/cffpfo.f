      SUBROUTINE CFFPFO(RESOCO,NBLIAI,NBLIAC,NDIM  )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 12/09/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT     NONE
      CHARACTER*24 RESOCO
      INTEGER      NBLIAI,NBLIAC,NDIM
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (RESOLUTION - PENALISATION)
C
C CALCUL DES COEFFICIENTS DE LAGRANGE MU POUR LE FROTTEMENT
C
C ----------------------------------------------------------------------
C 
C
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NBLIAI : NOMBRE DE LIAISONS DE CONTACT POSSIBLES
C IN  NBLIAC : NOMBRE DE LIAISONS ACTIVES
C IN  NDIM   : DIMENSION DU PROBLEME
C
C --------------- DEBUT DECLARATIONS NORMALISEES JEVEUX ---------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      REAL*8       COEFPT,COEFFF,LAMBDC,LAMBDF
      REAL*8       GLIS
      INTEGER      ILIAI,ILIAC
      CHARACTER*19 MU,LIAC
      INTEGER      JMU,JLIAC
      CHARACTER*24 TACFIN
      INTEGER      JTACF
      INTEGER      CFMMVD,ZTACF
C 
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      TACFIN = RESOCO(1:14)//'.TACFIN'
      LIAC   = RESOCO(1:14)//'.LIAC'
      MU     = RESOCO(1:14)//'.MU'
      CALL JEVEUO(TACFIN,'L',JTACF )
      CALL JEVEUO(LIAC,  'L',JLIAC )
      CALL JEVEUO(MU,    'E',JMU   )
      ZTACF  = CFMMVD('ZTACF')
C
C --- CALCUL DES COEFFICIENTS DE LAGRANGE MU
C
      DO 100 ILIAC = 1,NBLIAC
C
C ----- REPERAGE DE LA LIAISON
C
        ILIAI  = ZI(JLIAC-1+ILIAC)
C                                 
C ----- CALCUL DU JEU TANGENT
C
        CALL CFCGLT(RESOCO,ILIAI ,GLIS  )
C
C ----- COEFFICIENTS
C
        COEFFF = ZR(JTACF+ZTACF*(ILIAI-1)+1-1)
        COEFPT = ZR(JTACF+ZTACF*(ILIAI-1)+3-1)
C
C ----- LAMBDA DE CONTACT ET DE FROTTEMENT
C
        LAMBDC = ZR(JMU+ILIAC-1)
        IF ( LAMBDC .GT. 0.D0) THEN
          LAMBDF = COEFFF*LAMBDC
        ELSE
          LAMBDF = 0.D0
        ENDIF
C
C ----- ACTIVATION GLISSEMENT/ADHERENCE
C
        IF ( ZR(JMU+2*NBLIAI+ILIAI-1).NE.0.D0) THEN
          IF ( GLIS .LE. (LAMBDF/COEFPT) ) THEN
            ZR(JMU+3*NBLIAI+ILIAI-1) = SQRT(COEFPT)
          ELSE
            ZR(JMU+3*NBLIAI+ILIAI-1) = SQRT(LAMBDF/GLIS)
          ENDIF
        ELSE
          ZR(JMU+3*NBLIAI+ILIAI-1) = 0.D0
        ENDIF
 100  CONTINUE
C
      CALL JEDEMA()
C
      END
