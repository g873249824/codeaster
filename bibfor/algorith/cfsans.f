      SUBROUTINE CFSANS(DEFICO,RESOCO,NOMA  ,JCNSVR)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 22/12/2009   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*24 DEFICO,RESOCO
      CHARACTER*8  NOMA
      INTEGER      JCNSVR
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE DISCRETE - AFFICHAGE)
C
C TRAITEMENT DU CAS DE CONTACT SANS CALCUL (METHODE VERIF)
C  AFFICHAGE DES INTERPENETRATIONS ET ARRET OU ALARME
C
C ----------------------------------------------------------------------
C
C
C IN  DEFICO : SD DE DEFINITION DU CONTACT
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NOMA   : NOM DU MAILLAGE
C IN  JCNSVR : POINTEUR CHAM_NO_S POUR L'ARCHIVAGE DU CONTACT
C
C ------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------
C
      INTEGER      CFMMVD,ZAPME,ZRESU
      INTEGER      CFDISI,CFDISD      
      CHARACTER*24 APPARI,APMEMO
      INTEGER      JAPPAR,JAPMEM
      CHARACTER*8  NOMENT,NOMNOE
      INTEGER      CODRET
      INTEGER      POSNOE,NUMNOE,POSAPP
      INTEGER      INTERP
      INTEGER      NBLIAI,ILIAI
      INTEGER      IFM,NIV
      LOGICAL      ARRET
      REAL*8       JEU,JEUREF,CFDISR
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)
C 
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      APPARI = RESOCO(1:14)//'.APPARI'
      APMEMO = RESOCO(1:14)//'.APMEMO'
      CALL JEVEUO(APMEMO,'L',JAPMEM)          
      CALL JEVEUO(APPARI,'L',JAPPAR)
      ZRESU = CFMMVD('ZRESU')
      ZAPME = CFMMVD('ZAPME')        
C      
C --- ALARME OU ERREUR ?
C
      IF (CFDISI(DEFICO,'STOP_INTERP').EQ.1) THEN
        ARRET = .TRUE.
      ELSE
        ARRET = .FALSE.
      ENDIF
C      
C --- JEU MINIMUM POUR DECLARER INTERPENETRATION
C
      JEUREF = CFDISR(DEFICO,'TOLE_INTERP')
      NBLIAI = CFDISD(RESOCO,'NBLIAI' )
      INTERP = 0

      DO 120 ILIAI = 1,NBLIAI
C
C --- REPERAGE DE L'ESCLAVE
C
        POSNOE = ZI(JAPPAR+ILIAI)     
C
C --- NOM DU NOEUD ESCLAVE
C      
        CALL CFNOMM(NOMA  ,DEFICO,'NOEU',POSNOE,NOMNOE,
     &              CODRET)       
        IF (CODRET.LT.0) THEN
          CALL ASSERT(.FALSE.)
        ENDIF  
C
C --- NUMERO ABSOLU DU NOEUD ESCLAVE
C                
        CALL CFPOSM(NOMA  ,DEFICO,'NOEU',1     ,POSNOE,
     &              NUMNOE,CODRET)  
        IF (CODRET.LT.0) THEN
          CALL ASSERT(.FALSE.)
        ENDIF                    
C      
C --- REPERAGE DU MAITRE
C
        POSAPP = ZI(JAPMEM+ZAPME*(POSNOE-1)+3-1)
C
C --- NOM DU MAITRE
C
        IF (POSAPP.LT.0) THEN
          CALL CFNOMM(NOMA  ,DEFICO,'NOEU',POSAPP,NOMENT,
     &                CODRET)
          IF (CODRET.LT.0) THEN
            CALL ASSERT(.FALSE.)  
          ENDIF         
        ELSEIF (POSAPP.GT.0) THEN
          CALL CFNOMM(NOMA  ,DEFICO,'MAIL',POSAPP,NOMENT,
     &                CODRET)
          IF (CODRET.LT.0) THEN
            CALL ASSERT(.FALSE.)  
          ENDIF    
        ELSE  
          CALL ASSERT(.FALSE.)               
        ENDIF 
C
C --- VALEUR DU JEU
C
        JEU = ZR(JCNSVR-1+ (NUMNOE-1)*ZRESU+2 )
        IF (JEU.GE.JEUREF .AND. NIV.GE.2) THEN
          WRITE (IFM,2000) ' DU  NOEUD <',NOMNOE,'> AVEC <',NOMENT,
     &                     '> * JEU:',JEU
        ENDIF
        IF (JEU.LT.JEUREF) THEN
          INTERP = INTERP+1
          WRITE (IFM,2001) ' DU  NOEUD <',NOMNOE,'> AVEC <',NOMENT,
     &                     '> * JEU:',JEU
          ZR(JCNSVR-1+ (NUMNOE-1)*ZRESU +1) = 1.0D0
        ENDIF

 120  CONTINUE

      IF (INTERP.GE.1) THEN
        WRITE (IFM,3000) INTERP,JEUREF
        IF (ARRET) THEN
          CALL U2MESS('F','CONTACT_93')
        ELSE
          CALL U2MESS('A','CONTACT_93')
        ENDIF
      ENDIF

2001  FORMAT (' <CONTACT>   * INTERPENETRATION ',A12,A8,A8,A8,
     &        A8,1PE12.5)
2000  FORMAT (' <CONTACT>   * LIAISON LIBRE    ',A12,A8,A8,A8,
     &        A8,1PE12.5)
3000  FORMAT (' <CONTACT>   * IL Y A ',I6,
     &        ' NOEUDS INTERPENETRES (JEU REF.: ',1PE12.5,')')
C
C ----------------------------------------------------------------------
C
      CALL JEDEMA()

      END
