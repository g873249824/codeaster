      SUBROUTINE CFSANS(DEFICO,NPT   ,JEUX  ,ENTI  ,ZONE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT      NONE
      INCLUDE 'jeveux.h'
      CHARACTER*24  DEFICO
      INTEGER       NPT
      CHARACTER*24  JEUX,ENTI,ZONE      
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES MAILLEES - VERIF)
C
C AFFICHAGE DES INTERPENETRATIONS
C
C ----------------------------------------------------------------------
C
C
C IN  DEFICO : SD DE DEFINITION DU CONTACT
C IN  NPT    : NOMBRE DE POINTS EN MODE VERIF
C IN  JEUX   : NOM DE LA SD STOCKANT LE JEU
C IN  ENTI   : NOM DE LA SD STOCKANT LES NOMS DES ENTITES APPARIEES
C IN  ZONE   : NOM DE LA SD STOCKANT LA ZONE A LAQUELLE APPARTIENT LE
C              POINT
C
C
C
C     
      CHARACTER*16 NOMENT,NOMPT
      INTEGER      INTERP
      INTEGER      IFM,NIV
      LOGICAL      CFDISL,LSTOP
      REAL*8       JEU,JEUREF,MMINFR,R8VIDE
      INTEGER      IPT,IZONE
      INTEGER      JJEUX,JENTI,JZONE
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- ACCES SD PROVISOIRES
C
      CALL JEVEUO(JEUX  ,'L',JJEUX )
      CALL JEVEUO(ZONE  ,'L',JZONE )
      CALL JEVEUO(ENTI  ,'L',JENTI )
C
C --- INITIALISATIONS
C
      INTERP = 0
C      
C --- ALARME OU ERREUR ?
C
      LSTOP = CFDISL(DEFICO,'STOP_INTERP')
C
      DO 120 IPT = 1,NPT
C
C ----- INFORMATIONS SUR LE POINT
C
        JEU    = ZR(JJEUX+IPT-1)
        IZONE  = ZI(JZONE+IPT-1) 
        NOMPT  = ZK16(JENTI+2*(IPT-1)+1-1)
        NOMENT = ZK16(JENTI+2*(IPT-1)+2-1)
C
C ----- OPTIONS VERIF
C
        JEUREF = MMINFR(DEFICO,'TOLE_INTERP',IZONE) 
C
C ----- TEST DU JEU
C
        IF ((JEU.LT.JEUREF).AND.(JEU.NE.R8VIDE())) THEN
          IF (NIV.GE.2)THEN
            WRITE (IFM,2001) NOMPT,NOMENT,JEU
          ENDIF
          INTERP = INTERP+1
        ENDIF
C
 120  CONTINUE
C
C --- ALARME OU ERREUR FATALE ?
C
      IF (INTERP.GE.1) THEN
        IF(NIV.GE.2)THEN
          WRITE (IFM,3000) INTERP,JEUREF
        ENDIF
        IF (LSTOP) THEN
          CALL U2MESS('F','CONTACT_93')
        ELSE
          CALL U2MESS('A','CONTACT_93')
        ENDIF
      ENDIF
C
2001  FORMAT (' <CONTACT>   * INTERPENETRATION DE <',A16,'> AVEC <',
     &        A16,'> * JEU:'1PE12.5)
3000  FORMAT (' <CONTACT>   * IL Y A ',I6,
     &        ' NOEUDS INTERPENETRES (JEU REF.: ',1PE12.5,')')
C
C ----------------------------------------------------------------------
C
      CALL JEDEMA()

      END
