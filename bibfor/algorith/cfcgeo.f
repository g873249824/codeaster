      SUBROUTINE CFCGEO(NOMA  ,DEFICO,RESOCO,SOLALG,DVGEOM,
     &                  GEOERR,GEONOE,GEOVAL)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO,RESOCO
      LOGICAL      DVGEOM,GEOERR
      CHARACTER*19 SOLALG(*)
      CHARACTER*16 GEONOE
      REAL*8       GEOVAL
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES)
C
C CONVERGENCE LIEE AU CONTACT DISCRET - GEOMETRIE
C
C ----------------------------------------------------------------------
C
C
C IN  DEFICO : SD POUR LA DEFINITION DU CONTACT
C IN  RESOCO : SD POUR LA RESOLUTION DU CONTACT
C IN  NOMA   : NOM DU MAILLAGE
C IN  SOLALG : VARIABLE CHAPEAU POUR INCREMENTS SOLUTIONS
C OUT DVGEOM : .TRUE. SI BOUCLE GEOMETRIQUE NON CONVERGEE
C OUT GEOERR : .TRUE. SI ERREUR SUR LA BOUCLE DE GEOMETRIE
C OUT GEONOE : ENDROIT POUR MAX GEOMETRIE
C OUT GEOVAL : VALEUR DU MAX GEOMETRIE
C
C
C
C
      INTEGER      JDEPDE,II,NUMNO1,NUMNO2
      INTEGER      CFDISD,NEQ
      INTEGER      NCMP,CFDISI,MMITGO,NBREAG,MAXGEO
      REAL*8       AUTONO,TEMP1,TEMP2,R8PREM,EPSGEO,RMIN,R8VIDE
      CHARACTER*8  NOMNOE,LICMP(3)
      LOGICAL      PREMIE,ALARME
      CHARACTER*19 DEPDEL
      CHARACTER*24 MAXDEP,AUTOC1,AUTOC2
      INTEGER      JMAXDE,JAUTO1,JAUTO2
      REAL*8       CFDISR
      LOGICAL      GEOALA,CFDISL
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- DECOMPACTION VARIABLES CHAPEAUX
C
      CALL NMCHEX(SOLALG,'SOLALG','DEPDEL',DEPDEL)
C
C --- ACCES OBJETS
C
      AUTOC1 = RESOCO(1:14)//'.REA1'
      AUTOC2 = RESOCO(1:14)//'.REA2'
      MAXDEP = RESOCO(1:14)//'.MAXD'
      CALL JEVEUO(DEPDEL(1:19)//'.VALE','L',JDEPDE)
      CALL JEVEUO(AUTOC1(1:19)//'.VALE','E',JAUTO1)
      CALL JEVEUO(AUTOC2(1:19)//'.VALE','E',JAUTO2)
      CALL JEVEUO(MAXDEP,'E',JMAXDE)
C
C --- TOLERANCE POUR REACTUALISATION GEOMETRIQUE AUTOMATIQUE
C --- NOMBRE MAXI D'ITERATIONS DE REACTUALISATION GEOMETRIQUE EN AUTO
C
      EPSGEO = CFDISR(DEFICO,'RESI_GEOM'     )
      NBREAG = CFDISI(DEFICO,'NB_ITER_GEOM'  )
      MAXGEO = CFDISI(DEFICO,'ITER_GEOM_MAXI')
      NEQ    = CFDISD(RESOCO,'NEQ'   )
      GEOALA = .FALSE.
      GEOERR = .FALSE.
      DVGEOM = .TRUE.
      PREMIE = .FALSE.
      ALARME = .FALSE.
      GEONOE = ' '
      GEOVAL = R8VIDE()
      TEMP1  = 0.D0
      TEMP2  = 0.D0
C
C --- NOUVELLE ITERATION DE REACTUALISATION GEOMETRIQUE
C
      CALL MMBOUC(RESOCO,'GEOM','READ',MMITGO)
C
C --- CALCUL DU DEPLACEMENT
C
      DO 10 II = 1,NEQ
        ZR(JAUTO2-1+II) = ZR(JAUTO2-1+II) + ZR(JAUTO1-1+II)
        ZR(JAUTO1-1+II) = ZR(JDEPDE-1+II) - ZR(JAUTO2-1+II)
   10 CONTINUE
C
C --- CALCUL DU MAX DE LA NORME DU DEPLACEMENT (SAUF LAGRANGES)
C
      NCMP     = 3
      LICMP(1) = 'DX'
      LICMP(2) = 'DY'
      LICMP(3) = 'DZ'
      CALL CNOMAX(AUTOC1,NCMP,LICMP,TEMP1,NUMNO1)
      CALL CNOMAX(AUTOC2,NCMP,LICMP,TEMP2,NUMNO2)
C
C --- STOCKAGE DU MAX DE LA NORME DU DEPL
C
      IF (ZR(JMAXDE).LT.0.D0) THEN
        ZR(JMAXDE-1+1) = TEMP2
        RMIN           = R8PREM()
      ELSE
        ZR(JMAXDE-1+1) = MAX(ZR(JMAXDE-1+1),TEMP2)
        RMIN           = 1.D-6*ZR(JMAXDE-1+1)
      ENDIF
C
C --- VALEUR DE DEPL. MAX MESURE
C
      IF (TEMP2.LE.RMIN) THEN
        IF (TEMP2.EQ.0.D0) THEN
          AUTONO = 10.0D0*EPSGEO
          PREMIE = .TRUE.
        ELSE
          AUTONO = 1.D-1*EPSGEO
        ENDIF
      ELSE
        AUTONO = TEMP1/TEMP2
      ENDIF
C
C --- INFORMATIONS: NOM DU NOEUD ET VALEUR REACTUALISATION
C
      IF (NUMNO2.EQ.0) THEN
        NOMNOE = ' '
      ELSE
        CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',NUMNO2),NOMNOE)
      ENDIF
      GEONOE = NOMNOE//'        '
      GEOVAL = AUTONO
C
C --- CORRESPOND A REAC_GEOM = AUTOMATIQUE
C
      IF (CFDISL(DEFICO,'REAC_GEOM_AUTO')) THEN

        IF (AUTONO.LT.EPSGEO) THEN
          DVGEOM = .FALSE.
        ELSE
          DVGEOM = .TRUE.
          IF (MMITGO.EQ.MAXGEO) THEN
            CALL CFVERL(DEFICO,RESOCO)
            GEOERR = .TRUE.
          ENDIF
        END IF
C
C --- CORRESPOND A REAC_GEOM = SANS
C
      ELSE IF (CFDISL(DEFICO,'REAC_GEOM_SANS')) THEN
        IF (AUTONO.GE.EPSGEO) THEN
          ALARME = .TRUE.
        ENDIF
        DVGEOM = .FALSE.
      ELSE IF (CFDISL(DEFICO,'REAC_GEOM_MANU')) THEN
C
C --- CORRESPOND A REAC_GEOM = CONTROLE
C
        IF (MMITGO.EQ.NBREAG) THEN
          DVGEOM = .FALSE.
          IF (AUTONO.GE.EPSGEO) THEN
            ALARME = .TRUE.
          ENDIF
        ELSE
          DVGEOM = .TRUE.
        END IF
      ELSE
        CALL ASSERT(.FALSE.)
      END IF
C
C --- SI ON DEPASSE LA TOLERANCE DE 5% DE REAC_GEOM ET SI ON N'EST
C --- PAS AU PREMIER PAS DE TEMPS OU QU'ON N'A PAS DE CORPS RIGIDE
C
      IF (ALARME.AND..NOT.PREMIE) THEN
        GEOALA = .TRUE.
      ELSE
        GEOALA = .FALSE.
      ENDIF
C
C --- AFFICHAGE
C
      IF (GEOALA) THEN
        CALL U2MESS('A','CONTACT3_96')
      ENDIF
C
      CALL JEDEMA()
      END
