      SUBROUTINE NZFPRI(DEUXMU,TRANS,RPRIM,SEUIL,PHASP,NZ,FMEL,
     &                  ETA,UNSURN,DT,DP,FPLAS,
     &                  FP,FD,FPRIM,FDEVI)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 15/05/2000   AUTEUR JMBHH01 J.M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      IMPLICIT NONE
C-------------------------------------------------------------------
C  FONCTION FPRIM(X) POUR LOI VISCOPLASTIQUE AVEC METALLURGIE
C
C  CALCUL DE DP ==> RESOLUTION DE L'EQUATION SCALAIRE NON LINEAIRE
C                   FPRIM = 0
C
C  IN  DP    : DP RECHERCHE LORS DE LA RESOLUTION SCALAIRE
C  OUT FPLAS : VALEUR DE LA COMPOSANTE PLASTIQUE DE FPRIM
C  OUT FPRIM : VALEUR DE FPRIM
C  OUT FDEVI : DERIVEE DE FPRIM PAR RAPPORT A DP (SI DP>0)
C--------------------------------------------------------------------
C
      INTEGER   NZ,K
      REAL*8    DP, FPRIM, FPLAS, FDEVI
      REAL*8    DEUXMU,TRANS,RPRIM,SEUIL,ETA(NZ),UNSURN(NZ),DT
      REAL*8    PHASP(5),FP(NZ),FD(5),FMEL

      REAL*8    SPETRA, R0,ZALPHA,FZ(5)
C
      SPETRA = 1.5D0*DEUXMU*TRANS +1.D0
      R0     = 1.5D0*DEUXMU + RPRIM*SPETRA
      FPLAS  = SEUIL - R0*DP
      ZALPHA =0.D0
      DO 10 K=1,NZ-1
         ZALPHA=ZALPHA+PHASP(K)
  10  CONTINUE
      IF (ZALPHA .GT. 0.D0) THEN
        DO 20 K=1,NZ-1
           FZ(K)=FMEL*PHASP(K)/ZALPHA
  20    CONTINUE
      ELSE
        DO 21 K=1,NZ-1
           FZ(K)=0.D0
  21    CONTINUE
      ENDIF
      FZ(NZ)=(1-FMEL)
      FPRIM=FPLAS
      DO 100 K=1,NZ
         IF (PHASP(K) .GT. 0.D0) THEN
           FP(K)=FPLAS-SPETRA*ETA(K)*((DP/DT)**UNSURN(K))
         ELSE
           FP (K)=0.D0
         ENDIF
         FPRIM  = FPRIM - SPETRA*FZ(K)*ETA(K)*((DP/DT)**UNSURN(K))
 100  CONTINUE
      IF (DP.GT.0.D0) THEN
        FDEVI=-R0
        DO 200 K=1,NZ
          FD(K)=-R0-(ETA(K)*SPETRA/DT**UNSURN(K))*UNSURN(K)
     &          *DP**(UNSURN(K)-1)
          FDEVI = FDEVI-FZ(K)*(ETA(K)*SPETRA/DT**UNSURN(K))
     &           *UNSURN(K)*DP**(UNSURN(K)- 1)
 200    CONTINUE

      ENDIF
      END
