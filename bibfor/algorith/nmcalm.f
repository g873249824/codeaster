      SUBROUTINE NMCALM(TYPMAT,MODELZ,LISCHA,MATE  ,CARELE,
     &                  COMPOR,INSTAM,INSTAP,CARCRI,VALMOI,
     &                  VALPLU,SOLALG,OPTMAZ,BASE  ,MEELEM,
     &                  RESOCO,MATELE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 23/09/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*(*) MODELZ
      CHARACTER*(*) MATE,CARELE
      CHARACTER*24  COMPOR,CARCRI
      REAL*8        INSTAM,INSTAP
      CHARACTER*24  RESOCO
      CHARACTER*24  VALMOI(8),VALPLU(8)
      CHARACTER*19  LISCHA
      CHARACTER*6   TYPMAT
      CHARACTER*(*) OPTMAZ
      CHARACTER*1   BASE
      CHARACTER*19  MEELEM(*),SOLALG(*)
      CHARACTER*19  MATELE
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (CALCUL)
C
C CALCUL DES MATRICES ELEMENTAIRES
C      
C ----------------------------------------------------------------------
C
C
C IN  MODELE : NOM DU MODELE
C IN  LISCHA : LISTE DES CHARGEMENTS
C IN  CARCRI : PARAMETRES DES METHODES D'INTEGRATION LOCALES
C IN  TYPMAT : TYPE DE MATRICE A CALCULER
C                MERIGI  - MATRICE POUR RIGIDITE
C                MEDIRI  - MATRICE POUR CL DIRICHLET LAGRANGE
C                MEGEOM  - MATRICE POUR NON-LIN. GEOMETRIQUE
C                MEAMOR  - MATRICE POUR AMORTISSEMENT
C                MEMASS  - MATRICE POUR MASSE    
C                MESUIV  - MATRICE POUR CHARGEMENT SUIVEUR 
C                MESSTR  - MATRICE POUR SOUS-STRUCTURES
C                MECTCC  - MATRICE POUR CONTACT CONTINU 
C                MECTCF  - MATRICE POUR FROTTEMENT CONTINU
C                MEXFEC  - MATRICE POUR CONTACT XFEM
C                MEXFEF  - MATRICE POUR FROTTEMENT XFEM 
C                MEXFTC  - MATRICE POUR CONTACT XFEM (GRD GLIS)
C                MEXFTF  - MATRICE POUR FROTT. XFEM (GRD GLIS)
C IN  OPTCAL : OPTION DE CALCUL DU MATR_ELEM 
C IN  SOLALG : VARIABLE CHAPEAU POUR INCREMENTS SOLUTIONS
C IN  OPTMAT : OPTION DE CALCUL POUR LA MATRICE
C OUT MATELE : MATRICE ELEMENTAIRE
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      CHARACTER*19 MEMASS,MERIGI
      CHARACTER*19 NMCHEX
      CHARACTER*24 MODELE
      INTEGER      JINFC,JCHAR,JCHAR2,IAREFE
      INTEGER      NBCHAR
      INTEGER      IBID,IRET,I
      CHARACTER*16 OPTMAT  
      CHARACTER*24 DEPMOI,SIGPLU,VITPLU,VITMOI,ACCMOI,K24BID
      CHARACTER*19 DEPDEL
      CHARACTER*24 CHARGE,INFOCH
      CHARACTER*8  MAILLA
      LOGICAL      LVALM,LVALP,LMELE,LDEPA      
      INTEGER      IFM,NIV    
C      
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- INITIALISATIONS
C        
      OPTMAT = OPTMAZ
      MODELE = MODELZ
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE'  ,IBID  ,MAILLA,IRET)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE><MATR> CALCUL DES MATR_ELEM' //
     &                ' DE TYPE <',TYPMAT,'>'    
      ENDIF 
C
C --- INDICATEUR PRESENCE VARIABLES CHAPEAUX
C
      LVALM     = .FALSE.
      LVALP     = .FALSE.
      LDEPA     = .FALSE. 
      LMELE     = .FALSE.           
C
C --- DECOMPACTION DES VARIABLES CHAPEAUX
C
      IF (VALMOI(1)(1:1).NE.' ') THEN     
        CALL DESAGG(VALMOI,DEPMOI,K24BID,K24BID,K24BID,
     &              VITMOI,ACCMOI,K24BID,K24BID)
        LVALM  = .TRUE.      
      ENDIF
      IF (VALPLU(1)(1:1).NE.' ') THEN     
        CALL DESAGG(VALPLU,K24BID,SIGPLU,K24BID,K24BID,
     &              VITPLU,K24BID,K24BID,K24BID) 
        LVALP  = .TRUE.       
      ENDIF 
      IF (SOLALG(1)(1:1).NE.' ') THEN    
        DEPDEL = NMCHEX(SOLALG,'SOLALG','DEPDEL')    
        LDEPA  = .TRUE.      
      ENDIF    
      IF (MEELEM(1)(1:1).NE.' ') THEN 
        MERIGI = NMCHEX(MEELEM,'MEELEM','MERIGI')
        MEMASS = NMCHEX(MEELEM,'MEELEM','MEMASS')
        LMELE  = .TRUE.            
      ENDIF       
C
C --- TRANSFO CHARGEMENTS
C
      CHARGE = LISCHA(1:19)//'.LCHA'
      INFOCH = LISCHA(1:19)//'.INFC'
      CALL JEVEUO(INFOCH,'L',JINFC)
      NBCHAR = ZI(JINFC)
      CALL JEVEUO(CHARGE,'L',JCHAR)
      CALL WKVECT('&&NMCALC.LISTE_CHARGE','V V K8',NBCHAR,JCHAR2)
      DO 10 I = 1,NBCHAR
        ZK8(JCHAR2-1+I) = ZK24(JCHAR-1+I) (1:8)
   10 CONTINUE
C
      IF (TYPMAT.EQ.'MEDIRI') THEN
C
C --- MATR_ELEM DES CL DE DIRICHLET B
C      
        CALL MEDIME('V','ZERO',MODELE,LISCHA,MATELE)
C
C --- MATR_ELEM RIGIDITE GEOMETRIQUE
C            
      ELSEIF (TYPMAT.EQ.'MEGEOM') THEN           
        IF (.NOT.LVALP) CALL ASSERT(.FALSE.)
        CALL DETRSD('MATR_ELEM',MATELE)
        CALL MERIGE(MODELE(1:8),CARELE(1:8),SIGPLU,MATELE,'V',0)
C
C --- MATR_ELEM MASSES
C         
      ELSEIF (TYPMAT.EQ.'MEMASS') THEN           
        CALL MEMAME(OPTMAT,MODELE,NBCHAR,ZK8(JCHAR2),MATE  ,
     &              CARELE,.TRUE.,INSTAM,COMPOR     ,MATELE,
     &              BASE  )   
C
C --- MATR_ELEM AMORTISSEMENT
C         
      ELSEIF (TYPMAT.EQ.'MEAMOR') THEN  
        IF (.NOT.LMELE) CALL ASSERT(.FALSE.)            
        CALL MEAMME(OPTMAT,MODELE,NBCHAR,ZK8(JCHAR2),MATE  ,
     &              CARELE,.TRUE.,INSTAM,'V'        ,MERIGI,
     &              MEMASS,MATELE)
C
C --- MATR_ELEM POUR CHARGES SUIVEUSES
C      
      ELSEIF (TYPMAT.EQ.'MESUIV') THEN 
        IF (.NOT.LVALM) CALL ASSERT(.FALSE.)  
        IF (.NOT.LDEPA) CALL ASSERT(.FALSE.)            
        CALL MECGME(MODELE,CARELE,MATE  ,LISCHA,INSTAP,
     &              DEPMOI,DEPDEL,INSTAM,COMPOR,CARCRI,
     &              MATELE)
        CALL MECGM2(LISCHA,INSTAP,MATELE)
C
C --- MATR_ELEM DES SOUS-STRUCTURES
C     
      ELSEIF (TYPMAT.EQ.'MESSTR') THEN   
        CALL MEMARE(BASE  ,MATELE,MODELE(1:8),MATE,CARELE,
     &              OPTMAT)
        CALL JEVEUO(MATELE//'.RERR','E',IAREFE)
        ZK24(IAREFE-1+3) = 'OUI_SOUS_STRUC'
C
C --- MATR_ELEM DE CONTACT CONTINU
C
      ELSEIF (TYPMAT.EQ.'MECTCC') THEN 
        IF (.NOT.LVALM) CALL ASSERT(.FALSE.)  
        IF (.NOT.LDEPA) CALL ASSERT(.FALSE.) 
        IF (.NOT.LVALP) CALL ASSERT(.FALSE.)            
        CALL MMCMEM('CONT',MODELE,RESOCO,DEPMOI,DEPDEL,
     &              VITMOI,VITPLU,ACCMOI,MATELE)
C
C --- MATR_ELEM DE FROTTEMENT CONTINU
C
      ELSEIF (TYPMAT.EQ.'MECTCF') THEN 
        IF (.NOT.LVALM) CALL ASSERT(.FALSE.)  
        IF (.NOT.LDEPA) CALL ASSERT(.FALSE.) 
        IF (.NOT.LVALP) CALL ASSERT(.FALSE.)       
        CALL MMCMEM('FROT',MODELE,RESOCO,DEPMOI,DEPDEL,
     &              VITMOI,VITPLU,ACCMOI,MATELE)
C
C --- MATR_ELEM DE CONTACT XFEM
C
      ELSEIF (TYPMAT.EQ.'MEXFEC') THEN 
        IF (.NOT.LVALM) CALL ASSERT(.FALSE.)  
        IF (.NOT.LDEPA) CALL ASSERT(.FALSE.)      
        CALL XMCMEM('CONT',MODELE,MAILLA,DEPMOI,DEPDEL,
     &              RESOCO,MATELE)
C
C --- MATR_ELEM DE FROTTEMENT XFEM
C
      ELSEIF (TYPMAT.EQ.'MEXFEF') THEN 
        IF (.NOT.LVALM) CALL ASSERT(.FALSE.)  
        IF (.NOT.LDEPA) CALL ASSERT(.FALSE.)      
        CALL XMCMEM('FROT',MODELE,MAILLA,DEPMOI,DEPDEL,
     &              RESOCO,MATELE)     
C
C --- MATR_ELEM DE CONTACT XFEM - GDS GLISSEMENTS
C
      ELSEIF (TYPMAT.EQ.'MEXFTC') THEN 
        IF (.NOT.LVALM) CALL ASSERT(.FALSE.)  
        IF (.NOT.LDEPA) CALL ASSERT(.FALSE.)      
        CALL XTCMEM('CONT',MODELE,DEPMOI,DEPDEL,RESOCO,
     &              MATELE) 
C
C --- MATR_ELEM DE FROTTEMENT XFEM - GDS GLISSEMENTS
C
      ELSEIF (TYPMAT.EQ.'MEXFTF') THEN 
        IF (.NOT.LVALM) CALL ASSERT(.FALSE.)  
        IF (.NOT.LDEPA) CALL ASSERT(.FALSE.)      
        CALL XTCMEM('FROT',MODELE,DEPMOI,DEPDEL,RESOCO,
     &              MATELE)                          
      ELSE  
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- DEBUG
C
      IF (NIV.EQ.2) THEN
        CALL NMDEBG(' ',MATELE,IFM   )
      ENDIF      
C
C --- MENAGE
C
      CALL JEDETR('&&NMCALC.LISTE_CHARGE')
      CALL JEDEMA()      
C
      END
