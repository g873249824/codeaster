      SUBROUTINE  XSIDE2(ELREFP,NDIM,COORSE,ELRESE,IGEOM,
     &                   HE,NFH,DDLC,DDLM,NFE,
     &                   BASLOC,NNOP,NPG,IDECPG,TYPMOD,IMATE,COMPOR,
     &                   IDEPL,LSN,LST,NFISS,FISNO,SIG)

      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER       NDIM,IGEOM,IMATE,NNOP,NPG,IDEPL,IDECPG
      INTEGER       NFH,DDLC,NFE,NFISS,FISNO(NNOP,NFISS)
      CHARACTER*8   ELREFP,ELRESE,TYPMOD(*)
      CHARACTER*16  COMPOR(4)
      REAL*8        BASLOC(6*NNOP),HE(NFISS),COORSE(*)
      REAL*8        LSN(NNOP),LST(NNOP),SIG(4,NPG)


C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/04/2013   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C TOLE CRP_21 CRS_1404
C.......................................................................
C
C     BUT:  CALCUL DE L'OPTION SIEF_ELGA AVEC X-FEM EN 2D
C.......................................................................
C IN  ELREFP  : ELEMENT DE REFERENCE PARENT
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  COORSE  : COORDONNEES DES SOMMETS DU SOUS-ELEMENT
C IN  IGEOM   : COORDONNEES DES NOEUDS DE L'ELEMENT PARENT
C IN  HE      : VALEUR DE LA FONCTION HEAVISIDE SUR LE SOUS-ELT
C IN  NFH    : NOMBRE DE DDL HEAVYSIDE (PAR NOEUD)
C IN  DDLC    : NOMBRE DE DDL DE CONTACT (PAR NOEUD)
C IN  DDLM    : NOMBRE DE DDL PAR NOEUD MILIEU
C IN  NFE     : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT
C IN  BASLOC  : BASE LOCALE AU FOND DE FISSURE AUX NOEUDS
C IN  NNOP    : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C IN  NPG     : NOMBRE DE POINTS DE GAUSS DU SOUS-ELEMENT
C IN  TYPMOD  : TYPE DE MODELISATION
C IN  IMATE   : MATERIAU CODE
C IN  COMPOR  : COMPORTEMENT
C IN  IDEPL   : ADRESSE DU DEPLACEMENT A PARTIR DE LA CONF DE REF
C IN  LSN     : VALEUR DE LA LEVEL SET NORMALE AUX NOEUDS PARENTS
C IN  LST     : VALEUR DE LA LEVEL SET TANGENTE AUX NOEUDS PARENTS
C IN  NFISS   : NOMBRE DE FISSURES "VUES" PAR L'ÉLÉMENT
C IN  JFISNO  : POINTEUR DE CONNECTIVITÉ FISSURE/HEAVISIDE

C OUT SIG     : CONTRAINTES (SIEF_ELGA)

C......................................................................
C
      CHARACTER*2  K2BID
      CHARACTER*16 PHENOM
      INTEGER  KPG,N,I,J,INO,IRET,IPG
      INTEGER  NBSIGM,NNO,NNOS,NPGBIS,DDLS,DDLD,DDLM,NDIMB
      INTEGER  JCOOPG,JDFD2,JGANO,IDFDE,IVF,IPOIDS,NBSIG
      LOGICAL  GRDEPL,AXI
      REAL*8   F(3,3),EPS(6),BASLOG(6)
      REAL*8   FE(4),INSTAN,RAC2
      REAL*8   XG(NDIM),XE(NDIM),FF(NNOP),LSNG,LSTG
      REAL*8   RBID,R8BI7(7),R8BI3(3)
      REAL*8   DFDI(NNOP,NDIM),DGDGL(4,3)
      REAL*8   GRAD(3,3)
      REAL*8   ZERO,S,STH,D(4,4),R,EPSTH(6)
      INTEGER  NNOPS,IBID

      DATA    ZERO / 0D0 /
      DATA    RAC2 / 1.4142135623731D0 /
C--------------------------------------------------------------------

C     ON AUTORISE UNIQUEMENT L'ISOTROPIE
      CALL RCCOMA(IMATE,'ELAS',1,PHENOM,IRET)
      CALL ASSERT(IRET.EQ.0 .AND. PHENOM.EQ.'ELAS')

C     INITIALISATIONS
      INSTAN = 0.D0
      CALL VECINI(7,0.D0,R8BI7)
      CALL VECINI(3,0.D0,R8BI3)

C     NOMBRE DE DDL DE DEPLACEMENT À CHAQUE NOEUD SOMMET
      DDLD=NDIM*(1+NFH+NFE)
C
C     NOMBRE DE DDL TOTAL (DEPL+CONTACT) À CHAQUE NOEUD SOMMET
      DDLS=DDLD+DDLC
C
C ---- NOMBRE DE CONTRAINTES ASSOCIE A L'ELEMENT
C      -----------------------------------------
      NBSIG = NBSIGM()

      CALL ELREF4(' ','RIGI',IBID,IBID,NNOPS,IBID,IBID,IBID,IBID,IBID)
C
C - INITIALISATION
      GRDEPL = COMPOR(3).EQ. 'GROT_GDEP'
      AXI    = TYPMOD(1) .EQ. 'AXIS'
C

      K2BID = ' '
      IF (GRDEPL) THEN
        CALL U2MESS('F','XFEM2_2')
      ENDIF
C
      CALL ELREF5(ELRESE,'XINT',NDIMB,NNO,NNOS,NPGBIS,IPOIDS,JCOOPG,IVF,
     &                  IDFDE,JDFD2,JGANO)
      CALL ASSERT(NPG.EQ.NPGBIS.AND.NDIM.EQ.NDIMB)
C
C - CALCUL POUR CHAQUE POINT DE GAUSS

      DO 10 KPG=1,NPG

C       NUMERO DU PG DANS LE FAMILLE 'XFEM'
        IPG = IDECPG + KPG

C       COORDONNEES DU PT DE GAUSS DANS LE REPERE REEL : XG
        CALL VECINI(NDIM,0.D0,XG)
        DO 110 I=1,NDIM
          DO 111 N=1,NNO
          XG(I)=XG(I)+ZR(IVF-1+NNO*(KPG-1)+N)*COORSE(NDIM*(N-1)+I)
 111      CONTINUE
 110    CONTINUE
C
C       CALCUL DES FF
        CALL REEREF(ELREFP,AXI, NNOP,NNOPS,ZR(IGEOM),XG,IDEPL,GRDEPL,
     &              NDIM,HE,RBID,RBID,
     &              FISNO,NFISS,NFH,NFE,DDLS,DDLM,FE,DGDGL,'NON',
     &              XE,FF,DFDI,F,EPS,GRAD)

C-----------------------------------------------------------------------
C         BOUCLE SUR LES POINTS DE GAUSS DU SOUS-ELT
C-----------------------------------------------------------------------
C -     CALCUL DE LA DISTANCE A L'AXE (AXISYMETRIQUE)
        IF (AXI) THEN
          R  = 0.D0
          DO 1000 INO=1,NNOP
            R  = R  + FF(INO)*ZR(IGEOM-1+2*(INO-1)+1)
1000      CONTINUE
          CALL ASSERT(R.GE.0D0)
C          ATTENTION : LE POIDS N'EST PAS X R
C          CE SERA FAIT PLUS TARD AVEC JAC = JAC X R
        ENDIF
        IF (NFE.GT.0) THEN
C         BASE LOCALE AU POINT DE GAUSS
          CALL VECINI(6,0.D0,BASLOG)
          LSNG = 0.D0
          LSTG = 0.D0
          DO 113 INO=1,NNOP
            LSNG = LSNG + LSN(INO) * FF(INO)
            LSTG = LSTG + LST(INO) * FF(INO)
            DO 114 I=1,6
              BASLOG(I) = BASLOG(I) + BASLOC(6*(INO-1)+I) * FF(INO)
 114        CONTINUE
 113      CONTINUE

C
C         FONCTION D'ENRICHISSEMENT AU POINT DE GAUSS ET LEURS DERIVEES
          CALL XCALF2(HE(1),LSNG,LSTG,BASLOG,FE,DGDGL,IRET)
C         ON A PAS PU CALCULER LES DERIVEES DES FONCTIONS SINGULIERES
C         CAR ON SE TROUVE SUR LE FOND DE FISSURE
          CALL ASSERT(IRET.NE.0)
        ENDIF

C       CALCUL DES DEFORMATIONS EPS
        CALL REEREF(ELREFP,AXI, NNOP,NNOPS,ZR(IGEOM),XG,IDEPL,GRDEPL,
     &              NDIM,HE,R,RBID,
     &              FISNO,NFISS,NFH,NFE,DDLS,DDLM,FE,DGDGL,'OUI',
     &              XE,FF,DFDI,F,EPS,GRAD)

C       CALCUL DES DEFORMATIONS THERMIQUES EPSTH
        CALL VECINI( 6,0.D0,EPSTH)
        CALL EPSTMC('XFEM',NDIM,INSTAN,'+',IPG,1,R8BI3,R8BI7,IMATE,
     &              'CHAR_MECA_TEMP_R',EPSTH)
C
C       CALCUL DE LA MATRICE DE HOOKE (MATERIAU ISOTROPE)
        CALL DMATMC('XFEM',K2BID,IMATE, INSTAN, '+', IPG, 1,
     &                R8BI7, R8BI3, NBSIG, D)

C       VECTEUR DES CONTRAINTES
        DO 30 I = 1, NBSIG
          S   = ZERO
          STH = ZERO
          DO 40 J = 1, NBSIG
             S   = S   + EPS(J)*D(I,J)
             STH = STH + EPSTH(J)*D(I,J)
  40      CONTINUE
          SIG(I,KPG) = S - STH
  30    CONTINUE
        SIG(4,KPG) = SIG(4,KPG)*RAC2
C
 10   CONTINUE
C
      END
