      SUBROUTINE NMTADP(NDIMSI,CRIT,MAT,SIGEL,VIM,EPM,DP,SP,XI,G,IRET)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/02/2008   AUTEUR MACOCCO K.MACOCCO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

      IMPLICIT NONE
      INTEGER  NDIMSI, IRET
      REAL*8   CRIT(3),MAT(14),SIGEL(*),VIM(9),EPM(*),SP,XI
      REAL*8   DP,G
C ----------------------------------------------------------------------
C TAHERI :  RESOLUTION DE L'EQUATION SCALAIRE G(P,SP,XI) = 0 PAR RT A SP
C ----------------------------------------------------------------------
C IN  NDIMSI DIMENSION DES TENSEURS
C IN  CRIT   CRITERES DE CONVERGENCE LOCAUX
C IN  MAT    TABLEAU DES CONSTANTES MATERIAUX
C IN  SIGEL  DEVIATEUR DES CONTRAINTES ELASTIQUES
C IN  VIM    VARIABLES INTERNES EN T-
C IN  EPM    DEFORMATION PLASTIQUE EN T-
C OUT DP     INCREMENT DEFORMATION PLASTIQUE CUMULEE TQ  F(P,SP,XI) = 0
C IN  SP     CONTRAINTE DE PIC
C IN  XI     PILOTAGE DE EPN
C OUT G      VALEUR DU CRITERE                   G(P,SP,XI)
C OUT IRET   CODE RETOUR DE L'INTEGRATION DE LA LOI DE TAHERI
C               IRET=0 => PAS DE PROBLEME
C               IRET=1 => ABSENCE DE CONVERGENCE
C ----------------------------------------------------------------------

      INTEGER NITER
      REAL*8  F,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG(6),TANG(6,6)
      REAL*8  X(4),Y(4),GG(4)


C    EXAMEN DE LA SOLUTION DP = 0

      DP = 0.D0
      CALL NMTACR(1,NDIMSI,MAT,SIGEL,VIM,EPM,DP,SP,XI,
     &                  F,G,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)

      CALL ASSERT(F.GT.0.D0)

      X(2)  = DP
      Y(2)  = F
      GG(2) = G

C    EXAMEN DE LA SOLUTION DP = DPMAX

      CALL NMTACR(0,NDIMSI,MAT,SIGEL,VIM,EPM,DP,SP,XI,
     &                F,G,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)
      DP = DPMAX
      CALL NMTACR(1,NDIMSI,MAT,SIGEL,VIM,EPM,DP,SP,XI,
     &                F,G,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)
      IF (F.GT.0.D0)
     &  CALL U2MESS('F','ALGORITH8_31')
      X(1)  = DP
      Y(1)  = F
      GG(1) = G

C    CALCUL DE DP : EQUATION SCALAIRE F=0 AVEC  0 < DP < DPMAX

      X(3)  = X(1)
      Y(3)  = Y(1)
      GG(3) = GG(1)
      X(4)  = X(2)
      Y(4)  = Y(2)
      GG(4) = GG(2)

      DO 100 NITER = 1,INT(CRIT(1))
        IF ( ABS(Y(4))/MAT(4).LT.CRIT(3) .AND. X(4).NE.0.D0) GOTO 110
        CALL ZEROCO(X,Y)
        CALL NMTACR(1,NDIMSI,MAT,SIGEL,VIM,EPM,X(4),SP,XI,
     &                Y(4),GG(4),FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)
 100  CONTINUE
      IRET = 1
      GOTO 9999
 110  CONTINUE
      DP = X(4)
      G  = GG(4)

 9999 CONTINUE
      END
