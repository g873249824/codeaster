      SUBROUTINE NMIBLE(NIVEAU,
     &                  NUMINS,MODELE,NOMA  ,DEFICO,RESOCO,
     &                  FONACT,NUMEDD,SDDYNA,SDDISC,ITERAT,
     &                  VALMOI,VALPLU,SOLALG,LNOPRE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 23/09/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE MABBAS M.ABBAS
C TOLE CRP_21
C
      IMPLICIT NONE
      INTEGER      NIVEAU,NUMINS,ITERAT
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO,RESOCO
      CHARACTER*24 MODELE,NUMEDD
      CHARACTER*24 VALMOI(8),VALPLU(8)
      CHARACTER*19 SOLALG(*)
      CHARACTER*19 SDDYNA,SDDISC
      LOGICAL      FONACT(*),LNOPRE
C       
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME)
C
C ROUTINE D'AIGUILLAGE DES ITERATIONS SE SITUANT ENTRE LES PAS DE TEMPS
C ET L'EQUILIBRE
C      
C ----------------------------------------------------------------------
C
C POUR LE MOMENT, SERT:
C  - A LA METHODE CONTINUE DE CONTACT
C  - AUX METHODES DISCRETES AVEC BOUCLE GEOMETRIE
C 
C
C
C LES ITERATIONS ONT LIEU ENTRE CETTE ROUTINE ET SA COUSINE 
C (NMTBLE) QUI COMMUNIQUENT PAR LA VARIABLE NIVEAU
C
C IN  NIVEAU : INDICATEUR D'UTILISATION DE LA BOUCLE 
C                 -1     ON N'UTILISE PAS CETTE BOUCLE
C                  4     BOUCLE CONTACT 
C                         INITIALISATION 
C                  3     BOUCLE CONTACT 
C                         BOUCLE GEOMETRIE
C                  2     BOUCLE CONTACT METHODE CONTINUE
C                         BOUCLE SEUILS DE FROTTEMENT
C                  1     BOUCLE CONTACT METHODE CONTINUE
C                         BOUCLE CONTRAINTES ACTIVES
C IN  MODELE : NOM DU MODELE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NUMINS : NUMERO D'INSTANT
C IN  ITERAT : NUMERO D'ITERATION DE NEWTON
C IN  FONACT : FONCTIONNALITES ACTIVEES
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  RESOCO : SD POUR LA RESOLUTION DE CONTACT
C IN  SDDYNA : SD DEDIEE A LA DYNAMIQUE
C IN  SDDISC : SD DISCRETISATION TEMPORELLE
C IN  NUMEDD : NOM DU NUME_DDL
C IN  VALMOI : VARIABLE CHAPEAU POUR ETAT EN T-
C IN  VALPLU : VARIABLE CHAPEAU POUR ETAT EN T+
C IN  SOLALG : VARIABLE CHAPEAU POUR INCREMENTS SOLUTIONS
C IN  LNOPRE : .TRUE. SI ON DOIT SAUTER LA PREDICTION
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      CHARACTER*8  NOMO
      INTEGER      MMITGO,MMITCA,MMITFR
      CHARACTER*24 K24BLA,K24BID
      REAL*8       R8BID
      INTEGER      IBID
      LOGICAL      ISFONC,LCTFC,LCTCC,LCTCD,LXFCM,LTFCM
      LOGICAL      PREMIE       
C
C ----------------------------------------------------------------------
C 
      CALL JEMARQ()     
      NOMO   = MODELE  
      PREMIE = (NUMINS.EQ.1).AND.(NIVEAU.EQ.4 )
      LNOPRE = .FALSE.
      K24BLA = ' '             
C     
      IF (NIVEAU.LT.0) THEN 
        GOTO 9999
      ENDIF
C
C --- INFOS SUR LE CONTACT
C
      LCTCC  = ISFONC(FONACT,'CONT_CONTINU')
      LCTCD  = ISFONC(FONACT,'CONT_DISCRET')
      LCTFC  = ISFONC(FONACT,'FROT_CONTINU') 
      LXFCM  = ISFONC(FONACT,'CONT_XFEM')
      LTFCM  = .FALSE.
      IF (LXFCM) THEN
        CALL MMINFP(0    ,DEFICO,K24BLA,'XFEM_GG',
     &              IBID ,R8BID ,K24BID,LTFCM)
      ENDIF           
C       
C --- BRANCHEMENT
C
      GOTO (101,102,103,104) NIVEAU
C
C --- NIVEAU: 4   INITIALISATION
C
 104  CONTINUE    
      CALL MMINIT(DEFICO,RESOCO,SDDYNA,VALMOI,VALPLU)
      CALL MMBOUC(RESOCO,'GEOM','INIT',MMITGO)  
C
C --- NIVEAU: 3   BOUCLE GEOMETRIE
C      
 103  CONTINUE
 
      NIVEAU = 3
      CALL MMBOUC(RESOCO,'GEOM','INCR',MMITGO)
      CALL NMIMPR('IMPR','BCL_GEOME',' ',0.D0,MMITGO)
      IF (LTFCM) THEN
        CALL XAPPAR(PREMIE,NOMA  ,NOMO  ,DEFICO,RESOCO)        
      ELSEIF ((LCTCC).AND.(.NOT.LXFCM)) THEN     
        CALL MAPPAR(PREMIE,NOMA  ,NUMEDD,DEFICO,RESOCO)
      ELSEIF (LCTCD) THEN  
        CALL CFINIG(RESOCO,ITERAT,LNOPRE)
        GOTO 9999 
      ENDIF

      CALL MMBOUC(RESOCO,'FROT','INIT',MMITFR)
C
C --- SI PAS DE FROTTEMENT -> PAS DE BOUCLE SUR SEUIL DE FROTTEMENT
C      
      IF (.NOT.LCTFC) THEN 
        GOTO 666
      ENDIF  
C
C --- NIVEAU: 2   BOUCLE SEUILS DE FROTTEMENT
C 
 102  CONTINUE
      NIVEAU = 2
      CALL MMBOUC(RESOCO,'FROT','INCR',MMITFR)
      CALL NMIMPR('IMPR','BCL_SEUIL',' ',0.D0,MMITFR)
C
C -- BOUCLE DE CONTRAINTES ACTIVES (CONTACT ECP)
C
 666  CONTINUE
      CALL MMBOUC(RESOCO,'CONT','INIT',MMITCA)
C
C --- NIVEAU: 1   BOUCLE CONTRAINTES ACTIVES
C      
 101  CONTINUE
      NIVEAU = 1
      CALL MMBOUC(RESOCO,'CONT','INCR',MMITCA)
      CALL NMIMPR('IMPR','BCL_CTACT',' ',0.D0,MMITCA)
C
C --- INITIALISATIONS DES CHAMPS
C      
      CALL NMNBLE(NIVEAU,NUMEDD,SDDYNA,RESOCO,VALMOI,
     &            VALPLU,SOLALG)
C
C --- CREATION ELEMENTS TARDIFS ET CARTE
C 
      IF (LXFCM) THEN
        CALL XCONLI(NOMA  ,NOMO  ,PREMIE,DEFICO,RESOCO)
      ELSE
        CALL CONLIG(NOMA  ,DEFICO,RESOCO,SDDISC,SDDYNA,
     &              NUMINS) 
      END IF        
C
 9999 CONTINUE
C
      CALL JEDEMA()     
      END
