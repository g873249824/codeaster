      SUBROUTINE NMCERE(MODELE,NUMEDD,MATE  ,CARELE,COMREF,
     &                  COMPOR,LISCHA,CNFEXT,CARCRI,FONACT,
     &                  SDDYNA,ITERAT,INDRO ,POUGD ,RESOCO,
     &                  VALMOI,VALPLU,SECMBR,DEPALG,VEELEM,
     &                  MEELEM,MEASSE,OFFSET,RHO   ,ETA   ,
     &                  RESIDU,LDCCVG)
C
C 
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 19/02/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C TOLE CRP_21
C
      IMPLICIT NONE
      LOGICAL       FONACT(*)
      INTEGER       ITERAT, LDCCVG, INDRO
      REAL*8        ETA,RHO, OFFSET,RESIDU
      CHARACTER*19  LISCHA,SDDYNA
      CHARACTER*19  CNFEXT
      CHARACTER*24  MODELE,NUMEDD,MATE  ,CARELE,COMREF,COMPOR
      CHARACTER*24  CARCRI,RESOCO
      CHARACTER*24  VALMOI(8),POUGD (8),VALPLU(8),DEPALG(8),SECMBR(8) 
      CHARACTER*8   MEELEM(8),VEELEM(30)
      CHARACTER*19  MEASSE(8)
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME - PILOTAGE)
C
C CHOIX DU ETA DE PILOTAGE - CALCUL DU RESIDU
C      
C ----------------------------------------------------------------------
C
C
C IN  MODELE : MODELE
C IN  NUMEDD : NUME_DDL
C IN  MATE   : CHAMP MATERIAU
C IN  CARELE : CARACTERISTIQUES DES ELEMENTS DE STRUCTURE
C IN  COMREF : VARI_COM DE REFERENCE
C IN  COMPOR : COMPORTEMENT
C IN  LISCHA : L_CHARGES
C IN  PARMET : PARAMETRES DES METHODES DE RESOLUTION
C IN  CARCRI : PARAMETRES DES METHODES D'INTEGRATION LOCALES
C IN  ITERAT : NUMERO D'ITERATION DE NEWTON
C IN  VALMOI : ETAT EN T-
C IN  VALPLU : ETAT EN T+ 
C IN  SECMBR : SECONDS MEMBRES
C IN  POUGD  : INFOS POUTRES EN GRANDES ROTATIONS
C IN  DEPALG : VARIABLE CHAPEAU POUR DEPLACEMENTS 
C IN  CNFEXT : FORCES EXTERNES
C IN  RESOCO : SD CONTACT
C IN  MEELEM : MATRICES ELEMENTAIRES
C IN  MEASSE : MATRICES ASSEMBLEES
C IN  VEELEM : VECTEURS ELEMENTAIRES
C IN  ETA    : PARAMETRE DE PILOTAGE
C OUT LDCCVG : CODE RETOUR DE L'INTEGRATION DU COMPORTEMENT
C                       0 : CAS DE FONCTIONNEMENT NORMAL
C                       1 : ECHEC DE L'INTEGRATION DE LA LDC
C                       3 : SIZZ PAS NUL POUR C_PLAN DEBORST
C OUT RESIDU : RESIDU OPTIMAL SI L'ON A CHOISI LE RESIDU
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      LOGICAL      ISFONC,REAROT
      INTEGER      JDU, JDU0, JDU1, NEQ
      INTEGER      JDDEPL, JDEPPL,IRET
      INTEGER      JDEPPT, JDEPDT
      CHARACTER*8  K8BID
      CHARACTER*19 CNFINT, CNDIRI
      CHARACTER*24 K24BID,K24BLA
      CHARACTER*24 VALPLT(8),DEPALT(8)
      CHARACTER*24 DEPPLT,DEPDET,DDEP
      CHARACTER*24 DDEPLA,DEPOLD,DEPDEL,DEPPRE(2)
      CHARACTER*24 DEPPLU,SIGPLU,VARPLU,COMPLU
      CHARACTER*8  VEFINT,VEDIRI
      CHARACTER*8  MERIGI,MESSTR
      CHARACTER*19 SSTRU
      INTEGER      IFM,NIV
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('PILOTAGE',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<PILOTAGE> ...... CALCUL DU RESIDU' 
      ENDIF         
C
C --- INITIALISATIONS
C
      K24BLA = ' '
      REAROT = ISFONC(FONACT,'REAROT')     
      DDEP   = '&&CNCETA.CHP0'
      DEPDET = '&&CNCETA.CHP1'
      DEPPLT = '&&CNCETA.CHP2'
      CNFINT = '&&CNCETA.FIN1'
      CNDIRI = '&&CNCETA.DIR2'  
      CALL DISMOI('F','NB_EQUA',NUMEDD,'NUME_DDL',NEQ,K8BID,IRET)
C
C --- DECOMPACTION VARIABLES CHAPEAUX
C      
      CALL DESAGG(DEPALG,DDEPLA,DEPDEL,DEPOLD,DEPPRE(1),
     &            DEPPRE(2),K24BID,K24BID,K24BID)
      CALL DESAGG(VALPLU,DEPPLU,SIGPLU,VARPLU,COMPLU,
     &            K24BID,K24BID,K24BID,K24BID)  
      VEFINT = VEELEM(1)   
      VEDIRI = VEELEM(2) 
      MERIGI = MEELEM(1)
      MESSTR = MEELEM(6)
      SSTRU  = MEASSE(6)              
C
C --- ACCES AUX VECTEURS SOLUTIONS
C 
      CALL JEVEUO(DEPDEL(1:19)//'.VALE'   ,'E',JDU)
      CALL JEVEUO(DEPPRE(1)(1:19)//'.VALE','E',JDU0)
      CALL JEVEUO(DEPPRE(2)(1:19)//'.VALE','E',JDU1)          
      CALL JEVEUO(DDEP(1:19)  //'.VALE'   ,'E',JDDEPL)
      CALL JEVEUO(DEPPLT(1:19)//'.VALE'   ,'E',JDEPPT)
      CALL JEVEUO(DEPDET(1:19)//'.VALE'   ,'E',JDEPDT)
      CALL JEVEUO(DEPPLU(1:19)//'.VALE'   ,'L',JDEPPL)
C
C --- MISE A JOUR DES DEPLACEMENTS
C     
      CALL R8INIR(NEQ   ,0.D0      ,ZR(JDDEPL),1)
      CALL DAXPY (NEQ   ,RHO       ,ZR(JDU0)  ,1, ZR(JDDEPL),1)
      CALL DAXPY (NEQ   ,ETA-OFFSET,ZR(JDU1)  ,1, ZR(JDDEPL),1)

      CALL MAJOUR(NEQ,REAROT,ZI(INDRO),ZR(JDU),ZR(JDDEPL),
     &            1.D0,ZR(JDEPDT))
      CALL MAJOUR(NEQ,REAROT,ZI(INDRO),ZR(JDEPPL),ZR(JDDEPL),
     &            1.D0,ZR(JDEPPT))
C
C --- RECONSTRUCTION DES VARIABLES CHAPEAUX
C
      CALL AGGLOM(DEPPLT,SIGPLU,VARPLU,COMPLU,K24BLA,
     &            K24BLA,K24BLA,K24BLA, 4, VALPLT)
      CALL AGGLOM(DDEPLA,DEPDET,DEPOLD,DEPPRE(1),DEPPRE(2),
     &            K24BLA,K24BLA,K24BLA, 5, DEPALT)     
C
C --- REACTUALISATION DES EFFORTS EXTERIEURS (AVEC ETA)
C
      CALL NMFEXT(NUMEDD,ETA   ,SECMBR,FONACT,RESOCO,
     &            CNFEXT)
C
C --- CALCUL DES FORCES INTERIEURES ET DES REACTIONS D'APPUI
C
      CALL NMFINT('RAPH_MECA',
     &            MODELE,NUMEDD,MATE  ,CARELE,COMREF,
     &            COMPOR,LISCHA,CARCRI,FONACT,RESOCO,
     &            ITERAT,SDDYNA,LDCCVG,VALMOI,VALPLT,
     &            POUGD ,DEPALT,MERIGI,MESSTR,VEDIRI,
     &            VEFINT,SSTRU ,CNFINT,CNDIRI)
C
C --- CALCUL DU RESIDU
C
      IF (LDCCVG .EQ. 0) THEN 
        CALL NMPILR(NEQ   ,CNFINT,CNDIRI,CNFEXT,RESIDU)  
      ENDIF
C

      CALL JEDEMA()
      END
