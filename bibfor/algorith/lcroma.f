      SUBROUTINE LCROMA (IMATE, TEMP)
     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 10/10/2001   AUTEUR ADBHHVV V.CANO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE ADBHHVV V.CANO

      IMPLICIT NONE
      INTEGER  IMATE
      REAL*8   TEMP

C ******************************************************
C *       INTEGRATION DE LA LOI DE ROUSSELIER LOCAL    *
C *        LECTURE DES CARACTERISTIQUES DU MATERIAU    *
C ******************************************************

C IN  IMATE   : ADRESSE DU MATERIAU
C IN  TEMP    : TEMPERATURE A L'INSTANT DU CALCUL
C IN COMMON   : PM DOIT DEJA ETRE AFFECTE (PLASTICITE CUMULEE EN T-)

      INTEGER ITEMAX, JPROLP, JVALEP, NBVALP
      REAL*8  YOUNG,NU,MU,K,SIGY,ALPHA
      REAL*8  SIG1,D,F0,FCR,ACCE
      REAL*8  FONC,EQETR,PM,RPM,PREC
      COMMON /LCROU/ YOUNG,NU,MU,K,SIGY,ALPHA,
     &               SIG1,D,F0,FCR,ACCE,
     &               FONC,EQETR,PM,RPM,PREC,
     &               ITEMAX, JPROLP, JVALEP, NBVALP
     
      CHARACTER*2 CODRET(5)
      CHARACTER*8 NOMRES(5)
      REAL*8       R8BID,VALRES(5),PENTE,AIRE

C 1 - CARACTERISTIQUE ELASTIQUE E ET NU => CALCUL DE MU - K

      CALL RCVALA(IMATE,'ELAS',1,'TEMP',TEMP,1,'NU',NU,CODRET(1),'F ')
      CALL RCTRAC(IMATE,'TRACTION','SIGM',TEMP,JPROLP,JVALEP,NBVALP,
     &            YOUNG)
     
      MU= YOUNG/(2.D0*(1.D0+NU))
      K = YOUNG/(3.D0*(1.D0-2.D0*NU))
     
C 2 - COEFFICIENT DE DILATATION THERMIQUE
      
      CALL RCVALA(IMATE,'ELAS',1,'TEMP',TEMP,1,'ALPHA',
     &            VALRES(1),CODRET(1),'  ')       
      IF(CODRET(1).NE.'OK') VALRES(1)=0.D0
      ALPHA=VALRES(1)
        
C 3 - SIGY ET ECROUISSAGE EN P-
       
      CALL RCFONC('S','TRACTION',JPROLP,JVALEP,NBVALP,SIGY,
     &           R8BID,R8BID,R8BID,R8BID,R8BID,R8BID,R8BID,R8BID)

      CALL RCFONC('V','TRACTION',JPROLP,JVALEP,NBVALP,R8BID,
     &           R8BID,R8BID,PM,RPM,PENTE,AIRE,R8BID,R8BID)
             
C 4 - PARAMETRES DE CROISSANCE DE CAVITES

      NOMRES(1) = 'D'
      NOMRES(2) = 'SIGM_1'
      NOMRES(3) = 'PORO_INIT'
      NOMRES(4) = 'PORO_CRIT'
      NOMRES(5) = 'PORO_ACCE'
      
      CALL RCVALA(IMATE,'ROUSSELIER',1,'TEMP',TEMP,5,
     &            NOMRES,VALRES,CODRET,'F ')
      D    = VALRES(1)
      SIG1 = VALRES(2)
      F0   = VALRES(3)
      FCR  = VALRES(4)
      ACCE = VALRES(5)
     
      END
