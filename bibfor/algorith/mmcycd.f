      SUBROUTINE MMCYCD(NOMA  ,DEFICO,RESOCO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO,RESOCO
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE)
C
C DETECTION DES CYCLAGES
C
C ----------------------------------------------------------------------
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE DEFINITION DU CONTACT
C IN  RESOCO : SD DE RESOLUTION DU CONTACT
C
C
C
C
      INTEGER      IFM,NIV
      CHARACTER*24 TABFIN
      INTEGER      JTABF
      INTEGER      CFMMVD,ZTABF
      INTEGER      INDCO,INDFR
      REAL*8       RESE(3)
      INTEGER      NBMAE,NPTM
      LOGICAL      MMINFL,LVERI
      INTEGER      MMINFI,CFDISI,NZOCO
      LOGICAL      CFDISL,LFROT,LBOUCC
      INTEGER      IZONE,IPTM,IMAE,IPTC
      INTEGER      JDECME,POSMAE,NUMMAE
      CHARACTER*8  NOMMAE
      CHARACTER*16 NOMPT
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> ... CYCLAGE'
      ENDIF
C
C --- ACCES OBJETS
C
      TABFIN = RESOCO(1:14)//'.TABFIN'
      CALL JEVEUO(TABFIN,'L',JTABF )
      ZTABF  = CFMMVD('ZTABF')
C
C --- INITIALISATIONS
C
      NOMPT  = ' '
      NZOCO  = CFDISI(DEFICO,'NZOCO')
      LFROT  = CFDISL(DEFICO,'FROTTEMENT')
      LBOUCC = CFDISL(DEFICO,'CONT_BOUCLE')
C
C --- BOUCLE SUR LES ZONES
C
      IPTC   = 1
      DO 10 IZONE = 1,NZOCO
C
C ----- OPTIONS SUR LA ZONE DE CONTACT
C
        LVERI  = MMINFL(DEFICO,'VERIF' ,IZONE )
        NBMAE  = MMINFI(DEFICO,'NBMAE' ,IZONE )
        JDECME = MMINFI(DEFICO,'JDECME',IZONE )
C
C ----- MODE VERIF: ON SAUTE LES POINTS
C
        IF (LVERI) GOTO 25
C
C ----- BOUCLE SUR LES MAILLES ESCLAVES
C
        DO 20 IMAE = 1,NBMAE
C
C ------- NUMERO DE LA MAILLE ESCLAVE
C
          POSMAE = JDECME + IMAE
C
C ------- NOMBRE DE POINTS SUR LA MAILLE ESCLAVE
C
          CALL MMINFM(POSMAE,DEFICO,'NPTM',NPTM  )
C
C ------- BOUCLE SUR LES POINTS
C
          DO 30 IPTM = 1,NPTM
C
C --------- STATUTS
C
            INDCO   = NINT(ZR(JTABF+ZTABF*(IPTC-1)+22))
            INDFR   = NINT(ZR(JTABF+ZTABF*(IPTC-1)+23))
            RESE(1) = ZR(JTABF+ZTABF*(IPTC-1)+25)
            RESE(2) = ZR(JTABF+ZTABF*(IPTC-1)+26)
            RESE(3) = ZR(JTABF+ZTABF*(IPTC-1)+27)
C
C --------- NOM DU POINT DE CONTACT
C
            NUMMAE = NINT(ZR(JTABF+ZTABF*(IPTC-1)+1))
            CALL JENUNO(JEXNUM(NOMA//'.NOMMAI',NUMMAE),NOMMAE)
            CALL MMNPOI(NOMA  ,NOMMAE,-1,IPTM  ,NOMPT )
C
C --------- DETECTION DU CYCLE DE TYPE CONTACT/PAS CONTACT
C
            CALL MMCYC1(RESOCO,IPTC  ,NOMPT ,INDCO )
C
C --------- DETECTION DU CYCLE DE TYPE ADHERENT/GLISSANT
C
            IF (LFROT) CALL MMCYC2(RESOCO,IPTC  ,NOMPT ,INDCO ,INDFR )
C
C --------- DETECTION DU CYCLE DE TYPE GLISSANT AVANT/ARRIERE
C
            IF (LFROT) CALL MMCYC3(RESOCO,IPTC  ,NOMPT ,INDCO ,INDFR ,
     &                             RESE  )
C
C --------- DETECTION DU CYCLE DE TYPE FLIP-FLOP HISTORIQUE
C
            IF (LBOUCC) CALL MMCYC4(RESOCO,IPTC  ,NOMPT ,INDCO )
C
C --------- LIAISON DE CONTACT SUIVANTE
C
            IPTC   = IPTC + 1
  30      CONTINUE
  20    CONTINUE
  25    CONTINUE
  10  CONTINUE
C
      CALL JEDEMA()
      END
