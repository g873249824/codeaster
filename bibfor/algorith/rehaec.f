      SUBROUTINE REHAEC(NOMRES,RESGEN,NOMSST)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 24/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C***********************************************************************
C    T. KERBER     DATE 14/05/93
C-----------------------------------------------------------------------
C  BUT:      < RESTITUTION HARMONIQUE ECLATEE >
      IMPLICIT NONE
C
C      RESTITUER EN BASE PHYSIQUE SUR UNE SOUS-STRUCTURE LES RESULTATS
C                ISSU DE LA SOUS-STRUCTURATION GENERALE
C  LE CONCEPT RESULTAT EST UN RESULTAT COMPOSE "DYNA_HARMO"
C
C-----------------------------------------------------------------------
C
C NOMRES   /I/: NOM K8 DU CONCEPT DYNA HARMO RESULTAT
C RESGEN   /I/: NOM K8 DU HARM_GENE AMONT
C NOMSST   /I/: NOM K8 DE LA SOUS-STRUCTURE SUR LAQUELLE ON RESTITUE
C
C
C
      INCLUDE 'jeveux.h'
C
C
      CHARACTER*8 CHSYMB,SYMB(3)
      CHARACTER*8 NOMRES,BASMOD,MAILLA,LINT
      CHARACTER*8 NOMSST,MODGEN,RESGEN,SOUTR
      CHARACTER*19 NUMDDL
      CHARACTER*19 NUMGEN,CHAMNE,NUME(3)
      CHARACTER*24 CREFE(2),CHAMOL,CHAMBA
      CHARACTER*24 VALK(4)
      CHARACTER*24 CHAMNO,CHAREF
      CHARACTER*16 NOMPAR(1)
      INTEGER IAFREQ(1)
      INTEGER VALI(2)
      CHARACTER*8 KBID
      COMPLEX*16  CBID
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
      INTEGER I ,IAD ,IBID ,IEQ ,IER ,IRET ,ISYMB
      INTEGER J ,K ,LDNEW ,LLCHAB ,LLCHOL ,LLNUEQ ,LLORS
      INTEGER LLPRS ,LLREF ,LREF ,NBBAS ,NBDDG ,NBFREQ ,NBSST
      INTEGER NBSYMB ,NEQ ,NUSST ,NUTARS
      REAL*8 FREQ ,RBID
C-----------------------------------------------------------------------
      DATA SOUTR/'&SOUSSTR'/
      DATA NOMPAR/'FREQ'/
C-----------------------------------------------------------------------
C
C-----------------ECRITURE DU TITRE-------------------------------------
C
      CALL JEMARQ()
      CALL TITRE
C
C-----DETERMINATION DES CHAMPS CALCULES LORS DES CALCULS HARMONIQUES----
C
C     LES NOMS SYMBOLIQUES DES CHAMPS CALCULES, PARMI DEPL, VITE ET ACCE
C     SONT STOCKES DANS LE VECTEUR SYMB. LEURS NUME_DDL SONT ECRITS
C     DANS LE VECTEUR NUM.
C
      ISYMB = 0
C
      CALL RSEXCH(' ',RESGEN,'DEPL',1,CHAMNO,IRET)
C
      IF (IRET.EQ.0) THEN
        ISYMB = ISYMB + 1
        SYMB(ISYMB) = 'DEPL'
        CHAREF = CHAMNO(1:19)//'.REFE'
        CALL JEVEUO(CHAREF,'L',LREF)
        NUME(ISYMB) = ZK24(LREF+1)
        CALL JELIBE(CHAREF)
      END IF
C
C
      CALL RSEXCH(' ',RESGEN,'VITE',1,CHAMNO,IRET)
C
      IF (IRET.EQ.0) THEN
        ISYMB = ISYMB + 1
        SYMB(ISYMB) = 'VITE'
        CHAREF = CHAMNO(1:19)//'.REFE'
        CALL JEVEUO(CHAREF,'L',LREF)
        NUME(ISYMB) = ZK24(LREF+1)
        CALL JELIBE(CHAREF)
      END IF
C
C
      CALL RSEXCH(' ',RESGEN,'ACCE',1,CHAMNO,IRET)
C
      IF (IRET.EQ.0) THEN
        ISYMB = ISYMB + 1
        SYMB(ISYMB) = 'ACCE'
        CHAREF = CHAMNO(1:19)//'.REFE'
        CALL JEVEUO(CHAREF,'L',LREF)
        NUME(ISYMB) = ZK24(LREF+1)
        CALL JELIBE(CHAREF)
      END IF
C
C     NOMBRE DE CHAMPS SYMBOLIQUES CALCULES.
C     ON S'ASSURE QUE LEUR NOMBRE EST NON NUL.
C
      NBSYMB = ISYMB
C
      IF (NBSYMB.EQ.0) THEN
        VALK (1) = RESGEN
        CALL U2MESG('F', 'ALGORITH14_35',1,VALK,0,0,0,0.D0)
      END IF
C
C     VERIFICATION DE LA COHERENCE DES NUM_DDL
C
      NUMGEN = NUME(1)
      IF (NBSYMB.GT.1) THEN
        DO 10 I = 2,NBSYMB
          IF (NUME(I).NE.NUMGEN) THEN
        VALK (1) = SYMB(I)
        VALK (2) = NUME(I)
        VALK (3) = SYMB(1)
        VALK (4) = NUMGEN
            CALL U2MESG('F', 'ALGORITH14_36',4,VALK,0,0,0,0.D0)
          END IF

   10   CONTINUE
      END IF
C
C
C-------------------RECUPERATION DU MODELE GENERALISE-------------------
C
      NUMGEN(15:19) = '.NUME'
      CALL JEVEUO(NUMGEN//'.REFN','L',LLREF)
      MODGEN = ZK24(LLREF)
C
C--------------------RECUPERATION NUMERO DE SOUS-STRUCTURE--------------
C                 ET DU NOEUD TARDIF CORRESPONDANT
C
      CALL JENONU(JEXNOM(MODGEN//'      .MODG.SSNO',NOMSST),NUSST)
      IF (NUSST.EQ.0) THEN
        VALK (1) = MODGEN
        VALK (2) = NOMSST
        CALL U2MESG('F', 'ALGORITH14_25',2,VALK,0,0,0,0.D0)
      END IF
C
      CALL JENONU(JEXNOM(NUMGEN//'.LILI',SOUTR),IBID)
      CALL JEVEUO(JEXNUM(NUMGEN//'.ORIG',IBID),'L',LLORS)
      CALL JENONU(JEXNOM(NUMGEN//'.LILI',SOUTR),IBID)
      CALL JELIRA(JEXNUM(NUMGEN//'.ORIG',IBID),'LONMAX',NBSST,KBID)
C
      DO 20 I = 1,NBSST
        IF (ZI(LLORS+I-1).EQ.NUSST) NUTARS = I
   20 CONTINUE
C
C
C     NOMBRE DE MODES ET NUMERO DU PREMIER D.D.L. DE LA SOUS-STRUCTURE
C
      CALL JENONU(JEXNOM(NUMGEN//'.LILI',SOUTR),IBID)
      CALL JEVEUO(JEXNUM(NUMGEN//'.PRNO',IBID),'L',LLPRS)
      NBDDG = ZI(LLPRS+ (NUTARS-1)*2+1)
      IEQ = ZI(LLPRS+ (NUTARS-1)*2)
C
C----------------------RECUPERATION DE LA BASE MODALE-------------------
C   ET VERIFICATION COHERENCE NOMBRE DE CHAMPS DE LA BASE
C    (QUI NORMALEMENT NE DOIT PAS DECLENCHER MAIS ON EST PRUDENT)
C
      CALL MGUTDM(MODGEN,NOMSST,IBID,'NOM_BASE_MODALE',IBID,BASMOD)
C
      CALL DISMOI('F','NB_MODES_TOT',BASMOD,'RESULTAT',
     &                      NBBAS,KBID,IER)
      IF (NBBAS.NE.NBDDG) THEN
        VALK (1) = BASMOD
        VALI (1) = NBBAS
        VALI (2) = NBDDG
        CALL U2MESG('F', 'ALGORITH14_26',1,VALK,2,VALI,0,0.D0)
      END IF
C
      CALL JEVEUO(BASMOD//'           .REFD','L',LLREF)
      LINT = ZK24(LLREF+4)
      CALL JELIBE(BASMOD//'           .REFD')
      CALL DISMOI('F','NOM_MAILLA',LINT,'INTERF_DYNA',IBID,MAILLA,IRET)
      CALL DISMOI('F','NOM_NUME_DDL',LINT,'INTERF_DYNA',IBID,NUMDDL,
     &            IRET)
      CALL DISMOI('F','NB_EQUA',NUMDDL,'NUME_DDL',NEQ,KBID,IRET)
C
      CREFE(1) = MAILLA
      CREFE(2) = NUMDDL
C
C--------------RECUPERATION NOMBRE DE FREQUENCES D'EXCITATION ETUDIEES--
C
      CALL RSORAC(RESGEN,'LONUTI',IBID,RBID,KBID,CBID,RBID,KBID,
     &            NBFREQ,1,IBID)
C
C
C--------------------ALLOCATION STRUCTURE DE DONNEES RESULTAT-----------
C
      CALL RSCRSD('G',NOMRES,'DYNA_HARMO',NBFREQ)
C
C----------------------RESTITUTION PROPREMENT DITE----------------------
C
      CALL JEVEUO(NUMGEN//'.NUEQ','L',LLNUEQ)
C
C     BOUCLE SUR LES FREQUENCES ETUDIEES
C
      DO 30 I = 1,NBFREQ
C
C     BOUCLE SUR LES CHAMPS A RESTITUER
C
        DO 40 ISYMB = 1,NBSYMB
C
          CHSYMB = SYMB(ISYMB)
C
C  REQUETTE NOM ET ADRESSE CHAMNO GENERALISE
          CALL DCAPNO(RESGEN,CHSYMB,I,CHAMOL)
          CALL JEVEUO(CHAMOL,'L',LLCHOL)
C
C  REQUETTE NOM ET ADRESSE NOUVEAU CHAMNO
          CALL RSEXCH(' ',NOMRES,CHSYMB,I,CHAMNE,IER)
C  CREATION DU .REFE
          CALL VTCREA(CHAMNE,CREFE,'G','C',NEQ)
          CALL JEVEUO(CHAMNE//'.VALE','E',LDNEW)
C
C  BOUCLE SUR LES MODES PROPRES DE LA BASE
C
          DO 50 J = 1,NBDDG
C
            CALL DCAPNO(BASMOD,'DEPL',J,CHAMBA)
            CALL JEVEUO(CHAMBA,'L',LLCHAB)
CALL JEIMPO('MESSAGE',CHAMBA,' ','BIDON')
C
            IAD = LLCHOL + ZI(LLNUEQ+IEQ+J-2) - 1
C
C     BOUCLE SUR LES EQUATIONS PHYSIQUES
C
            DO 60 K = 1,NEQ
C
              ZC(LDNEW+K-1) = ZC(LDNEW+K-1) + ZR(LLCHAB+K-1)*ZC(IAD)
   60       CONTINUE
C
            CALL JELIBE(CHAMBA)
   50     CONTINUE
          CALL RSNOCH(NOMRES,CHSYMB,I)
C
          CALL JELIBE(CHAMOL)
   40   CONTINUE
C
C     ENREGISTREMENT DES FREQUENCES DANS NOMRES
C
        CALL RSADPA(RESGEN,'L',1,NOMPAR,I,0,IAFREQ,KBID)
        FREQ = ZR(IAFREQ(1))
        CALL RSADPA(NOMRES,'E',1,NOMPAR,I,0,IAFREQ,KBID)
        ZR(IAFREQ(1)) = FREQ
C
   30 CONTINUE
C
      CALL JELIBE(NUMGEN//'.NUEQ')
C
      GOTO 9999

 9999 CONTINUE
      CALL JEDEMA()
      END
