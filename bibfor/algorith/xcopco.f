      SUBROUTINE XCOPCO(CHSGE ,CHSCF ,ALIAS ,NDIM  ,NUMMAE,
     &                  IFACE ,KSI1  ,KSI2  ,NPTE,GEOM  ) 
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 21/12/2010   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRS_1404
C
      IMPLICIT NONE
      CHARACTER*19 CHSGE,CHSCF
      CHARACTER*8  ALIAS
      INTEGER      NUMMAE,NDIM,IFACE,NPTE
      REAL*8       KSI1
      REAL*8       KSI2
      REAL*8       GEOM(3)
C      
C ----------------------------------------------------------------------
C
C ROUTINE XFEM (CONTACT - GRANDS GLISSEMENTS)
C
C CALCUL DES COORDONNEES D'UN POINT DE CONTACT A PARTIR
C DES COORDONNEES PARAMETRIQUES POUR LE CONTACT METHODE CONTINUE
C
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C
C ----------------------------------------------------------------------
C
C
C IN  CHSGE  : CHAMP CONTENANT LA GEOMETRIE DE LA FACETTE ESCLAVE
C IN  CHSCF  : CHAMP CONTENANT LES NUMEROS LOCAUX DES POINTS 
C              D'INTERSECTIONS DES FACETTE DE CONTACT
C IN  ALIAS  : TYPE DE MAILLE DE CONTACT
C IN  NDIM   : DIMENSION DU PROBLÈME
C IN  NUMMAE : INDICE DE LA MAILLE DANS CONTAMA
C IN  KSI1   : COORDONNEE PARAMETRIQUE KSI DU PROJETE
C IN  IFACE  : NUMERO DE LA FACE ESCLAVE
C IN  KSI2   : COORDONNEE PARAMETRIQUE ETA DU PROJETE
C OUT GEOM   : COORDONNEES DU PROJETE (EN 2D Z=0)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      JCSD1,JCSV1,JCSL1,JCSD2,JCSV2,JCSL2
      INTEGER      I,J,IAD,NUMPI(3)
      REAL*8       FF(9),COOR(27)
C-----------------------------------------------------------------------
C
      CALL JEMARQ()

C --- ON RECUPERE LA GEOMETRIE DES FACETTES ESCLAVES
C
      CALL JEVEUO(CHSGE//'.CESD','L',JCSD1)
      CALL JEVEUO(CHSGE//'.CESV','L',JCSV1)
      CALL JEVEUO(CHSGE//'.CESL','L',JCSL1)
C
C --- ON RECUPERE LES INFOS SUR LA CONNECTIVITE DES FACETTES DE CONTACT
C
      CALL JEVEUO(CHSCF//'.CESD','L',JCSD2)
      CALL JEVEUO(CHSCF//'.CESV','L',JCSV2)
      CALL JEVEUO(CHSCF//'.CESL','L',JCSL2)
C
C --- INITIALISATIONS
C
      DO 100 I=1,NDIM
        GEOM(I) = 0.D0
  100 CONTINUE

C --- RECUPERATION DES NUMEROS LOCAUX DES POINTS D'INTERSECTIONS
C     DE LA FACETTE DANS LA MAILLE
C
      DO 150 I=1,NPTE
        CALL CESEXI('S',JCSD2,JCSL2,NUMMAE,1,1,(IFACE-1)*NDIM+I,IAD)
        CALL ASSERT(IAD.GT.0)
        NUMPI(I) = ZI(JCSV2-1+IAD)
 150  CONTINUE
C 
C --- RECUPERATION DES COORDONNES REELLES DES POINTS D'INTERSECTION
C     DE LA FACETTE ESCLAVE
C
      DO 200 I=1,NPTE
       DO 210 J=1,NDIM
        CALL CESEXI('S',JCSD1,JCSL1,NUMMAE,1,1,NDIM*(NUMPI(I)-1)+J,IAD)
        CALL ASSERT(IAD.GT.0)
        COOR(NDIM*(I-1)+J)=ZR(JCSV1-1+IAD)
 210   CONTINUE
 200  CONTINUE
C
C --- CALCUL DU POINT
C
      CALL MMNONF(NDIM,NPTE,ALIAS,KSI1,KSI2,FF)
      DO 300 I = 1,NDIM
        DO 310 J = 1,NPTE
          GEOM(I) = FF(J)*COOR((J-1)*NDIM+I) + GEOM(I)
  310   CONTINUE
  300 CONTINUE

      CALL JEDEMA()
      END
