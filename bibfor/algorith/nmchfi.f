      SUBROUTINE NMCHFI(PARMET,METHOD,FONACT,SDDISC,SDDYNA,
     &                  NUMINS,ITERAT,DEFICO,LCFINT,LCDIRI,
     &                  LCBUDI,LCRIGI,OPTION)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      REAL*8       PARMET(*)
      CHARACTER*16 METHOD(*)
      INTEGER      FONACT(*)
      CHARACTER*16 OPTION
      CHARACTER*19 SDDISC,SDDYNA
      INTEGER      NUMINS,ITERAT
      CHARACTER*24 DEFICO
      LOGICAL      LCFINT,LCRIGI,LCDIRI,LCBUDI
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (CALCUL)
C
C CHOIX DE L'OPTION POUR CALCUL DES FORCES INTERNES
C      
C ----------------------------------------------------------------------
C
C
C IN  METHOD : INFORMATIONS SUR LES METHODES DE RESOLUTION
C                 VOIR DETAIL DES COMPOSANTES DANS NMLECT
C IN  PARMET : PARAMETRES DES METHODES DE RESOLUTION
C                 VOIR DETAIL DES COMPOSANTES DANS NMLECT
C IN  FONACT : FONCTIONNALITES ACTIVEES (VOIR NMFONC)
C IN  SDDISC : SD DISC_INST
C IN  SDDYNA : SD DYNAMIQUE 
C IN  NUMINS : NUMERO D'INSTANT
C IN  ITERAT : NUMERO D'ITERATION
C IN  DEFICO : SD DEF. CONTACT
C OUT LCFINT  : .TRUE. SI CALCUL DES VECT_ELEM DES FORCES INTERIEURS
C OUT LCDIRI  : .TRUE. SI CALCUL DES VECT_ELEM DES REACTIONS D'APPUI
C OUT LCRIGI  : .TRUE. SI CALCUL DES MATR_ELEM DES MATRICES DE RIGIDITE
C OUT OPTION : NOM D'OPTION PASSE A MERIMO POUR FORCES INTERNES
C
C
C
C
      LOGICAL      REASMA
      CHARACTER*16 METCOR,METPRE
      LOGICAL      ISFONC,NDYNLO
      LOGICAL      LUNIL,LCTCD,LRELI,LEXPL
C      
C ----------------------------------------------------------------------
C
      CALL JEMARQ() 
C
C --- FONCTIONNALITES ACTIVEES
C    
      LUNIL  = ISFONC(FONACT,'LIAISON_UNILATER')
      LCTCD  = ISFONC(FONACT,'CONT_DISCRET')
      LRELI  = ISFONC(FONACT,'RECH_LINE')
      LEXPL  = NDYNLO(SDDYNA,'EXPLICITE')              
C
C --- CHOIX DE REASSEMBLAGE DE LA MATRICE GLOBALE
C 
      CALL NMCHRM('FORCES_INT',
     &            PARMET,METHOD,FONACT,SDDISC,SDDYNA,
     &            NUMINS,ITERAT,DEFICO,METPRE,METCOR,
     &            REASMA)
C
C --- OPTION DE CALCUL 
C
      IF (REASMA) THEN
        IF (METCOR.EQ.'TANGENTE') THEN
          OPTION = 'FULL_MECA'
        ELSE
          OPTION = 'FULL_MECA_ELAS'
        ENDIF
      ELSE
        OPTION = 'RAPH_MECA'
      END IF 
C
C --- DOIT-ON CALCULER LES VECT_ELEM DES FORCES INTERNES ?
C --- INUTILE SI FULL_MECA (DEJA FAIT)
C
      IF (.NOT.LRELI .OR. ITERAT.EQ.0) THEN                   
        LCFINT = .TRUE. 
      ELSE
        IF (OPTION .EQ. 'FULL_MECA') THEN
          LCFINT = .TRUE.
        ELSE
          LCFINT = .FALSE.
        END IF
      ENDIF  
C      
      IF (LCTCD.OR.LUNIL) THEN
        LCFINT = .TRUE.
      ENDIF
C
      IF (LEXPL) THEN
        LCFINT = .FALSE.
      ENDIF  
C
C --- DOIT-ON CALCULER LES MATR_ELEM DE RIGIDITE ?
C
      IF (OPTION.NE.'RAPH_MECA') THEN
        LCRIGI = .TRUE.
      ELSE
        LCRIGI = .FALSE.  
      ENDIF 
C
C --- DOIT-ON CALCULER LES VECT_ELEM DES REACTIONS D'APPUI ?
C    -> NON SI DEJA CALCULE DANS RECHERCHE LINEAIRE
      LCDIRI = LCFINT
      LCBUDI = LCFINT 
C
      CALL JEDEMA()      
C
      END
