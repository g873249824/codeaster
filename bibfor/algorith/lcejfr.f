      SUBROUTINE LCEJFR(FAMI,KPG,KSP,NDIM,MATE,OPTION,EPSM,DEPS,SIGMA,
     &                  DSIDEP,VIM,VIP,COOROT,TYPMOD,INSTAM,INSTAP)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/04/2013   AUTEUR CUVILLIE M.CUVILLIEZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRS_1404
C RESPONSABLE KAZYMYRENKO C.KAZYMYRENKO

      IMPLICIT NONE
      INTEGER MATE,NDIM,KPG,KSP
      REAL*8  EPSM(6),DEPS(6)
      REAL*8  SIGMA(6),DSIDEP(6,6)
      REAL*8  VIM(*),VIP(*),INSTAM,INSTAP,COOROT(NDIM+NDIM*NDIM)
      CHARACTER*8  TYPMOD(*)
      CHARACTER*16 OPTION
      CHARACTER*(*)  FAMI

C-----------------------------------------------------------------------
C     LOI DE COMPORTEMENT  MOHR-COULOMB
C     POUR LES ELEMENTS DE JOINT ET JOINT_HYME 2D ET 3D.
C
C DEUX TYPES DE CALCUL SONT POSSIBLES:
C
C 1) MODELISATIONS *_JOINT_HYME, AVEC PARAMETRE HYDRO PRESENTS
C    CALCUL COUPLE HYDRO-MECANIQUE SUR UN MAILLAGE QUADRATIQUE
C
C 2) MODELISATIONS *_JOINT, PAS DE PARAMETRES HYDRO POSSIBLE 
C    CALCUL MECA (AVEC ENVENTUELLEMENT PRES_FLUIDE)
C    SUR UN MAILLAGE LINEAIRE OU QUADRATIQUE
C
C IN : EPSM SAUT INSTANT MOINS ET GRAD PRESSION ET PRES FLUIDE SI HYME
C IN : DEPS INCR DE SAUT ET INCR GRAD PRESS ET INC PRES FL SI HYME
C IN : MATE, OPTION, VIM, COOROT,INSTAM, INSTAP
C OUT : SIGMA , DSIDEP , VIP
C-----------------------------------------------------------------------
      INTEGER NBPA
      PARAMETER (NBPA=9)
      INTEGER COD(NBPA)
      INTEGER I,J,N,IFPLAS,KRONEC, IFOUV
      REAL*8  KN,KT,KAPPA,MU,ADHE,A(NDIM),PLASTI(NDIM),DPLAS(NDIM)
      REAL*8  LAMBDA,DLAM,VAL(NBPA),CRITER
      REAL*8  ABSTAU,TAU(NDIM),COEFD,COEFHD,R8BID
      REAL*8  INST,VALPAR(NDIM+1),RHOF,VISF,AMIN,PRESFL,PRESG
      REAL*8  GP(NDIM-1),GPLOC(NDIM),GPGLO(NDIM),FHLOC(NDIM),FHGLO(NDIM)
      REAL*8  INVROT(NDIM,NDIM),RIGART
      CHARACTER*8 NOM(NBPA),NOMPAR(NDIM+1)
      CHARACTER*1 POUM
      LOGICAL RESI,RIGI,ELAS,IFPAHM,IFHYME

C OPTION CALCUL DU RESIDU OU CALCUL DE LA MATRICE TANGENTE
C CALCUL DE CONTRAINTE (RESIDU)
      RESI = OPTION(1:9).EQ.'FULL_MECA' .OR. OPTION.EQ.'RAPH_MECA'
C CALCUL DE LA MATRICE TANGEANTE (RIGIDITE)
      RIGI = OPTION(1:9).EQ.'FULL_MECA' .OR. OPTION(1:9).EQ.'RIGI_MECA'
C CALCUL DE LA MATRICE ELASTIQUE A LA PLACE DE LA MATRICE TANGENTE
      ELAS = OPTION.EQ.'FULL_MECA_ELAS' .OR. OPTION.EQ.'RIGI_MECA_ELAS'

C INDICATEUR AVEC/SANS HYDRO 
      IF (TYPMOD(2).EQ.'EJ_HYME')  IFHYME=.TRUE.
      IF (TYPMOD(2).EQ.'ELEMJOIN') IFHYME=.FALSE.

C #####################################
C RECUPERATION DES PARAMETRES PHYSIQUES
C #####################################
      NOM(1) = 'K_N'
      NOM(2) = 'MU'
      NOM(3) = 'ADHESION'
      NOM(4) = 'K_T'
      NOM(5) = 'PENA_TANG'
      NOM(6) = 'PRES_FLUIDE'
      NOM(7) = 'RHO_FLUIDE'
      NOM(8) = 'VISC_FLUIDE'
      NOM(9) = 'OUV_MIN'

      IF (OPTION.EQ.'RIGI_MECA_TANG') THEN
        POUM = '-'
      ELSE
        POUM = '+'
      ENDIF
      
C INSTANT DE CALCUL T- OU T+
      INST = INSTAM
      IF (RESI) INST = INSTAP
      
      CALL RCVALB(FAMI,KPG,KSP,POUM,MATE,' ','JOINT_MECA_FROT',0,' ',
     &            0.D0,3,NOM,VAL,COD,2)
C DEFINITION DE PARAMETRES PHYSIQUE:
C     PENTE ELASTIQUE NORMALE
      KN=VAL(1)
C     COEFFICIENT DE FROTTEMENT
      MU=VAL(2)
C     ADHESION
      ADHE=VAL(3)

C PENTE ELASTIQUE TANGENTIELLE
C (SI ELLE N'EST PAS DEFINI ALORS K_T=K_N)
      CALL RCVALB(FAMI,KPG,KSP,POUM,MATE,' ','JOINT_MECA_FROT',0,' ',
     &            0.D0,1,NOM(4),VAL(4),COD(4),0)
      IF (COD(4).EQ.0) THEN
        KT = VAL(4)
      ELSE
        KT = KN
      ENDIF
C PARAMETRE PENA_TANG
C (SI IL N'EST PAS DEFINI ALORS KAPPA=(K_N+K_T)*1E-6)
      CALL RCVALB(FAMI,KPG,KSP,POUM,MATE,' ','JOINT_MECA_FROT',0,' ',
     &            0.D0,1,NOM(5),VAL(5),COD(5),0)
      IF (COD(5).EQ.0) THEN
        KAPPA = VAL(5)
      ELSE
        KAPPA = (KN+KT)*1.D-6
      ENDIF


C DEFINITION DES PARAMETRES POUR LA RECUPERATION DES FONCTIONS
      NOMPAR(1)='INST'
      NOMPAR(2)='X'
      NOMPAR(3)='Y'
      VALPAR(1)= INST
      VALPAR(2)= COOROT(1)
      VALPAR(3)= COOROT(2)
      IF (NDIM.EQ.3) THEN
        NOMPAR(4)='Z'
        VALPAR(4)= COOROT(3)
      ENDIF

C RECUPERATION DE LA PRESS FLUIDE IMPOSEE (FCT DE L'ESPACE ET DU TEMPS)
      CALL RCVALB(FAMI,KPG,KSP,POUM,MATE,' ','JOINT_MECA_FROT',NDIM+1,
     &            NOMPAR,VALPAR,1,NOM(6),VAL(6),COD(6),0)
      IF (COD(6).EQ.0) THEN
        PRESFL = VAL(6)
      ELSE
        PRESFL = 0.D0
      ENDIF

C RECUPERATION DE LA MASSE VOL ET DE LA VISCO (MODELISATION JOINT HM)
C--------------------------------------------------------------------

      CALL RCVALB(FAMI,KPG,KSP,POUM,MATE,' ','JOINT_MECA_FROT',0,' ',
     &            0.D0,1,NOM(7),VAL(7),COD(7),0)
      CALL RCVALB(FAMI,KPG,KSP,POUM,MATE,' ','JOINT_MECA_FROT',0,' ',
     &            0.D0,1,NOM(8),VAL(8),COD(8),0)
      CALL RCVALB(FAMI,KPG,KSP,POUM,MATE,' ','JOINT_MECA_FROT',0,' ',
     &            0.D0,1,NOM(9),VAL(9),COD(9),0)

      IF (COD(7).EQ.0) RHOF = VAL(7)
      IF (COD(8).EQ.0) VISF = VAL(8)
      IF (COD(9).EQ.0) AMIN = VAL(9)

C INDICATEUR SI LES PARAMETRES HYDRO SONT RENSEIGNES
      IFPAHM = (COD(7).EQ.0).AND.(COD(8).EQ.0).AND.(COD(9).EQ.0)

C VERIFICATION DE LA PRESENCE/ABSENCE DES PARAMETRES
C EN FONCTION DE LA MODELISATION MECA PUR OU HYDRO MECA

      IF (IFHYME) THEN
C       POUR LE CALCUL HYDRO => PAS DE PRES_FLUIDE       
        IF ((COD(6).EQ.0)) CALL U2MESS('F','ALGORITH17_14')
C       POUR LE CALCUL HYDRO => PRESENCE DE PARA HYDRO        
        IF (.NOT.IFPAHM) CALL U2MESS('F','ALGORITH17_15')
      ELSE
C       POUR LE CALCUL MECA => PAS DE PARAMETRE HYDRO
        IF (IFPAHM) CALL U2MESS('F','ALGORITH17_16')
      ENDIF


C #####################################
C INITIALISATION DE VARIABLES
C #####################################

C CALCUL DU SAUT EN T+ OU T- EN FOCTION DE L'OPTION DE CALCUL
C     A=AM
      CALL DCOPY(NDIM,EPSM,1,A,1)
C     A=A+DA
      IF (RESI) CALL DAXPY(NDIM,1.D0,DEPS,1,A,1)

C GRADIENT DE PRESSION ET PRESSION EN T- OU T+
      IF (IFHYME) THEN
      
        DO 10 N=1,NDIM-1
          GP(N) = EPSM(NDIM+N)
          IF (RESI) GP(N) = GP(N) + DEPS(NDIM+N)
  10    CONTINUE
  
        PRESG = EPSM(2*NDIM)
        IF (RESI) PRESG = PRESG + DEPS(2*NDIM)
  
      ENDIF

C INITIALISATION DE VARIABLE INTERNES
C     LAMBDA = accumutation de deplacement tang en valeur absolue
C     PLASTI = vecteur de deplacement plastique tangentiel
      LAMBDA=VIM(1)
      DO 11 I=2,NDIM
        PLASTI(I) = VIM(I+1)
11    CONTINUE
C     IDICATEUR DE PLASTIFICATION A L'INSTANT ACTUEL
      IF (ELAS) THEN
        IFPLAS = 0
      ELSE
        IFPLAS = NINT(VIM(2))
      END IF
C     INDICATEUR D'OUVERTURE
      IFOUV=NINT(VIM(5))

C #####################################
C CALCUL DE LA CONTRAINTE
C #####################################

C     INITIALISATION DE LA CONTRAINTE A ZERO
      CALL R8INIR(6, 0.D0, SIGMA,1)
      
C CALCUL DE LA CONTRAINTE HYDRO : DEBIT (LOI CUBIQUE)
      IF (IFHYME) THEN
        DO 44 N=1,NDIM-1
          SIGMA(NDIM+N) = -RHOF*GP(N)*(MAX(AMIN,A(1)+AMIN))**3/(12*VISF)
  44    CONTINUE
      ENDIF

C     CONTRAINTE NORMALE DE CONTACT PENALISE +
C     INDICATEUR D'OUVERTURE COMPLETE
C     (CONTRAINTE TANGENTIEL EST MISE A ZERO)
      IF (KN*A(1).LT.(ADHE/MU)) THEN
        IFOUV=0
        SIGMA(1) = KN*A(1)
      ELSE
        IFOUV=1
        SIGMA(1) = ADHE/MU
      END IF
     
C     CONTRAINTE TANGENTIELLE
      DO 20 I=2,NDIM
        SIGMA(I) = KT*( A(I)-PLASTI(I) )
20    CONTINUE

C     DIRECTION DE GLISSEMENT = SIGMA TANG SANS INCREMENT PLASTIQUE
      DO 27 I=2,NDIM
        TAU(I) = SIGMA(I)
27    CONTINUE

C     MODULE DE SIGMA TANGENTE SANS INCREMENT PLASTIQUE
C     NB:SI ABSTAU==0 ON EST TOUJOURS DANS LE REGIME ELASTIQUE
      ABSTAU=0.D0
      DO 30 I=2,NDIM
        ABSTAU = ABSTAU+TAU(I)**2
30    CONTINUE
      ABSTAU=SQRT(ABSTAU)

C ###########################################################
C SI ON CALCULE LA RIGIDITE SEULEMENT, ON Y SAUTE DIRECTEMENT
C ###########################################################
      IF (.NOT. RESI) GOTO 5000

C     CRITERE DE PLASTICITE  NB: SIGMA(1)<0 EN COMPRESSION
      CRITER=ABSTAU+MU*SIGMA(1)-KAPPA*LAMBDA - ADHE
C     VERIFICATION DE CRITERE DE PLASTICITE
      IF (CRITER.LE.0.D0) THEN
C     PAS DE PLASTICITE
         IFPLAS=0
         DO 32 I=2,NDIM
            DPLAS(I) = 0.D0
32       CONTINUE
         DLAM=0.D0
      ELSE
C     AVEC LA PLASTICITE
         IFPLAS=1
         DO 34 I=2,NDIM
            DPLAS(I) = CRITER/(KT+KAPPA)*TAU(I)/ABSTAU
            SIGMA(I) = SIGMA(I)-KT*DPLAS(I)
34       CONTINUE
         DLAM=CRITER/(KT+KAPPA)
      ENDIF

C     PRISE EN COMPTE DE LA PRESSION DE FLUIDE EVENTUELLE 
C     PRESFL : IMPOSEE, PRESG : CALCULEE
     
      IF (IFHYME) THEN
        SIGMA(1) = SIGMA(1) - PRESG
      ELSE  
        SIGMA(1) = SIGMA(1) - PRESFL
      ENDIF


C ACTUALISATION DES VARIABLES INTERNES
C   V1 :  LE DEPLACEMENT PLASTIQUE CUMULE (SANS ORIENTATION) LAMBDA:
C            LAMBDA NE PEUX QU'AUGMENTER
C   V2 : INDICATEUR DE PLASTIFICATION (0 : NON, 1 : OUI)
C   V3-V4 :  VECTEUR DE DEPLACEMENT TANG PAR RAPPORT AU POINT DE DEPART
C                        (INDIQUE LA POSITION D'EQUILIBRE ACTUELLE)
C   V5    : INDICATEUR D'OUVERTURE COMPLETE
C                         (CONTRAINTE TANGENTIEL EST MIS A ZERO)
C   V6    : MODULE DE LA CONTRAINTE TANGENTE
C   V7 A V9 : VALEURS DU SAUT
C   V10 : PAS UTILISEE DANS CETTE LOI
C   V11 : CONTRAINTE MECANIQUE NORMALE (SANS PRESSION DE FLUIDE)
C   V12 A V14 : COMPOSANTES DU GRAD DE PRESSION DANS LE REPERE GLOBAL
C   V15 A V17 : COMPOSANTES DU FLUX HYDRO DANS LE REPERE GLOBAL
C   V18 : PRESSION DE FLUIDE INTERPOLEE

      VIP(1) = LAMBDA+DLAM
      VIP(2) = IFPLAS
      DO 36 I=2,NDIM
         VIP(I+1) = VIM(I+1)+DPLAS(I)
36    CONTINUE
      VIP(5) = MAX(NINT(VIM(5)),IFOUV)

      VIP(6)=VIM(6)
      DO 37 I=2,NDIM
         VIP(6) = VIP(6)+SIGMA(I)**2
      VIP(6)=SQRT(VIP(6))
37    CONTINUE
      VIP(7) = A(1)
      DO 38 I=2,NDIM
         VIP(I+6)=A(I)
38    CONTINUE

      VIP(10) = 0.D0
       
C     VISUALISATION DES FLUX, DES GRAD DE PRESSION ET DE LA PRESSION
C     DANS LE REPERE GLOBAL 
      IF (IFHYME) THEN

        GPLOC(1) = 0.D0
        GPLOC(2) = GP(1)
        IF (NDIM.EQ.3) THEN
          GPLOC(3) = GP(2)
        ENDIF

        FHLOC(1) = 0.D0
        FHLOC(2) = SIGMA(NDIM+1)
        IF (NDIM.EQ.3) THEN
          FHLOC(3) = SIGMA(2*NDIM-1)
        ENDIF

        CALL MATINV('S',NDIM,COOROT(NDIM+1),INVROT,R8BID)
        CALL PMAVEC('ZERO',NDIM,INVROT,GPLOC,GPGLO)
        CALL PMAVEC('ZERO',NDIM,INVROT,FHLOC,FHGLO)

C       CONTRAINTE MECANIQUE NORMALE SANS PRESSION DE FLUIDE CALCULEE
C       ON ANNULE SON INFLUENCE
        VIP(11) = SIGMA(1) + PRESG
        
        VIP(12) = GPGLO(1)
        VIP(13) = GPGLO(2)
        VIP(15) = FHGLO(1)
        VIP(16) = FHGLO(2)

        IF (NDIM.EQ.3) THEN
          VIP(14) = GPGLO(3)
          VIP(17) = FHGLO(3)
        ELSE
          VIP(14) = 0.D0
          VIP(17) = 0.D0
        ENDIF
        
C       PRESSION DE FLUIDE CALCULEE AUX NOEUDS (DDL) ET INTERPOL AU PG
        VIP(18) = PRESG

      ELSE
      
C       CONTRAINTE MECANIQUE NORMALE SANS PRESSION DE FLUIDE IMPOSEE
C       ON ANNULE SON INFLUENCE
        VIP(11) = SIGMA(1) + PRESFL

        VIP(12) = 0.D0
        VIP(13) = 0.D0
        VIP(14) = 0.D0
        VIP(15) = 0.D0
        VIP(16) = 0.D0
        VIP(17) = 0.D0

C       PRESSION DE FLUIDE IMPOSEE AU PG :
        VIP(18) = PRESFL
        
      ENDIF


C #####################################
C CALCUL DE LA MATRICE TANGENTE
C #####################################

5000  CONTINUE
      IF (.NOT. RIGI) GOTO 9999

C     INITIALISATION DE DSIDEP
      CALL R8INIR(6*6, 0.D0, DSIDEP,1)
      
C CALCUL DE LA MATRICE TANGENTE HYDRO
C------------------------------------

      IF (IFHYME) THEN

C       TERME : DW/DGP  (POUR KTAN P P)
        DO 42 N=1,NDIM-1

          DSIDEP(NDIM+N,NDIM+N)=-RHOF*(MAX(AMIN,A(1)+AMIN))**3/(12*VISF)

  42    CONTINUE

C       TERME : DW/DDELTA_N  (POUR KTAN P U)
        DO 43 N=1,NDIM-1

          IF (A(1).LT.0.D0) THEN
            DSIDEP(NDIM+N,1) = 0.D0
          ELSE
            DSIDEP(NDIM+N,1) = -3*RHOF*GP(N)*(A(1)+AMIN)**2/(12*VISF)
          ENDIF

  43    CONTINUE

      ENDIF
               
C DSIGMA_N/DDELTA_N
      IF (IFOUV.EQ.0) THEN
         DSIDEP(1,1)=KN
      ELSE
         DSIDEP(1,1)=0.D0
      ENDIF
C DSIGMA_N/DDELTA_T
      DO 40 I=2,NDIM
         DSIDEP(1,I)=0.D0
40    CONTINUE
C DSIGMA_T/DDELTA_N
      DO 50 I=2,NDIM
         IF ((IFOUV.EQ.0).AND.(IFPLAS.EQ.1)) THEN
            DSIDEP(I,1)=-TAU(I)*MU*KN*KT/ABSTAU/(KT+KAPPA)
         ELSE
            DSIDEP(I,1)=0.D0
         ENDIF
50    CONTINUE
C DSIGMA_T/DDELTA_T
      IF (IFPLAS.EQ.1) THEN
         COEFHD= - (KAPPA*LAMBDA+ADHE-MU*SIGMA(1))
     &     *KT**2/ABSTAU**3/(KT+KAPPA)
         COEFD=KAPPA*KT/(KT+KAPPA) - COEFHD*ABSTAU**2
         DO 60 J=2,NDIM
            DO 70 I=J,NDIM
              IF (I.EQ.J) THEN
                 KRONEC=1
              ELSE
                 KRONEC=0
              ENDIF
              DSIDEP(J,I) = COEFHD*TAU(J)*TAU(I) + COEFD*KRONEC
              DSIDEP(I,J) = COEFHD*TAU(I)*TAU(J) + COEFD*KRONEC
70          CONTINUE
60       CONTINUE
      ELSE
         DO 80 I=2,NDIM
            DSIDEP(I,I)=KT
80       CONTINUE
      ENDIF


CCC MATRICE TANGENTE DE CONTACT OUVERT

C DANS LE CAS OU L'ELEMENT EST TOTALEMENT CASSE ON INTRODUIT UNE
C RIGIDITE ARTIFICIELLE DANS LA MATRICE TANGENTE POUR ASSURER
C LA CONVERGENCE

      RIGART=1.D-8

C     POUR LE JOINT OUVERT LA PARTIE NORMALE EST CORRIGEE
      IF (IFOUV.EQ.1) THEN
C       COMPLETEMENT CASSE NORMALE
        DSIDEP(1,1) = KN*RIGART
      END IF
C     POUR LE JOINT SANS ECROUISSAGE LA PARTIE TANGENTIELLE EST DECALEE
      IF (KAPPA.EQ.0.D0) THEN
C     COMPLETEMENT CASSE TANGENTE
        DO 90 I=2,NDIM
          DSIDEP(I,I) =  DSIDEP(I,I) + KT*RIGART
90      CONTINUE
      END IF


 9999 CONTINUE

      END
