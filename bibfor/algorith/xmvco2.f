       SUBROUTINE XMVCO2(NDIM  ,NNO   ,NNOL  ,NNOS  ,LAMB  ,
     &                   AM    ,DELTA ,PLA   ,LACT  ,NFH   ,
     &                   DDLS  ,DDLM  ,NFISS ,IFISS ,JHEAFA,
     &                   IFA   ,NCOMPH,JFISNO,JAC   ,FFC   ,
     &                   FFP   ,SINGU ,R     ,RR    ,VTMP  ,
     &                   P)
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER     NDIM,NNO,NNOL
      INTEGER     NFH,DDLS,PLA(27),LACT(8)
      INTEGER     SINGU
      REAL*8      VTMP(400),DELTA(6)
      REAL*8      FFP(27),JAC
      REAL*8      RR,FFC(8)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 19/12/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C
C ROUTINE CONTACT (METHODE XFEM HPP - CALCUL ELEM.)
C
C --- CALCUL DES SECONDS MEMBRES DE COHESION
C
C ----------------------------------------------------------------------
C
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  NNO    : NOMBRE DE NOEUDS DE L'ELEMENT DE REF PARENT
C IN  SIGMA  : VECTEUR CONTRAINTE EN REPERE LOCAL
C IN  NFH    : NOMBRE DE FONCTIONS HEAVYSIDE
C IN  DDLS   : NOMBRE DE DDL (DEPL+CONTACT) À CHAQUE NOEUD SOMMET
C IN  JAC    : PRODUIT DU JACOBIEN ET DU POIDS
C IN  FFP    : FONCTIONS DE FORME DE L'ELEMENT PARENT
C IN  SINGU  : 1 SI ELEMENT SINGULIER, 0 SINON
C IN  RR     : DISTANCE AU FOND DE FISSURE
C I/O VTMP   : VECTEUR ELEMENTAIRE DE CONTACT/FROTTEMENT
C
C
      INTEGER I,J,K,PLI,NLI,DDLM,IER,IFA,IFH,IFISS
      INTEGER IN,JFISNO,JHEAFA,NCOMPH,NFISS,NNOS
      REAL*8  FFI,AM(3),COEFI,HFIX(3),H(3),LAMB(3),R
      REAL*8  P(3,3),PTR(3,3)
      LOGICAL LMULTC
C
C ---------------------------------------------------------------------
C
C INITIALISATIONS
C
      CALL VECINI(3,0.D0,H)
      CALL VECINI(3,0.D0,HFIX)
      CALL MATINI(3,3,0.D0,PTR)
C
C CALCUL DE H = R*DELTA - FORCE COHESIVE AUGMENTEE EN BASE COVARIANTE
C RAPPEL : AM INVERSE PAR RAPPORT AUX CONVENTIONS X-FEM
C
      DO 1 I=1,NDIM
         H(I) = - LAMB(I) - R*AM(I) + R*DELTA(I)
1     CONTINUE
C
C CONVERSION DE H EN BASE FIXE : {HFIX} = [P]T {H}
C RAPPEL : P MATRICE DE PASSAGE BASE FIXE --> BASE COVARIANTE
C
      CALL TRANSP(P,3,NDIM,NDIM,PTR,3)
      CALL PRMAVE(0,PTR,3,NDIM,NDIM,H,NDIM,HFIX,NDIM,IER)
C
C ON STOCKE DANS LE VECTEUR SECOND MEMBRE ELEMENTAIRE DE L EQUILIBRE
C ! IL Y A DEJA UN MOINS DU AUX CONVENTIONS POUR LE SAUT
C
      COEFI = 2
      LMULTC = NFISS.GT.1
       DO 10 I = 1,NNO
          CALL INDENT(I,DDLS,DDLM,NNOS,IN)
          DO 11 IFH = 1,NFH
            IF (LMULTC) THEN
              COEFI = ZI(JHEAFA-1+NCOMPH*(NFISS*(IFISS-1)
     &                  +ZI(JFISNO-1+NFH*(I-1)+IFH)-1)+2*IFA)
     &              - ZI(JHEAFA-1+NCOMPH*(NFISS*(IFISS-1)
     &                  +ZI(JFISNO-1+NFH*(I-1)+IFH)-1)+2*IFA-1)
            ENDIF
            DO 12 J = 1,NDIM
              VTMP(IN+NDIM*IFH+J) = VTMP(IN+NDIM*IFH+J) -
     &        COEFI*FFP(I)*HFIX(J)*JAC
12          CONTINUE
11        CONTINUE
          DO 13 J = 1,SINGU*NDIM
            VTMP(IN+NDIM*(1+NFH)+J) = VTMP(IN+NDIM*(1+NFH)+J) -
     &      COEFI*FFP(I)*RR*HFIX(J)*JAC
13        CONTINUE
10     CONTINUE
C
C SECOND MEMBRE DE L EQUATION D INTERFACE: EXPRESSION DIRECTE
C ATTENTION INVERSION DE CONVENTIONS
C
      DO 20 I = 1,NNOL
         PLI=PLA(I)
         FFI=FFC(I)
         NLI=LACT(I)
         IF (NLI.EQ.0) GOTO 20
         DO 21 K=1,NDIM
             VTMP(PLI-1+K) = VTMP(PLI-1+K)
     &     + (AM(K)-DELTA(K))*FFI*JAC
21       CONTINUE
20     CONTINUE
C
       END
