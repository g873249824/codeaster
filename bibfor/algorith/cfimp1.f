      SUBROUTINE CFIMP1(NOMA  ,DEFICO,RESOCO,IFM   )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 14/09/2010   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO
      CHARACTER*24 RESOCO
      INTEGER      IFM
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE DISCRETE - APPARIEMENT - UTILITAIRE)
C
C IMPRESSION DES LIAISONS ESCLAVE/MAITRE
C
C ----------------------------------------------------------------------
C
C
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NOMA   : NOM DU MAILLAGE
C IN  IFM    : UNITE D'IMPRESSION DU MESSAGE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      ILIAC,ILIAI,ACTIF,IZONE,IP
      CHARACTER*8  NOMAPP
      CHARACTER*16 NOMNOE
      INTEGER      CFDISD
      CHARACTER*14 CHAIAC
      CHARACTER*4  TYPE2
      CHARACTER*2  TYPLIA
      REAL*8       JEU
      CHARACTER*10 TYPLI
      CHARACTER*24 APJEU
      INTEGER      JAPJEU 
      CHARACTER*19 LIAC,TYPL 
      INTEGER      JLIAC,JTYPL
      CHARACTER*24 NUMLIA
      INTEGER      JNUMLI     
      CHARACTER*19 SDAPPA
      INTEGER      TYPAPP,ENTAPP 
      INTEGER      BTOTAL,NDIMG,NBLIAI
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ ()
C
C --- ACCES SD CONTACT
C  
      LIAC   = RESOCO(1:14)//'.LIAC'
      TYPL   = RESOCO(1:14)//'.TYPL'
      APJEU  = RESOCO(1:14)//'.APJEU'
      NUMLIA = RESOCO(1:14)//'.NUMLIA'
C
      CALL JEVEUO(LIAC  ,'L',JLIAC)
      CALL JEVEUO(APJEU ,'L',JAPJEU)         
      CALL JEVEUO(TYPL  ,'L',JTYPL)   
      CALL JEVEUO(NUMLIA,'L',JNUMLI)     
C
C --- SD APPARIEMENT
C
      SDAPPA = RESOCO(1:14)//'.APPA'
C
C --- INFORMATIONS SUR CONTACT
C  
      NDIMG  = CFDISD(RESOCO,'NDIM'  )
      NBLIAI = CFDISD(RESOCO,'NBLIAI')
      BTOTAL = CFDISD(RESOCO,'BTOTAL')                  
C
C --- BOUCLE SUR LES LIAISONS
C
      DO 500 ILIAI = 1,NBLIAI
C
C ----- POINT DE CONTACT
C      
        IP     = ZI(JNUMLI+3*(ILIAI-1)+1-1)
C
C ----- INFOS APPARIEMENT
C
        CALL APINFI(SDAPPA,'APPARI_TYPE'  ,IP    ,TYPAPP )
        CALL APINFI(SDAPPA,'APPARI_ENTITE',IP    ,ENTAPP )
        CALL APINFI(SDAPPA,'APPARI_ZONE'  ,IP    ,IZONE  )       
C
C ----- NOM DU NOEUD ESCLAVE
C
        CALL APNOMP(SDAPPA,IP    ,NOMNOE)
C
C ----- NOM ET TYPE DU MAITRE
C
        CALL CFNOAP(NOMA  ,DEFICO,TYPAPP,ENTAPP,NOMAPP,
     &              TYPE2 )      
C
C --- JEU
C
        JEU = ZR(JAPJEU-1+ILIAI)
C
C --- ACTIF OU NON ?
C
        ACTIF = 0

        DO 10 ILIAC = 1,BTOTAL
          IF (ZI(JLIAC-1+ILIAC).EQ.ILIAI) THEN 
            ACTIF  = 1
C
C --- TYPE LIAISON
C
            TYPLI  = 'CONT.     ' 
            TYPLIA = ZK8(JTYPL-1+ILIAC)(1:2)
            IF (TYPLIA.EQ.'C0') THEN
              TYPLI = 'CONT.     '
            ELSE IF (TYPLIA.EQ.'F0') THEN
              IF (NDIMG.EQ.3) THEN
                TYPLI = 'FROT. 1&2 '
              ELSE
                TYPLI = 'FROT.     '
              ENDIF
            ELSE IF (TYPLIA.EQ.'F1') THEN
              TYPLI = 'FROT. 1   '
            ELSE IF (TYPLIA.EQ.'F2') THEN
              TYPLI = 'FROT. 2   '
            ELSE IF (TYPLIA.EQ.'F3') THEN
              TYPLI = 'FROT.     '
            ENDIF        
          ENDIF
  10    CONTINUE
C
C --- IMPRESSION
C
        IF (ACTIF.EQ.1) THEN
          CHAIAC = ' ACTIVE (JEU: '
        
          WRITE (IFM,1000) ILIAI,'(',NOMNOE,TYPE2,NOMAPP,'): ',
     &                     CHAIAC,JEU,',TYPE: ',TYPLI,')'
        ELSE
          CHAIAC = ' LIBRE  (JEU: '
          WRITE (IFM,1010) ILIAI,'(',NOMNOE,TYPE2,NOMAPP,'): ',
     &                     CHAIAC,JEU,')'

        ENDIF
  500 CONTINUE
   
 1000 FORMAT (' <CONTACT> <> LIAISON ',I5,A1,A16,A4,A8,A3,A14,E10.3,
     &         A7,A10,A1)

 1010 FORMAT (' <CONTACT> <> LIAISON ',I5,A1,A16,A4,A8,A3,A14,E10.3,
     &         A1)
C
      CALL JEDEMA()
C
      END
