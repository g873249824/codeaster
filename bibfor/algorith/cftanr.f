      SUBROUTINE CFTANR(NOMA  ,NDIMG ,DEFICO,IZONE ,FFORME,
     &                  POSNOE,TYPENM,POSENM,NUMENM,KSI1  ,
     &                  KSI2  ,TAU1M ,TAU2M ,TAU1  ,TAU2  )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 23/09/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8   NOMA,FFORME
      INTEGER       POSENM,POSNOE,NUMENM
      INTEGER       IZONE
      INTEGER       NDIMG
      REAL*8        KSI1,KSI2
      CHARACTER*4   TYPENM
      CHARACTER*24  DEFICO
      REAL*8        TAU1(3),TAU2(3)
      REAL*8        TAU1M(3),TAU2M(3)
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE DISCRETE - APPARIEMENT)
C
C MODIFIE LES VECTEURS TANGENTS LOCAUX SUIVANT OPTIONS
C
C ----------------------------------------------------------------------
C
C  NB: LE REPERE EST ORTHONORME ET TEL QUE LA NORMALE POINTE VERS
C  L'INTERIEUR DE LA MAILLE
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  NDIMG  : DIMENSION DU MODELE
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  IZONE  : ZONE DE CONTACT ACTIVE
C IN  TYPENM : TYPE DE L'ENTITE MAITRE RECEVANT LA PROJECTION
C               'MAIL' UNE MAILLE
C               'NOEU' UN NOEUD
C IN  POSENM : NUMERO ENTITE MAITRE QUI RECOIT LA PROJECTION
C IN  NUMENM : NUMERO ABSOLU ENTITE MAITRE QUI RECOIT LA PROJECTION
C IN  POSNOE : NOEUD ESCLAVE
C IN  KSI1   : COORDONNEE PARAMETRIQUE SUR MAITRE DU POINT ESCLAVE
C              PROJETE
C IN  KSI2   : COORDONNEE PARAMETRIQUE SUR MAITRE DU POINT ESCLAVE
C              PROJETE
C IN  TAU1M  : PREMIERE TANGENTE SUR LA MAILLE MAITRE AU POINT ESCLAVE
C              PROJETE
C IN  TAU2M  : SECONDE TANGENTE SUR LA MAILLE MAITRE AU POINT ESCLAVE
C              PROJETE
C OUT TAU1   : PREMIER VECTEUR TANGENT LOCAL AU POINT ESCLAVE PROJETE
C OUT TAU2   : SECOND VECTEUR TANGENT LOCAL AU POINT ESCLAVE PROJETE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      LOGICAL      LLISS,LBID,LMFIXE,LEFIXE,LMAIT,LESCL
      LOGICAL      LPOUTR,LPOINT
      CHARACTER*24 K24BID,K24BLA
      INTEGER      I,IBID
      INTEGER      POSMAM,POSMAE,NUMMAE,NUMMAM
      INTEGER      INORM,ITYPEM,ITYPEE
      CHARACTER*8  ALIASE,ALIASM
      REAL*8       R8BID,VECTOR(3)
      REAL*8       TAU1E(3),TAU2E(3)
      CHARACTER*24 TANINI,MANOCO
      INTEGER      JTGINI,JMANO
      INTEGER      NBMA,JDECMA,CODRET
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- RECUPERATION DE QUELQUES DONNEES
C
      TANINI = DEFICO(1:16)//'.TANINI'
      MANOCO = DEFICO(1:16)//'.MANOCO'
      CALL JEVEUO(TANINI,'L',JTGINI)
      CALL JEVEUO(MANOCO,'L',JMANO)
C
C --- INITIALISATIONS
C
      K24BLA = ' '
      LMFIXE = .FALSE.
      LEFIXE = .FALSE.
      LMAIT  = .FALSE.
      LESCL  = .FALSE.      
C
C --- LISSAGE OU PAS ?
C
      CALL MMINFP(IZONE ,DEFICO,K24BLA,'LISSAGE',
     &            IBID  ,R8BID ,K24BID,LLISS   )
C
C --- TYPE DE LA NORMALE A CHOISIR
C
      CALL MMINFP(IZONE ,DEFICO,K24BLA,'NORMALE',
     &            INORM ,R8BID ,K24BID,LBID     )
C
C --- NORMALES A MODIFIER
C
      IF (INORM.EQ.0) THEN
        LMAIT = .TRUE.
        LESCL = .FALSE.
      ELSEIF (INORM.EQ.1) THEN
        LMAIT = .TRUE.
        LESCL = .TRUE.
      ELSEIF (INORM.EQ.2) THEN
        LMAIT = .FALSE.
        LESCL = .TRUE.
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- INCOMPATIBILITE SCHEMA INTEGRATION GAUSS AVEC OPTION ESCLAVE
C
      IF (LESCL) THEN
        IF (POSNOE.EQ.0) THEN
          CALL U2MESS('F','CONTACT_98')
        ENDIF     
      ENDIF      
C
C --- DIMENSION MAILLE ESCLAVE: ON PREND LA PREMIERE MAILLE ATTACHEE
C --- AU NOEUD ESCLAVE
C
      IF (LESCL) THEN
        CALL CFNBEN(NOMA  ,DEFICO,POSNOE,'NOEU',NBMA  ,
     &              JDECMA)
        POSMAE = ZI(JMANO+JDECMA+1-1)
        CALL CFPOSM(NOMA  ,DEFICO,'MAIL',1     ,POSMAE,
     &              NUMMAE,CODRET)
        IF (CODRET.LT.0) THEN
          CALL ASSERT(.FALSE.)
        ENDIF
        CALL MMELTY(NOMA  ,NUMMAE,ALIASE,IBID  ,IBID  )
      ENDIF     
C
C --- DIMENSION MAILLE MAITRE
C
      IF (LMAIT) THEN
C
C --- RECUP. MAILLE SI APPARIEMENT NODAL
C      
        IF (TYPENM.EQ.'NOEU') THEN  
          CALL CFNBEN(NOMA  ,DEFICO,POSENM,'NOEU',NBMA  ,
     &                JDECMA)
          POSMAM = ZI(JMANO+JDECMA+1-1)
        ELSE
          POSMAM = POSENM  
        ENDIF
          
        CALL CFPOSM(NOMA  ,DEFICO,'MAIL',1     ,POSMAM,
     &              NUMMAM,CODRET)
        IF (CODRET.LT.0) THEN
          CALL ASSERT(.FALSE.)
        ENDIF
        CALL MMELTY(NOMA  ,NUMMAM,ALIASM,IBID  ,IBID  )       
      ENDIF
C
C --- RECUPERATION TANGENTES ESCLAVES SI NECESSSAIRE
C
      IF (LESCL) THEN
        DO 10 I = 1,3
          TAU1E(I) = ZR(JTGINI+6*(POSNOE-1)+I-1)
          TAU2E(I) = ZR(JTGINI+6*(POSNOE-1)+3+I-1)
 10     CONTINUE
      ENDIF
C
C --- MODIFICATIONS DES NORMALES MAITRES
C
      IF (LMAIT) THEN
        CALL MMINFP(IZONE ,DEFICO,K24BLA,'VECT_MAIT',
     &              ITYPEM,VECTOR,K24BID,LBID)
        IF ((NDIMG.EQ.2).AND.(ITYPEM.EQ.2)) THEN
          CALL U2MESS('F','CONTACT3_43')
        ENDIF
        LPOUTR = (NDIMG.EQ.3).AND.(ALIASM(1:2).EQ.'SG')
        LPOINT = ALIASM.EQ.'POI1'
        IF (LPOINT) THEN
          CALL U2MESS('F','CONTACT_75')
        ENDIF
        CALL CFNORS(NOMA  ,DEFICO,FFORME,POSMAM,TYPENM,
     &              NUMENM,LPOUTR,LPOINT,KSI1  ,KSI2  ,
     &              LLISS ,ITYPEM,VECTOR,TAU1M ,TAU2M ,
     &              LMFIXE)
      ENDIF
C
C --- MODIFICATIONS DES NORMALES ESCLAVES
C
      IF (LESCL) THEN
        CALL MMINFP(IZONE ,DEFICO,K24BLA,'VECT_ESCL',
     &              ITYPEE,VECTOR,K24BID,LBID)
        IF ((NDIMG.EQ.2).AND.(ITYPEE.EQ.2)) THEN
          CALL U2MESS('F','CONTACT3_43')
        ENDIF
        LPOUTR = (NDIMG.EQ.3).AND.(ALIASE(1:2).EQ.'SG')
        LPOINT = ALIASE.EQ.'POI1'
        CALL CFNORS(NOMA  ,DEFICO,FFORME,POSMAE,TYPENM,
     &              NUMENM,LPOUTR,LPOINT,R8BID ,R8BID ,
     &              .FALSE.,ITYPEE,VECTOR,TAU1E ,TAU2E ,
     &              LEFIXE)
      ENDIF
C
C --- CHOIX DE LA NORMALE -> CALCUL DES TANGENTES
C
      CALL CFCHNO(NOMA  ,DEFICO,NDIMG ,IZONE ,POSNOE,
     &            TYPENM,NUMENM,LMAIT ,LESCL ,LMFIXE,
     &            LEFIXE,TAU1M ,TAU2M ,TAU1E ,TAU2E ,
     &            TAU1  ,TAU2  )
C
      CALL JEDEMA()
C
      END
