      SUBROUTINE XMELEL(NDIM,JMAIL,JTYMAI,NUMAE,NUMAM,
     &                  IMOD,IATT,IMAIL,NNO)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 27/06/2011   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT NONE
      INTEGER      NDIM,JMAIL,JTYMAI,NUMAE,NUMAM
      INTEGER      IMOD,IATT(2),IMAIL(2),NNO(2)
C
C ----------------------------------------------------------------------
C ROUTINE APPELLEE PAR : XMLIGR
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C RETOURNE DES INFOS SUR LES ELEMENTS DE CONTACT FORMES ENTRE
C DEUX ELEMENTS DE SURFACE
C
C IN  NDIM   : DIMENSSION DU MAILLAGE
C IN JMAIL   : POINTEUR DES TYPES D'ÉLÉMENTS
C IN JTYMAI  : POINTEUR DES TYPES DE MAILLES
C IN NUMAE   : NUMÉRO DE MAILLE ESCLAVE
C IN NUMAM   : NUMÉRO DE MAILLE MAITRE
C OUT IMOD   : NUMÉRO DE MODÉLISATION
C OUT IATT   : NUMÉRO D'ATTRIBUT ESCLAVE ET MAITRE
C OUT IMAIL  : NUMÉRO DE MAILLE ESCLAVE ET MAITRE
C OUT NNO    : NOMBRE DE NOEUDS ESCLAVE ET MAITRE
C
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM,JEXATR
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      CHARACTER*16  TYPMA,TYPEL
      CHARACTER*16  MODE(3),ATTR(7),MAIL(2,4)
      CHARACTER*16  ATT,MOD
      INTEGER      NBNO(2,4),ITYEL,ITYMA,K,IBID,IER
C
      DATA (MODE(K),K=1,3) /'C_','D_','3D'/
      DATA (ATTR(K),K=1,7) /
     &      'XHC','XHTC','XTC','XH2C','XH3C','XH4C','ARETE'/
      DATA (MAIL(1,K),K=1,4) /'TRIA3','QUAD4','TRIA6','QUAD8'/
      DATA (MAIL(2,K),K=1,4) /'TETRA4','PENTA6','HEXA8',' '/
      DATA (NBNO(1,K),K=1,4) /3,4,6,8/
      DATA (NBNO(2,K),K=1,3) /4,6,8/
C
C ----------------------------------------------------------------------
C
      IMOD = 0
      IATT(1) = 0
      IATT(2) = 0
      IMAIL(1) = 0
      IMAIL(2) = 0
C
C
C --- TYPE MAILLE ET ELEMENT ESCLAVE
C
      CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(JTYMAI-1+NUMAE)),TYPMA)
      CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',ZI(JMAIL-1+NUMAE)),TYPEL)
C --- RECUPERATION DE LA MODÉLISATION POUR L'ESCALVE
      CALL DISMTE('MODELISATION',TYPEL,IBID,MOD,IER)
      DO 10 K = 1,3
        IF (MODE(K).EQ.MOD(1:2)) IMOD = K
  10  CONTINUE
      CALL ASSERT(IMOD.NE.0)
C --- RECUPERATION DES L'ATTRIBUT POUR L'ESCALVE
      CALL TEATTR(TYPEL,'S','XLAG',ATT,IER)
      IF (ATT.NE.'ARETE') CALL TEATTR(TYPEL,'S','XFEM',ATT,IER)
      DO 20 K = 1,7
        IF (ATT.EQ.ATTR(K)) IATT(1) = K
  20  CONTINUE
      CALL ASSERT(IATT(1).NE.0)
C --- RECUPERATION DU TYPE DE MAILLE POUR L'ESCALVE
      DO 30 K = 1,4
        IF (TYPMA.EQ.MAIL(NDIM-1,K)) IMAIL(1) = K
  30  CONTINUE
      CALL ASSERT(IMAIL(1).NE.0)
      NNO(1) = NBNO(NDIM-1,IMAIL(1))
C
C
C --- TYPE MAILLE ET ELEMENT MAITRE
C
      CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(JTYMAI-1+NUMAM)),TYPMA)
      CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',ZI(JMAIL-1+NUMAM)),TYPEL)
C --- RECUPERATION DE LA MODÉLISATION POUR LE MAITRE
      CALL DISMTE('MODELISATION',TYPEL,IBID,MOD,IER)
      CALL ASSERT(MOD(1:2).EQ.MODE(IMOD))
C --- RECUPERATION DES L'ATTRIBUT POUR LE MAITRE
      CALL TEATTR(TYPEL,'S','XLAG',ATT,IER)
      IF (ATT.NE.'ARETE') CALL TEATTR(TYPEL,'S','XFEM',ATT,IER)
      DO 40 K = 1,7
        IF (ATT.EQ.ATTR(K)) IATT(2) = K
  40  CONTINUE
      CALL ASSERT(IATT(2).NE.0)
C --- RECUPERATION DU TYPE DE MAILLE
      DO 50 K = 1,4
        IF (TYPMA.EQ.MAIL(NDIM-1,K)) THEN
          IMAIL(2) = K
C ---POUR LE FAUX QUADRATIQUE
          IF (ATT.EQ.'ARETE') IMAIL(2) = K-2
        ENDIF
  50  CONTINUE
      CALL ASSERT(IMAIL(2).NE.0)
      NNO(2) = NBNO(NDIM-1,IMAIL(2))

C ---POUR L'ELEMENT EXCLUSIVEEMENT CRACK-TIP
      IF (IATT(2).EQ.3) THEN
        CALL ASSERT(IATT(1).EQ.3)
        NNO(2)=0
      ENDIF
      END
