      SUBROUTINE CRCMEL(NBMO1,MOCLEF,COMPOR,CES2,MODELE,NCMPMA,NOMCMP,
     &                  NT)
C RESPONSABLE PELLET J.PELLET
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 03/04/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     INITIALISATION D'UNE CARTE DE COMPORTEMENT
C     RELATION='ELAS' SUR TOUT LE MAILLAGE
C
C IN   NBMO1  :  NOMBRE DE MOTS CLES FACTEURS (COMP_ELAS / COMP_INCR)
C IN MOCLEF   :  LISTE DES MOTS-CLES (COMP_INCR / COMP_ELAS)
C OUT COMPOR  :  CARTE DE COMPORTEMENT CREEE
C OUT CES2    :  CHAMELEM SIMPLE ISSU DE COMPOR, DEFINIR SUR LES
C                ELEMENTS QUI CALCULENT FULL_MECA
C IN MODELE   : LE MODELE
C IN NCMPMA   : NOMBRE DE CMP DE LA GRANDEUR COMPOR
C IN NOMCMP   : NOMS DES CMP DE LA GRANDEUR COMPOR
C OUT NT      : NOMBRE D'OCCURRENCES DE COMP_INCR / COMP_ELAS
C ---------------------------------------------------------------------
      IMPLICIT NONE
      INTEGER IRET,NBMO1,I,NT,N1,NCMPMA,IBID,IREPE
      INTEGER JNCMP,JVALV,ICMP,IEXI
      CHARACTER*16 MOCLEF(2)
      CHARACTER*8  NOMCMP(NCMPMA),NOMA,K8B
      CHARACTER*19 COMPOR
      CHARACTER*24 MODELE,LIGREL
      CHARACTER*19 CES1,CES2,CEL1
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
      INTEGER        ZI
      COMMON /IVARJE/ZI(1)
      REAL*8         ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16     ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL        ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8    ZK8
      CHARACTER*16          ZK16
      CHARACTER*24                  ZK24
      CHARACTER*32                          ZK32
      CHARACTER*80                                  ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C ----------------------------------------------------------------------

      CALL JEMARQ()


C    -----------------------------------------------------------
C     ON INITIALISE UNE CARTE PAR DEFAUT, SUR TOUT LE MAILLAGE
C     AVEC LES CARACTERISTIQUES SUIVANTES :
C          COMP_INCR ( RELATION    : ELAS
C                      DEFORMATION : PETIT
C                      TOUT        : OUI)
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE',I,NOMA,IRET)
      CALL ALCART('V',COMPOR,NOMA,'COMPOR')
      CALL JEVEUO(COMPOR//'.NCMP','E',JNCMP)
      CALL JEVEUO(COMPOR//'.VALV','E',JVALV)
      DO 90 ICMP = 1,NCMPMA
        ZK8(JNCMP+ICMP-1) = NOMCMP(ICMP)
   90 CONTINUE


      ZK16(JVALV-1+1) = 'ELAS'
      ZK16(JVALV-1+2) = '1'
      ZK16(JVALV-1+3) = 'PETIT'
      ZK16(JVALV-1+4) = 'COMP_INCR'
      ZK16(JVALV-1+5) = 'ANALYTIQUE'
      ZK16(JVALV-1+6) = '1'
      DO 20 I=7,NCMPMA
         ZK16(JVALV-1+I) = ' '
   20 CONTINUE

      NT=0
      DO 10 I=1,NBMO1
         CALL GETFAC(MOCLEF(I),N1)
         NT=MAX(NT,N1)
   10 CONTINUE
      IF (NT.EQ.0) THEN
C        UTILISE EN PARTICULIER POUR LA COMMANDE CALC_G :
C        SI AUCUN DES DEUX COMPORTEMENTS COMP_ELAS ET COMP_INCR N'EST
C        SPECIFIE PAR L'UTILISATEUR,      IRET=1
C        DANS CE CAS ON CHOISIT COMP_ELAS
        ZK16(JVALV-1+4) = 'COMP_ELAS'
      END IF

      CALL NOCART(COMPOR,1,K8B,K8B,0,K8B,IBID,K8B,NCMPMA)

C ======================================================================
C    ETAPE UTILE POUR LES VERIFICATIONS DE COHERENCE
C    CREATION D'UN CHAM_ELEM ASSOCIE A LA CARTE COMPO1 DEFINI SEULEMENT
C    SUR LES MAILLES SACHANT CALCULER FULL_MECA
C ======================================================================

      IF (NT.NE.0) THEN
          LIGREL = MODELE(1:8)//'.MODELE    .LIEL'
          CALL JEVEUO(LIGREL(1:19)//'.REPE','L',IREPE)
          CES1='&&CRCMEL.CES1'
          CEL1='&&CRCMEL.CEL1'
          CALL CARCES(COMPOR,'ELEM',' ','V',CES1,'A',IBID)
          CALL CESCEL(CES1,LIGREL,'FULL_MECA','PCOMPOR',
     &                'OUI',IBID,'V',CEL1,'A',IBID)
C         ON RECUPERE UN CHAM_ELEM DEFINI SEULEMENT SUR LES ELEMENTS
C         QUI SAVENT CALCULER FULL_MECA

C         -- SI LE CHAMP EST VIDE, C'EST QU'AUCUN ELEMENT DU MODELE
C            NE SAIT CALCULER FULL_MECA :
          CALL EXISD('CHAMP',CEL1,IEXI)
          IF (IEXI.EQ.0) CALL U2MESS('F','CALCULEL5_5')
          CALL CELCES(CEL1,'V',CES2)
          CALL DETRSD('CHAMP',CES1)
          CALL DETRSD('CHAMP',CEL1)
      ENDIF

      CALL JEDEMA()
      END
