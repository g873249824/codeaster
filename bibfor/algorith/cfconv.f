      SUBROUTINE CFCONV(MAILLA,NEQ,DEPDEL,AUTOC1,AUTOC2,
     &                  VECONT,CTCGEO,CTCFIX,
     &                  GEONOE,GEOVAL)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 24/08/2005   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE MABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  MAILLA
      INTEGER      NEQ
      CHARACTER*24 DEPDEL
      CHARACTER*19 AUTOC1
      CHARACTER*19 AUTOC2
      INTEGER      VECONT(2)
      LOGICAL      CTCGEO
      LOGICAL      CTCFIX
      CHARACTER*16 GEONOE
      REAL*8       GEOVAL
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : NMCONV
C ----------------------------------------------------------------------
C
C CONVERGENCE LIEE A LA REACTUALISATION GEOMETRIQUE DU CONTACT
C
C IN  MAILLA : NOM DU MAILLAGE
C IN  NEQ    : NOMBRE D'EQUATIONS
C IN  DEPDEL : INCREMENT DE DEPLACEMENT CUMULE
C IN  AUTOC1 : NOM DE L'OBJET JEVEUX POUR LA REACTUALISATION
C               GEOMETRIQUE AUTOMATIQUE (NOM LIE A OP0069/OP0070)
C               CONTIENT L'INCREMENT DE DEPLACEMENT ENTRE L'ITERATION
C               DE NEWTON PRECEDENTE ET L'ACTUELLE
C IN  AUTOC2 : NOM DE L'OBJET JEVEUX POUR LA REACTUALISATION
C               GEOMETRIQUE AUTOMATIQUE (NOM LIE A OP0069/OP0070)
C               CONTIENT LE DEPLACEMENT CUMULE ENTRE LE DEBUT
C               ET JUSTE AVANT L'ITERATION ACTUELLE
C I/O VECONT : (1) = NOMBRE DE REACTUALISATION GEOMETRIQUE A EFFECTUER
C                     / -1 SI AUTOMATIQUE             
C                     /  0 SI PAS DE REACTUALISATION  
C                     /  N REACTUALISATIONS  
C              (2) = NOMBRE DE REACTUALISATIONS GEOMETRIQUES EFFECTUEES
C I/O LREAC  : (1) = TRUE  SI REACTUALISATION A FAIRE 
C              (2) = TRUE  SI ATTENTE POINT FIXE CONTACT
C OUT CTCGEO : VAUT .TRUE. SI REAC GEOM A FAIRE
C OUT CTCFIX : VAUT .TRUE. SI ATTENTE POINT FIXE
C OUT GEONOE : NOM DU NOEU SUR LEQUEL LE DEPLACEMENT EST MAX (REAC_GEOM)
C OUT GEOVAL : VALEUR DE DEPLACEMENT MAX (REAC_GEOM)
C
C --------------- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------
C
      CHARACTER*32       JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      INTEGER      JDEPDE,JAUTO1,JAUTO2,II,IRET,JMAXDE,NUMNO1,NUMNO2
      INTEGER      GEOITE
      REAL*8       AUTONO,TEMP1,TEMP2,R8PREM,GEOTOL,RMIN
      CHARACTER*8  MAXDEP,NOMNO
      INTEGER      IFM,NIV     
C
C ----------------------------------------------------------------------
C
      CALL INFNIV(IFM,NIV)
      CALL JEMARQ()

C
C --- TOLERANCE POUR REACTUALISATION GEOMETRIQUE AUTOMATIQUE
C --- NOMBRE MAXI D'ITERATIONS DE REACTUALISATION GEOMETRIQUE EN AUTO
C
      GEOTOL   = 5.0D-2
      GEOITE   = 99
      MAXDEP   = '&&CVGCNT'
      CTCGEO   = .FALSE.
      GEONOE   = ' '
      TEMP1    = 0.D0
      TEMP2    = 0.D0
C
C --- CONVERGENCE GEOMETRIQUE MAIS ATTENTE POINT FIXE CONTACT
C     
      IF (CTCFIX) THEN
        CTCGEO   = .FALSE.
      ELSE
        VECONT(2) = VECONT(2) + 1

        IF (VECONT(2).GT.GEOITE) THEN
          CALL UTMESS('F',
     &                'CFCONV',
     &                'ERREUR CONTACT - TROP DE REAC. GEOM.')
        ENDIF
C
C --- CALCUL DU DEPLACEMENT POUR SAVOIR SI REACTUALISATION A FAIRE OU
C --- PAS
C
        CALL JEVEUO(DEPDEL(1:19)//'.VALE','L',JDEPDE)
        CALL JEVEUO(AUTOC1//'.VALE','E',JAUTO1)
        CALL JEVEUO(AUTOC2//'.VALE','E',JAUTO2)

        DO 10 II = 1,NEQ
          ZR(JAUTO2-1+II) = ZR(JAUTO2-1+II) + ZR(JAUTO1-1+II)
          ZR(JAUTO1-1+II) = ZR(JDEPDE-1+II) - ZR(JAUTO2-1+II)
   10   CONTINUE
C
C ---  CALCUL DU MAX DE LA NORME DU DEPLACEMENT (SAUF LAGRANGES)
C
        CALL CNOMAX(AUTOC1,TEMP1,NUMNO1)
        CALL CNOMAX(AUTOC2,TEMP2,NUMNO2)
        CALL JEEXIN(MAXDEP,IRET)
C ---  STOCKAGE DU MAX DE LA NORME DU DEPL
        IF (IRET.EQ.0) THEN
           CALL WKVECT(MAXDEP,'V V R',1,JMAXDE)
           ZR(JMAXDE-1+1) = TEMP2
           RMIN           = R8PREM()
        ELSE
           CALL JEVEUO(MAXDEP,'E',JMAXDE)
           ZR(JMAXDE-1+1) = MAX(ZR(JMAXDE-1+1),TEMP2)
           RMIN           = 1.D-6*ZR(JMAXDE-1+1)
        ENDIF

        IF (TEMP2.LT.RMIN) THEN
          IF (TEMP2.EQ.0.D0) THEN
            AUTONO = 10.0D0*GEOTOL
          ELSE
            AUTONO = 1.D-1*GEOTOL
          ENDIF
        ELSE
          AUTONO = TEMP1/TEMP2
        ENDIF
C
C --- INFORMATIONS: NOM DU NOEUD ET VALEUR REACTUALISATION
C
        CALL JENUNO(JEXNUM(MAILLA//'.NOMNOE',NUMNO2),NOMNO)

        GEONOE = NOMNO//'        '
        GEOVAL = AUTONO
C
C --- CORRESPOND A REAC_GEOM = AUTOMATIQUE
C    
        IF (VECONT(1).LT.0) THEN

          IF (AUTONO.LT.GEOTOL) THEN
            CTCGEO   = .FALSE.
          ELSE 
            VECONT(2) = VECONT(2) + 1 
            CTCGEO   = .TRUE.
          END IF
C
C --- CORRESPOND A REAC_GEOM = SANS
C     
        ELSE IF (VECONT(1).EQ.0) THEN
          IF (AUTONO.GE.GEOTOL) THEN
            CALL UTMESS('A',
     &                  'CFCONV',
     &                  'REAC. GEOM. DU CONTACT SUPERIEURE A 5%')
          ENDIF
          CTCGEO   = .FALSE.
        ELSE
C
C --- CORRESPOND A REAC_GEOM = CONTROLE
C     
          IF (VECONT(2).EQ.VECONT(1)) THEN
            CTCGEO   = .FALSE.
            IF (AUTONO.GE.GEOTOL) THEN
              CALL UTMESS('A',
     &                    'CFCONV',
     &                    'REAC. GEOM. DU CONTACT SUPERIEURE A 5%')
            ENDIF
          ELSE
            CTCGEO   = .TRUE.
          END IF
        END IF
      END IF

      CALL JEDEMA()
C
      END
