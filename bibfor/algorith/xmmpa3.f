      SUBROUTINE XMMPA3(NDIM  ,NNO   ,NNOS  ,NNOL  ,PLA,
     &                  FFC   ,FFP   ,JAC   ,
     &                  NFH   ,ND    ,CPENCO,
     &                  SINGU ,RR    ,DDLS  ,DDLM  ,
     &                  JFISNO,NFISS ,IFISS ,JHEAFA,NCOMPH,IFA,
     &                  MMAT )

      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER     NDIM,NNO,NNOS,NNOL
      INTEGER     NFH,DDLS,DDLM
      INTEGER     SINGU,PLA(27),JFISNO,NFISS,IFISS,JHEAFA,NCOMPH,IFA
      REAL*8      MMAT(216,216),ND(3)
      REAL*8      FFC(8),FFP(27),JAC
      REAL*8      CPENCO,RR

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 19/12/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C
C ROUTINE CONTACT (METHODE XFEM HPP - CALCUL ELEM.)
C
C --- CALCUL DES MATRICES A, AT, AU - CAS DU CONTACT
C
C ----------------------------------------------------------------------
C
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  NNO    : NOMBRE DE NOEUDS DE L'ELEMENT DE REF PARENT
C IN  NNOS   : NOMBRE DE NOEUDS SOMMET DE L'ELEMENT DE REF PARENT
C IN  NNOL   : NOMBRE DE NOEUDS PORTEURS DE DDLC
C IN  NNOF   : NOMBRE DE NOEUDS DE LA FACETTE DE CONTACT
C IN  PLA    : PLACE DES LAMBDAS DANS LA MATRICE
C IN  IPGF   : NUMÉRO DU POINTS DE GAUSS
C IN  IVFF   : ADRESSE DANS ZR DU TABLEAU FF(INO,IPG)
C IN  FFC    : FONCTIONS DE FORME DE L'ELEMENT DE CONTACT
C IN  FFP    : FONCTIONS DE FORME DE L'ELEMENT PARENT
C IN  JAC    : PRODUIT DU JACOBIEN ET DU POIDS
C IN  NFH    : NOMBRE DE FONCTIONS HEAVYSIDE
C IN  NOEUD  : INDICATEUR FORMULATION (T=NOEUDS , F=ARETE)
C IN  ND     : NORMALE À LA FACETTE ORIENTÉE DE ESCL -> MAIT
C                 AU POINT DE GAUSS
C IN  CPENCO : COEFFICIENT DE PENALISATION DU CONTACT
C IN  SINGU  : 1 SI ELEMENT SINGULIER, 0 SINON
C IN  RR     : DISTANCE AU FOND DE FISSURE
C IN  DDLS   : NOMBRE DE DDL (DEPL+CONTACT) À CHAQUE NOEUD SOMMET
C IN  DDLM   : NOMBRE DE DDL A CHAQUE NOEUD MILIEU
C I/O MMAT   : MATRICE ELEMENTAITRE DE CONTACT/FROTTEMENT 
C
C
      INTEGER I,J,L,JN,JFH,COEFJ
      INTEGER PLI,PLJ
      REAL*8  FFI,FFJ
      LOGICAL LMULTC
C
C ----------------------------------------------------------------------
C
      COEFJ = 2
      LMULTC = NFISS.GT.1
C I.1 CALCUL DE A
      DO 130 I = 1,NNOL

        PLI=PLA(I)
        FFI=FFC(I)

        DO 131 J = 1,NNO
          CALL INDENT(J,DDLS,DDLM,NNOS,JN)
          DO 134 JFH = 1,NFH
            IF (LMULTC) THEN
              COEFJ = ZI(JHEAFA-1+NCOMPH*(NFISS*(IFISS-1)
     &                  +ZI(JFISNO-1+NFH*(J-1)+JFH)-1)+2*IFA)
     &              - ZI(JHEAFA-1+NCOMPH*(NFISS*(IFISS-1)
     &                  +ZI(JFISNO-1+NFH*(J-1)+JFH)-1)+2*IFA-1)
            ENDIF
            DO 132 L = 1,NDIM
              MMAT(PLI,JN+NDIM*JFH+L) = MMAT(PLI,JN+NDIM*JFH+L)
     &        + COEFJ * FFI * FFP(J) * ND(L) * JAC
C
C LBB : ON PREND AUSSI LE TERME EN PENALISATION
C
              MMAT(JN+NDIM*JFH+L,PLI) = MMAT(JN+NDIM*JFH+L,PLI)
     &        + COEFJ * FFI * FFP(J) * ND(L) * JAC
 132        CONTINUE
C
 134      CONTINUE
          DO 133 L = 1,SINGU*NDIM
            MMAT(PLI,JN+NDIM*(1+NFH)+L) = MMAT(PLI,JN+NDIM*(1+NFH)+L)
     &      + COEFJ * FFI * FFP(J) * RR * ND(L) * JAC
C
            MMAT(JN+NDIM*(1+NFH)+L,PLI)= MMAT(JN+NDIM*(1+NFH)+L,PLI)
     &      + COEFJ * FFI * FFP(J) * RR * ND(L) * JAC
 133      CONTINUE

 131    CONTINUE

 130  CONTINUE
C
C     CALCUL DE C
C
      DO 220 I = 1,NNOL

        PLI=PLA(I)
        FFI=FFC(I)

        DO 221 J = 1,NNOL

          PLJ=PLA(J)
          FFJ=FFC(J)
C
          MMAT(PLI,PLJ) = MMAT(PLI,PLJ)
     &             - FFJ * FFI * JAC / CPENCO

 221    CONTINUE
 220  CONTINUE

      END
