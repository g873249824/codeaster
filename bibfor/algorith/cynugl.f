      SUBROUTINE CYNUGL(PROFNO,INDIRF,MODCYC,MAILSK)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 18/10/2010   AUTEUR NISTOR I.NISTOR 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
C
C***********************************************************************
C    P. RICHARD     DATE 10/02/92
C-----------------------------------------------------------------------
C  BUT: < CYCLIQUE NUMEROTATION GLOBALE >
C
C    CREER A PARTIR D'UN RESULTAT CYCLIQUE ET UN MAILLAGE GLOBAL
C  SQUELLETTE LE PROFIL CHAMNO ET UNE FAMILLE NUMEROTEE
C ,DONT CHAQUE OBJET CORRESPOND A UN SECTEUR, ET EST DIMENSIONNE
C  A 2*NBDDL ENGENDRE PAR LE SECTEUR:C
C
C            2*(I-1)+1 --> NUMERO EQUATION DANS NUMDDL SECTEUR
C            2*(I-1)+2 --> NUMERO EQUATION DANS PROFNO GLOBAL
C
C-----------------------------------------------------------------------
C
C NOM----- / /:
C
C PROFNO   /I/: NOM K19 DU PROF_CHNO A CREER
C INDIRF   /I/: NOM K24 DE LA FAMILLE DES INDIRECTIONS A CREER
C MODCYC   /I/: NOM DU RESULTAT CYCLIQUE EN AMONT
C MAILSK   /I/: NOM DU MAILLAGE SKELETTE
C
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
      CHARACTER*32 JEXNOM, JEXNUM
C
C-----  FIN  COMMUNS NORMALISES  JEVEUX  -------------------------------
C
      PARAMETER    (NBCPMX=300)
      CHARACTER*6  PGC
      CHARACTER*8  MODCYC,MAILSK,BASMOD,INTF
      CHARACTER*8  K8BID
      CHARACTER*19 NUMDDL,PROFNO
      CHARACTER*24 INDIRF,LILI,PRNO,DEEQ,NUEQ
      INTEGER      IDEC(NBCPMX)
C
C-----------------------------------------------------------------------
C
      CALL JEMARQ()
      PGC='CYNUGL'
C
C
C-------------------RECUPERATION DE LA BASE MODALE----------------------
C
      CALL JEVEUO(MODCYC//'.CYCL_REFE','L',LLREF1)
      BASMOD=ZK24(LLREF1+2)
      CALL JELIBE(MODCYC//'.CYCL_REFE')
C
C----------------RECUPERATION DU NUMDDLET DE L'INTERF_DYNA--------------
C
C
      CALL JEVEUO(BASMOD//'           .REFD','L',LLREF2)
      INTF  =ZK24(LLREF2+4)
      NUMDDL=ZK24(LLREF2+3)
      CALL JELIBE(BASMOD//'           .REFD')
C
C---------------RECUPERATION DU NOMBRE DE COMPOSANTES-------------------
C
      CALL DISMOI('F','NB_CMP_MAX',INTF,'INTERF_DYNA',NBCMP,K8BID,IRET)
      CALL DISMOI('F','NB_EC',INTF,'INTERF_DYNA',NBEC,K8BID,IRET)
      IF (NBEC.GT.10) THEN
        CALL U2MESS('F','MODELISA_94')
      ENDIF
C
C---------------RECUPERATION DU NOMBRE DE SECTEURS----------------------
C
C
      CALL JEVEUO(MODCYC//'.CYCL_NBSC','L',LLNOMS)
      NBSEC=ZI(LLNOMS)
      CALL JELIBE(MODCYC//'.CYCL_NBSC')
C
C-------------RECUPERATION DIMENSION MAILLAGE SQUELETTE-----------------
C
      CALL DISMOI('F','NB_NO_MAILLA',MAILSK,'MAILLAGE',NBNOT,
     &             K8BID,IRET)
C
C------------RECUPERATION DU .INV.SKELETON------------------------------
C
      CALL JEVEUO(MAILSK//'.INV.SKELETON','L',LLINSK)
C
C--------------RECUPERATION DU PRNO DU SECTEUR--------------------------
C
C----ON AJOUT .NUME POUR OBTENIR LE PROF_CHNO
      NUMDDL(15:19)='.NUME'
      CALL JENONU(JEXNOM(NUMDDL//'.LILI','&MAILLA'),IBID)
      CALL JEVEUO(JEXNUM(NUMDDL//'.PRNO',IBID),'L',LLPRNO)
      CALL JEVEUO(NUMDDL//'.NUEQ','L',LLNUEQ)
      CALL DISMOI('F','NB_EQUA',NUMDDL,'NUME_DDL',NEQSEC,K8BID,IRET)
C
C--------------------ALLOCATION DU VECTEUR DE TRAVAIL-------------------
C     POUR STOCKAGE NOMBRE DE DDL GLOBAUX ENGENDRE PAR SECTEUR
C
      CALL WKVECT('&&'//PGC//'.TAIL.DDL.SECT','V V IS',NBSEC,LTTDS)
C
C--------------BOUCLE DE COMPTAGE DES DDL FINAUX------------------------
C
      NDDLT=0
      DO 10 I=1,NBNOT
        NUMSEC=ZI(LLINSK+I-1)
        NUMNOS=ZI(LLINSK+NBNOT+I-1)
        NDDLT=NDDLT+ZI(LLPRNO+(NUMNOS-1)*(2+NBEC)+1)
        ZI(LTTDS+NUMSEC-1)=ZI(LTTDS+NUMSEC-1)+
     &                     ZI(LLPRNO+(NUMNOS-1)*(2+NBEC)+1)
 10   CONTINUE
C
C-----------------ALLOCATION DES DIVERS OBJETS--------------------------
C
      LILI=PROFNO//'.LILI'
      PRNO=PROFNO//'.PRNO'
      DEEQ=PROFNO//'.DEEQ'
      NUEQ=PROFNO//'.NUEQ'
C
      CALL JECREO(LILI,'G N K24')
      CALL JEECRA(LILI,'NOMMAX',2,' ')
C
C
      CALL WKVECT(DEEQ,'G V I',NDDLT*2,LDDEEQ)
C
C
      CALL WKVECT(NUEQ,'G V I',NDDLT,LDNUEQ)
C
      CALL JECREC(PRNO,'G V I','NU','CONTIG','VARIABLE',2)
C
C
      CALL JECROC(JEXNOM(PRNO(1:19)//'.LILI','&MAILLA'))
      CALL JECROC(JEXNOM(PRNO(1:19)//'.LILI','LIAISONS'))
C
C
      CALL JECREC(INDIRF,'V V I','NU',
     &'DISPERSE','VARIABLE',NBSEC)
C
C
      DO 20 I=1,NBSEC
        CALL JECROC(JEXNUM(INDIRF,I))
        NTAIL=2*ZI(LTTDS+I-1)
        CALL JEECRA(JEXNUM(INDIRF,I),'LONMAX',NTAIL,' ')
        ZI(LTTDS+I-1)=0
 20   CONTINUE
C
C
C---------------REMPLISSAGE DES OBJETS EVIDENTS-------------------------
C
      CALL JEECRA(JEXNUM(PRNO,1),'LONMAX',NBNOT*(2+NBEC),K8BID)
      CALL JEECRA(JEXNUM(PRNO,2),'LONMAX',1,K8BID)
      CALL JEECRA(PRNO,'LONT',NBNOT*(2+NBEC)+1,' ')
C
C-------------------------REMPLISSAGE DES OBJETS------------------------
C  SUPPOSE QUE LES NOEUDS GLOBAUX SONT RANGES PAR SOUS-STRUCTURES
C
C
      CALL JENONU(JEXNOM(PRNO(1:19)//'.LILI','&MAILLA'),IBID)
      CALL JEVEUO(JEXNUM(PRNO,IBID),'E',LDPRNO)
C
      NSECPR=1
      CALL JEVEUO(JEXNUM(INDIRF,NSECPR),'E',LTINSE)
      ICOMP=0
      DO 30 I=1,NBNOT
C
        NUMSEC=ZI(LLINSK+I-1)
        NUMNOS=ZI(LLINSK+NBNOT+I-1)
        IEQ=ZI(LLPRNO+(NUMNOS-1)*(2+NBEC))
        NBDDL=ZI(LLPRNO+(NUMNOS-1)*(2+NBEC)+1)
        CALL ISDECO(ZI(LLPRNO+(NUMNOS-1)*(2+NBEC)+2),IDEC,NBCMP)
C
        ZI(LDPRNO+(I-1)*(2+NBEC))=ICOMP+1
        ZI(LDPRNO+(I-1)*(2+NBEC)+1)=NBDDL
        DO 40 IEC = 1, NBEC
          ZI(LDPRNO+(I-1)*(2+NBEC)+1+IEC)=
     &                        ZI(LLPRNO+(NUMNOS-1)*(2+NBEC)+1+IEC)
  40    CONTINUE
        IF(NUMSEC.NE.NSECPR) THEN
          CALL JELIBE(JEXNUM(INDIRF,NSECPR))
          NSECPR=NUMSEC
          CALL JEVEUO(JEXNUM(INDIRF,NSECPR),'E',LTINSE)
        ENDIF
        IAD=0
        DO 50 J=1,NBCMP
          IF(IDEC(J).GT.0) THEN
            IAD=IAD+1
            ICOMP=ICOMP+1
            ZI(LDDEEQ+(ICOMP-1)*2)=I
            ZI(LDDEEQ+(ICOMP-1)*2+1)=J
            ZI(LDNUEQ+ICOMP-1)=ICOMP
            LINUEQ=ZI(LLNUEQ+IEQ+IAD-2)
            IPOINT=LTINSE-1+2*ZI(LTTDS+NUMSEC-1)
            ZI(IPOINT+1)=LINUEQ
            ZI(IPOINT+2)=ICOMP
            ZI(LTTDS+NUMSEC-1)=ZI(LTTDS+NUMSEC-1)+1
          ENDIF
 50     CONTINUE
 30   CONTINUE
C
      CALL JELIBE(JEXNUM(INDIRF,NSECPR))
C
C------------------SAUVEGARDE DES OBJETS--------------------------------
C
      CALL JEDETR('&&'//PGC//'.TAIL.DDL.SECT')
C
      CALL JEDEMA()
      END
