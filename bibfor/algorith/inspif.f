      SUBROUTINE INSPIF ( SIGRX,EPSRX,ETS,EDC,EPST,IFISU,JFISU,
     1 RTM,EPSC,DEFR,TANG,IPLA,EQSTR,EPSEQ)
        IMPLICIT REAL*8 (A-H,O-Z)
C       -----------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/02/2004   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C       -----------------------------------------------------------
C    NADAI_B :
C            CALCUL DE LA CONTRAINTE SIGRX A T (REPERE ORTHOTROPE)
C            ET INITIALISATION DES VARIABLES INTERNES LORSQU'IL Y A
C            DECHARGE A PARTIR D'UN POINT INTEGRE.
C            ON SE BRANCHE ALORS SUR LA COURBE 1-D
C            REMARQUE : TOUTES LES VARIABLES SONT SCALAIRES (1-D)
C
C IN
C     SIGRX: CONTRAINTE UNIAXIALE A T
C     EPSRX: DEFORMATION UNIAXIALE A T
C     EPST : DEFORMATION EN COMPRESSION CORRESPONDANT A -LTR
C             APRES DECHARGE
C     EPSC : DEFORMATION EN COMPRESSION A L'INSTANT DE LA DECHARGE
C     RTM  : RESISTANCE EN TRACTION ACTUELLE
C     EDC  : PENTE DECHARGE EN COMPRESSION
C     DEFR : DEFORMATION RESIDUELLE APRES DECHARGE EN COMPRESSION
C     ETS  : PENTE EN TENSION-SOFTENING
C     TANG : PENTE ACTUELLE
C     JFISU: INDICATEUR DE FISSURATION : RESTE TOUJOURS A 1
C            SI LE BETON EST FISSURE
C     IFISU: INDICATEUR DE FISSURATION = 1 FISSURE OUVERTE
C                                      = 0 FISSURE FERMEE
C     IPLA : INDICATEUR DE PLASTICITE
C               =0 COMPORTEMENT PRE PIC EN COMPRESSION
C               =1 SOFTENING LINEAIRE EN COMPRESSION
C               =2 RUPTURE EN COMPRESSION (EPSRX > KRUPC)
C     EQSTR: CONTRAINTE  EQUIVALENTE = ABS (SIGRX)
C     EPSEQ: DEFORMATION EQUIVALENTE = ABS (EPSRX)
C
C OUT
C     SIGRX, EPSRX, ETS, EDC, EPST, IFISU, JFISU, RTM, EPSC
C     DEFR, TANG, IPLA, EQSTR, EPSEQ
C       -----------------------------------------------------------
C COMMENTAIRES
C     PF : POINT FOCAL
C     RTMC : RESISTANCE EN TRACTION MODIFIEE SI DECHARGE
C            EN COMPRESSION
C------------------------------------------------------------------
      INTEGER   IFISU,IPLA,JFISU
      REAL*8    EPSRX,SIGRX,EQSTR,EPSEQ,TANG,EPST
      REAL*8    EPSC,EDC,RTM,DEFR,ETS
      COMMON /DBETO/ POU1(4),RB,ALPHA,EX,PXY,EMAX,EPSUT,FTC,FLIM,
     1   EPO, EPO1,POU2(6),ICC,IPOU(9)
C------------------------------------------------------------------
      ZERO = 0.D0
      RBT  = ALPHA * RB
      EPOC = -1.D0 * EPO1
      IF( EPSRX .LT. (-1.D0*EMAX) ) THEN
C
C     ON SUPPOSE QUE LE POINT EST ROMPU EN COMPRESSION
C
      IPLA  = 2
      SIGRX = ZERO
      RTM   = ZERO
      TANG  = ZERO
      ETS   = ZERO
      EQSTR = ZERO
      EPSEQ = ABS(EPSRX)
      GOTO 9999
      ENDIF
C
      IF( EPSRX .LE. EPOC ) THEN
C******************************************************************
C    POINT EN REGIME POST-PIC (COMPRESSION)
C******************************************************************
       IPLA = 1
       AA = -1.D0 * RB / ( EMAX - EPO1 )
       BB = AA * EMAX
       DELTA = SIGRX - (AA * EPSRX + BB )
       IF( DELTA .GT. ZERO ) THEN
C
C  POINT EN DECHARGE A PARTIR DE LA COURBE DE COMPRESSION
C
        CALL INSINF (EPSRX , SIGRX , EPSC , EQSTR , EDC , IPLA , DEFR)
        EPSEQ = ABS(EPSC)
        GOTO 100
       ELSE
C
C     POINT EN CHARGE (SOFTENING)
C     COURBE DE COMPRESSION TRANSLATEE
C
        SEQ  = ABS(SIGRX)
        IPLA = 1
        CALL INSEFT ( EPSEQ , SEQ , IPLA )
        SIGRX = -1.D0 * SEQ
        EPSRX = -1.D0 * EPSEQ
        EQSTR = SEQ
       ENDIF
      GOTO 9999
      ENDIF
C******************************************************************
C    POINT EN REGIME PRE-PIC (COMPRESSION)
C******************************************************************
      DELTA   = 1.D0
      DEFRLI = EPSRX - SIGRX / EX
      IF ( ABS(DEFRLI) .LT. 1.D-8 ) DEFRLI = ZERO
      IF( EPSRX .LE. ZERO ) THEN
       EPEQ = ABS(EPSRX)
       SEQ2 = EX*EPEQ/(1.D0+(EX/RB-2.D0/EPO1)*EPEQ+(EPEQ/EPO1)**2)
       DELTA= -1.D0 * SEQ2 - SIGRX
      ENDIF
      IF(DELTA.LT.ZERO.OR.(DEFRLI.LT.ZERO.AND.EPSRX.GE.ZERO)) THEN
C
C    POINT EN DECHARGE A PARTIR DE LA COURBE DE COMPRESSION
C
       CALL INSINF (EPSRX , SIGRX , EPSC , EQSTR , EDC , IPLA , DEFR)
       EPSEQ = ABS(EPSC)
       GOTO 100
      ENDIF
C--------------------------------------------------------------
C    POINT CONSIDERE INITIALLEMENT TENDU
C--------------------------------------------------------------
      EPSF0 = RTM / EX
      IF( EPSRX .GE. ZERO .AND. SIGRX .GE. ZERO ) THEN
         IF( SIGRX .GE. RTM ) THEN
           SIGRX = RTM
           IFISU = 1
           JFISU = 1
         ENDIF
         IF( EPSRX .GE. EPSF0 ) THEN
           EPSRX = EPSF0 + ( RTM - SIGRX ) / ETS
           IFISU = 1
           JFISU = 1
         ELSE
           EPSRX = SIGRX / EX
         ENDIF
         GOTO 9999
      ENDIF
C==============================================================
C    POINT INITIALLEMENT COMPRIME
C    COURBE DE COMPRESSION TRANSLATEE
C
      SEQ  = ABS(SIGRX)
      IPLA = 0
      CALL INSEFT( EPSEQ , SEQ , IPLA )
      SIGRX = -1.D0 * SEQ
      EPSRX = -1.D0 * EPSEQ
      EQSTR = SEQ
      GOTO 9999
C==============================================================
  100 CONTINUE
C
C       RESISTANCE EN TRACTION MODIFIEE
C
      EPOC = -1.D0 * EPO1
      IF( ABS(DEFR) .LT. 1.D-10 ) DEFR = 0.D0
      IF( DEFR .LT. EPOC ) THEN
        RTMC = ZERO
      ELSE
        IF( RTM .LT. ZERO ) RTM = ZERO
        AS2  = RBT / ( EPO1 + RBT / EX )
        IF( ABS( EDC - AS2 ) .GT. 1.D-5 ) THEN
          EPS1 = ( EDC * DEFR - AS2 * EPOC ) / ( EDC - AS2 )
          RTMC = EDC * ( EPS1 - DEFR )
        ELSE
          RTMC = ZERO
        ENDIF
C
        IF(RTMC.GT.RBT) RTMC = RBT
      ENDIF
      IF( RTMC .LT. ZERO ) RTMC = ZERO
      IF( RTMC .LT. RTM  ) RTM  = RTMC
C
C       CALCUL DE LA DEFORMATION CORRESPONDANT A - LTR
C
      IF( ABS(EDC) .LT. 1.D-5 ) EDC = EX
      EPST = DEFR - RBT / EDC
C
C   DANS LE CAS OU LA DECHARGE S EFFECTUE DANS LA BRANCHE DESCENDANTE
C   EN DESOUS DU NIVEAU ABS(LTR) ON MEMORISE EPSC
C
      IF( IPLA .EQ. 1 .AND. EQSTR .LE. RBT ) EPST = EPSC
C
 9999 CONTINUE
      END
