      SUBROUTINE VEDIME(MODELE,CHARGE,INFCHA,INSTAP,TYPRES,
     &                  VECELZ)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 12/02/2013   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT      NONE
      INCLUDE 'jeveux.h'
      CHARACTER*(*) VECELZ,TYPRES
      CHARACTER*24  MODELE,CHARGE,INFCHA
      REAL*8        INSTAP
C
C ----------------------------------------------------------------------
C
C CALCUL DES VECTEURS ELEMENTAIRES DES ELEMENTS DE LAGRANGE
C PRODUIT UN VECT_ELEM DEVANT ETRE ASSEMBLE PAR LA ROUTINE ASASVE
C
C ----------------------------------------------------------------------
C
C IN  MODELE  : NOM DU MODELE
C IN  CHARGE  : LISTE DES CHARGES
C IN  INFCHA  : INFORMATIONS SUR LES CHARGEMENTS
C IN  INSTAP  : INSTANT DU CALCUL
C OUT VECELE  : VECT_ELEM RESULTAT.
C
C ----------------------------------------------------------------------
C
      CHARACTER*8  LPAIN(3),LPAOUT
      CHARACTER*19 LCHIN(3),LCHOUT
      CHARACTER*8  NOMCHA
      CHARACTER*8  K8BID,NEWNOM
      CHARACTER*16 OPTION
      CHARACTER*19 VECELE,LIGRMO
      CHARACTER*24 LIGRCH,CHGEOM,CHTIME
      INTEGER      IBID,IRET,NCHAR,ILVE,JINF,JCHAR,ICHA
      INTEGER      NUMDI
      LOGICAL BIDON
      COMPLEX*16   CBID
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      NEWNOM = '.0000000'
      LIGRMO = MODELE(1:8)//'.MODELE'
      IF (TYPRES.EQ.'R') THEN
        LPAOUT = 'PVECTUR'
      ELSE
        LPAOUT = 'PVECTUC'
      END IF
      LCHOUT = '&&VEDIME.???????'
C
C --- DETECTION DE LA PRESENCE DE CHARGES
C
      BIDON = .TRUE.
      CALL JEEXIN(CHARGE,IRET)
      IF (IRET.NE.0) THEN
        CALL JELIRA(CHARGE,'LONMAX',NCHAR,K8BID)
        IF (NCHAR.NE.0) THEN
          BIDON = .FALSE.
          CALL JEVEUO(CHARGE,'L',JCHAR)
          CALL JEVEUO(INFCHA,'L',JINF)
        END IF
      END IF
C
C --- CALCUL DU NOM DU RESULTAT :
C
      VECELE = VECELZ
      IF (VECELE.EQ.' ') VECELE = '&&VEDIME'
C
C --- ALLOCATION DU VECT_ELEM RESULTAT :
C
      CALL DETRSD('VECT_ELEM',VECELE)
      CALL MEMARE('V',VECELE,MODELE(1:8),' ',' ','CHAR_MECA')
      CALL REAJRE(VECELE,' ','V')
      IF (BIDON) GOTO 99
C
C --- CARTES GEOMETRIE
C
      CALL MEGEOM(MODELE(1:8),CHGEOM)
C
C --- CARTE INSTANTS
C
      CHTIME = '&&VEDIME.CH_INST_R'
      CALL MECACT('V',CHTIME,'MODELE',LIGRMO,'INST_R  ',
     &            1,'INST',IBID,INSTAP,CBID,K8BID)
C
C --- CHAMPS IN
C
      LPAIN(2) = 'PGEOMER'
      LCHIN(2) = CHGEOM(1:19)
      LPAIN(3) = 'PTEMPSR'
      LCHIN(3) = CHTIME(1:19)
C
C --- CALCUL
C
      ILVE = 0
      DO 30 ICHA = 1,NCHAR
        NUMDI = ZI(JINF+ICHA)
        IF ((NUMDI.GT.0) .AND. (NUMDI.LE.3)) THEN
          NOMCHA   = ZK24(JCHAR+ICHA-1) (1:8)
          LIGRCH   = NOMCHA//'.CHME.LIGRE'
          LCHIN(1) = NOMCHA//'.CHME.CIMPO'
          IF (NUMDI.EQ.1) THEN
            IF (TYPRES.EQ.'R') THEN
              OPTION = 'MECA_DDLI_R'
              LPAIN(1) = 'PDDLIMR'
            ELSE
              OPTION = 'MECA_DDLI_C'
              LPAIN(1) = 'PDDLIMC'
            END IF
          ELSE IF (NUMDI.EQ.2) THEN
            OPTION = 'MECA_DDLI_F'
            LPAIN(1) = 'PDDLIMF'
          ELSE IF (NUMDI.EQ.3) THEN
            OPTION = 'MECA_DDLI_F'
            LPAIN(1) = 'PDDLIMF'
          END IF
          ILVE = ILVE + 1
          CALL GCNCO2(NEWNOM)
          LCHOUT(10:16) = NEWNOM(2:8)
          CALL CORICH('E',LCHOUT,ICHA,IBID)
          CALL CALCUL('S',OPTION,LIGRCH,3,LCHIN,LPAIN,1,LCHOUT,LPAOUT,
     &                 'V','OUI')
          CALL REAJRE(VECELE,LCHOUT,'V')

        ENDIF
   30 CONTINUE
C
   99 CONTINUE
      VECELZ = VECELE//'.RELR'

      CALL JEDEMA()
      END
