      SUBROUTINE CFCALN(NOMA  ,DEFICO,NEWGEO,METHOZ,ITEMAX,
     &                  EPSMAX)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 01/04/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8   NOMA
      CHARACTER*24  DEFICO,NEWGEO
      INTEGER       ITEMAX
      REAL*8        EPSMAX
      CHARACTER*(*) METHOZ
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (TOUTES METHODES - APPARIEMENT)
C
C CALCUL ET STOCKAGE DES NORMALES MOYENNEES AUX NOEUDS ET
C DES VECTEURS TANGENTS A CHAQUE NOEUD 
C      
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : NOUVELLE GEOMETRIE (AVEC DEPLACEMENT GEOMETRIQUE)
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  METHOD : METHODE DE CONTACT
C               'DISCRETE' OU 'CONTINUE'
C IN  ITEMAX : NOMBRE MAXI D'ITERATIONS DE NEWTON POUR LA PROJECTION
C IN  EPSMAX : RESIDU POUR CONVERGENCE DE NEWTON POUR LA PROJECTION
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      POSNO,NNOCO
      INTEGER      IFM,NIV
      INTEGER      NBMA,JCOOR,NDIMG
      INTEGER      INO,IMA,I,CFDISI,NIVERR
      INTEGER      POSMAI,JDECMA,IBID,IZONE
      REAL*8       TAU1(3),TAU2(3),NORMAL(3),NORMN
      REAL*8       VNORM(3),COORNO(3),R8BID,R8PREM,NOOR
      CHARACTER*8  METHOD,FFORME
      CHARACTER*24 NDIMCO,CONTNO,TANINI,PMANO,MANOCO,NOZOCO
      INTEGER      JDIM  ,JNOCO ,JTGINI,JPOMA,JMANO ,JZOCO
      CHARACTER*24 K24BLA,K24BID
      CHARACTER*4  TYPNOE
      LOGICAL      CFCALD,LFFISS,LPOINT,LPOUTR 
      REAL*8       COORMA(27)
      INTEGER      NNO,NDIM
      INTEGER      NUMMAI
      CHARACTER*8  ALIAS,NOMMAI,NOMNO
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> CALCUL DES NORMALES SUR' //
     &  ' TOUS LES NOEUDS (MAITRES ET ESCLAVES)' 
      ENDIF
C
C --- RECUPERATION DE QUELQUES DONNEES
C
      NDIMCO = DEFICO(1:16)//'.NDIMCO'
      CONTNO = DEFICO(1:16)//'.NOEUCO'
      NOZOCO = DEFICO(1:16)//'.NOZOCO'    
      TANINI = DEFICO(1:16)//'.TANINI'
      PMANO  = DEFICO(1:16)//'.PMANOCO'
      MANOCO = DEFICO(1:16)//'.MANOCO'
      
      CALL JEVEUO(NDIMCO,'L',JDIM)      
      CALL JEVEUO(CONTNO,'L',JNOCO)
      CALL JEVEUO(NOZOCO,'L',JZOCO)    
      CALL JEVEUO(TANINI,'E',JTGINI)      
      CALL JEVEUO(PMANO ,'L',JPOMA)
      CALL JEVEUO(MANOCO,'L',JMANO)
           
      CALL JEVEUO(NEWGEO(1:19)//'.VALE','L',JCOOR)  
C
C --- INITIALISATIONS
C
      IZONE     = 0
      NNOCO     = CFDISI(DEFICO,'NNOCO',IZONE)
      METHOD    = METHOZ 
      K24BLA    = ' '
      COORNO(1) = 0.D0
      COORNO(2) = 0.D0
      COORNO(3) = 0.D0  
      DO 5 I = 1,27
        COORMA(I) = 0.D0     
 5    CONTINUE       
      NDIMG     = ZI(JDIM) 
      CALL MMINFP(0     ,DEFICO,K24BLA,'FOND_FISSURE',
     &            IBID  ,R8BID ,K24BID,LFFISS)
C
C --- TYPE DE FORMULATION DE CONTACT
C      
      IF (METHOD.EQ.'CONTINUE') THEN
        FFORME = 'CONTINUE'
      ELSEIF (METHOD.EQ.'DISCRETE') THEN
        FFORME = 'STANDARD'
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF     
C
C --- BOUCLE SUR TOUS LES NOEUDS
C
      DO 10 INO = 1,NNOCO 
        POSNO     = INO
        NORMAL(1) = 0.D0
        NORMAL(2) = 0.D0
        NORMAL(3) = 0.D0
C
C --- INFOS SUR LE NOEUD 
C                   
        CALL CFCARN(NOMA  ,DEFICO,NEWGEO,POSNO ,IBID  ,
     &              COORNO,TYPNOE,NOMNO )    
C
C --- INFOS SUR LES MAILLES CONTENANT LE NOEUD
C
        IZONE   = ZI(JZOCO+POSNO-1)
        NBMA    = ZI(JPOMA+POSNO) - ZI(JPOMA+POSNO-1)
        JDECMA  = ZI(JPOMA+POSNO-1) 
C
C --- CALCUL OU PAS ?
C
        IF (.NOT.CFCALD(DEFICO,IZONE ,TYPNOE)) THEN
          GOTO 91
        ENDIF 
C
C --- CALCUL INTERDIT SI FOND_FISSURE
C
        IF (LFFISS) THEN
          CALL U2MESS('F','CONTACT3_17')
        ENDIF                                     
C
C --- BOUCLE SUR LES MAILLES CONTENANT LE NOEUD
C
        DO 20 IMA = 1,NBMA
C
C --- MAILLE COURANTE
C      
          POSMAI = ZI(JMANO+JDECMA+IMA-1)  
C
C --- CARACTERISTIQUES DE LA MAILLE
C
          CALL CFCARM(NOMA  ,DEFICO,NEWGEO,POSMAI,NUMMAI,
     &                ALIAS ,NOMMAI,NDIM  ,NNO   ,COORMA)  
C
C --- TYPE DE MAILLE
C 
          LPOUTR = (ALIAS(1:2).EQ.'SG').AND.(NDIMG.EQ.3)
          LPOINT = ALIAS.EQ.'PO1' 
C
C --- DEFINITION BASE TANGENTE NULLE DANS LE CAS D'UNE MAILLE POINT
C           
          IF (LPOINT) THEN
            IF (NBMA.NE.1) THEN
              CALL ASSERT(.FALSE.)
            ENDIF  
            CALL MMCPOI(NOMA  ,DEFICO,NDIMG  ,IZONE  ,NUMMAI,
     &                  TYPNOE,TAU1  ,TAU2  )  
            GOTO 89    
          ENDIF                     
C
C --- CALCUL DES TANGENTES EN CE NOEUD
C       
          CALL MMCTAN(NOMA  ,FFORME,NOMMAI,ALIAS ,NNO   ,
     &                NDIM  ,COORMA,COORNO,ITEMAX,EPSMAX,
     &                TAU1  ,TAU2  )
C
C --- DEFINITION BASE TANGENTE LOCALE DANS LE CAS DES POUTRES
C
          IF (LPOUTR) THEN
            CALL MMCPOU(NOMA  ,DEFICO,IZONE  ,NUMMAI,TYPNOE,
     &                  TAU1  ,TAU2  )
          ENDIF       
C
C --- CALCUL DE LA NORMALE _INTERIEURE_ 
C     
          CALL MMNORM(NDIMG,TAU1,TAU2,VNORM,NOOR)  
          IF (NOOR.LE.R8PREM()) THEN
            CALL U2MESS('I','CONTACT3_33')
            CALL U2MESG('F','CONTACT3_23',1,NOMMAI,0,0,3,COORNO) 
          ENDIF             
C  
C --- NORMALE RESULTANTE 
C
          DO 60 I = 1,3
            NORMAL(I) = NORMAL(I) + VNORM(I)
 60       CONTINUE 
 20     CONTINUE
C
C --- MOYENNATION DE LA NORMALE SUR TOUTES LES MAILLES LIEES AU NOEUD
C
        DO 70 I = 1,3
          NORMAL(I) = NORMAL(I) / NBMA
 70     CONTINUE
C
C --- NORMALISATION DE LA NORMALE SUR TOUTES LES MAILLES LIEES AU NOEUD
C 
        CALL NORMEV(NORMAL,NORMN)
        IF (NORMN.LE.R8PREM()) THEN
          CALL U2MESS('I','CONTACT3_33')
          CALL U2MESG('F','CONTACT3_23',1,NOMMAI,0,0,3,COORNO) 
        ENDIF                                     
C
C --- RE-CONSTRUCTION DES VECTEURS TANGENTS APRES LISSAGE
C
        CALL MMMRON(NDIMG ,NORMAL,TAU1  ,TAU2  )        
C
C --- NORMALISATION DES TANGENTES
C
        CALL MMTANN(NDIMG ,TAU1  ,TAU2  ,NIVERR)
        IF (NIVERR.EQ.1) THEN  
          CALL U2MESS('I','CONTACT3_33')
          CALL U2MESG('F','CONTACT3_14',1,NOMMAI,0,0,3,COORNO)          
        ENDIF         
C 
C --- STOCKAGE DES VECTEURS TANGENTS EXTERIEURS SUR LE NOEUD  
C  
 89     CONTINUE       
        DO 90 I = 1,3
          ZR(JTGINI+6*(POSNO-1)+I-1)   = TAU1(I)
          IF (NDIMG.EQ.3) THEN
            ZR(JTGINI+6*(POSNO-1)+3+I-1) = TAU2(I)
          ENDIF  
 90     CONTINUE    
 91     CONTINUE    
 10   CONTINUE
C
      CALL JEDEMA()
      END
