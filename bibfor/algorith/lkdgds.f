      SUBROUTINE LKDGDS(NMAT,MATERF,PARA,VARA,DEVSIG,I1,VAL,DS2HDS,
     &                  VECN,DFDS,BPRIMP,NVI,VINT,DHDS,DGDS,IRET)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 17/09/2012   AUTEUR FOUCAULT A.FOUCAULT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE FOUCAULT A.FOUCAULT
      IMPLICIT   NONE
C     ------------------------------------------------------------------
C     CALCUL DE DERIVEE DE G PAR RAPPORT A SIGMA 
C     POUR G PLASTIQUE OU VISQUEUX
C     IN  NMAT   : DIMENSION TABLE PARAMETRES MATERIAU
C         MATERF : TABLE DES PARAMETRES MATERIAU A T+DT
C         PARA   : VECTEUR CONTENANT AXI, SXI ET MXI
C         VARA   : VECTEUR CONTENANT ADXI, BDXI, DDXI ET KDXI
C         DEVSIG : DEVIATEUR DES CONTRAINTES
C         I1     : TRACE DES CONTRAINTES
C         VAL    : BOOLEEN PRECISANT DILATANCE EN PRE(0) OU POST-PIC(1)
C         VECN   : VECTEUR UNITAIRE DE DILATANCE
C         DFDS   : DERIVEE SEUIL PAR RAPPORT A SIGMA
C         DS2HDS : DERIVEE DE SII*H PAR RAPPORT A SIGMA
C         BPRIMP : PARAMETRE DE DILATANCE FCTN SIGMA
C         NVI    : NOMBRE DE VARIABLES INTERNES
C         VINT   : VARIABLES INTERNES
C         DHDS   : DERIVEE DE H(THETA) PAR RAPPORT A SIGMA
C     OUT DGDS   : DERIVEE DU POTENTIEL G PAR RAPPORT A SIGMA (NDTXNDT)
C         IRET   : CODE RETOUR
C     ------------------------------------------------------------------
      INTEGER         IRET,NMAT,NVI,VAL
      REAL*8          MATERF(NMAT,2),DGDS(6,6),VECN(6),DFDS(6),VINT(NVI)
      REAL*8          PARA(3),VARA(4),DS2HDS(6),DEVSIG(6),I1,BPRIMP
      REAL*8          DHDS(6)
C
      INTEGER         I,J,NDI,NDT
      REAL*8          D2FDS2(6,6),D2FDSN(6)
      REAL*8          DFDNPN(6,6),DFPNDN(6,6)
      REAL*8          ZERO,DFDSDN(6),DFDSVN,D2SHDS(6,6),VARH(3)
      REAL*8          H0E,H0C,HTHETA,RCOS3T,COS3T,LGLEPS,PATM
      REAL*8          DNDSIG(6,6),D2FN2(6,6)
      PARAMETER       (ZERO  =  0.D0 )
      PARAMETER       (LGLEPS = 1.0D-8 )
C     ------------------------------------------------------------------
      COMMON /TDIM/   NDT,NDI
C     ------------------------------------------------------------------

      PATM = MATERF(1,2)

C --- APPEL A HOC ET  H(THETA) 
      RCOS3T = COS3T(DEVSIG,PATM,LGLEPS)
      CALL LKHTET(NMAT,MATERF,RCOS3T,H0E,H0C,HTHETA)
      VARH(1) = H0E
      VARH(2) = H0C
      VARH(3) = HTHETA

C --- CONSTRUCTION D2FDS2
      CALL LKD2SH(NMAT,MATERF,VARH,DHDS,DEVSIG,RCOS3T,D2SHDS,IRET)

      CALL LKD2FS(NMAT,MATERF,PARA,VARA,VARH,I1,DEVSIG,DS2HDS,
     &            D2SHDS,D2FDS2,IRET)

C --- CONSTRUCTION DNDSIG 
      CALL LKDNDS(NMAT,MATERF,I1,DEVSIG,BPRIMP,NVI,VINT,VAL,
     &            PARA,DNDSIG)

C --- CONSTRUCTION DE (D2FDS2:N)*N
      DO 10 I = 1, NDT
        D2FDSN(I) = ZERO
        DO 20 J = 1, NDT
          D2FDSN(I) = D2FDSN(I) + VECN(J)*D2FDS2(J,I)
  20    CONTINUE
  10  CONTINUE
      CALL LCPRTE(VECN,D2FDSN,D2FN2)

C --- CONSTRUCTION DE (DFDS*DNDSIG)*VECN
      DO 30 I = 1, NDT
        DFDSDN(I) = ZERO
        DO 40 J = 1, NDT
          DFDSDN(I) = DFDSDN(I)+DFDS(J)*DNDSIG(J,I)
  40    CONTINUE
  30  CONTINUE
      CALL LCPRTE(VECN,DFDSDN,DFDNPN)

C --- CONSTRUCTION DE (DFDS:VECN)*DNDSIG
      CALL LCPRSC(DFDS,VECN,DFDSVN)
      CALL LCPRSM(DFDSVN,DNDSIG,DFPNDN)

C --- CONSTRUCTION DE DGDS
      DO 50 I = 1, NDT
        DO 60 J = 1, NDT
          DGDS(I,J) = D2FDS2(I,J)-D2FN2(I,J)-DFDNPN(I,J)-DFPNDN(I,J)
  60    CONTINUE  
  50  CONTINUE

      END
