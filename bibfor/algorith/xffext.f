      SUBROUTINE XFFEXT(JINFO,NFON,NMAFON,LISTPT,PTEXTR,NBPTEX)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 07/01/2013   AUTEUR LADIER A.LADIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER      JINFO,NFON,NMAFON,NBPTEX
      CHARACTER*19 LISTPT,PTEXTR
C
C ----------------------------------------------------------------------
C
C ROUTINE XFEM
C
C              RECHERCHE DES POINTS EXTREMITES DU FOND DE FISSURE
C
C ----------------------------------------------------------------------
C
C
C IN  JINFO      : ADRESSE DU VECTEUR INFO DE LA SD
C IN  NFON       : NOMBRE DE POINTS AU FOND DE FISSURE
C IN  NMAFON     : NOMBRE DE MAILLES CONNECTEES AU FOND
C IN  LISTPT     : VECTEUR CONTENANT LES INDICES DES POINTS DU FOND PAR
C                  MAILLE

C IN/OUT PTEXTR  : VECTEUR CONTENANT LES INDICES DES POINTS EXTREMITES
C                  DU OU DES FONDS DE FISSURE
C OUT    NBPTEX  : NOMBRE D'EXTREMITES DU OU DES FONDS

C
      INTEGER       IMA,IPT,JLISTP,JPTEXT,NOCC,PTASSO
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C     
      CALL WKVECT(PTEXTR,'V V I',NFON,JPTEXT)

      CALL JEVEUO(LISTPT,'L',JLISTP)

      NBPTEX = 0

      DO 10 IPT=1,NFON
C       COMPTEUR DU NOMBRE D'OCCURENCES DU POINT IPT DANS LE TABLEAU
C       LISTPT
        NOCC   = 0
C       INDICE DU POINT ASSOCIE AU POINT IPT
        PTASSO = 0

        DO 11 IMA=1,NMAFON
          IF ((ZI(JLISTP-1+2*(IMA-1)+1).EQ.IPT) .AND.
     &         (ZI(JLISTP-1+2*(IMA-1)+2).NE.0) .AND.
     &        (PTASSO.NE.ZI(JLISTP-1+2*(IMA-1)+2) )) THEN

            PTASSO = ZI(JLISTP-1+2*(IMA-1)+2)
            NOCC = NOCC + 1

          ELSEIF ((ZI(JLISTP-1+2*(IMA-1)+2).EQ.IPT) .AND.
     &        (PTASSO.NE.ZI(JLISTP-1+2*(IMA-1)+1) )) THEN

            PTASSO = ZI(JLISTP-1+2*(IMA-1)+1)
            NOCC = NOCC + 1
          ENDIF

          IF (NOCC.EQ.2) GOTO 10

 11     CONTINUE

C       UNE SEULE OCCURENCE DU POINT IPT:
C       C'EST UN POINT EXTREMITE DU FOND DE FISSURE
        IF (NOCC.EQ.1) THEN
          NBPTEX = NBPTEX + 1
          ZI(JPTEXT-1+NBPTEX) = IPT
        ENDIF
 10   CONTINUE

C     ON DOIT AVOIR UN NOMBRE PAIR D'EXTREMITES
      CALL ASSERT(MOD(NBPTEX,2).EQ.0)

      ZK16(JINFO-1+3) = 'OUVERT'
C     CAS D'UN FOND FERME SI ABSCENCE D'EXTREMITES
      IF (NBPTEX.EQ.0)THEN
        ZK16(JINFO-1+3) = 'FERME'
        ZI(JPTEXT-1+1) = 1
      ENDIF

      CALL JEDEMA()
      END
