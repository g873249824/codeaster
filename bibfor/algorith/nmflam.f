      SUBROUTINE NMFLAM(OPTION,MODELE,NUMEDD,NUMFIX,CARELE,
     &                  COMPOR,SOLVEU,NUMINS,MATE  ,COMREF,
     &                  LISCHA,DEFICO,RESOCO,PARMET,FONACT,
     &                  CARCRI,ITERAT,SDDISC,SDTIME,SDDYNA,
     &                  SDPOST,VALINC,SOLALG,MEELEM,MEASSE,
     &                  VEELEM)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 21/02/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C TOLE CRP_21
C
      IMPLICIT NONE
      INTEGER      NUMINS,ITERAT
      REAL*8       PARMET(*)
      CHARACTER*16 OPTION
      CHARACTER*19 MEELEM(*)
      CHARACTER*24 RESOCO,DEFICO
      CHARACTER*19 LISCHA,SOLVEU,SDDISC,SDDYNA,SDPOST
      CHARACTER*24 MODELE,NUMEDD,NUMFIX,CARELE,COMPOR
      CHARACTER*19 VEELEM(*),MEASSE(*)
      CHARACTER*19 SOLALG(*),VALINC(*)
      CHARACTER*24 MATE,SDTIME
      CHARACTER*24 CARCRI,COMREF
      INTEGER      FONACT(*)
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME)
C
C CALCUL DE MODES
C      
C ----------------------------------------------------------------------
C      
C
C IN  OPTION : TYPE DE CALCUL
C              'FLAMBSTA' MODES DE FLAMBEMENT EN STATIQUE
C              'FLAMBDYN' MODES DE FLAMBEMENT EN DYNAMIQUE
C              'VIBRDYNA' MODES VIBRATOIRES
C IN  MODELE : MODELE
C IN  NUMEDD : NUME_DDL (VARIABLE AU COURS DU CALCUL)
C IN  NUMFIX : NUME_DDL (FIXE AU COURS DU CALCUL)
C IN  MATE   : CHAMP MATERIAU
C IN  CARELE : CARACTERISTIQUES DES ELEMENTS DE STRUCTURE
C IN  COMREF : VARI_COM DE REFERENCE
C IN  COMPOR : COMPORTEMENT
C IN  LISCHA : LISTE DES CHARGES
C IN  RESOCO : SD RESOLUTION CONTACT
C IN  DEFICO : SD DEF. CONTACT
C IN  SDDYNA : SD POUR LA DYNAMIQUE
C IN  PARMET : PARAMETRES DES METHODES DE RESOLUTION (VOIR NMLECT)
C IN  SOLVEU : SOLVEUR
C IN  CARCRI : PARAMETRES METHODES D'INTEGRATION LOCALES (VOIR NMLECT)
C IN  SDDISC : SD DISC_INST
C IN  NUMINS : NUMERO D'INSTANT
C IN  ITERAT : NUMERO D'ITERATION
C IN  VALINC : VARIABLE CHAPEAU POUR INCREMENTS VARIABLES
C IN  SOLALG : VARIABLE CHAPEAU POUR INCREMENTS SOLUTIONS
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      INTEGER      NFREQ,NFREQC
      INTEGER      I,ISLVK,LJEVEU,IBID,IRET
      INTEGER      DEFO,LDCCVG,NUMORD
      REAL*8       BANDE(2),R8BID,R8VIDE
      REAL*8       FREQM,FREQV,FREQA,FREQR,R8MAEM
      CHARACTER*4  MOD45
      CHARACTER*8  SDMODE
      CHARACTER*8  SYME
      CHARACTER*16 K16BID,OPTMOD,VARACC,TYPMAT
      CHARACTER*19 MATGEO,MATAS2,VECMOD,CHAMP
      CHARACTER*24 K24BID,DDLEXC
      INTEGER      NDDLE                  
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      CALL JEVEUO(SOLVEU(1:19)//'.SLVK','E',ISLVK)
      MATGEO = '&&NMFLAM.MAGEOM'      
      MATAS2 = '&&NMFLAM.MATASS'
C
C --- NOM DE LA SD DE STOCKAGE DES MODES
C
      SDMODE  = '&&NM45BI' 
C  
C --- RECUPERATION DES OPTIONS
C
      CALL NMFLAL(OPTION,COMPOR,SDPOST,MOD45  ,DEFO  ,
     &            NFREQ ,TYPMAT,OPTMOD,BANDE  ,NDDLE ,
     &            DDLEXC) 
C
C --- ON FORCE LA MATRICE TANGENTE EN SYMETRIQUE A CAUSE DE SORENSEN
C
      SYME   = ZK24(ISLVK+5-1)(1:8)
      ZK24(ISLVK+5-1) = 'OUI'    
C
C --- CALCUL DE LA MATRICE TANGENTE ASSEMBLEE ET DE LA MATRICE GEOM.
C
      CALL NMFLMA(TYPMAT,MOD45 ,DEFO  ,PARMET,MODELE,
     &            MATE  ,CARELE,SDDISC,SDDYNA,FONACT,
     &            NUMINS,ITERAT,VALINC,SOLALG,LISCHA,
     &            COMREF,DEFICO,RESOCO,SOLVEU,NUMEDD,
     &            NUMFIX,COMPOR,CARCRI,SDTIME,MEELEM,
     &            MEASSE,VEELEM,LDCCVG,MATAS2,MATGEO)
C
C --- RETABLISSEMENTS VALEURS
C 
      ZK24(ISLVK+5-1) = SYME(1:3)
C
C --- CALCUL DES MODES PROPRES
C    
C  ON DIFFERENCIE NFREQ (DONNEE UTILISATEUR) DE NFREQC
C  QUI EST LE NB DE FREQ TROUVEES PAR L'ALGO DANS NMOP45
C
      NFREQC = NFREQ
      CALL NMOP45(MATAS2,MATGEO,DEFO  ,OPTMOD,NFREQC,
     &            BANDE ,MOD45 ,DDLEXC,NDDLE ,SDMODE)
      IF (NFREQC.EQ.0) THEN
        FREQR  = R8VIDE()
        NUMORD = -1
        GOTO 999
      ENDIF
C
C --- SELECTION DU MODE DE PLUS PETITE FREQUENCE
C
      IF ( MOD45 .EQ. 'VIBR' ) THEN
        VARACC = 'FREQ'
      ELSEIF ( MOD45 .EQ. 'FLAM' ) THEN  
        VARACC = 'CHAR_CRIT'
      ELSE
        CALL ASSERT(.FALSE.)  
      ENDIF
      FREQM  = R8MAEM()
      NUMORD = 0
      DO 60 I = 1,NFREQC
        CALL RSADPA(SDMODE,'L',1,VARACC,I,0,LJEVEU,K16BID)
        FREQV = ZR(LJEVEU)
        FREQA = ABS(FREQV)
        IF (FREQA.LT.FREQM) THEN
          NUMORD = I
          FREQM  = FREQA
          FREQR  = FREQV
        END IF
   60 CONTINUE
C
C --- NOM DU MODE
C
      IF ( MOD45 .EQ. 'VIBR' ) THEN
        CALL NMLESD('POST_TRAITEMENT',SDPOST,'SOLU_MODE_VIBR',
     &              IBID             ,R8BID ,VECMOD)
      ELSEIF ( MOD45 .EQ. 'FLAM' ) THEN
        CALL NMLESD('POST_TRAITEMENT',SDPOST,'SOLU_MODE_FLAM',
     &              IBID             ,R8BID ,VECMOD)
      ELSE
        CALL ASSERT(.FALSE.)  
      ENDIF
      CALL RSEXCH(SDMODE,'DEPL',NUMORD,CHAMP ,IRET)
      CALL COPISD('CHAMP_GD','V',CHAMP,VECMOD)
C
C --- AFFICHAGE DES MODES
C
      IF ( MOD45 .EQ. 'VIBR' ) THEN
        CALL NMIMPR('IMPR','IMPR_MODE_VIBR',K24BID,FREQR ,NUMORD)
      ELSEIF ( MOD45 .EQ. 'FLAM' ) THEN 
        CALL NMIMPR('IMPR','IMPR_MODE_FLAM',K24BID,FREQR ,NUMORD)
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF     
C
C --- MODE SELECTIONNE ECRIT DANS SDPOST
C
 999  CONTINUE
C
      IF ( MOD45 .EQ. 'VIBR' ) THEN
        CALL NMECSD('POST_TRAITEMENT',SDPOST,'SOLU_FREQ_VIBR',
     &              IBID             ,FREQR ,K24BID) 
        CALL NMECSD('POST_TRAITEMENT',SDPOST,'SOLU_NUME_VIBR',
     &              NUMORD           ,R8BID ,K24BID)
        CALL NMECSD('POST_TRAITEMENT',SDPOST,'SOLU_MODE_VIBR',
     &              IBID             ,R8BID ,VECMOD)
      ELSEIF ( MOD45 .EQ. 'FLAM' ) THEN  
        CALL NMECSD('POST_TRAITEMENT',SDPOST,'SOLU_FREQ_FLAM',
     &              IBID             ,FREQR ,K24BID) 
        CALL NMECSD('POST_TRAITEMENT',SDPOST,'SOLU_NUME_FLAM',
     &              NUMORD           ,R8BID ,K24BID)
        CALL NMECSD('POST_TRAITEMENT',SDPOST,'SOLU_MODE_FLAM',
     &              IBID             ,R8BID ,VECMOD)      
      ELSE
        CALL ASSERT(.FALSE.)  
      ENDIF 
C
C --- DESTRUCTION DE LA SD DE STOCKAGE DES MODES
C
      CALL JEDETC('G',SDMODE,1)
      
      CALL JEDEMA()
      END
