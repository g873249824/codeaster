      SUBROUTINE ZEVOLU (CINE,Z,ZM,DINST,TP,K,N,TDEQ,TFEQ,
     &                    COEFFC,M,AR,BR,G,DG)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 22/07/2008   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================

      IMPLICIT NONE
      REAL*8        Z,ZM,DINST,TP
      REAL*8        K,N,TDEQ,TFEQ,COEFFC,M,AR,BR
      REAL*8        G,DG
      CHARACTER*4  CINE

C............................................
C CALCUL PHASE METALLURGIQUE POUR EDGAR
C CALCUL DE LA FONCTION G ET DE SA DERIVEE
C............................................

C IN   CINE     : CINTETIQUE A INTEGRER
C                 'CHAU' : CINETIQUE AU CHAUFFAGE
C                 'REFR' : CINETIQUE AU REFROIDISSEMENT
C IN   Z        : PROPORTION DE PHASE BETA
C IN   ZM       : PROPORTION DE PHASE BETA A INSTANT MOINS
C IN   DINST    : INCREMENT DE TEMPS ENTRE PLUS ET MOINS
C IN   TP       : TEMPERATURE
C IN   K,N      : PARAMETRES MATERIAU POUR MODELE A L EQUILIBRE
C IN   TDEQ     : TEMPERATURE A L EQUILIBRE DE DEBUT DE TRANSF.
C IN   TFEQ     : TEMPERATURE QUASISTATIQUE DE FIN DE TRANSF.
C IN   COEFFC,M : PARAMETRES MATERIAU POUR MODELE AU CHAUFFAGE
C IN   AR,BR    : PARAMETRES MATERIAU POUR MODELE AU REFROIDISSEMENT
C OUT  G        : LOI D EVOLUTION DE LA PROPORTION DE LA PHASE BETA
C OUT  DG       : DERIVEE DE LA FONCTION G PAR RAPPORT A Z

      REAL*8        TEQ,DVTEQ
      
C 1 - CALCUL DE LA TEMPERATURE A L EQUILIBRE POUR Z ET DE SA DERIVEE

      CALL TEMPEQ(Z,TDEQ,TFEQ,K,N,TEQ,DVTEQ)

C 2 - CALCUL DE LA FONCTION G ET DE SA DERIVEE

      IF (CINE .EQ. 'CHAU') THEN        
        G=Z-ZM-DINST*COEFFC*((ABS(TP-TEQ))**M)
        DG=M*DINST*COEFFC*((ABS(TP-TEQ))**(M-1.D0))*DVTEQ
        DG=1.D0+DG
      ELSE
        G=EXP(AR+BR*ABS(TP-TEQ))
        G=Z-ZM+DINST*ABS(TP-TEQ)*G*Z*(1.D0-Z)
        DG=1.D0+DINST*ABS(TP-TEQ)*EXP(AR+BR*ABS(TP-TEQ))*(1.D0-2.D0*Z)
        DG=DG+DINST*DVTEQ*Z*(1.D0-Z)*EXP(AR+BR*ABS(TP-TEQ))
     &       *(1.D0+BR*ABS(TP-TEQ))
      ENDIF        
            
      END
