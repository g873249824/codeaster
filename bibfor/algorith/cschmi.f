      SUBROUTINE CSCHMI(CA,NDIM,CVEC,CBAS,NDIMAX,NBBAS)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
C
C***********************************************************************
C    P. RICHARD                                 DATE 31/07/91
C-----------------------------------------------------------------------
C  BUT: < COMPLEXE ORTHOGONALISATION DE SCHMIDT >
C
C ORTHOGONALISER UN VECTEUR COMPLEXE A DES VECTEURS DE REFERENCE
C  PAR RAPPORT A UNE MATRICE COMPLEXES HERMITIENNE
C
C-----------------------------------------------------------------------
C
C CA       /I/: MATRICE CARRE COMPLEXE SERVANT DE NORME
C NDIM     /I/: DIMENSION DE LA MATRICE
C CVEC     /M/: VECTEUR A ORTHOGONALISER
C CBAS     /I/: MATRICE DES VECTEURS DE REFERENCE
C NBBAS    /I/: NOMBRE DES VECTEURS DE REFERENCE
C
C-----------------------------------------------------------------------
C
      COMPLEX*16 CA(*)
      COMPLEX*16 CBAS(NDIMAX,NBBAS)
      COMPLEX*16 CVEC(NDIM),CTRAV1,CTRAV2,CPROD,CCONJ,CMODU
      REAL*8  PREA,PIMA
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
C
C CAS OU IL N'Y A PAS DE VECTEUR DE REFERENCE
C
C-----------------------------------------------------------------------
      INTEGER I ,IC ,ICDIAG ,IL ,ILDIAG ,IRETOU ,J 
      INTEGER NBBAS ,NDIM ,NDIMAX 
C-----------------------------------------------------------------------
       IF(NBBAS.EQ.0) THEN
C
C   NORMALISATION
C
        CALL CVNORM(CA,CVEC,NDIM,IRETOU)
        IF(IRETOU.EQ.1) CALL U2MESS('F','ALGORITH2_22')

        GOTO 9999
      ENDIF
C
C       BOUCLE SUR LES VECTEURS DE REFERENCE
C
      DO 10 J=1,NBBAS
C
C   MULTIPLICATION ET CALCUL PRODUIT ET NORMES
C
        CPROD=DCMPLX(0.D0,0.D0)
        CMODU=DCMPLX(0.D0,0.D0)
        DO 20 IL=1,NDIM
          CTRAV1=DCMPLX(0.D0,0.D0)
          CTRAV2=DCMPLX(0.D0,0.D0)
          ILDIAG = IL*(IL-1)/2+1
          DO 30 IC=1,NDIM
            ICDIAG=IC*(IC-1)/2+1
            IF (IC.GE.IL) THEN
               CTRAV1=CTRAV1+(CA(ICDIAG+IC-IL)*CVEC(IC))
               CTRAV2=CTRAV2+(CA(ICDIAG+IC-IL)*CBAS(IC,J))
            ELSE
               CTRAV1=CTRAV1+(DCONJG(CA(ILDIAG+IL-IC))*CVEC(IC))
               CTRAV2=CTRAV2+(DCONJG(CA(ILDIAG+IL-IC))*CBAS(IC,J))
            ENDIF
 30       CONTINUE
          CALL ZCONJU(CBAS(IL,J),PREA,PIMA)
          CCONJ=DCMPLX(PREA,-PIMA)
          CPROD=CPROD+(CTRAV1*CCONJ)
          CMODU=CMODU+(CTRAV2*CCONJ)
 20     CONTINUE
C
C
C
C   ORTHOGONALISATION DE SCHMIT (LE POTE DE GRAM)
C
        DO 40 I=1,NDIM
          CVEC(I)=CVEC(I)-(CPROD*CBAS(I,J)/CMODU)
 40     CONTINUE
C
 10   CONTINUE
C
      CALL CVNORM(CA,CVEC,NDIM,IRETOU)
      IF(IRETOU.EQ.1) CALL U2MESS('F','ALGORITH2_22')

C
C
 9999 CONTINUE
      END
