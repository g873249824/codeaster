      SUBROUTINE LCUMEF(ICOU,DEP,AN,BN,CN,DEPSI,SIGI,NSTRS,DSIGT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/01/2003   AUTEUR YLEPAPE Y.LEPAPE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE YLEPAPE Y.LEPAPE
C
C ROUTINE APPELE DANS FLU
C LCUMEF     SOURCE    BENBOU   01/03/26
C
C_______________________________________________________________________
C
C ROUTINE QUI CALCUL L INCREMENT DE CONTRAINTES (ELASTIQUE)
C  CORRIGE PAR LE FLUAGE TOTAL (PROPRE + DESSICCATION)
C
C IN  ICOU     : COUPLAGE ENTRE CONTRAINTES ET MODELE DE FLUAGE 
C IN  DEP      : MATRICE ELASTIQUE DE HHOKE
C IN  AN       : PRISE EN COMPTE DES DEFORMATIONS DE FLUAGE (CF LCUMMD)
C IN  BN       : PRISE EN COMPTE DES DEFORMATIONS DE FLUAGE (CF LCUMMD)
C IN  CN       : PRISE EN COMPTE DES DEFORMATIONS DE FLUAGE (CF LCUMMD)
C IN  DEPSI    : INCREMENT DE DEFORMATION TOTALE
C IN  SIGI     : CONTRAINTES INITIALES
C IN  NSTRS    : DIMENSION DES VECTEURS CONTRAINTE ET DEFORMATION
C OUT DSIGT    : INCREMENT DE CONTRAINTES
C_______________________________________________________________________
C
      IMPLICIT REAL*8(A-H,O-Z)
C MODIFI DU 18 DECEMBRE 2002 - YLP SUPPRESSION DE LA DECLARATION DE
C LA VARIABLE ISING APPELEE DANS INVERMAT (RESORBEE PAR MGAUSS)
      INTEGER  I,ICOU,J,K,NSTRS
C MODIFI DU 6 JANVIER 2003 - YLP SUPPRESSION DES DECLARATIONS
C IMPLICITES DE TABLEAUX
C      REAL*8   AN(NSTRS),BN(NSTRS,NSTRS),CN(NSTRS,NSTRS)
C      REAL*8   DEP(NSTRS,NSTRS),SIGI(NSTRS),DSIGT(NSTRS),DEPSI(NSTRS)
      REAL*8   AN(6),BN(6,6),CN(6,6)
      REAL*8   DEP(6,6),SIGI(6),DSIGT(6),DEPSI(6)
C MODIFI DU 6 JANVIER 2002 - YLP SUPPRESSION DES DECLARATIONS
C IMPLICITES DES TABLEAUX
C      REAL*8   EFLU(NSTRS,NSTRS),DEFELA(NSTRS),TEMP(NSTRS,NSTRS)
C      REAL*8   DEFLUN(NSTRS),TEMP2(NSTRS,NSTRS)
      REAL*8   EFLU(6,6),DEFELA(6),TEMP(6,6)
      REAL*8   DEFLUN(6),TEMP2(6,6)
      LOGICAL  LGTEST
C
C INITIALISATION DES VARIABLES
C
      DO 11 I=1,NSTRS
        DSIGT(I)  = 0.D0
        DEFELA(I) = 0.D0
        DEFLUN(I) = 0.D0
        DO 12 J=1,NSTRS
          TEMP(I,J) = 0D0
          EFLU(I,J)  = 0D0
12      CONTINUE
11    CONTINUE
C
      IF (ICOU.EQ.1) THEN
        DO 5 I=1,NSTRS
          DO 5 J=1,NSTRS
            DO 5 K=1,NSTRS
              TEMP(I,J) = TEMP(I,J) + DEP(I,K) * CN(K,J)
  5     CONTINUE
C
C CONSTRUCTION DE LA MATRICE D ELASTICITE CORRIGE PAR LE FLUAGE
C
C  SI ICOU = 1
C
C  EQUATION : (3.1-2)
C
        DO 10 I=1,NSTRS
          TEMP(I,I) = 1.D0 + TEMP(I,I)
  10    CONTINUE
C EN COMMENTAIRES CI-DESSOUS, L'ANCIENNE VERSION UTILISANT UNE ROUTINE 
C D'INVERSION FOURNIE AVEC LE PAQUET DES SOURCES INITIALES. ELLE A ETE
C DEBRANCHEE AU PROFIT DE LA ROUTINE MGAUSS PRE-EXISTANT DANS CODE_ASTER
C        CALL LCUMIN(TEMP,NSTRS,ISING)
C        IF (ISING.EQ.1) THEN
C          CALL UTMESS ('F','LCUMFP','ERREUR DANS LE CALCUL DE LA 
C     &              MATRICE TANGENTE CORRIGEE PAR LE FLUAGE SOUS
C     &              ELAS_FO POUR LE FLUAGE PROPRE DANS LCUMEF --
C     &              MATRICE NON INVERSIBLE')
C        ENDIF
C
       DO 16 I=1,NSTRS
         DO 15 J=1,NSTRS
           IF (I.EQ.J) THEN
             TEMP2(I,J) = 1.D0
           ELSE
             TEMP2(I,J) = 0.D0
           ENDIF
 15      CONTINUE
 16    CONTINUE
C
       LGTEST = .TRUE.
C MODIFI DU 6 JANVIER - YLP      
C       CALL MGAUSS(TEMP, TEMP2, NSTRS, NSTRS, NSTRS, ZERO, LGTEST)
        CALL MGAUSS(TEMP, TEMP2, 6, NSTRS, NSTRS, ZERO, LGTEST)
       IF (.NOT.LGTEST) THEN
         CALL UTMESS ('F','LCUMFP','ERREUR DANS LE CALCUL DE LA 
     &          MATRICE TANGENTE CORRIGEE PAR LE FLUAGE SOUS
     &          ELAS_FO POUR LE FLUAGE PROPRE DANS LCUMEF --
     &          MATRICE NON INVERSIBLE')
       ENDIF

       DO 17 I=1,NSTRS
         DO 18 J=1,NSTRS
           TEMP(I,J) = TEMP2(I,J)
 18      CONTINUE
 17    CONTINUE
C
C CALCUL DE (1 + CN)-1 * E0
C
        DO 20 I=1,NSTRS
          DO 20 J=1,NSTRS
            DO 20 K=1,NSTRS
              EFLU(I,J) = EFLU(I,J) + TEMP(I,K)*DEP(K,J)
  20    CONTINUE
C
        DO 30 I=1,NSTRS
          DO 30 J=1,NSTRS
            DEP(I,J) = EFLU(I,J)
  30    CONTINUE
      ENDIF
C
C CALCUL DES DEFORMATIONS ELASTIQUES
C
      DO 40 I=1,NSTRS
        DEFLUN(I) = DEFLUN(I) + AN(I)
        DO 40 J=1,NSTRS
          DEFLUN(I) = DEFLUN(I) + 
     &                (BN(I,J)+CN(I,J)) * SIGI(J)
  40  CONTINUE
C
      DO 50 I=1,NSTRS
        DEFELA(I) = DEPSI(I) - DEFLUN(I)
  50  CONTINUE
C
C CALCUL DE L INCREMENT DE CONTRAINTES ELASTIQUES
C
C  EQUATION (3.1-3)
C
      DO 60 I=1,NSTRS
        DO 60 J=1,NSTRS
          DSIGT(I) = DSIGT(I) + DEP(I,J) * DEFELA(J)
  60  CONTINUE
C
      END
