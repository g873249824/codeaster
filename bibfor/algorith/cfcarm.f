      SUBROUTINE CFCARM(NOMA  ,DEFICO,NEWGEO,POSMAI,TYPMAI,
     &                  NUMMAI,ALIAS ,NOMMAI,NDIM  ,NNOMAM,
     &                  COORMA)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      CHARACTER*8  NOMA,ALIAS
      CHARACTER*24 DEFICO
      CHARACTER*19 NEWGEO
      INTEGER      POSMAI,NUMMAI
      INTEGER      NNOMAM,NDIM
      REAL*8       COORMA(27)
      CHARACTER*8  NOMMAI
      CHARACTER*4  TYPMAI
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES MAILLEES - UTILITAIRE)
C
C CARACTERISTIQUES DE LA MAILLE
C
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE CONTACT (DEFINITION)
C IN  NEWGEO : COORDONNEES REACTUALISEES DES NOEUDS DU MAILLAGE
C IN  POSMAI : INDICE DE LA MAILLE (DANS SD CONTACT)
C OUT TYPMAI : TYPE DE LA MAILLE (MAITRE OU ESCLAVE)
C OUT NUMMAI : INDICE ABSOLU DE LA MAILLE (DANS SD MAILLAGE)
C OUT NNOMAM : NOMBRE DE NOEUDS DE LA MAILLE (AU SENS DES SD CONTACT)
C OUT NDIM   : DIMENSION DE LA MAILLE
C OUT ALIAS  : TYPE GEOMETRIQUE DE LA MAILLE
C OUT NOMMAI : NOM DE LA MAILLE
C OUT COORMA : COORDONNEES DES NOEUDS DE LA MAILLE
C
C
C
C
      INTEGER      NBNMAX
      PARAMETER   (NBNMAX = 9)
C
      INTEGER      NO(NBNMAX)
      INTEGER      INO,I,IBID
      INTEGER      JDEC,JCOOR
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ ()
C
C --- RECUPERATION DE QUELQUES DONNEES
C
      CALL JEVEUO(NEWGEO(1:19)//'.VALE','L',JCOOR)
C
C --- INITIALISATIONS
C
      DO 7 I = 1,27
        COORMA(I) = 0.D0
 7    CONTINUE
C
C --- TYPE DE LA MAILLE
C
      CALL CFTYPM(DEFICO,POSMAI,TYPMAI)
C
C --- NUMERO ABSOLU DE LA MAILLE
C
      CALL CFNUMM(DEFICO,1     ,POSMAI,NUMMAI)
C
C --- NOMBRE DE NOEUDS DE LA MAILLE
C
      CALL CFNBEN(DEFICO,POSMAI,'CONNEX',NNOMAM,IBID  )
      IF (NNOMAM.GT.NBNMAX) THEN
        CALL ASSERT(.FALSE.)
      END IF
C
C --- TYPE DE LA MAILLE
C
      CALL MMTYPM(NOMA  ,NUMMAI,NNOMAM,ALIAS ,NDIM  )
C
C --- NUMEROS ABSOLUS DES NOEUDS DE LA MAILLE
C
      CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',NUMMAI),'L',JDEC)
      DO 61 INO = 1,NNOMAM
        NO(INO)     = ZI(JDEC+INO-1)
 61   CONTINUE
C
C --- COORDONNEES DES NOEUDS DE LA MAILLE
C
      DO 70 INO = 1,NNOMAM
        COORMA(3*(INO-1)+1) = ZR(JCOOR+3*(NO(INO)-1))
        COORMA(3*(INO-1)+2) = ZR(JCOOR+3*(NO(INO)-1)+1)
        COORMA(3*(INO-1)+3) = ZR(JCOOR+3*(NO(INO)-1)+2)
  70  CONTINUE
C
C --- NOM DE LA MAILLE
C
      CALL JENUNO(JEXNUM(NOMA//'.NOMMAI',NUMMAI),NOMMAI)
C
      CALL JEDEMA()
C
      END
