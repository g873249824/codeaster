      SUBROUTINE CFCARM(NOMA  ,DEFICO,NEWGEO,POSMAI,NUMMAI,
     &                  ALIAS ,NOMMAI,NDIM  ,NBNO  ,COORMA)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 22/04/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  NOMA,ALIAS
      CHARACTER*24 DEFICO,NEWGEO
      INTEGER      POSMAI,NUMMAI
      INTEGER      NBNO,NDIM
      REAL*8       COORMA(27)
      CHARACTER*8  NOMMAI
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE DISCRETE - APPARIEMENT - UTILITAIRE)
C
C CARACTERISTIQUES DE LA MAILLE 
C
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE CONTACT (DEFINITION)
C IN  NEWGEO : COORDONNEES REACTUALISEES DES NOEUDS DU MAILLAGE
C IN  POSMAI : INDICE DE LA MAILLE MAITRE (DANS SD CONTACT)
C OUT NUMMAI : INDICE ABSOLU DE LA MAILLE (DANS SD MAILLAGE)
C OUT NBNO   : NOMBRE DE NOEUDS DE LA MAILLE 
C OUT NDIM   : DIMENSION DE LA MAILLE 
C OUT ALIAS  : TYPE DE LA MAILLE 
C OUT NOMMAI : NOM DE LA MAILLE
C OUT COORMA : COORDONNEES DES NOEUDS DE LA MAILLE 
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32       JEXNUM
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      INO,NO(9),I,JDEC,NNO
      CHARACTER*24 CONTMA,PNOMA,NOMACO,CONTNO
      INTEGER      JMACO,JPONO,JNOMA,JNOCO,JCOOR  
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ ()
C      
C --- RECUPERATION DE QUELQUES DONNEES      
C
      CONTMA = DEFICO(1:16)//'.MAILCO'
      PNOMA  = DEFICO(1:16)//'.PNOMACO'
      NOMACO = DEFICO(1:16)//'.NOMACO'
      CONTNO = DEFICO(1:16)//'.NOEUCO'
C
      CALL JEVEUO(CONTMA,'L',JMACO)
      CALL JEVEUO(PNOMA, 'L',JPONO)
      CALL JEVEUO(NOMACO,'L',JNOMA)
      CALL JEVEUO(CONTNO,'L',JNOCO)
      CALL JEVEUO(NEWGEO(1:19)//'.VALE','L',JCOOR)
C
C --- INITIALISATIONS
C      
      DO 7 I = 1,27
        COORMA(I) = 0.D0
 7    CONTINUE       
C
C --- NUMERO ABSOLU DE LA MAILLE MAITRE ET NOMBRE DE NOEUDS
C
      IF (POSMAI.EQ.0) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
      NUMMAI = ZI(JMACO+POSMAI-1)
      NBNO   = ZI(JPONO+POSMAI) - ZI(JPONO+POSMAI-1)
      IF (NBNO .GT.9) THEN
        CALL ASSERT(.FALSE.)
      END IF
C
C --- TYPE DE LA MAILLE MAITRE
C
      CALL MMELTY(NOMA  ,NUMMAI,ALIAS ,NNO ,NDIM)  
C
C --- CAS DES COQUES_3D: PAS DE DX/DY/DZ SUR NOEUD MILIEU
C      
      IF ((NNO.EQ.9).AND.(NBNO.EQ.8)) THEN
        ALIAS = 'QU8'  
      ENDIF    
      IF ((NNO.EQ.7).AND.(NBNO.EQ.6)) THEN
        ALIAS = 'TR6'  
      ENDIF  
C
C --- CAS DES QUAD8 EN 3D: 4 NOEUDS ET RELATIONS LINEAIRES 
C       
      IF ((NNO.EQ.8).AND.(NBNO.EQ.4)) THEN
        ALIAS = 'QU4'
      ENDIF       
C
C --- NUMEROS ABSOLUS DES NOEUDS DE LA MAILLE MAITRE
C
      CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',NUMMAI),'L',JDEC)
      DO 61 INO = 1,NBNO 
        NO(INO)     = ZI(JDEC+INO-1)
 61   CONTINUE 
C
C --- COORDONNEES DES NOEUDS DE LA MAILLE MAITRE
C
      DO 70 INO = 1,NBNO
        COORMA(3*(INO-1)+1) = ZR(JCOOR+3*(NO(INO)-1))
        COORMA(3*(INO-1)+2) = ZR(JCOOR+3*(NO(INO)-1)+1)
        COORMA(3*(INO-1)+3) = ZR(JCOOR+3*(NO(INO)-1)+2)      
  70  CONTINUE
C
C --- NOM DE LA MAILLE
C
      CALL JENUNO(JEXNUM(NOMA//'.NOMMAI',NUMMAI),NOMMAI)   
C
      CALL JEDEMA()
C 
      END
