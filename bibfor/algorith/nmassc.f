      SUBROUTINE NMASSC (ICODMA,COELMA,COELEM,COEVIL,ICAS)
C ----------------------------------------------------------------------
      IMPLICIT NONE
      INTEGER    ICODMA, ICAS
      INTEGER    NBCLMA, NBCLEM, NBCVIL
      PARAMETER (NBCLMA=12, NBCLEM=7, NBCVIL=4)
      REAL*8     COELMA(NBCLMA),COELEM(NBCLEM),COEVIL(NBCVIL)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 02/03/2004   AUTEUR JMBHH01 J.M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C     RECUPERATION DES CARACTERISTIQUES DU MATERIAU POUR LES ELEMENTS
C     DE POUTRE DANS LE CAS DES COMPORTEMENTS D'ASSEMBLAGE DE
C     COMBUSTIBLE.
C ----------------------------------------------------------------------
C
C IN :
C     ICODMA : NUMERO DU MATERIAU CODE
C
C OUT :
C     COELMA : COEFFICIENTS CONSTANTS POUR LA LOI DE FLUAGE DE LMARC
C     COELEM : COEFFICIENTS CONSTANTS POUR LA LOI DE FLUAGE DE LEMAITRE
C     COEVIL : COEFFICIENTS CONSTANTS POUR LA LOI DE VISC_IRRA_LOG
C     ICAS   : FLAG MATERIAU
C         1 LOI DE TYPE LMARC
C         2 LOI DE TYPE LEMAITRE AVEC FLUAGE SOUS IRRADIATION
C         3 LOI DE TYPE VISC_IRRA_LOG
C
C *************** DECLARATION DES VARIABLES LOCALES ********************
C
      INTEGER      NBPAR, NBRES
      REAL*8       VALPAR
      CHARACTER*2  BL2
      CHARACTER*8  NOMPAR
      REAL*8       VALLMA(NBCLMA),VALLEM(NBCLEM),VALVIL(NBCVIL)
      CHARACTER*2  CODLMA(NBCLMA),CODLEM(NBCLEM),CODVIL(NBCVIL)
      CHARACTER*8  NOMLMA(NBCLMA),NOMLEM(NBCLEM),NOMVIL(NBCVIL)    
C
C *********** FIN DES DECLARATIONS DES VARIABLES LOCALES ***************
C
C ****************************** DATA  *********************************
C
      DATA NOMLMA / 'K','N','DE_0','P','P1','P2','M','RM',
     &              'A_0','Y_0','Y_I','B'/
      DATA NOMLEM / 'N', 'UN_SUR_K', 'UN_SUR_M', 'QSR_K',
     &              'BETA','PHI_ZERO','L'/     
      DATA NOMVIL / 'A', 'B', 'CSTE_TPS', 'ENER_ACT'/
     
C
C ********************* DEBUT DE LA SUBROUTINE *************************
C
C --- INITIALISATIONS
C
      CALL R8INIR (NBCLMA,0.D0,COELMA,1)
      CALL R8INIR (NBCLEM,0.D0,COELEM,1)
      CALL R8INIR (NBCVIL,0.D0,COEVIL,1)
      
      ICAS = 0
C
      BL2 = '  '
C
      CALL R8INIR (NBCLMA,0.D0,VALLMA,1)
      CALL R8INIR (NBCLEM,0.D0,VALLEM,1)
      CALL R8INIR (NBCVIL,0.D0,VALVIL,1)     
C
C--  FLUAGE DES CRAYONS ZIRCALOY AVEC LOI DU LMARC
C    ET FLUAGE DES TUBES GUIDES AVEC LOI DE LEMAITRE
C
      NBPAR  = 0
      NOMPAR = ' '
      VALPAR = 0.D0
C
      NBRES = 12
      CALL RCVALA ( ICODMA,'LMARC',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMLMA(1),VALLMA(1),CODLMA(1), BL2 )
      NBRES = 3
      CALL RCVALA ( ICODMA,'LEMAITRE',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMLEM(1),VALLEM(1),CODLEM(1), BL2 )
      NBRES = 4
      CALL RCVALA ( ICODMA,'FLU_IRRA',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMLEM(4),VALLEM(4),CODLEM(2), BL2 )
      NBRES = 4
      CALL RCVALA ( ICODMA,'VISC_IRRA_',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMVIL(1),VALVIL(1),CODVIL(1), BL2 )
C          
      IF (CODLMA(1).EQ.'OK'.AND.
     &    CODLEM(1).NE.'OK'.AND.
     &    CODVIL(1).NE.'OK') THEN
C        LOI DE TYPE LMARC      
         CALL R8COPY (NBCLMA,VALLMA,1,COELMA,1)
         ICAS = 1
      ELSEIF (CODLMA(1).NE.'OK'.AND.
     &    CODLEM(1).EQ.'OK'.AND.
     &    CODVIL(1).NE.'OK') THEN
C        LOI DE LEMAITRE AVEC FLUAGE SOUS IRRADIATION     
         CALL R8COPY (NBCLEM,VALLEM,1,COELEM,1)
         ICAS = 2
      ELSEIF (CODLMA(1).NE.'OK'.AND.
     &    CODLEM(1).NE.'OK'.AND.
     &    CODVIL(1).EQ.'OK') THEN
C        LOI DE VISC_IRRA_LOG      
         CALL R8COPY (NBCVIL,VALVIL,1,COEVIL,1)
         ICAS = 3        
      ELSE
         CALL UTMESS ('F','NMASSC',
     &  'DEFINITION MULTIPLE DU COMPORTEMENT POUR UN ELEMENT DE POUTRE')
      ENDIF
      END
