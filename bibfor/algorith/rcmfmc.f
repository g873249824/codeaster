      SUBROUTINE RCMFMC ( CHMAT , MATE )
      IMPLICIT   NONE
      CHARACTER*(*)       CHMAT , MATE
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 30/08/2005   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     CREATION DE LA CARTE DU MATERIAU CODE A PARTIR DU CHAMP_MATER
C
C IN  CHMAT  :  NOM UTILISATEUR DU CHAMP_MATER
C VAR MATE    : NOM DE LA CARTE DE MATERIAU CODE
C ----------------------------------------------------------------------
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
      CHARACTER*32       JEXNUM , JEXNOM  , JEXATR
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
      INTEGER NBCVRC,JVCNOM
      COMMON /CAII14/NBCVRC,JVCNOM

      INTEGER        NBVAL, JCART, IRET, IRETC, JVALE, IGD, JDESC, KK
      INTEGER        NBGRP, I, ICOMPT, IGRP, INGRP,NBCMP,IER,J,K,NBMAT
      INTEGER        INBMAT,ISMAEM
      CHARACTER*4    KNUMAT,KBID
      CHARACTER*8    K8B, MATERI,NOMGD
      CHARACTER*19   CODI, CH19
      CHARACTER*24   CH24
C     ------------------------------------------------------------------
C
      CALL JEMARQ()
C
      MATERI = CHMAT(1:8)
      CH24 = MATERI//'.CHAMP_MAT'
      MATE = MATERI//'.MATE_CODE'
      CALL JELIRA ( CH24(1:19)//'.VALE', 'LONMAX', NBVAL, K8B )
      CALL JEVEUO ( CH24(1:19)//'.VALE', 'L', JCART )

      CALL JEEXIN ( MATE(1:19)//'.VALE', IRET )
      IF ( IRET .NE. 0 ) THEN
        CH19 = ZK8(JCART)
        CALL JEEXIN ( CH19//'.CODI', IRETC )
        IF ( IRETC .NE. 0 ) GOTO 9999
      ENDIF

C     -- MISE A JOUR DU COMON CAII13 POUR RCVARC :
      CALL JEEXIN(MATERI//'.CVRCNOM',IRET)
      IF ( IRET .NE. 0 ) THEN
        CALL JEVEUT(MATERI//'.CVRCNOM','L',JVCNOM)
        CALL JELIRA(MATERI//'.CVRCNOM','LONMAX',NBCVRC,KBID)
      ELSE
        NBCVRC=0
        JVCNOM=ISMAEM()
      END IF
C
      CALL JEVEUO ( CH24(1:19)//'.DESC', 'L', JDESC )
      CALL JENUNO(JEXNUM('&CATA.GD.NOMCMP',ZI(JDESC)),NOMGD)
      CALL DISMOI('F','NB_CMP_MAX',NOMGD,'GRANDEUR',NBCMP,K8B,IER)
      CALL ASSERT((NBVAL/NBCMP)*NBCMP.EQ.NBVAL)
C
      CALL COPISD ( 'CHAMP_GD', 'V', CH24(1:19), MATE(1:19) )
      CALL JEDETR ( MATE(1:19)//'.VALE' )
      NBGRP=NBVAL/NBCMP
      CALL WKVECT ( MATE(1:19)//'.VALE', 'V V I', NBGRP, JVALE )
      CALL JENONU ( JEXNOM('&CATA.GD.NOMGD','ADRSJEVE'), IGD )
      CALL JEVEUO ( MATE(1:19)//'.DESC', 'E', JDESC )
      ZI(JDESC) = IGD
C
C     --- CODAGE DU MATERIAU ---
C
      ICOMPT=0
      DO 9 I=1,NBVAL
         IF (ZK8(JCART-1+I).NE.' ') ICOMPT=ICOMPT+1
  9   CONTINUE
      CALL ASSERT(ICOMPT.GT.0)

      CALL JEEXIN (MATERI//'.MATE_CODE.GRP' , IRET )
      IF ( IRET .NE. 0 ) THEN
         CALL JEDETR (MATERI//'.MATE_CODE.GRP' )
      ENDIF
      CALL JEEXIN (MATERI//'.MATE_CODE.NGRP', IRET )
      IF ( IRET .NE. 0 ) THEN
         CALL JEDETR (MATERI//'.MATE_CODE.NGRP')
      ENDIF
      CALL WKVECT(MATERI//'.MATE_CODE.GRP' ,'V V K8',ICOMPT,IGRP)
      CALL WKVECT(MATERI//'.MATE_CODE.NGRP','V V I',NBGRP,INGRP)

      ICOMPT=0
      INBMAT=0
      DO 11 I=1,NBGRP
         DO 12 J=1,NBCMP
            K=(I-1)*NBCMP+J
            IF (ZK8(JCART-1+K).NE.' ') THEN
               ZK8(IGRP+ICOMPT)=ZK8(JCART-1+K)
               ICOMPT=ICOMPT+1
               INBMAT=INBMAT+1
            ENDIF
  12     CONTINUE
         ZI(INGRP-1+I)=INBMAT
         INBMAT=0
  11  CONTINUE

      CODI = ' '
      CALL JEVEUO(MATERI//'.MATE_CODE.GRP' ,'L',IGRP)
      CALL JEVEUO(MATERI//'.MATE_CODE.NGRP','L',INGRP)
      ICOMPT=0
      DO 10 KK = 1,NBGRP
         NBMAT=ZI(INGRP-1+KK)
         IF (NBMAT.EQ.0) GO TO 10
         CALL RCMACO ( CHMAT(1:8), ICOMPT,NBMAT, KK )
         CALL CODENT ( KK, 'D0', KNUMAT )

C  LE NOM DU CODI EST CELUI DU PREMIER MATERIAU DU GROUPE KK

         CODI(1:8)  = ZK8(IGRP+ICOMPT)
         CODI(9:13) = '.'//KNUMAT
         CALL JEVEUO ( CODI//'.CODI', 'L', ZI(JVALE+KK-1) )
         ICOMPT=ICOMPT+NBMAT
 10   CONTINUE
C
 9999 CONTINUE
C
      CALL JEDEMA()
      END
