      SUBROUTINE RCMFMC(CHMATZ,CHMACZ)
      IMPLICIT   NONE
      CHARACTER*(*) CHMATZ,CHMACZ
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 14/04/2008   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     CREATION DE LA CARTE DU MATERIAU CODE A PARTIR DU CHAMP_MATER
C
C IN  CHMATZ    : CHAM_MATER
C OUT CHMACZ    : CARTE DE MATERIAU CODE
C ----------------------------------------------------------------------
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
      INTEGER NBCVRC,JVCNOM
      COMMON /CAII14/NBCVRC,JVCNOM

      INTEGER NBVAL,JCHEV,IRET,IRETC,JVALE,IGD,JDESC,KK
      INTEGER NBGRP,I,ICOMPT,IGRP,INGRP,NBCMP,IER,J,K,NBMAT
      INTEGER INBMAT,ISMAEM,JCHNV,JDEJAV,KGRP
      CHARACTER*4 KNUMAT,KBID
      CHARACTER*8 K8B,CHMAT,NOMGD,MAT1
      CHARACTER*19 CODI,CH19
      CHARACTER*19 CHEMAT,CHNMAT,CHMACN,CHMACE
      LOGICAL LCHN
C     ------------------------------------------------------------------
C
      CALL JEMARQ()
C     CALL IMPTOU(' ','ALL','ALL')
C
      CHMAT=CHMATZ
      CHEMAT=CHMAT//'.CHAMP_MAT'
      CHNMAT=CHMAT//'.CHAMP_MATN'
      CHMACE=CHMAT//'.MATE_CODE'
      CHMACN=CHMAT//'.MATN_CODE'

      CALL JELIRA(CHEMAT//'.VALE','LONMAX',NBVAL,K8B)
      CALL JEVEUO(CHEMAT//'.VALE','L',JCHEV)
      CALL JEEXIN(CHNMAT//'.VALE',IRET)
      LCHN=IRET .GT. 0


C     -- SI CHMACE EXISTE, C'EST QUE L'ON A DEJA APPELE
C        LA ROUTINE RCMFMC. TOUT EST DEJA FAIT :
C     ----------------------------------------------------------
      CALL EXISD('CARTE',CHMACE,IRET)
      IF (IRET.GT.0) GOTO 90


C     -- MISE A JOUR DU COMON CAII13 POUR RCVARC :
C     -----------------------------------------------
      CALL JEEXIN(CHMAT//'.CVRCNOM',IRET)
      IF (IRET.NE.0) THEN
        CALL JEVEUT(CHMAT//'.CVRCNOM','L',JVCNOM)
        CALL JELIRA(CHMAT//'.CVRCNOM','LONMAX',NBCVRC,KBID)
      ELSE
        NBCVRC=0
        JVCNOM=ISMAEM()
      ENDIF


C     -- TRAITEMENT DU MATERIAU PAR ELEMENTS :
C     -----------------------------------------------
      CALL JEVEUO(CHEMAT//'.DESC','L',JDESC)
      CALL JENUNO(JEXNUM('&CATA.GD.NOMCMP',ZI(JDESC)),NOMGD)
      CALL DISMOI('F','NB_CMP_MAX',NOMGD,'GRANDEUR',NBCMP,K8B,IER)
      CALL ASSERT(NBCMP.GE.30)
      CALL ASSERT((NBVAL/NBCMP)*NBCMP.EQ.NBVAL)

      CALL COPISD('CHAMP_GD','V',CHEMAT,CHMACE)
      CALL JEDETR(CHMACE//'.VALE')
      NBGRP=NBVAL/NBCMP
      CALL WKVECT(CHMACE//'.VALE','V V I',NBGRP,JVALE)
      CALL JENONU(JEXNOM('&CATA.GD.NOMGD','ADRSJEVE'),IGD)
      CALL JEVEUO(CHMACE//'.DESC','E',JDESC)
      ZI(JDESC)=IGD


C     --- CODAGE DU MATERIAU ---
C     -------------------------------------------------
      ICOMPT=0
      DO 10 I=1,NBVAL
        IF (ZK8(JCHEV-1+I).NE.' ')ICOMPT=ICOMPT+1
   10 CONTINUE
      CALL ASSERT(ICOMPT.GT.0)

      CALL JEDETR(CHMAT//'.MATE_CODE.GRP')
      CALL JEDETR(CHMAT//'.MATE_CODE.NGRP')
      CALL WKVECT(CHMAT//'.MATE_CODE.GRP','V V K8',ICOMPT,IGRP)
      CALL WKVECT(CHMAT//'.MATE_CODE.NGRP','V V I',NBGRP,INGRP)

      ICOMPT=0
      INBMAT=0
      DO 40 I=1,NBGRP
C        -- IL NE PEUT PAS Y AVOIR PLUS DE 28 MATERIAUX
        DO 20 J=1,28
          K=(I-1)*NBCMP+J
          IF (ZK8(JCHEV-1+K).EQ.'TREF=>')GOTO 30
          IF (ZK8(JCHEV-1+K).NE.' ') THEN
            ZK8(IGRP+ICOMPT)=ZK8(JCHEV-1+K)
            ICOMPT=ICOMPT+1
            INBMAT=INBMAT+1
          ENDIF
   20   CONTINUE
   30   CONTINUE
        ZI(INGRP-1+I)=INBMAT
        INBMAT=0
   40 CONTINUE

      CODI=' '
      CALL JEVEUO(CHMAT//'.MATE_CODE.GRP','L',IGRP)
      CALL JEVEUO(CHMAT//'.MATE_CODE.NGRP','L',INGRP)
      ICOMPT=0
      DO 50 KK=1,NBGRP
        NBMAT=ZI(INGRP-1+KK)
        IF (NBMAT.EQ.0)GOTO 50
        CALL RCMACO(CHMAT(1:8),ICOMPT,NBMAT,KK,'E')
        CALL CODENT(KK,'D0',KNUMAT)

C  LE NOM DU CODI EST CELUI DU PREMIER MATERIAU DU GROUPE KK

        CODI(1:8)=ZK8(IGRP+ICOMPT)
        CODI(9:13)='.'//KNUMAT
        CALL JEVEUO(CODI//'.CODI','L',ZI(JVALE+KK-1))
        ICOMPT=ICOMPT+NBMAT
   50 CONTINUE


C     -- TRAITEMENT DU MATERIAU PAR NOEUDS S'IL EXISTE :
C     ---------------------------------------------------
      IF (.NOT.LCHN)GOTO 90

      CALL JEVEUO(CHNMAT//'.VALE','L',JCHNV)
      CALL JELIRA(CHNMAT//'.VALE','LONMAX',NBVAL,K8B)
      CALL JEVEUO(CHNMAT//'.DESC','L',JDESC)

      CALL COPISD('CHAMP_GD','V',CHNMAT,CHMACN)
      CALL JEDETR(CHMACN//'.VALE')
      CALL WKVECT(CHMACN//'.VALE','V V I',NBVAL,JVALE)
      CALL JENONU(JEXNOM('&CATA.GD.NOMGD','ADRSJEVE'),IGD)
      CALL JEVEUO(CHMACN//'.DESC','E',JDESC)
      ZI(JDESC)=IGD


C     --- CODAGE DU MATERIAU ---
C     -------------------------------------------------

C     -- ON COMPTE LES MATERIAUX DIFFERENTS :
      CALL JECREO('&&RCMFMC.PNDIFF','V N K8')
      CALL JEECRA('&&RCMFMC.PNDIFF','NOMMAX',NBVAL,KBID)
      DO 60 K=1,NBVAL
        MAT1=ZK8(JCHNV-1+K)
        IF (MAT1.EQ.' ')GOTO 60

        CALL JENONU(JEXNOM('&&RCMFMC.PNDIFF',MAT1),KGRP)
        IF (KGRP.EQ.0) THEN
          CALL JECROC(JEXNOM('&&RCMFMC.PNDIFF',MAT1))
        ENDIF
   60 CONTINUE
      CALL JELIRA('&&RCMFMC.PNDIFF','NOMUTI',NBGRP,KBID)

      CALL JEDETR(CHMAT//'.MATN_CODE.GRP')
      CALL JEDETR(CHMAT//'.MATN_CODE.NGRP')
      CALL WKVECT(CHMAT//'.MATN_CODE.GRP','V V K8',NBGRP,IGRP)
      CALL WKVECT(CHMAT//'.MATN_CODE.NGRP','V V I',NBVAL,INGRP)

      DO 70 K=1,NBVAL
        MAT1=ZK8(JCHNV-1+K)
        IF (MAT1.EQ.' ')GOTO 70
        CALL JENONU(JEXNOM('&&RCMFMC.PNDIFF',MAT1),KGRP)
        CALL ASSERT(KGRP.GT.0)
        ZK8(IGRP-1+KGRP)=MAT1
        ZI(INGRP-1+K)=1
   70 CONTINUE

      CALL WKVECT('&&RCMFMC.DEJAVU','V V I',NBGRP,JDEJAV)
      CODI=' '
      DO 80 K=1,NBVAL
        MAT1=ZK8(JCHNV-1+K)
        NBMAT=ZI(INGRP-1+K)
        IF (NBMAT.EQ.0)GOTO 80

        CALL JENONU(JEXNOM('&&RCMFMC.PNDIFF',MAT1),KGRP)
        CALL ASSERT(KGRP.GT.0 .AND. KGRP.LE.NBGRP)
        IF (ZI(JDEJAV-1+KGRP).EQ.0) THEN
          CALL RCMACO(CHMAT(1:8),KGRP-1,1,KGRP,'N')
          CALL CODENT(KGRP,'D0',KNUMAT)
          CODI(1:8)=ZK8(IGRP-1+KGRP)
          CODI(9:13)='.'//KNUMAT
          CALL JEVEUO(CODI//'.CODI','L',ZI(JVALE+K-1))
          ZI(JDEJAV-1+KGRP)=ZI(JVALE+K-1)
        ELSE
          ZI(JVALE+K-1)=ZI(JDEJAV-1+KGRP)
        ENDIF

   80 CONTINUE
      CALL JEDETR('&&RCMFMC.PNDIFF')
      CALL JEDETR('&&RCMFMC.DEJAVU')

   90 CONTINUE
      CHMACZ=CHMACE
      CALL JEDEMA()

      END
