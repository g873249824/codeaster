      SUBROUTINE ORTPRO(NOMRES,CLASSE,BASMOD,NOMMAT,TYPMAT)
      IMPLICIT REAL*8 (A-H,O-Z)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 06/04/2007   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C***********************************************************************
C  P. RICHARD     DATE 23/05/91
C-----------------------------------------------------------------------
C
C  BUT : < PROJECTION MATRICE SUR BASE MODALE ORTHOGONALE >
C
C        CONSISTE A PROJETER UNE MATRICE ASSSEMBLEE SUR UNE BASE MODALE
C        DONT LES MODES PROPRES SONT ORTHOGONAUX A CETTE MEME MATRICE
C
C        LA MATRICE RESULTAT EST SYMETRIQUE ET STOCKEE TRIANGLE SUP
C
C-----------------------------------------------------------------------
C
C NOMRES /I/ : NOM K18 DE LA MATRICE CARREE RESULTAT
C CLASSE /I/ : CLASSE DE LA BASE JEVEUX DE L'OBJET RESULTAT
C BASMOD /I/ : NOM UT DE LA BASE MODALE DE PROJECTION
C NOMMAT /I/ : NOM K8 DE LA MATRICE A PROJETER
C TYPMAT /I/ : TYPE DE LA MATRICE A PROJETER (RAIDEUR,MASSE)
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C----------  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      CHARACTER*1  CLASSE
      CHARACTER*6  PGC
      CHARACTER*(*) TYPMAT
      CHARACTER*19 NOMMAT
      CHARACTER*8  BASMOD,K8BID
      CHARACTER*14 NUM
      CHARACTER*16 LPARA
      CHARACTER*24 CHAMVA, NOMRES
      CHARACTER*24 VALK
C
C-----------------------------------------------------------------------
      DATA PGC /'ORTPRO'/
C-----------------------------------------------------------------------
C
C --- CREATION DU .REFE
C
      CALL JEMARQ()
      CALL WKVECT(NOMRES(1:18)//'_REFE','G V K24',2,LDREF)
      ZK24(LDREF)=BASMOD
      ZK24(LDREF+1)=NOMMAT
C
C --- RECUPERATION DU NOM DE LA MATRICE
C
      IF (TYPMAT(1:8).EQ.'RAIDEUR ') THEN
         LPARA = 'RIGI_GENE              '
      ELSEIF (TYPMAT(1:8).EQ.'MASSE   ') THEN
         LPARA = 'MASS_GENE              '
      ELSE
        VALK = TYPMAT(1:8)
         CALL U2MESG('F', 'ALGORITH13_86',1,VALK,0,0,0,0.D0)
      ENDIF
C
C --- RECUPERATION DES DIMENSIONS DE LA BASE MODALE
C
C --- NOMBRE DE MODES PROPRES
C
      CALL BMNBMD(BASMOD,'MODE',NBMOD)
C
C --- NOMBRE TOTAL DE MODES ET DEFORMEES
C
      CALL BMNBMD(BASMOD,'TOUT',NBDEF)
C
C --- CREATION DU .DESC
C
      CALL WKVECT(NOMRES(1:18)//'_DESC','G V I',3,LDDES)
      ZI(LDDES)=2
      ZI(LDDES+1)=NBDEF
      ZI(LDDES+2)=3
C
C --- ALLOCATION DE LA MATRICE RESULTAT
C
      NTAIL=NBDEF*(NBDEF+1)/2
      CALL WKVECT(NOMRES(1:18)//'_VALE',CLASSE//' V R',NTAIL,LDRES)
C
C --- PROJECTION SUR LES MODES PROPRES
C
      DO 10 I=1,NBMOD
        CALL RSADPA(BASMOD,'L',1,LPARA,I,0,LLPAGE,K8BID)
        PGEN=ZR(LLPAGE)
        IAD=I*(I+1)/2
        ZR(LDRES+IAD-1)=PGEN
10    CONTINUE
C
      IF(NBDEF.EQ.NBMOD) GOTO 9999
C
C --- CONTROLE D'EXISTENCE DE LA MATRICE
C
      CALL MTEXIS(NOMMAT(1:8),IER)
      IF(IER.EQ.0) THEN
        VALK = NOMMAT(1:8)
        CALL U2MESG('F', 'ALGORITH12_39',1,VALK,0,0,0,0.D0)
      ENDIF
C
C --- ALLOCATION DESCRIPTEUR DE LA MATRICE
C
      CALL MTDSCR(NOMMAT(1:8))
      CALL JEVEUO(NOMMAT(1:19)//'.&INT','E',LMAT)
C
C --- RECUPERATION NUMEROTATION ET NB EQUATIONS
C
      CALL DISMOI('F','NB_EQUA',NOMMAT(1:8),'MATR_ASSE',NEQ,K8BID,IRET)
      CALL DISMOI('F','NOM_NUME_DDL',NOMMAT(1:8),'MATR_ASSE',IBID,
     +            NUM,IRET)
      CALL JEVEUO(NUM//'.NUME.DEEQ','L',IDDEEQ)
C
C --- ALLOCATION VECTEUR DE TRAVAIL
C
      CALL WKVECT('&&'//PGC//'.VECT1','V V R',NEQ,LTVEC1)
C
C --- PROJECTION SUR DEFORMEES NON MODALES
C
      CALL WKVECT('&&'//PGC//'.VECT2','V V R',NEQ,LTVEC2)
      DO 20 I=NBMOD+1,NBDEF
C
C ----- CALCUL PRODUIT MATRICE DEFORMEE
C
        CALL DCAPNO(BASMOD,'DEPL    ',I,CHAMVA)
        CALL JEVEUO(CHAMVA,'L',LLCHAM)
        CALL DCOPY(NEQ,ZR(LLCHAM),1,ZR(LTVEC2),1)
        CALL ZERLAG(ZR(LTVEC2),NEQ,ZI(IDDEEQ))
        CALL MRMULT('ZERO',LMAT,ZR(LTVEC2),'R',ZR(LTVEC1),1)
        CALL ZERLAG(ZR(LTVEC1),NEQ,ZI(IDDEEQ))
C
C ----- PRODUIT AVEC LA DEFORMEE COURANTE
C
        IAD=I*(I+1)/2
        ZR(LDRES+IAD-1)= DDOT(NEQ,ZR(LTVEC1),1,ZR(LTVEC2),1)
        CALL JELIBE(CHAMVA)
C
C ----- PRODUIT AVEC DEFORMEES D'ORDRE SUPERIEURE
C
        IF(I.LT.NBDEF) THEN
          DO 30 J=I+1,NBDEF
            CALL DCAPNO(BASMOD,'DEPL    ',J,CHAMVA)
            CALL JEVEUO(CHAMVA,'L',LLCHAM)
            IAD=(J-1)*J/2+I
            ZR(LDRES+IAD-1) = DDOT(NEQ,ZR(LTVEC1),1,ZR(LLCHAM),1)
            CALL JELIBE(CHAMVA)
30        CONTINUE
        ENDIF
C
C ----- PRODUIT AVEC MODES PROPRES
C
        DO 40 J=1,NBMOD
          CALL DCAPNO(BASMOD,'DEPL    ',J,CHAMVA)
          CALL JEVEUO(CHAMVA,'L',LLCHAM)
          IAD=(I-1)*I/2+J
          ZR(LDRES+IAD-1)= DDOT(NEQ,ZR(LTVEC1),1,ZR(LLCHAM),1)
          CALL JELIBE(CHAMVA)
40      CONTINUE
20     CONTINUE


C
C
C --- DESTRUCTION DESCRIPTEUR DE LA MATRICE
C
      CALL JEDETR('&&'//PGC//'.VECT2')
      CALL JEDETR('&&'//PGC//'.VECT1')
C
 9999 CONTINUE
      CALL JEDEMA()
      END
