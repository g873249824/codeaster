      SUBROUTINE XAJULS(NOMA,NBMA,CNSLT,CNSLN,JCONX1,JCONX2,CLSM)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      INTEGER       NBMA,JCONX1,JCONX2,CLSM
      CHARACTER*8   NOMA
      CHARACTER*19  CNSLT,CNSLN

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 04/03/2013   AUTEUR MARTIN A.MARTIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE MASSIN P.MASSIN
C
C     ------------------------------------------------------------------
C     XFEM : REAJUSTEMENT DES LEVEL SETS (BOOK III 06/02/04)
C     -        ---
C     BUT : ON MODIFIE LES VALEURS DE LS AUX NOEUDS SI TROP
C           PROCHES DE 0 POUR EVITER LES ERREURS D'INTEGRATION
C
C    ENTREE :
C              IFM    :   FICHIER D'IMPRESSION
C              NOMA   :   OBJET MAILLAGE
C              NBMA   :   NOMBRE DE MAILLES DU MAILLAGE
C              CNSLN  :   LEVEL-SET NORMALE  (PLAN DE LA FISSURE)
C              CNSLT  :   LEVEL-SET TANGENTE (TRACE DE LA FISSURE)
C       JCONX1,JCONX2 :   INDICES DE LA CONNECTIVITE
C
C    SORTIE :
C              CNSLN  :   LEVEL-SET NORMALE
C              CNSLT  :   LEVEL-SET TANGENTE
C              CLSM   :   NOMBRE DE LEVEL SETS MODIFIEES
C
C     ------------------------------------------------------------------

      INTEGER       JMA,IMA,ITYPMA,AR(12,3),NBAR,IA
      INTEGER       NA,NB,NM,NUNOA,NUNOB,NUNOM
      INTEGER       JLNSV,JLTSV,NMAABS,NDIME,NDIM,IRET
      REAL*8        D1,LSNA,LSNB,CRILSN,LSTA,LSTB,CRILST,R8PREM,D2
      REAL*8        LSNM,LSTM,LSNMAX,LSTMAX
      CHARACTER*19  MAI
      CHARACTER*8   TYPMA
      CHARACTER*32  KBID
      LOGICAL       ISMALI

      PARAMETER     (CRILSN=1.D-2, CRILST=1.D-3)

C-----------------------------------------------------------------------
C     DEBUT
C-----------------------------------------------------------------------
      CALL JEMARQ()

      CALL JEVEUO(CNSLN//'.CNSV','E',JLNSV)
      CALL JEVEUO(CNSLT//'.CNSV','E',JLTSV)

      D2=999.D0
      
C     COMPTEUR DES LSN ET LST MODIFIÉES
      CLSM=0
      MAI=NOMA//'.TYPMAIL'
      CALL JEVEUO(MAI,'L',JMA)

C     RECUPERATION DE LA DIMENSION DE L'ESPACE
      CALL DISMOI('F','DIM_GEOM',NOMA,'MAILLAGE',NDIME,KBID,IRET)
 
C     BOUCLE SUR TOUTES LES MAILLES DU MAILLAGE

      DO 200 IMA=1,NBMA
        NMAABS=IMA
        ITYPMA=ZI(JMA-1+IMA)
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYPMA),TYPMA)

C       RECUPERATION DE LA DIMENSION TOPOLOGIQUE DE L'ELEMENT 
        CALL DISMOI('F','DIM_TOPO',TYPMA,'TYPE_MAILLE',NDIM,KBID,IRET)

C       LES ELEMENTS DE BORD NE SONT PAS TRAITES
        IF (NDIM.LT.NDIME) GOTO 200

C       BOUCLE SUR LES ARETES DE LA MAILLE VOLUMIQUE
        CALL CONARE(TYPMA,AR,NBAR)
        LSNMAX=0.D0
        LSTMAX=0.D0

C       BOUCLE POUR RECUPERER LE MAX DE ABS(LSN) SUR UNE MAILLE
C       (UNIQUEMENT LES NOEUDS SOMMETS) ET FIT TO VERTEX LES NOEUDS
C       SOMMETS

        DO 210 IA=1,NBAR
          NA=AR(IA,1)
          NB=AR(IA,2)
          NUNOA=ZI(JCONX1-1+ZI(JCONX2+NMAABS-1)+NA-1)
          NUNOB=ZI(JCONX1-1+ZI(JCONX2+NMAABS-1)+NB-1)
          LSNA=ZR(JLNSV-1+(NUNOA-1)+1)
          LSNB=ZR(JLNSV-1+(NUNOB-1)+1)
          LSTA=ZR(JLTSV-1+(NUNOA-1)+1)
          LSTB=ZR(JLTSV-1+(NUNOB-1)+1)
          IF (ABS(LSNA).GT.LSNMAX) LSNMAX=ABS(LSNA)
          IF (ABS(LSNB).GT.LSNMAX) LSNMAX=ABS(LSNB)
          IF (ABS(LSTA).GT.LSTMAX) LSTMAX=ABS(LSTA)
          IF (ABS(LSTB).GT.LSTMAX) LSTMAX=ABS(LSTB)

C         REAJUSTEMENT DE LA LEVEL SET NORMALE AUX NOEUDS SOMMETS, QUAND
C         LA VALEUR D'UN LSN DIVISE PAR LA DIFFERENCE DES VALEURS AUX
C         DEUX EXTREMITES SONT INFERIEURES D'UN CERTAINE NOMBRE, ON MET
C         LES LSN A ZERO.
          IF (ABS(LSNA-LSNB).GT.R8PREM()) THEN
            D1=LSNA/(LSNA-LSNB)
            IF (ABS(D1).LE.CRILSN) THEN
C              REAJUSTEMENT DE LSNA
               ZR(JLNSV-1+(NUNOA-1)+1)=0.D0
               CLSM=CLSM+1
            ENDIF
            IF (ABS(D1-1.D0).LE.(CRILSN)) THEN
C              REAJUSTEMENT DE LSNB
               ZR(JLNSV-1+(NUNOB-1)+1)=0.D0
               CLSM=CLSM+1
            ENDIF
          ENDIF

C         REAJUSTEMENT DE LA LEVEL SET TANGENTE AUX NOEUDS SOMMETS, 
C         QUAND LA VALEUR D'UN LST DIVISE PAR LA DIFFERENCE DES VALEURS
C         AUX DEUX EXTREMITES SONT INFERIEURES D'UN CERTAINE NOMBRE, ON
C         MET LES LST A ZERO.

          IF (ABS(LSTA-LSTB).GT.R8PREM()) THEN
            D1=LSTA/(LSTA-LSTB)
            IF (ABS(D1).LE.CRILST) THEN
C              REAJUSTEMENT DE LSTA
               ZR(JLTSV-1+(NUNOA-1)+1)=0.D0
               CLSM=CLSM+1
            ENDIF
            IF (ABS(D1-1.D0).LE.(CRILST)) THEN
C              REAJUSTEMENT DE LSTB
               ZR(JLTSV-1+(NUNOB-1)+1)=0.D0
               CLSM=CLSM+1
            ENDIF
          ENDIF

210     CONTINUE

        DO 230 IA=1,NBAR
          NA=AR(IA,1)
          NB=AR(IA,2)
          NUNOA=ZI(JCONX1-1+ZI(JCONX2+NMAABS-1)+NA-1)
          NUNOB=ZI(JCONX1-1+ZI(JCONX2+NMAABS-1)+NB-1)

          IF (.NOT. ISMALI(TYPMA)) THEN
            NM=AR(IA,3)
            NUNOM=ZI(JCONX1-1+ZI(JCONX2+NMAABS-1)+NM-1)
            LSNM=ZR(JLNSV-1+(NUNOM-1)+1)
            LSTM=ZR(JLTSV-1+(NUNOM-1)+1)

            IF (ZR(JLNSV-1+(NUNOA-1)+1).EQ.0.D0 .AND.
     &          ZR(JLNSV-1+(NUNOB-1)+1).EQ.0.D0) THEN
              D2=LSNM/LSNMAX
              IF(ABS(D2).LE.CRILSN) THEN
C             REAJUSTEMENT DE LSNM AUX NOEUDS MILIEUX,QUAND LA VALEUR
C             D'UN LSNM SUR LA VALEUR LSNM LE PLUS GRAND DANS LE MEME
C             ELEMENT EST INFERIEURE A UN CERTAIN NOMBRE, ON MET LES
C             LSN A ZERO.
                ZR(JLNSV-1+(NUNOM-1)+1)=0.D0
                CLSM=CLSM+1
              ELSE
                CALL U2MESS('A','XFEM_63')
              ENDIF
            ENDIF
            IF (ZR(JLTSV-1+(NUNOA-1)+1).EQ.0.D0.AND.
     &          ZR(JLTSV-1+(NUNOB-1)+1).EQ.0.D0) THEN
              D2=LSTM/LSTMAX
              IF(ABS(D2).LE.CRILST) THEN
C             REAJUSTEMENT DE LSTM AUX NOEUDS MILIEUX,QUAND LA VALEUR
C             D'UN LSTM SUR LA VALEUR LSTM LE PLUS GRAND DANS LE MEME
C             ELEMENT EST INFERIEURE A UN CERTAIN NOMBRE, ON MET LES
C             LST A ZERO.
                ZR(JLTSV-1+(NUNOM-1)+1)=0.D0
                CLSM=CLSM+1
              ELSE
                CALL U2MESS('A','XFEM_63')
              ENDIF
            ENDIF
          ENDIF

 230    CONTINUE

 200  CONTINUE

C-----------------------------------------------------------------------
C     FIN
C-----------------------------------------------------------------------
      CALL JEDEMA()
      END
