      SUBROUTINE XPRGEO(NOMA,CNSLN,CNSLT,GRLN,GRLT,VPOINT,CNSBL,DELTAT,
     &                  NODTOR,LIGGRD,CNSBET,LISTP)

      IMPLICIT NONE

      REAL*8         DELTAT
      CHARACTER*8    NOMA
      CHARACTER*19   CNSLN,CNSLT,GRLN,GRLT,CNSBL,NODTOR,LIGGRD,CNSBET,
     &               LISTP,VPOINT
     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 20/09/2011   AUTEUR COLOMBO D.COLOMBO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE COLOMBO D.COLOMBO
C TOLE CRP_6


C     ------------------------------------------------------------------
C
C       XPRGEO   : X-FEM PROPAGATION GEOMETRIQUE DES LEVEL SETS
C       ------     -     --          ---
C    PROPAGATION DES LEVEL SETS AU PAS DE TEMP SUIVANT FAIT PAR
C    UN ALGORITHME GEOMETRIQUE
C
C    ENTREE
C        NOMA    : NOM DU CONCEPT MAILLAGE
C        CNSLT   : CHAM_NO_S LEVEL SET TANGENTIELLE
C        CNSLN   : CHAM_NO_S LEVEL SET NORMALE
C        GRLT    : CHAM_NO_S GRADIENT DE LEVEL SET TANGENTIELLE
C        GRLN    : CHAM_NO_S GRADIENT DE LEVEL SET NORMALE
C        VPOINT  : VECTEUR DES VITESSES DE PROPAGATION EN CHAQUE POINT
C                  DU DOMAINE DE CALCUL (MODULE DE LA VITESSE DU POINT
C                  PROJETE SUR LE FOND DE LA FISSURE)
C        CNSBL   : CHAM_NO_S DES VECTEURS NORMALE ET TANGENTIELLE DE LA
C                  BASE LOCALE IN CHAQUE NODE DU MAILLAGE
C        DELTAT  : TEMPS TOTAL DU PAS DE PROPAGATION
C        NODTOR  : LISTE DES NOEUDS DEFINISSANT LE DOMAINE DE CALCUL
C        LIGGRD  : LIGREL DU DOMAINE DE CALCUL (VOIR XPRTOR.F)
C        CNSBET  : VECTEUR DES ANGLES DE BIFURCATION DE LA FISSURE
C                  EN CHAQUE POINT DU DOMAINE DE CALCUL (ANGLE AU POINT
C                  PROJETE SUR LE FOND DE LA FISSURE)
C        LISTP   : VECTEUR (A 3 COMPOSANTES) OU LES CORDONNEES DU
C                  PROJETE DE CHAQUE POINT DU DOMAINE DE CALCUL SUR LE
C                  FOND DE LA FISSURE SONT STOCKEES
C
C    SORTIE
C        CNSLT   : CHAM_NO_S LEVEL SET TANGENTIELLE
C        CNSLN   : CHAM_NO_S LEVEL SET NORMALE
C        GRLT    : CHAM_NO_S GRADIENT DE LEVEL SET TANGENTIELLE
C        GRLN    : CHAM_NO_S GRADIENT DE LEVEL SET NORMALE
C
C     ------------------------------------------------------------------

C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16             ZK16
      CHARACTER*24                      ZK24
      CHARACTER*32                               ZK32
      CHARACTER*80                                        ZK80
      COMMON  /KVARJE/ ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER    I,IFM,NIV,NBNO,IRET,JLTNO,JLNNO,NDIM,J,JNODTO,NODE,
     &           IER,IBID,JBL,JBETA,JLISTP,JCOOR,POS,POS1,JVP
      CHARACTER*8      K8B,KBID,LPAIN(2),LPAOUT(1)
      CHARACTER*19     CHGRLT,CHGRLN,CHAMS,CNOLT,CNOLN
      CHARACTER*24     LCHIN(2),LCHOUT(1)
      REAL*8           T1(3),N1(3),P1(3),DELTAA,NEWLSN,NEWLST,CBETA,
     &                 SBETA

C-----------------------------------------------------------------------
C     DEBUT
C-----------------------------------------------------------------------

      CALL JEMARQ()
      CALL INFMAJ()
      CALL INFNIV(IFM,NIV)

C     RETRIEVE THE LOCAL REFERENCE SYSTEM FOR EACH NODE IN THE MESH
      CALL JEVEUO(CNSBL//'.CNSV','E',JBL)

C     RETRIEVE THE DIMENSION OF THE PROBLEM (2D AND 3D ARE SUPPORTED)
      CALL DISMOI('F','DIM_GEOM',NOMA,'MAILLAGE',NDIM,KBID,IRET)
      CALL JEVEUO(NOMA//'.COORDO    .VALE','L',JCOOR)

C     RETRIEVE THE NUMBER OF THE NODES THAT MUST TO BE USED IN THE
C     CALCULUS (SAME ORDER THAN THE ONE USED IN THE CONNECTION TABLE)
      CALL JEVEUO(NODTOR,'L',JNODTO)

C     RETRIEVE THE TOTAL NUMBER OF THE NODES THAT MUST BE ELABORATED
      CALL JELIRA(NODTOR,'LONMAX',NBNO,K8B)

      CALL JEVEUO(CNSBET,'L',JBETA)
      CALL JEVEUO(LISTP,'L',JLISTP)
      CALL JEVEUO(VPOINT,'L',JVP)

C ***************************************************************
C UPDATE THE LEVEL SETS
C ***************************************************************

C     RECUPERATION DE L'ADRESSE DES VALEURS DE LT ET LN
      CALL JEVEUO(CNSLT//'.CNSV','E',JLTNO)
      CALL JEVEUO(CNSLN//'.CNSV','E',JLNNO)

C     UPDATE THE LEVEL SETS FOR EACH NODE IN THE TORE
      DO 100 I=1,NBNO

C         RETREIVE THE NODE NUMBER
          NODE = ZI(JNODTO-1+I)

C         PROPAGATION VECTOR DELTA_A
          DELTAA=ZR(JVP-1+NODE)*DELTAT

C         STORE THE COS AND SIN OF THE PROPAGATION ANGLE
          CBETA = COS(ZR(JBETA-1+NODE))
          SBETA = SIN(ZR(JBETA-1+NODE))

C         POINTERS INSIDE THE JEVEUX OBJECTS
          POS   = 2*NDIM*(NODE-1)
          POS1  = 3*(NODE-1)

C         RESET THE NEW VALUE OF THE TWO LEVEL SETS
          NEWLSN = 0.D0
          NEWLST = 0.D0

          DO 105 J=1,NDIM
C            NEW T-AXIS BY A RIGID ROTATION AT THE NEW CRACK TIP
             T1(J) = CBETA*ZR(JBL-1+POS+NDIM+J)+SBETA*ZR(JBL-1+POS+J)
C            NEW N-AXIS BY A RIGID ROTATION AT THE NEW CRACK TIP
             N1(J) = CBETA*ZR(JBL-1+POS+J)-SBETA*ZR(JBL-1+POS+NDIM+J)
C            NEW CRACK TIP POSITION
             P1(J) = ZR(JLISTP-1+POS1+J)+DELTAA*T1(J)
C            NEW VALUES OF THE TWO LEVEL SETS
             NEWLSN = NEWLSN+(ZR(JCOOR-1+POS1+J)-P1(J))*N1(J)
             NEWLST = NEWLST+(ZR(JCOOR-1+POS1+J)-P1(J))*T1(J)
105       CONTINUE

C         MODIFY THE NORMAL LEVEL SET ONLY IN THE POINTS WHERE THE
C         TANGENTIAL LEVEL SET IS POSITIVE
          IF (ZR(JLTNO-1+NODE).GT.0.D0) ZR(JLNNO-1+NODE) = NEWLSN

C         STORE THE NEW VALUE OF THE TANTENGIAL LEVEL SET
          ZR(JLTNO-1+NODE) = NEWLST

 100  CONTINUE

C-----------------------------------------------------------------------
C     CALCUL DES GRADIENTS DES LEVEL SETS RESULTANTES
C-----------------------------------------------------------------------

C     CREATION DES OBJETS VOLATILES
      CHGRLT = '&&XPRLS.CHGRLT'
      CHGRLN = '&&XPRLS.CHGRLN'
      CHAMS  = '&&XPRLS.CHAMS'
      CNOLT  = '&&XPRLS.CNOLT'
      CNOLN  = '&&XPRLS.CNOLN'

C  GRADIENT DE LT
      CALL CNSCNO(CNSLT,' ','NON','V',CNOLT,'F',IBID)

      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = NOMA//'.COORDO'
      LPAIN(2) = 'PNEUTER'
      LCHIN(2) = CNOLT
      LPAOUT(1)= 'PGNEUTR'
      LCHOUT(1)= CHGRLT

      CALL CALCUL('S','GRAD_NEUT_R',LIGGRD,2,LCHIN,LPAIN,1,
     &            LCHOUT,LPAOUT,'V','OUI')

      CALL CELCES ( CHGRLT, 'V', CHAMS )
      CALL CESCNS ( CHAMS, ' ', 'V', GRLT, ' ', IER )

C  GRADIENT DE LN
      CALL CNSCNO(CNSLN,' ','NON','V',CNOLN,'F',IBID)

      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = NOMA//'.COORDO'
      LPAIN(2) = 'PNEUTER'
      LCHIN(2) = CNOLN
      LPAOUT(1)= 'PGNEUTR'
      LCHOUT(1)= CHGRLN

      CALL CALCUL('S','GRAD_NEUT_R',LIGGRD,2,LCHIN,LPAIN,1,
     &            LCHOUT,LPAOUT,'V','OUI')

      CALL CELCES ( CHGRLN, 'V', CHAMS )
      CALL CESCNS ( CHAMS, ' ', 'V', GRLN, ' ', IER )

C  DESTRUCTION DES OBJETS VOLATILES
      CALL JEDETR(CHGRLT)
      CALL JEDETR(CHGRLN)
      CALL JEDETR(CHAMS)
      CALL JEDETR(CNOLT)
      CALL JEDETR(CNOLN)

C-----------------------------------------------------------------------
C     FIN
C-----------------------------------------------------------------------
      CALL JEDEMA()
      END
