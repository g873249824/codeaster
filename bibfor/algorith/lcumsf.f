      SUBROUTINE LCUMSF(SIGI,SIGF,NSTRS,VARI,NVARI,CMAT,NMAT,
     &                   ISPH,TDT,HINI,HFIN,VARF)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/03/2011   AUTEUR FOUCAULT A.FOUCAULT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE YLEPAPE Y.LEPAPE
C
C ROUTINE APPELE DANS FLU
C LCUMSF     SOURCE    BENBOU   01/03/26
C
C_______________________________________________________________________
C
C ROUTINE QUI CALCULE LA DEFORMATION DE FLUAGE DE DESSICCATION
C ET DE FLUAGE PROPRE A LA FIN DU PAS DE TEMPS
C ELLES SONT SAUVEGARDES DANS VARF(NVARI)
C
C IN  SIGI     : CONTRAINTES INITIALES
C IN  SIGF     : CONTRAINTES FINALES
C IN  NSTRS    : DIMENSION DES VECTEURS CONTRAINTE ET DEFORMATION
C IN  VARI     : VARIABLES INTERNES INITIALES
C IN  NVARI    : DIMENSION DES VECTEURS VARIABLES INTERNES
C IN  CMAT     : VECTEUR DE PARAMETRES (MATERIAU ET AUTRE)
C IN  NMAT     : DIMENSION DE CMAT
C IN  ISPH     : MODE DE CALCUL DE LA PARTIE SPHERIQUE
C IN  TDT      : PAS DE TEMPS
C IN  HINI     : HUMIDITE INITIALE
C IN  HFIN     : HUMIDITE FINALE
C
C OUT VARF     : VARIABLES INTERNES FINALES
C_______________________________________________________________________
C
      IMPLICIT NONE
      INTEGER I,ICOU,IFOU,IFPO,IDES,ISPH,J,NMAT,NSTRS,NVARI
      REAL*8 ERSP,EISP
      REAL*8 VARF(20),CMAT(15),SIGF(NSTRS),SIGI(NSTRS)
C MODIFI DU 6 JANVIER 2003 - YLP SUPPRESSION DES DECLARATIONS
C IMPLICITES DES TABLEAUX
C      REAL*8 AFD(NSTRS),BFD(NSTRS,NSTRS),CFD(NSTRS,NSTRS)
C      REAL*8 AFPD(NSTRS),SIGFI(NSTRS),VARI(20)
      REAL*8 AFD(6),BFD(6,6),CFD(6,6)
      REAL*8 AFPD(6),SIGFI(6),VARI(20)
      REAL*8 EFDE(6),EPSDVR(6),EPSDVI(6),SIGIDV(6),SIGFDV(6)
      REAL*8 AFPS,BFPS,BFPD,CFPD,CFPS,DEISP,HFIN,HINI
      REAL*8 SIGFSP,SIGISP,TDT,VISP, R8PREM
C
C RECUPERATION DES VALEURS DES PARAMETRES MATERIAU
C

      IFOU = NINT(CMAT(12))
      IFPO = NINT(CMAT(13))
      IDES = NINT(CMAT(14))
      ICOU = NINT(CMAT(15))

C --- VISCOSITE SPHERIQUE IRREVERSIBLE
      VISP = CMAT(6)
C
C RECUPERATION DES VARIABLES INTERNES INITIALES
C
C   FLUAGE PROPRE
C
C     - SPHERIQUE  : ERSP EISP
C     - DEVIATOIRE : EPSDVR EPSDVI
C
C   FLUAGE DE DESSICCATION : EFDE
C
      ERSP = VARI(1)
      EISP = VARI(2)
C
      EPSDVR(1) = VARI(3)
      EPSDVI(1) = VARI(4)
      EPSDVR(2) = VARI(5)
      EPSDVI(2) = VARI(6)
      EPSDVR(3) = VARI(7)
      EPSDVI(3) = VARI(8)
      EPSDVR(4) = VARI(12)
      EPSDVI(4) = VARI(13)
      EPSDVR(5) = VARI(14)
      EPSDVI(5) = VARI(15)
      EPSDVR(6) = VARI(16)
      EPSDVI(6) = VARI(17)
C
      EFDE(1) = VARI(9)
      EFDE(2) = VARI(10)
      EFDE(3) = VARI(11)
      EFDE(4) = VARI(18)
      EFDE(5) = VARI(19)
      EFDE(6) = VARI(20)
C
C INITIALISATION DES VARIABLES
C
C MOFI DU 6 JANVIER 2003 - YLP NSTRS --> 6
C      DO 11 I=1,NSTRS
C        AFPD(I)  = 0.D0
C        AFD(I)   = 0.D0
C        SIGFI(I) = 0.D0
C        DO 12 J=1,NSTRS
C          BFD(I,J)  = 0D0
C          CFD(I,J)  = 0D0
C12      CONTINUE
C11    CONTINUE
      DO 11 I=1,6
        AFPD(I)  = 0.D0
        AFD(I)   = 0.D0
        SIGFI(I) = 0.D0
        DO 12 J=1,6
          BFD(I,J)  = 0D0
          CFD(I,J)  = 0D0
12      CONTINUE
11    CONTINUE
      DO 13 I=1,6  
        SIGIDV(I) = 0.D0
        SIGFDV(I) = 0.D0
13    CONTINUE
C
C TEST DE COUPLAGE AVEC ICOU
C
      IF (ICOU.EQ.0) THEN
        DO 10 I=1,NSTRS
          SIGFI(I) = SIGI(I)
  10    CONTINUE
      ELSE
        DO 20 I=1,NSTRS
          SIGFI(I) = SIGF(I)
  20    CONTINUE
      ENDIF
C
C CALCUL DES CONTRAINTES SPHERIQUES INI ET FIN
C
C  => EQUATION (3.11-1)
C
      SIGISP = (SIGI(1) + SIGI(2)) / 3.D0
      SIGFSP = (SIGFI(1) + SIGFI(2)) / 3.D0
C
      IF (IFOU.NE.-2) THEN
        SIGISP = SIGI(3) / 3.D0 + SIGISP
        SIGFSP = SIGFI(3) / 3.D0 + SIGFSP
      ENDIF
C
C CALCUL DES CONTRAINTES DEVIATOIRES INI ET FIN
C
C  => EQUATION (3.11-2)
C
      SIGIDV(1) = SIGI(1) - SIGISP
      SIGIDV(2) = SIGI(2) - SIGISP
C
      SIGFDV(1) = SIGFI(1) - SIGFSP
      SIGFDV(2) = SIGFI(2) - SIGFSP
C
      IF (IFOU.NE.-2) THEN
        SIGIDV(3) = SIGI(3) - SIGISP
        SIGIDV(4) = SIGI(4)
C
        SIGFDV(3) = SIGFI(3) - SIGFSP
        SIGFDV(4) = SIGFI(4)
C
        IF (IFOU.EQ.2) THEN
          SIGIDV(5) = SIGI(5)
          SIGIDV(6) = SIGI(6)
C
          SIGFDV(5) = SIGFI(5)
          SIGFDV(6) = SIGFI(6)
        ENDIF
      ELSE
        SIGIDV(3) = SIGI(3)
C
        SIGFDV(3) = SIGFI(3)
      ENDIF
C_______________________________________________________________________
C
C  FLUAGE DE DESSICCATION : CALCUL DES MATRICES
C
C --> DEBRANCHE le 11 octobre 2002 - ylp.
C --> REBRANCHE le 25 aout 2004 - ylp.
C_______________________________________________________________________
C
       IF ((IDES.EQ.1).OR.(IDES.EQ.2)) THEN
        CALL LCUMFB(SIGI,NSTRS,VARI,NVARI,CMAT,NMAT,TDT,HINI,HFIN,
     &              AFD,BFD,CFD)
       ENDIF
C  CALCUL DES DEFORMATIONS FINALES FLUAGE DE DESSICCATION
C
C  => EQUATION (3.11-3)
C
       DO 30 I=1,NSTRS
        EFDE(I) = AFD(I) + EFDE(I) + BFD(I,I) * SIGI(I)
     &            + CFD(I,I) * SIGFI(I)
30     CONTINUE
C
C  FLUAGE PROPRE
C
       IF (IFPO.NE.0) THEN
C
C   FLUAGE PROPRE SPHERIQUE REVERSIBLE ET IRREVERSIBLE
C
C  => EQUATION (3.11-4)
C
        CALL LCUMFS(VARI,NVARI,CMAT,NMAT,1,ISPH,TDT,HINI,HFIN,
     &                  AFPS,BFPS,CFPS)
        ERSP = ERSP + AFPS + BFPS*SIGISP
     &                 + CFPS*SIGFSP
C
        CALL LCUMFS(VARI,NVARI,CMAT,NMAT,2,ISPH,TDT,HINI,HFIN,
     &                  AFPS,BFPS,CFPS)
        DEISP = AFPS + BFPS*SIGISP
     &                 + CFPS*SIGFSP
C
C     TEST SUR LA CROISSANCE DE LA DEFORMATION DE FLUAGE
C          PROPRE SPHERIQUE
C
        IF (ISPH.NE.0) THEN
          IF (DEISP.GT.0.D0) THEN
            ISPH = 2
            GOTO 60
          ENDIF
          IF ((SIGFSP/VISP.GT.-R8PREM()).AND.
     &        (SIGISP/VISP.GT.-R8PREM())) THEN
            ISPH = 2
            GOTO 60
          ENDIF
        ENDIF
C
        EISP = EISP + DEISP
C
C   FLUAGE PROPRE DEVIATORIQUE REVERSIBLE ET IRREVERSIBLE
C
C  => EQUATION (3.11-5)
C
        CALL LCUMFD(VARI,NVARI,NSTRS,CMAT,NMAT,1,TDT,HINI,HFIN,
     &            AFPD,BFPD,CFPD)
        DO 40 I=1,NSTRS
          EPSDVR(I) = EPSDVR(I) + AFPD(I)+ BFPD*SIGIDV(I)
     &                 + CFPD*SIGFDV(I)
  40    CONTINUE
        CALL LCUMFD(VARI,NVARI,NSTRS,CMAT,NMAT,2,TDT,HINI,HFIN,
     &            AFPD,BFPD,CFPD)
        DO 50 I=1,NSTRS
          EPSDVI(I) = EPSDVI(I) + AFPD(I) + BFPD*SIGIDV(I)
     &                 + CFPD*SIGFDV(I)
  50    CONTINUE
      ENDIF
C
C  SAUVEGARDE DES DEFORMATIONS DE FLUAGE
C
C    PROPRE
C
C      - SPHERIQUE
C
          VARF(1)  = ERSP
          VARF(2)  = EISP
C
C      - DEVIATORIQUE
C
      VARF(3)  = EPSDVR(1)
      VARF(4)  = EPSDVI(1)
      VARF(5)  = EPSDVR(2)
      VARF(6)  = EPSDVI(2)
      VARF(7)  = EPSDVR(3)
      VARF(8)  = EPSDVI(3)
      VARF(12) = EPSDVR(4)
      VARF(13) = EPSDVI(4)
      VARF(14) = EPSDVR(5)
      VARF(15) = EPSDVI(5)
      VARF(16) = EPSDVR(6)
      VARF(17) = EPSDVI(6)
C
C    DESSICCATION
C
      VARF(9)  = EFDE(1)
      VARF(10) = EFDE(2)
      VARF(11) = EFDE(3)
      VARF(18) = EFDE(4)
      VARF(19) = EFDE(5)
      VARF(20) = EFDE(6)
C
  60  CONTINUE
C
      END
