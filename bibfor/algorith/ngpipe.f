      SUBROUTINE NGPIPE(TYPILO,NPG,NEPS,NDDL,B,LI2LDC,TYPMOD,MAT, 
     &   COMPOR,LGPG,DDLM,SIGM,VIM,DDLD,DDL0,DDL1,TAU,ETAMIN,ETAMAX,
     &   COPILO)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/03/2012   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

       IMPLICIT NONE

       CHARACTER*8   TYPMOD(*)
       CHARACTER*16  TYPILO, COMPOR(4)

       INTEGER NPG,NEPS,NDDL,MAT,LGPG
       REAL*8  DDLM(NDDL),DDLD(NDDL),DDL0(NDDL),DDL1(NDDL)
       REAL*8  SIGM(0:NEPS*NPG-1), VIM(LGPG,NPG),TAU
       REAL*8  COPILO(5,NPG), ETAMIN, ETAMAX
       REAL*8  B(NEPS,NPG,NDDL),LI2LDC(0:NEPS-1)
C.......................................................................
C
C     BUT:  CALCUL  DES COEFFICIENTS DE PILOTAGE POUR PRED_ELAS
C.......................................................................
C IN  TYPILO : MODE DE PILOTAGE: 'DEFORMATION', 'PRED_ELAS'
C IN  NPG    : NOMBRE DE POINTS DE GAUSS
C IN  NEPS   : NOMBRE DE COMPOSANTES DE DEFORMATIONS / CONTRAINTES
C IN  NDDL   : NOMBRE DE DDL DANS L'ELEMENT
C IN  B      : MATRICE CINEMATIQUE
C IN  LI2LDC : CONVERSION CONTRAINTE --> AVEC RACINE DE DEUX
C IN  TYPMOD : TYPE DE MODELISATION
C IN  MAT    : MATERIAU CODE
C IN  COMPOR : COMPORTEMENT
C IN  LGPG   : "LONGUEUR" DES VARIABLES INTERNES POUR 1 POINT DE GAUSS
C             CETTE LONGUEUR EST UN MAJORANT DU NBRE REEL DE VAR. INT.
C IN  DDLM   : DDL U,ALPHA,MU EN T-
C IN  SIGM   : CONTRAINTES DE CAUCHY EN T- (INUTILE)
C IN  VIM    : VARIABLES INTERNES EN T-
C IN  DDLD   : INCREMENT DE DDL U,ALPHA,MU A L'ITERATION NEWTON COURANTE
C IN  DDL0   : CORRECTION DE DDL U,ALPHA,MU POUR FORCES FIXES
C IN  DDL1   : CORRECTION DE DDL U,ALPHA,MU POUR FORCES PILOTEES
C OUT COPILO : COEFFICIENTS A0 ET A1 POUR CHAQUE POINT DE GAUSS
C ----------------------------------------------------------------------
      INTEGER    NPGMAX,EPSMAX
      PARAMETER (NPGMAX=27,EPSMAX=20)
C ----------------------------------------------------------------------
      INTEGER G,NEPG,IEG
      REAL*8  SIGMAM(0:EPSMAX*NPGMAX-1)
      REAL*8  EPSM(0:EPSMAX*NPGMAX-1),EPSD(0:EPSMAX*NPGMAX-1)
      REAL*8  EPSP(0:EPSMAX*NPGMAX-1)
      REAL*8  R8VIDE
C ----------------------------------------------------------------------
      INTEGER OS
      OS(G) = (G-1)*NEPS
C ----------------------------------------------------------------------


C -- INITIALISATION

      CALL R8INIR(NPG*5,R8VIDE(),COPILO,1)
      NEPG = NEPS*NPG


C -- DEFORMATIONS

      CALL DGEMV('N',NEPG,NDDL,1.D0,B,NEPG,DDLM,1,0.D0,EPSM,1)
      CALL DGEMV('N',NEPG,NDDL,1.D0,B,NEPG,DDLD,1,0.D0,EPSP,1)
      CALL DGEMV('N',NEPG,NDDL,1.D0,B,NEPG,DDL0,1,1.D0,EPSP,1)
      CALL DGEMV('N',NEPG,NDDL,1.D0,B,NEPG,DDL1,1,0.D0,EPSD,1)


C -- PRETRAITEMENT SI NECESSAIRE
      IF (TYPILO.EQ.'PRED_ELAS') THEN
        DO 10 IEG = 0,NEPG-1
          SIGMAM(IEG) = SIGM(IEG)*LI2LDC(MOD(IEG,NEPS))
 10     CONTINUE
      END IF

C -- TRAITEMENT DE CHAQUE POINT DE GAUSS

      DO 20 G=1,NPG
        CALL PIL000(TYPILO,COMPOR,NEPS,TAU,MAT,VIM(1,G),SIGMAM(OS(G)),
     &    EPSM(OS(G)),EPSP(OS(G)),EPSD(OS(G)),TYPMOD,ETAMIN,ETAMAX,
     &    COPILO(1,G))
 20   CONTINUE
 
      END
