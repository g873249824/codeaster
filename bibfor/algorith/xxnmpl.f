      SUBROUTINE  XXNMPL(ELREFP,ELRESE,NDIM,COORSE,IGEOM,HE,NFH,
     &                   DDLC,DDLM,NFE,INSTAM,INSTAP,IDEPLP,SIGM,VIP,
     &                   BASLOC,NNOP,NPG,TYPMOD,OPTION,IMATE,COMPOR,
     &                   LGPG,CRIT,IDEPL,LSN,LST,IDECPG,
     &                   SIG,VI,MATUU,IVECTU,CODRET,NFISS,FISNO)

      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER       NDIM,IGEOM,IMATE,LGPG,CODRET,NNOP,NPG
      INTEGER       NFH,DDLC,DDLM,NFE,IDEPL,IVECTU,IDEPLP
      INTEGER       NFISS,FISNO(NNOP,NFISS),IDECPG
      CHARACTER*8   ELREFP,TYPMOD(*)
      CHARACTER*8   ELRESE
      CHARACTER*16  OPTION,COMPOR(4)
      REAL*8        BASLOC(3*NDIM*NNOP),CRIT(3),HE(NFISS)
      REAL*8        LSN(NNOP),LST(NNOP),COORSE(*)
      REAL*8        VI(LGPG,NPG),VIP(LGPG,NPG),SIG(2*NDIM,NPG),MATUU(*)
      REAL*8        INSTAM,INSTAP,SIGM(2*NDIM,NPG),SIGN(6)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/01/2013   AUTEUR FERTE G.FERTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C TOLE CRP_21 CRS_1404
C
C.......................................................................
C
C     BUT:  CALCUL  DES OPTIONS RIGI_MECA_TANG, RAPH_MECA ET FULL_MECA
C           EN HYPER-ELASTICITE AVEC X-FEM EN 2D ET EN 3D
C.......................................................................
C
C IN  ELREFP  : ÉLÉMENT DE RÉFÉRENCE PARENT
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  COORSE  : COORDONNÉES DES SOMMETS DU SOUS-ÉLÉMENT
C IN  IGEOM   : COORDONNÉES DES NOEUDS DE L'ÉLÉMENT PARENT
C IN  HE      : VALEUR DE LA FONCTION HEAVISIDE SUR LE SOUS-ÉLT
C IN  NFH     : NOMBRE DE FONCTIONS HEAVYSIDE
C IN  DDLC    : NOMBRE DE DDL DE CONTACT (PAR NOEUD)
C IN  DDLM    : NOMBRE DE DDL PAR NOEUD MILIEU (EN 2D)
C IN  NFE     : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT
C IN  BASLOC  : BASE LOCALE AU FOND DE FISSURE AUX NOEUDS
C IN  NNOP    : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C IN  NPG     : NOMBRE DE POINTS DE GAUSS DU SOUS-ÉLÉMENT
C IN  TYPMOD  : TYPE DE MODELISATION
C IN  OPTION  : OPTION DE CALCUL
C IN  IMATE   : MATERIAU CODE
C IN  COMPOR  : COMPORTEMENT
C IN  LGPG    : "LONGUEUR" DES VARIABLES INTERNES POUR 1 POINT DE GAUSS
C               CETTE LONGUEUR EST UN MAJORANT DU NBRE REEL DE VAR. INT.
C IN  CRIT    : CRITERES DE CONVERGENCE LOCAUX
C IN  IDEPL   : ADRESSE DU DEPLACEMENT A PARTIR DE LA CONF DE REF
C IN  LSN     : VALEUR DE LA LEVEL SET NORMALE AUX NOEUDS PARENTS
C IN  LST     : VALEUR DE LA LEVEL SET TANGENTE AUX NOEUDS PARENTS

C OUT SIG     : CONTRAINTES DE CAUCHY (RAPH_MECA ET FULL_MECA)
C OUT VI      : VARIABLES INTERNES    (RAPH_MECA ET FULL_MECA)
C OUT MATUU   : MATRICE DE RIGIDITE PROFIL (RIGI_MECA_TANG ET FULL_MECA)
C OUT IVECTU  : VECTEUR FORCES NODALES (RAPH_MECA ET FULL_MECA)
C......................................................................
C
      INTEGER  I,IG,IRET,J,J1,KKD,KL,KPG,L,M,N,NN,MN
      INTEGER  DDLS,DDLD,CPT,IBID,IDFDE,IPOIDS,IVF,DEC(NNOP)
      INTEGER  JCOOPG,JDFD2,JGANO,NDIMB,NNO,NNOPS,NNOS,NPGBIS
      REAL*8   DSIDEP(6,6),F(3,3),EPS(6),DEPS(6),SIGMA(6),FTF,DETF
      REAL*8   TMP1,TMP2,SIGP(6),FE(4),BASLOG(3*NDIM)
      REAL*8   XG(NDIM),XE(NDIM),FF(NNOP),JAC,LSNG,LSTG
      REAL*8   RBID,RBID4(4),RBID10(10),RBID33(3,3)
      REAL*8   DFDI(NNOP,NDIM),PFF(6,NNOP,NNOP),DGDGL(4,3)
      REAL*8   DEF(6,NNOP,NDIM*(1+NFH+NFE)),R,UR
      REAL*8   ELGEOM(10,27),DFDIB(27,3),DEPLB1(3,27),DEPLB2(3,27)
      LOGICAL  GRDEPL,AXI,CPLAN
C
      INTEGER  INDI(6),INDJ(6)
      REAL*8   RIND(6),RAC2,ANGMAS(3)

      DATA    INDI / 1 , 2 , 3 , 1 , 1 , 2 /
      DATA    INDJ / 1 , 2 , 3 , 2 , 3 , 3 /
      DATA    RIND / 0.5D0,0.5D0,0.5D0,0.70710678118655D0,
     &               0.70710678118655D0,0.70710678118655D0 /
      DATA    RAC2 / 1.4142135623731D0 /
      DATA    ANGMAS /0.D0, 0.D0, 0.D0/
C--------------------------------------------------------------------
C     ATTENTION, EN 3D, ZR(IDEPL) ET ZR(VECTU) SONT DIMENSIONNÉS DE
C     TELLE SORTE QU'ILS NE PRENNENT PAS EN COMPTE LES DDL SUR LES
C     NOEUDS MILIEU

C     NOMBRE DE DDL DE DEPLACEMENT À CHAQUE NOEUD SOMMET
      DDLD = NDIM*(1+NFH+NFE)

C     NOMBRE DE DDL TOTAL (DEPL+CONTACT) À CHAQUE NOEUD SOMMET
      DDLS = DDLD+DDLC

C     RECUPERATION DU NOMBRE DE NOEUDS SOMMETS DE L'ELEMENT PARENT
      CALL ELREF4(' ','RIGI',IBID,IBID,NNOPS,IBID,IBID,IBID,IBID,IBID)

C - INITIALISATION
      GRDEPL = .FALSE.
      AXI    = TYPMOD(1) .EQ. 'AXIS'
      CPLAN  = TYPMOD(1) .EQ. 'C_PLAN'
C

      CALL ELREF5(ELRESE,'XINT',NDIMB,NNO,NNOS,NPGBIS,IPOIDS,JCOOPG,IVF,
     &            IDFDE,JDFD2,JGANO)

      CALL ASSERT(NPG.EQ.NPGBIS.AND.NDIM.EQ.NDIMB)

C - CALCUL DES ELEMENTS GEOMETRIQUES SPECIFIQUES LOIS DE COMPORTEMENT
      CALL LCEGEO(NNO      ,NPG   ,IPOIDS,IVF   ,IDFDE ,
     &            ZR(IGEOM),TYPMOD,COMPOR,NDIM  ,DFDIB ,
     &            DEPLB1   ,DEPLB2,ELGEOM)

      DO 178 N=1,NNOP
         CALL INDENT(N,DDLS,DDLM,NNOPS,DEC(N))
178   CONTINUE
C-----------------------------------------------------------------------
C - CALCUL POUR CHAQUE POINT DE GAUSS
      DO 1000 KPG=1,NPG

C       COORDONNÉES DU PT DE GAUSS DANS LE REPÈRE RÉEL : XG
        CALL VECINI(NDIM,0.D0,XG)
        DO 100 I=1,NDIM
          DO 101 N=1,NNO
            XG(I) = XG(I) + ZR(IVF-1+NNO*(KPG-1)+N)*COORSE(NDIM*(N-1)+I)
 101      CONTINUE
 100    CONTINUE

C             JUSTE POUR CALCULER LES FF
        CALL REEREF(ELREFP,AXI,NNOP,NNOPS,ZR(IGEOM),XG,IDEPL,GRDEPL,
     &              NDIM,HE,RBID,RBID,FISNO,NFISS,NFH,NFE,DDLS,DDLM,
     &              FE,DGDGL,'NON',XE,FF,DFDI,F,EPS,RBID33)

        IF (NFE.GT.0) THEN
C         BASE LOCALE  ET LEVEL SETS AU POINT DE GAUSS
          CALL VECINI(3*NDIM,0.D0,BASLOG)
          LSNG = 0.D0
          LSTG = 0.D0
          DO 110 N=1,NNOP
            LSNG = LSNG + LSN(N) * FF(N)
            LSTG = LSTG + LST(N) * FF(N)
            DO 111 I=1,3*NDIM
              BASLOG(I) = BASLOG(I) + BASLOC(3*NDIM*(N-1)+I) * FF(N)
 111        CONTINUE
 110      CONTINUE
C
C         FONCTION D'ENRICHISSEMENT AU POINT DE GAUSS ET LEURS DÉRIVÉES
          IF (NDIM.EQ.2) THEN
            CALL XCALF2(HE,LSNG,LSTG,BASLOG,FE,DGDGL,IRET)
          ELSEIF (NDIM.EQ.3) THEN
            CALL XCALFE(HE,LSNG,LSTG,BASLOG,FE,DGDGL,IRET)
          ENDIF
C         ON A PAS PU CALCULER LES DERIVEES DES FONCTIONS SINGULIERES
C         CAR ON SE TROUVE SUR LE FOND DE FISSURE
          CALL ASSERT(IRET.NE.0)
        ENDIF
C
C -     CALCUL DE LA DISTANCE A L'AXE (AXISYMETRIQUE)
C       ET DU DEPL. RADIAL
        IF (AXI) THEN
          R  = 0.D0
          UR = 0.D0
          DO 120 N=1,NNOP
            R  = R  + FF(N)*ZR(IGEOM-1+2*(N-1)+1)
            UR = UR + FF(N)*ZR(IDEPL-1+DDLS*(N-1)+1)
            DO 121 IG=1,NFH
              UR = UR + FF(N)
     &                     *ZR(IDEPL-1+DDLS*(N-1)+NDIM*IG+1)
 121        CONTINUE
            DO 122 IG=1,NFE
                UR = UR + FF(N)
     &                    *ZR(IDEPL-1+DDLS*(N-1)+NDIM*(NFH+IG)+1)
     &                    *FE(IG)

 122        CONTINUE

 120      CONTINUE

          CALL ASSERT(R.GT.0D0)
C          ATTENTION : LE POIDS N'EST PAS X R
C          CE SERA FAIT PLUS TARD AVEC JAC = JAC X R
        ENDIF

C       COORDONNÉES DU POINT DE GAUSS DANS L'ÉLÉMENT DE RÉF PARENT : XE
C       ET CALCUL DE FF, DFDI, DEPS ET EPS
        CALL REEREF(ELREFP,AXI,NNOP,NNOPS,ZR(IGEOM),XG,IDEPLP,GRDEPL,
     &              NDIM,HE,R,UR,FISNO,NFISS,NFH,NFE,DDLS,DDLM,
     &              FE,DGDGL,'OUI',XE,FF,DFDI,F,DEPS,RBID33)

        CALL REEREF(ELREFP,AXI,NNOP,NNOPS,ZR(IGEOM),XG,IDEPL,GRDEPL,
     &              NDIM,HE,R,UR,FISNO,NFISS,NFH,NFE,DDLS,DDLM,
     &              FE,DGDGL,'OUI',XE,FF,DFDI,F,EPS,RBID33)

C - CALCUL DES ELEMENTS GEOMETRIQUES

C       CALCUL DES PRODUITS SYMETR. DE F PAR N,
        DO 130 N=1,NNOP
C         FONCTIONS DE FORME CLASSIQUES
          DO 131 I=1,NDIM
            DEF(1,N,I) =  F(I,1)*DFDI(N,1)
            DEF(2,N,I) =  F(I,2)*DFDI(N,2)
            DEF(3,N,I) =  0.D0
            DEF(4,N,I) = (F(I,1)*DFDI(N,2) + F(I,2)*DFDI(N,1))/RAC2
            IF (NDIM.EQ.3) THEN
              DEF(3,N,I) =  F(I,3)*DFDI(N,3)
              DEF(5,N,I) = (F(I,1)*DFDI(N,3) + F(I,3)*DFDI(N,1))/RAC2
              DEF(6,N,I) = (F(I,2)*DFDI(N,3) + F(I,3)*DFDI(N,2))/RAC2
            ENDIF
 131      CONTINUE

C       TERME DE CORRECTION (3,3) AXI QUI PORTE EN FAIT SUR LE DDL 1
          IF (AXI) THEN
            DEF(3,N,1) = F(3,3) * FF(N)/R
          ENDIF

C         ENRICHISSEMENT PAR HEAVYSIDE
          DO 132 IG=1,NFH
            DO 133 I=1,NDIM
              CPT=NDIM*(1+IG-1)+I
              DO 134 M=1,2*NDIM
                DEF(M,N,CPT) =  DEF(M,N,I) * HE(FISNO(N,IG))
 134          CONTINUE
              IF (NDIM.EQ.2) THEN
                DEF(3,N,CPT) = 0.D0
              ENDIF
 133        CONTINUE

C  ATTENTION:TERME DE CORRECTION (3,3) AXI SUR LE DDL 1+NDIM*IG
            IF (AXI) THEN
              DEF(3,N,(1+NDIM*IG)) = F(3,3) * FF(N)/R * HE(FISNO(N,IG))
            ENDIF

  132      CONTINUE
C         ENRICHISSEMENT PAR LES NFE FONTIONS SINGULIÈRES
          DO 135 IG=1,NFE
            DO 136 I=1,NDIM
              CPT = NDIM*(1+NFH+IG-1)+I
              DEF(1,N,CPT) =  F(I,1) *
     &             (DFDI(N,1) * FE(IG) + FF(N)*DGDGL(IG,1))

              DEF(2,N,CPT) =  F(I,2) *
     &             (DFDI(N,2) * FE(IG) + FF(N)*DGDGL(IG,2))

              DEF(3,N,CPT) =  0.D0

              DEF(4,N,CPT) =
     &         ( F(I,1)* (DFDI(N,2)*FE(IG)+FF(N)*DGDGL(IG,2))
     &         + F(I,2)* (DFDI(N,1)*FE(IG)+FF(N)*DGDGL(IG,1)) )/RAC2
              IF (NDIM.EQ.3) THEN
                DEF(3,N,CPT) =  F(I,3) *
     &             (DFDI(N,3) * FE(IG) + FF(N)*DGDGL(IG,3))
                DEF(5,N,CPT) =
     &           ( F(I,1)* (DFDI(N,3)*FE(IG)+FF(N)*DGDGL(IG,3))
     &           + F(I,3)* (DFDI(N,1)*FE(IG)+FF(N)*DGDGL(IG,1)) )/RAC2
                DEF(6,N,CPT) =
     &           ( F(I,3)* (DFDI(N,2)*FE(IG)+FF(N)*DGDGL(IG,2))
     &           + F(I,2)* (DFDI(N,3)*FE(IG)+FF(N)*DGDGL(IG,3)) )/RAC2
              ENDIF
 136        CONTINUE

C   ATTENTION:TERME DE CORRECTION (3,3) AXI LE DDL 1+NDIM*(NFH+IG)
            IF (AXI) THEN
              DEF(3,N,(1+NDIM*(NFH+IG))) = F(3,3)* FF(N)/R * FE(IG)
            ENDIF
 135       CONTINUE

          CALL ASSERT(CPT.EQ.DDLD)

 130    CONTINUE
C
C       POUR CALCULER LE JACOBIEN DE LA TRANSFO SSTET->SSTET REF
C       ON ENVOIE DFDM2D OU DFDM3D AVEC LES COORD DU SS-ELT
        IF (NDIM.EQ.2) THEN
          CALL DFDM2D(NNO,KPG,IPOIDS,IDFDE,COORSE,RBID10,RBID10,JAC)
        ELSEIF (NDIM.EQ.3) THEN
          CALL DFDM3D(NNO,KPG,IPOIDS,IDFDE,COORSE,RBID4,RBID4,RBID4,JAC)
        ENDIF

        IF (AXI) THEN
          JAC= JAC * R
        ENDIF

C      CALCUL DES PRODUITS DE FONCTIONS DE FORMES (ET DERIVEES)
        IF ( (OPTION(1:10) .EQ. 'RIGI_MECA_'
     &  .OR.  OPTION(1: 9) .EQ. 'FULL_MECA'    ) .AND. GRDEPL) THEN
          DO 140 N=1,NNOP
            DO 141 M=1,N
              PFF(1,N,M) =  DFDI(N,1)*DFDI(M,1)
              PFF(2,N,M) =  DFDI(N,2)*DFDI(M,2)
              PFF(3,N,M) =  0.D0
              PFF(4,N,M) =(DFDI(N,1)*DFDI(M,2)+DFDI(N,2)*DFDI(M,1))/RAC2
              IF (NDIM.EQ.3) THEN
               PFF(3,N,M)= DFDI(N,3)*DFDI(M,3)
               PFF(5,N,M)=(DFDI(N,1)*DFDI(M,3)+DFDI(N,3)*DFDI(M,1))/RAC2
               PFF(6,N,M)=(DFDI(N,2)*DFDI(M,3)+DFDI(N,3)*DFDI(M,2))/RAC2
              ENDIF
 141        CONTINUE
 140      CONTINUE
        ENDIF


C - LOI DE COMPORTEMENT : S(E) ET DS/DE

C      CALL NMCPEL('RIGI',KPG,1,POUM,NDIM,TYPMOD,ANGMAS,IMATE,COMPOR,
C   &               CRIT,OPTION,EPS,SIGMA,VI(1,KPG),DSIDEP,CODRET)

        DO 150 M=1,3
          SIGN(M) = SIGM(M,KPG)
 150    CONTINUE
        DO 160 M=4,2*NDIM
          SIGN(M) = SIGM(M,KPG)*RAC2
 160    CONTINUE

        CALL R8INIR(6,0.0D0,SIGMA,1)
        CALL NMCOMP('XFEM',IDECPG+KPG,1,NDIM,TYPMOD,IMATE,COMPOR,CRIT,
     &              INSTAM,INSTAP,6,EPS,DEPS,6,SIGN,VI(1,KPG),OPTION,
     &              ANGMAS,10,ELGEOM(1,KPG),SIGMA,VIP(1,KPG),36,DSIDEP,
     &              1,RBID,CODRET)

C - CALCUL DE LA MATRICE DE RIGIDITE
C
        IF ( OPTION(1:10) .EQ. 'RIGI_MECA_'
     &  .OR. OPTION(1: 9) .EQ. 'FULL_MECA'    ) THEN
C
          DO 170 N=1,NNOP
            NN=DEC(N)

            DO 171 I=1,DDLD
              KKD = (NN+I-1) * (NN+I) /2
              DO 172 KL=1,2*NDIM
                SIGP(KL) = 0.D0
                DO 173 L=1,2*NDIM
                  SIGP(KL) = SIGP(KL)+DEF(L,N,I)*DSIDEP(L,KL)
 173            CONTINUE
 172          CONTINUE
              DO 174 J=1,DDLD
                DO 175 M=1,N
                  MN=DEC(M)

                  IF (M.EQ.N) THEN
                    J1 = I
                  ELSE
                    J1 = DDLD
                  ENDIF
C
C                 RIGIDITE GEOMETRIQUE
                  TMP1 = 0.D0
                  IF (GRDEPL .AND. I.EQ.J) THEN
                    TMP1 = 0.D0
                    DO 176 L=1,2*NDIM
                      TMP1 = TMP1+PFF(L,N,M)*SIGMA(L)
 176                CONTINUE

C                  TERME DE CORRECTION AXISYMETRIQUE
C                    IF (AXI .AND. I.EQ.1) THEN
C                      TMP1 = TMP1+ZR(IVF+N+(KPG-1)*NNO-1)*
C     &                           ZR(IVF+M+(KPG-1)*NNO-1)/(R*R)*SIGMA(3)
C                    ENDIF
                  ENDIF
C
C                 RIGIDITE ELASTIQUE
                  TMP2=0.D0
                  DO 177 L=1,2*NDIM
                    TMP2=TMP2+SIGP(L)*DEF(L,M,J)
 177              CONTINUE

C                 STOCKAGE EN TENANT COMPTE DE LA SYMETRIE
                  IF (J.LE.J1) THEN
                    MATUU(KKD+MN+J) = MATUU(KKD+MN+J) + (TMP1+TMP2)*JAC
                  ENDIF
C
 175            CONTINUE
 174          CONTINUE
 171        CONTINUE
 170      CONTINUE
        ENDIF
C

C - CALCUL DE LA FORCE INTERIEURE ET DES CONTRAINTES DE CAUCHY
C
        IF(OPTION(1:9).EQ.'FULL_MECA'.OR.
     &     OPTION(1:9).EQ.'RAPH_MECA') THEN

          DO 180 N=1,NNOP
            NN=DEC(N)
            DO 181 I=1,DDLD
              DO 182 L=1,2*NDIM
              ZR(IVECTU-1+NN+I)=
     &                       ZR(IVECTU-1+NN+I) + DEF(L,N,I)*SIGMA(L)*JAC
 182          CONTINUE
 181        CONTINUE
 180      CONTINUE

          IF (GRDEPL) THEN
C           CONVERSION LAGRANGE -> CAUCHY
            IF (CPLAN) F(3,3) = SQRT(ABS(2.D0*EPS(3)+1.D0))
            DETF = F(3,3) * (F(1,1)*F(2,2)-F(1,2)*F(2,1))
            IF (NDIM.EQ.3) THEN
              DETF = DETF - F(2,3)*(F(1,1)*F(3,2)-F(3,1)*F(1,2))
     &                    + F(1,3)*(F(2,1)*F(3,2)-F(3,1)*F(2,2))
            ENDIF
            DO 190 I = 1,2*NDIM
              SIG(I,KPG) = 0.D0
              DO 191 L = 1,2*NDIM
                FTF = (F(INDI(I),INDI(L))*F(INDJ(I),INDJ(L)) +
     &              F(INDI(I),INDJ(L))*F(INDJ(I),INDI(L)))*RIND(L)
                SIG(I,KPG) = SIG(I,KPG) + FTF*SIGMA(L)
 191          CONTINUE
              SIG(I,KPG) = SIG(I,KPG)/DETF
 190        CONTINUE
          ELSE
C           SIMPLE CORRECTION DES CONTRAINTES
            DO 200 L=1,3
              SIG(L,KPG) = SIGMA(L)
 200        CONTINUE
            DO 210 L=4,2*NDIM
              SIG(L,KPG) = SIGMA(L)/RAC2
 210        CONTINUE
          ENDIF
        ENDIF

 1000 CONTINUE

      END
