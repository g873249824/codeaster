       SUBROUTINE XMMSA3(NDIM,NNO,NNOS,FFP,NDDL,NVEC,V1,
     &                  V2,V3,NFH,SINGU,RR,DDLS,DDLM,
     &                  JFISNO,NFISS,IFISS,JHEAFA,NCOMPH,IFA,
     &                  SAUT)

      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER     NDIM,NNO,NNOS
      INTEGER     NFH,DDLS,DDLM
      INTEGER     SINGU,NVEC,NDDL,JFISNO,NFISS,IFISS,JHEAFA,NCOMPH,IFA
      REAL*8      SAUT(3),RR,FFP(27)
      REAL*8      V1(NDDL),V2(*),V3(*)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21

C
C ROUTINE CONTACT (METHODE XFEM HPP - CALCUL ELEM.)
C
C CALCUL DU SAUT
C
C ----------------------------------------------------------------------
C
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  NNO    : NOMBRE DE NOEUDS DE L'ELEMENT DE REF PARENT
C IN  NNOS   : NOMBRE DE NOEUDS SOMMET DE L'ELEMENT DE REF PARENT
C IN  FFP    : FONCTIONS DE FORME DE L'ELEMENT PARENT
C IN  NDDL   : NOMBRE TOTAL DE DDL DE L ELEMENT
C IN  NVEC   : NOMBRE VECTEURS DEPLACEMENT
C IN  VEC1   : PREMIER VECTEUR
C IN  VEC2   : DEUXIEME VECTEUR
C IN  VEC3   : TROISIEME VECTEUR
C IN  NFH    : NOMBRE DE FONCTIONS HEAVYSIDE
C IN  SINGU  : 1 SI ELEMENT SINGULIER, 0 SINON
C IN  RR     : DISTANCE AU FOND DE FISSURE
C IN  DDLS   : NOMBRE DE DDL (DEPL+CONTACT) À CHAQUE NOEUD SOMMET
C IN  DDLM   : NOMBRE DE DDL A CHAQUE NOEUD MILIEU
C I/O SAUT   : SAUT
C
C

C
      INTEGER I,J,IN,IFH,COEFI
      LOGICAL LMULTC
C
C ----------------------------------------------------------------------

      CALL ASSERT(NVEC.GT.0.AND.NVEC.LE.3)
      CALL VECINI(3,0.D0,SAUT)
      COEFI = 2
      LMULTC = NFISS.GT.1
      DO 161 I = 1,NNO
        CALL INDENT(I,DDLS,DDLM,NNOS,IN)
        DO 164 IFH=1,NFH
          IF (LMULTC) THEN
            COEFI = ZI(JHEAFA-1+NCOMPH*(NFISS*(IFISS-1)
     &                +ZI(JFISNO-1+NFH*(I-1)+IFH)-1)+2*IFA)
     &            - ZI(JHEAFA-1+NCOMPH*(NFISS*(IFISS-1)
     &                +ZI(JFISNO-1+NFH*(I-1)+IFH)-1)+2*IFA-1)
          ENDIF
          DO 162 J = 1,NDIM
            SAUT(J) = SAUT(J) - COEFI*FFP(I)*V1(IN+NDIM*IFH+J)
            IF(NVEC.GE.2) 
     &        SAUT(J) = SAUT(J) - COEFI*FFP(I)*V2(IN+NDIM*IFH+J)
            IF(NVEC.EQ.3) 
     &        SAUT(J) = SAUT(J) - COEFI*FFP(I)*V3(IN+NDIM*IFH+J)
 162      CONTINUE
 164    CONTINUE
        DO 163 J = 1,SINGU*NDIM
           SAUT(J) = SAUT(J)-COEFI*FFP(I)*RR*V1(IN+NDIM*(1+NFH)+J)
           IF(NVEC.GE.2) 
     &        SAUT(J) = SAUT(J)-COEFI*FFP(I)*RR*V2(IN+NDIM*(1+NFH)+J)
           IF(NVEC.EQ.3) 
     &        SAUT(J) = SAUT(J)-COEFI*FFP(I)*RR*V3(IN+NDIM*(1+NFH)+J)
 163    CONTINUE
 161  CONTINUE

      END
