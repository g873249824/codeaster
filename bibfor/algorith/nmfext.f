      SUBROUTINE NMFEXT(ETA   ,FONACT,SDDYNA,VEASSE,CNFEXT)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      REAL*8        ETA
      INTEGER       FONACT(*)
      CHARACTER*19  SDDYNA
      CHARACTER*19  VEASSE(*)
      CHARACTER*19  CNFEXT
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME)
C
C RESULTANTE DES EFFORTS EXTERIEURS 
C      
C ----------------------------------------------------------------------
C
C
C IN  SDDYNA : SD DYNAMIQUE
C IN  FONACT : FONCTIONNALITES ACTIVEES
C IN  ETA    : COEFFICIENT DE PILOTAGE
C IN  VEASSE : VARIABLE CHAPEAU POUR NOM DES VECT_ASSE
C OUT CNFEXT : CHARGEMENT EXTERIEUR RESULTANT
C
C
C
C
      INTEGER      IFM,NIV
      CHARACTER*19 CNUNIL,CNCTDC
      CHARACTER*19 CNFFDO,CNFFPI,CNFVDO,CNVADY
      LOGICAL      ISFONC,LCTCD,LUNIL
      REAL*8       NDYNRE,COEEQU 
      LOGICAL      NDYNLO,LDYNA,LALLV       
      INTEGER      IFDO,N
      CHARACTER*19 VECT(20)
      REAL*8       COEF(20)        
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE> CALCUL DES FORCES EXTERNES' 
      ENDIF 
C
C --- FONCTIONNALITES ACTIVEES
C    
      LCTCD  = ISFONC(FONACT,'CONT_DISCRET'    )
      LUNIL  = ISFONC(FONACT,'LIAISON_UNILATER') 
      LALLV  = ISFONC(FONACT,'CONT_ALL_VERIF'  )     
C
C --- INITIALISATIONS
C
      IFDO   = 0   
      CNFFDO = '&&CNCHAR.FFDO'
      CNFFPI = '&&CNCHAR.FFPI'
      CNFVDO = '&&CNCHAR.FVDO'                   
      CNVADY = '&&CNCHAR.FVDY'         
      LDYNA  = NDYNLO(SDDYNA,'DYNAMIQUE')             
      CALL VTZERO(CNFEXT)
C
C --- FORCES DE CONTACT DISCRET
C
      IF (LCTCD.AND.(.NOT.LALLV)) THEN
        CALL NMCHEX(VEASSE,'VEASSE','CNCTDC',CNCTDC) 
        IFDO       = IFDO + 1 
        COEF(IFDO) = -1.D0   
        VECT(IFDO) = CNCTDC
      ENDIF      
C
C --- FORCES DE LIAISON_UNILATER
C
      IF (LUNIL) THEN
        CALL NMCHEX(VEASSE,'VEASSE','CNUNIL',CNUNIL) 
        IFDO       = IFDO + 1 
        COEF(IFDO) = -1.D0   
        VECT(IFDO) = CNUNIL
      ENDIF
C
C --- CALCUL DU VECTEUR DES CHARGEMENTS FIXES        (NEUMANN)      
C       
      CALL NMASFI(FONACT,SDDYNA,VEASSE,CNFFDO,CNFFPI)
C      
C --- CALCUL DU VECTEUR DES CHARGEMENTS VARIABLES    (NEUMANN)     
C
      CALL NMASVA(SDDYNA,VEASSE,CNFVDO)
C      
C --- CALCUL DU VECTEUR DES CHARGEMENTS VARIABLES DYNAMIQUES (NEUMANN)
C
      IF (LDYNA) THEN
        COEEQU = NDYNRE(SDDYNA,'COEF_MPAS_EQUI_COUR') 
        CALL NDASVA('CORR',SDDYNA,VEASSE,CNVADY)      
      ENDIF                
C
C --- CHARGEMENTS EXTERIEURS DONNEES
C
      IFDO       = IFDO + 1 
      COEF(IFDO) = 1.D0   
      VECT(IFDO) = CNFFDO
      IFDO       = IFDO + 1 
      COEF(IFDO) = 1.D0   
      VECT(IFDO) = CNFVDO
C
C --- CHARGEMENTS EXTERIEURS PILOTES
C      
      IFDO       = IFDO + 1 
      COEF(IFDO) = ETA   
      VECT(IFDO) = CNFFPI 
C
C --- TERMES DE RAPPEL DYNAMIQUE
C      
      IF (LDYNA) THEN
        IFDO    = IFDO + 1 
        COEF(IFDO) = COEEQU
        VECT(IFDO) = CNVADY
      ENDIF          
C
C --- VECTEUR RESULTANT 
C     
      IF (IFDO.GT.20) THEN
        CALL ASSERT(.FALSE.)
      ENDIF 
      DO 10 N = 1,IFDO
        CALL VTAXPY(COEF(N),VECT(N),CNFEXT)   
 10   CONTINUE  
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        CALL NMDEBG('VECT',CNFEXT,IFM   )      
      ENDIF 
C
      CALL JEDEMA()
      END
