      SUBROUTINE NMFEXT(NUMEDD,ETA,SECMBR,RESOCO,DEFICO,CNFEXT)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 14/02/2006   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE MABBAS M.ABBAS
      IMPLICIT NONE
      REAL*8        ETA
      CHARACTER*19  CNFEXT
      CHARACTER*24  SECMBR,NUMEDD
      CHARACTER*(*) RESOCO,DEFICO

C ----------------------------------------------------------------------
C
C COMMANDE STAT_NON_LINE : RESULTANTE DES EFFORTS EXTERIEURS
C
C ----------------------------------------------------------------------
C     IN   NUMEDD : NUME_DDL
C     IN   ETA    : COEFFICIENT DE PILOTAGE
C     IN   SECMBR : VECTEURS ASSEMBLES DES CHARGEMENTS
C     IN   RESOCO : SD CONTACT (POUR REACTIONS DE CONTACT)
C     IN   DEFICO : SD CONTACT (POUR REACTIONS DE CONTACT)
C IN/JXOUT CNFEXT : RESULTANTE DES EFFORTS EXTERIEURS
C
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      INTEGER      NEQ,I,ICONTA
      INTEGER      JFEDO, JFEPI, JFSDO, JFSPI, JFEXT, JATMU, IRET
      CHARACTER*8  K8BID
      CHARACTER*24 CNFEDO, CNFEPI, CNDIDO, CNDIPI, CNFSDO, CNFSPI
      CHARACTER*24 K24BID
      CHARACTER*14 RESOCZ     
C      
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      RESOCZ = RESOCO
C
C --- DEMONTAGE DES SECONDS MEMBRES      
C      
      CALL DESAGG (SECMBR, CNFEDO, CNFEPI, CNDIDO, CNDIPI,
     &                     CNFSDO, CNFSPI, K24BID, K24BID)
C
C --- CREATION DU CHAM_NO CNFEXT S'IL N'EXISTE PAS
C
      CALL EXISD('CHAMP_GD',CNFEXT,IRET)
      IF (IRET.EQ.0) THEN 
        CALL VTCREB(CNFEXT, NUMEDD, 'V', 'R',NEQ)
      ENDIF     
C
C --- REMPLISSAGE DU CHAM_NO CNFEXT COMME SOMME DES SECONDS MEMBRES
C
      CALL JEVEUO(CNFEDO(1:19) // '.VALE', 'L', JFEDO)
      CALL JEVEUO(CNFEPI(1:19) // '.VALE', 'L', JFEPI)
      CALL JEVEUO(CNFSDO(1:19) // '.VALE', 'L', JFSDO)
      CALL JEVEUO(CNFSPI(1:19) // '.VALE', 'L', JFSPI)
      CALL JEVEUO(CNFEXT       // '.VALE', 'E', JFEXT)
      CALL JELIRA(CNFEDO(1:19) // '.VALE', 'LONMAX', NEQ, K8BID)

      DO 10 I = 0, NEQ-1
        ZR(JFEXT+I) = ZR(JFEDO+I) + ZR(JFSDO+I)
     &                + ETA * (ZR(JFEPI+I) + ZR(JFSPI+I))
 10   CONTINUE 
C
C --- REACTIONS DE CONTACT (METHODES DISCRETES)
C     
      CALL JEEXIN(RESOCZ // '.ATMU', ICONTA)
      IF (ICONTA.NE.0) THEN
        CALL JEVEUO(RESOCZ // '.ATMU','L',JATMU)
        DO 20 I = 0, NEQ-1
          ZR(JFEXT+I) = ZR(JFEXT+I) - ZR(JATMU+I)
 20     CONTINUE
      END IF

      CALL JEDEMA()
      END
