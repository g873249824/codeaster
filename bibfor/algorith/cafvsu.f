      SUBROUTINE CAFVSU(CONT,TANGE,MAXFA,NFACE,FKS,DFKS1,DFKS2,
     &     MOBFA,DMOB1,DMOB2,DMOB1V,DMOB2V,FLUX,DFLX1,DFLX2,
     &     DFLX1V,DFLX2V,NBVOIS,NVOIMA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 23/03/2010   AUTEUR ANGELINI O.ANGELINI 
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C============================================================       
C  CETTE SUBROUTINE PERMET DE CALCULER D UNE MANIERE 
C  GENERIQUE LE FLUX TOTAL VOLUMIQUE
C============================================================
C ****IN :
C     FKS(IFA)          : FLUX SUR IFA
C     DFKS1(1,IFA)      : DERIVEE DE FKS(IFA) PAR RAPPORT A LA PREMIERE 
C                        INCONNUE AU CENTRE
C     DFKS1(JFA+1,IFA)  : DERIVEE DE FKS(IFA) PAR RAPPORT A LA PREMIERE
C                        INCONNUE FACE
C     DFKS2(1,IFA)     : DERIVEE DE FKS(IFA) PAR RAPPORT A LA DEUXIEME
C                        INCONNUE AU CENTRE
C     DFKS2(JFA+1,IFA) : DERIVEE DE FKS(IFA) PAR RAPPORT A LA DEUXIEME 
C                        INCONNUE FACE
C     MOBFA            : MOBILITE  
C     DMOB1            : DERIVEE DE LA MOBILITE PAR RAPPORT A LA 
C                        PREMIERE VARIABLE
C     DMOB2            : DERIVEE DE LA MOBILITE PAR RAPPORT A LA 
C                        SECONDE VARIABLE
   
C ****IN-OUT :  
C     FLUX            : SOMME DES FLUX DE DARCY ENTRANT DASN MAILLE
C     DFLX1           : DERIVEE DU FLUX PAR RAPPORT A LA PREMIERE
C                       VARIABLE
C            DFLX1(1)   = DFLUX/ VARIABLE 1 AU CENTRE MAILLE COURANTE
C            DFLX1(I+1) = DFLUX/ VARIABLE 1 SUR FACE I
C     DFLX2          :DERIVEE DU FLUX PAR RAPPORT A LA SECONDE 
C                      VARIABLE
C            DFLX2(1)   = DFLUX/ VARIABLE 2 AU CENTRE MAILLE COURANTE
C            DFLX2(I+1) = DFLUX/ VARIABLE 2 SUR FACE I
C     DFLX1V(I) = DFLUX / VARIABLE 1 AU CENTRE DU VOISIN DE LA FACE I
C     DFLX2V(I) = DFLUX / VARIABLE 2 AU CENTRE DU VOISIN DE LA FACE I
C ================================================
C     FLUX = SOMME ( MOBFA * F_{K,SIGMA})
C     SI PAS DE DECENTRAGE
C        DFLUX/DPK=DMOBFA/DPK* F_{K,SIGMA} +MOBFA* DF_{K,SIGMA}/DPK
C        DFLUX/DP_{K,SIGMA}=MOBFA* DF_{K,SIGMA}/DP_{K,SIGMA}
C     SI DECENTRAGE
C        DFLUX/DPL=DMOBFA/DPL* F_{K,SIGMA}
C        DFLUX/DP_{L,SIGMA}=0
C================================================
C     REMARQUE :
C        POUR CALCULER LE FLUX ON BOUCLE SUR LES IVOIS CAR ON A PEUT 
C        ETRE DECENTRE LA MOBILITÉ ALORS QUE POUR CALCULER DFLX1 ET
C        DFLX2 ON REGARDE UNIQUEMENT LA DERIVEE DE LA MOBILITE SUR 
C        L ELEMENT COURANT(0) CAR SI IL Y A DU DECENTRAGE ON VA STOCKER 
C        DANS DFLX1V ET DFLX2 
      IMPLICIT NONE
      LOGICAL       CONT,TANGE
      INTEGER       MAXFA
      INTEGER       NFACE
      INTEGER       NBVOIS,NVOIMA
      REAL*8        FLUX
      REAL*8        FKS(1:NFACE)
      REAL*8        MOBFA(0:NVOIMA,1:NFACE)
      REAL*8        DMOB1(0:NVOIMA,1:NFACE),DMOB2(0:NVOIMA,1:NFACE)
      REAL*8        DMOB1V(0:NVOIMA,1:NFACE),DMOB2V(0:NVOIMA,1:NFACE)
      REAL*8        DFLX1(1:NFACE+1),DFLX2(1:NFACE+1)
      REAL*8        DFLX1V(1:NFACE), DFLX2V(1:NFACE)
      REAL*8        DFKS1(1:MAXFA+1,NFACE), DFKS2(1:MAXFA+1,NFACE)
      INTEGER       IFA,JFA,IVOIS
      IF ( CONT ) THEN
         DO  1 IVOIS = 0,NBVOIS
            DO 2 IFA = 1,NFACE
               FLUX = FLUX + MOBFA(IVOIS,IFA) * FKS(IFA)
   2        CONTINUE
   1     CONTINUE
      ENDIF
      IF ( TANGE) THEN
         DO 3 JFA = 1,NFACE
            DFLX1(1) = DFLX1(1) + DMOB1(0,JFA) * FKS(JFA)
            DFLX2(1) = DFLX2(1) + DMOB2(0,JFA) * FKS(JFA)
            DO  31 IVOIS=0,NBVOIS
               DFLX1(1) = DFLX1(1) + MOBFA(IVOIS,JFA) * DFKS1(1,JFA)
               DFLX2(1) = DFLX2(1) + MOBFA(IVOIS,JFA) * DFKS2(1,JFA)
   31       CONTINUE 
   3     CONTINUE   
         DO 4 IFA = 1,NFACE
            DO 4 JFA = 1,NFACE
               DO 41 IVOIS = 0,NBVOIS
                  DFLX1(1+IFA) = DFLX1(1+IFA) + MOBFA(IVOIS,JFA)
     >                         * DFKS1(IFA+1,JFA)
                  DFLX2(1+IFA) = DFLX2(1+IFA) + MOBFA(IVOIS,JFA)
     >                         * DFKS2(IFA+1,JFA)
   41          CONTINUE                      
   4     CONTINUE  
         DO 5 IVOIS = 1,NBVOIS          
            DO 5 JFA = 1,NFACE       
               DFLX1V(JFA) = DFLX1V(JFA) + DMOB1V(IVOIS,JFA) * FKS(JFA)
               DFLX2V(JFA) = DFLX2V(JFA) + DMOB2V(IVOIS,JFA) * FKS(JFA)
   5     CONTINUE 
      ENDIF
      END
