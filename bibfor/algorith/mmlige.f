      SUBROUTINE MMLIGE(NOMA  ,DEFICO,TYPELT,NBTYP ,COMPT ,
     &                  COMPF ,NNDTOT,NBGREL)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 23/09/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO
      CHARACTER*24 TYPELT
      INTEGER      NBTYP
      INTEGER      COMPT(NBTYP),COMPF(NBTYP) 
      INTEGER      NNDTOT,NBGREL    
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - CREATION OBJETS - LIGREL)
C
C LISTE DES ELEMENTS DE CONTACT TARDIF
C      
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C OUT TYPELT : LISTE DES NOUVELLES MAILLES
C                 * NUMERO DU TYPE DE MAILLE
C                 * NOMBRE DE NOEUDS DU TYPE DE MAILLE
C IN  NBTYP  : NOMBRE DE TYPE D'ELEMENTS DE CONTACT DISPONIBLES
C OUT COMPT  : VECTEUR DU NOMBRE D'ELEMENT POUR CHAQUE TYPE
C OUT COMPF  : VECTEUR TYPE CONTACT OU FROTTEMENT POUR CHAQUE TYPE
C                SI COMPF(IELT) = 0 CONTACT
C                SI COMPF(IELT) = 1 FROTTEMENT
C OUT NNDTOT : NOMBRE DE NOEUDS TOTAL 
C OUT NBGREL : NOMBRE DE GREL
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
      CHARACTER*32 JEXNUM
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      INTEGER      CFMMVD,ZTABF
      INTEGER IPC,NTPC,ITYP,IBID
      INTEGER JTYMAI,JTYNMA
      INTEGER NUMMAM,NUMMAE,IZONE
      INTEGER      CFDISI,NDIM
      INTEGER      NNDEL,NUMTYP,MMELTN
      CHARACTER*8  NTYMAE,NTYMAM
      INTEGER      ITYMAE,ITYMAM
      CHARACTER*24 TABFIN
      INTEGER      JTABF
      INTEGER      IFM,NIV
      LOGICAL      LFROTT
      CHARACTER*24 K24BLA,K24BID
      REAL*8       R8BID
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)   
C
C --- ACCES OBJETS
C
      TABFIN = DEFICO(1:16)//'.TABFIN' 
      CALL JEVEUO(TABFIN,'L',JTABF)
      CALL JEVEUO(NOMA//'.TYPMAIL','L',JTYMAI)   
      ZTABF = CFMMVD('ZTABF')
C
C --- INITIALISATIONS
C
      DO 10 ITYP = 1,NBTYP
        COMPT(ITYP) = 0
        COMPF(ITYP) = 0
   10 CONTINUE
      NTPC   = NINT(ZR(JTABF-1+1))
      IZONE  = 0
      NDIM   = CFDISI(DEFICO,'NDIM',IZONE)
      K24BLA = ' '
      NNDTOT = 0
      NBGREL = 0
C      
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> ... NOMBRE TOTAL D''ELEMENTS :',NTPC 
      ENDIF
C
C --- CREATION DU VECTEUR
C --- DEUX ENTIERS PAR NOUVELLE MAILLE :
C       * NUMERO DU TYPE DE MAILLE
C       * NOMBRE DE NOEUDS DU TYPE DE MAILLE
C
      CALL WKVECT(TYPELT,'V V I',2*NTPC,JTYNMA)
C
C --- BOUCLE SUR LES POINTS DE CONTACT
C
      DO 20 IPC = 1,NTPC
C      
C --- TYPE MAILLES MAITRE/ESCLAVE
C
        NUMMAE  = NINT(ZR(JTABF+ZTABF*(IPC-1)+1))
        NUMMAM  = NINT(ZR(JTABF+ZTABF*(IPC-1)+2))
C
C --- INFOS MAILLES MAITRE/ESCLAVE
C
        IZONE   = NINT(ZR(JTABF+ZTABF*(IPC-1)+15))
        CALL MMINFP(IZONE ,DEFICO,K24BLA,'FROTTEMENT_ZONE',
     &              IBID  ,R8BID ,K24BID,LFROTT)
        ITYMAE  = ZI(JTYMAI-1+NUMMAE)
        ITYMAM  = ZI(JTYMAI-1+NUMMAM)
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYMAE),NTYMAE)
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYMAM),NTYMAM)
C      
C --- TYPE MAILLE DE CONTACT CREEE
C
        CALL MMELEL(NDIM  ,0     ,NTYMAE,NTYMAM,ITYP  ,
     &              NNDEL ,NUMTYP)  
        ZI(JTYNMA-1+2*(IPC-1)+1) = NUMTYP
        ZI(JTYNMA-1+2*(IPC-1)+2) = NNDEL
C
C --- ELEMENT DE TYPE FROTTEMENT OU CONTACT
C
        IF (LFROTT)  THEN 
          COMPF(ITYP) = 1
        ELSE
          COMPF(ITYP) = 0        
        ENDIF
C          
C --- COMPTEUR DE CE TYPE D'ELEMENT DE CONTACT
C
        COMPT(ITYP) = COMPT(ITYP) + 1

   20 CONTINUE
C
C --- ON COMPTE LE NOMBRE DE NOEUDS A STOCKER AU TOTAL
C
      DO 170 ITYP = 1,NBTYP
        NNDEL  = MMELTN(ITYP)
        NNDTOT = NNDTOT + COMPT(ITYP)*(NNDEL+1)
  170 CONTINUE
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> ... NOMBRE TOTAL DE NOEUDS :',NNDTOT 
      ENDIF      
C
C --- ON COMPTE LE NOMBRE DE GREL
C 
      DO 60 ITYP = 1,NBTYP
        IF (COMPT(ITYP).GT.0) THEN
          NBGREL = NBGREL + 1
        ENDIF
   60 CONTINUE    
C
      CALL JEDEMA()
      END
