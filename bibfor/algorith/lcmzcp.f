      SUBROUTINE LCMZCP(FAMI, KPG,    KSP, NDIM, IMATE,
     &                  EPSM, DEPS,   VIM, TM,   TP,
     &                  TREF, OPTION, SIG, VIP,  DSIDEP)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 02/04/2013   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
      IMPLICIT NONE
      CHARACTER*16   OPTION
      CHARACTER*(*)  FAMI
      INTEGER        NDIM,IMATE,KPG,KSP
      REAL*8         EPSM(*),DEPS(*),VIM(*),TM,TP,TREF
      REAL*8         SIG(*),VIP(*),DSIDEP(6,6)
C --- ------------------------------------------------------------------
C
C                 LOI DE COMPORTEMENT : MAZARS EN CP
C
C --- ------------------------------------------------------------------
C
C IN
C     FAMI    : FAMILLE DE POINT DE GAUSS (RIGI,MASS,...)
C     KPG     : NUMERO DU POINT DE GAUSS
C     KSP     : NUMERO DU SOUS-POINT
C     NDIM    : DIMENSION DE L'ESPACE
C     IMATE   : MATERIAU
C     EPSM    : DEFORMATION EN T-
C     DEPS    : INCREMENT DE DEFORMATION
C     VIM     : VARIABLES INTERNES EN T-
C     TM      : TEMPERATURE EN T-
C     TP      : TEMPERATURE EN T+
C     TREF    : TEMPERATURE DE REFERENCE
C     OPTION  : OPTION DEMANDEE
C                 RIGI_MECA_TANG ->     DSIDEP      --> RIGI
C                 FULL_MECA      -> SIG DSIDEP VIP  --> RIGI  RESI
C                 RAPH_MECA      -> SIG        VIP  -->       RESI
C OUT
C     SIG     : CONTRAINTE
C     VIP     : VARIABLES INTERNES en T+
C     DSIDEP  : MATRICE TANGENTE
C
C --- ------------------------------------------------------------------
C     VARIABLES INTERNES
C        1  -> ICELS  : CRITERE SIGMA
C        2  -> ICELU  : CRITERE EPSI
C        3  -> IDOMM  : ENDOMMAGEMENT
C        4  -> IEPSQT : VALEUR DE EPSEQP DE TRACTION
C        5  -> IEPSQC : VALEUR DE EPSEQP DE COMPRESSION
C        6  -> IRSIGM : FACTEUR DE TRIAXIALITE EN CONTRAINTE
C        7  -> ITEMP  : TEMPERATURE MAXIMALE ATTEINTE PAR LE MATERIAU
C        8  -> IDISSD : DISSIPATION D'ENDOMMAGEMENT
C --- ------------------------------------------------------------------
C     INDEX DES VARIABLES INTERNES
      INTEGER        ITEMP
      PARAMETER     (ITEMP=7)
C
      INTEGER        NBVAL
      PARAMETER     (NBVAL=8)
      INTEGER        ICODRE(NBVAL)
      CHARACTER*8    NOMRES(NBVAL),MAZARS(NBVAL)
      REAL*8         VALRES(NBVAL+1)

      LOGICAL        RESI
      INTEGER        NDIMSI,IRET,IISNAN
      REAL*8         EPSTHE,KDESS,BENDO,R8NNEM,EPSANE
      REAL*8         EE,NU
      REAL*8         TEMP,TMAX,TMAXM,HYDR,SECH,SREF
      REAL*8         VALPAR
      CHARACTER*1    POUM
      CHARACTER*8    NOMPAR
      CHARACTER*30   VALKM(3)

      DATA MAZARS /'EPSD0','K','AC','BC','AT','BT',
     &             'SIGM_LIM','EPSI_LIM'/

C
C ----------------------------------------------------------------------
C
C       RIGI  = (OPTION(1:4).EQ.'RIGI' .OR. OPTION(1:4).EQ.'FULL')
      RESI  = (OPTION(1:4).EQ.'RAPH' .OR. OPTION(1:4).EQ.'FULL')
C
      NDIMSI = 2*NDIM
C
C     DETERMINATION DE LA TEMPERATURE DE REFERENCE (TMAX) ET
C     DES CONDITIONS D HYDRATATION OU DE SECHAGE
      TMAXM = VIM(ITEMP)
      CALL RCVARC(' ','SECH','REF',FAMI,KPG,KSP,SREF,IRET)
      IF ( IRET.NE.0) SREF = 0.0D0
      IF (RESI) THEN
         TEMP = TP
         CALL RCVARC(' ','HYDR','+',FAMI,KPG,KSP,HYDR,IRET)
         IF ( IRET.NE.0) HYDR = 0.0D0
         POUM='+'
         CALL RCVARC(' ','SECH','+',FAMI,KPG,KSP,SECH,IRET)
         IF ( IRET.NE.0) SECH = 0.0D0
         IF (IISNAN(TP).GT.0) THEN
            TMAX   = R8NNEM()
            VIP(ITEMP) = 0.0D0
         ELSE
            TMAX = MAX(TMAXM, TP)
            IF (TMAX.GT.TMAXM) VIP(ITEMP) = TMAX
         ENDIF
      ELSE
         TEMP = TM
         CALL RCVARC(' ','HYDR','-',FAMI,KPG,KSP,HYDR,IRET)
         IF ( IRET.NE.0) HYDR = 0.0D0
         CALL RCVARC(' ','SECH','-',FAMI,KPG,KSP,SECH,IRET)
         IF ( IRET.NE.0) SECH = 0.0D0
         POUM='-'
         TMAX = TMAXM
      ENDIF

C     RECUPERATION DES CARACTERISTIQUES MATERIAUX QUI PEUVENT VARIER
C     AVEC LA TEMPERATURE (MAXIMALE), L'HYDRATATION OU LE SECHAGE
      NOMPAR = 'TEMP'
      VALPAR = TMAX
C     LECTURE DES CARACTERISTIQUES ELASTIQUES
      NOMRES(1) = 'E'
      NOMRES(2) = 'NU'
      NOMRES(3) = 'ALPHA'
      CALL RCVALB(FAMI,KPG,KSP,POUM,IMATE,' ','ELAS',1,
     &            NOMPAR,VALPAR,2,NOMRES,VALRES,ICODRE,1)
      CALL RCVALB(FAMI,KPG,KSP,POUM,IMATE,' ','ELAS',1,NOMPAR,
     &            VALPAR,1,NOMRES(3),VALRES(3),ICODRE(3),0)
      IF ((IISNAN(TP).EQ.0).AND.(IISNAN(TM).EQ.0)) THEN
         IF ((IISNAN(TREF).NE.0).OR.(ICODRE(3).NE.0)) THEN
            CALL U2MESS('F','CALCULEL_15')
         ELSE
            EPSTHE = VALRES(3)*(TEMP-TREF)
         ENDIF
      ELSE
         VALRES(3) = 0.0D0
         EPSTHE    = 0.0D0
      ENDIF
      EE    = VALRES(1)
      NU    = VALRES(2)

C     LECTURE DU RETRAIT ENDOGENE ET RETRAIT DE DESSICCATION
      NOMRES(1)='B_ENDOGE'
      NOMRES(2)='K_DESSIC'
      CALL RCVALA(IMATE,' ','ELAS',0,' ',0.0D0,2,
     &            NOMRES,VALRES,ICODRE, 0)
      IF (ICODRE(1).NE.0) VALRES(1) = 0.0D0
      IF (ICODRE(2).NE.0) VALRES(2) = 0.0D0
      BENDO = VALRES(1)
      KDESS = VALRES(2)
C     DEFORMATION ANELASTIQUE : THERMIQUE, SECHAGE, HYDRATATION
      EPSANE = (EPSTHE-KDESS*(SREF-SECH)-BENDO*HYDR)
C
C --- ON RECUPERE LES PARAMETRES MATERIAU
      CALL R8INIR(NBVAL,0.D0,VALRES,1)
      CALL RCVALB(FAMI,KPG,KSP,POUM,IMATE,' ','MAZARS',
     &            1,NOMPAR,VALPAR,NBVAL,MAZARS,VALRES,ICODRE,1)
      IF ( ICODRE(7)+ICODRE(8).NE.0 ) THEN
         VALKM(1)='MAZARS_GC'
         VALKM(2)=MAZARS(7)
         VALKM(3)=MAZARS(8)
         CALL U2MESK('F','COMPOR1_76',3,VALKM)
      ENDIF
C --- AJOUT DE NU DANS VALRES
      VALRES(NBVAL+1) = NU
C     LOI DE MAZARS EN CONTRAINTES PLANES
      CALL MAZACP(OPTION,NDIMSI,EPSM,DEPS,EPSANE,
     &            EE,VALRES,VIM,VIP,SIG,
     &            DSIDEP)
      END
