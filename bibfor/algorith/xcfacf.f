      SUBROUTINE XCFACF(PTINT,PTMAX,IPT,AINTER,LSN,LST,IGEOM,NNO,NDIM,
     &                                              TYPMA,NOMA,NMAABS)
      IMPLICIT NONE

      INCLUDE 'jeveux.h'
      INTEGER       PTMAX,IPT,IGEOM,NNO,NDIM,NMAABS
      REAL*8        LSN(NNO),LST(NNO),PTINT(*),AINTER(*)
      CHARACTER*8   TYPMA,NOMA

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/04/2013   AUTEUR JAUBERT A.JAUBERT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C              TROUVER LES PTS D'INTERSECTION ENTRE LE FOND DE FISSURE
C                 ET LES FACES POUR LES ELEMENTS EN FOND DE FISSURE
C
C     ENTREE
C       PTINT    : COORDONNEES DES POINTS D'INTERSECTION
C       PTMAX    : NOMBRE MAX DE POINTS D'INTERSECTION
C       IPT      : COMPTEUR DE NOMBRE DE POINTS D'INTERSECTION
C       AINTER   : INFOS SUR LES ARETES ASSOCIEES
C       LSN      : VALEURS DE LA LEVEL SET NORMALE
C       LST      : VALEURS DE LA LEVEL SET TANGENTE
C       IGEOM    : ADRESSE DES COORDONNEES DES NOEUDS DE L'ELT PARENT
C       NNO      : NOMBRE DE NOEUDS DE L'ELEMENT
C       NDIM     : DIMENSION DE L'ESPACE
C       TYPMA    : TYPE DE LA MAILLE ASSOCIEE A L'ELEMENT
C       NOMA     : NOM DU MAILLAGE
C       NMAABS   : INDICE DE LA MAILLE
C
C     SORTIE
C       PTINT    : COORDONNEES DES POINTS D'INTERSECTION
C       IPT      : COMPTEUR DE NOMBRE DE POINTS D'INTERSECTION
C       AINTER   : INFOS SUR LES ARETES ASSOCIEES
C
C     ------------------------------------------------------------------
C
      CHARACTER*8   ELREF
      REAL*8        R8MAEM,RBID,MAXLSN,MINLSN,MAXLST,MINLST
      REAL*8        R8PREM,A(3),B(3),C(3)
      REAL*8        LONCAR,PADIST,DST
      REAL*8        M(3)
      INTEGER       I,NBF,IBID,IFQ,J,CODRET
      INTEGER       FA(6,4),IBID3(12,3),INDPTF(3)
C ----------------------------------------------------------------------

      CALL JEMARQ()
      CALL ELREF1(ELREF)

C     INITIALISATION DES MIN ET MAX
      MAXLSN=-1.D0*R8MAEM()
      MINLSN=R8MAEM()
      MAXLST=-1.D0*R8MAEM()
      MINLST=R8MAEM()

C     RECHERCHE DU MIN ET MAX DES LEVEL SETS SUR LES NOEUDS
      DO 100 I=1,NNO
        MAXLSN=MAX(LSN(I),MAXLSN)
        MAXLST=MAX(LST(I),MAXLST)
        MINLSN=MIN(LSN(I),MINLSN)
        MINLST=MIN(LST(I),MINLST)
 100  CONTINUE

C     SI CE N'EST PAS UN ELEMENT EN FOND DE FISSURE, ON SORT
C     EN FAIT, CE TEST NE PERMET PAS DE DETECTER TOUS LES CAS
C     IL SE PEUT DONC QUE L'ON NE SORTE PAS MAIS QUE L'ON NE
C     SOIT PAS SUR UNE ELEMENT EN FOND DE FISSURE
      IF (MINLSN*MAXLSN.GE.0.D0.OR.MINLST*MAXLST.GE.0.D0) GOTO 9999

      CALL CONFAC(TYPMA,IBID3,IBID,FA,NBF)

C     BOUCLE SUR LES FACES
      DO 200 IFQ=1,NBF

C       RECHERCHE DES INTERSECTION ENTRE LE FOND DE FISSURE ET LA FACE
        CALL INTFAC(NOMA,NMAABS,IFQ,FA,NNO,LST,LSN,NDIM,'NON',IBID,
     &              IBID,IGEOM,M,INDPTF,RBID,RBID,CODRET)
        IF (CODRET .EQ. 0) GOTO 200

C       POUR IGNORER LES POINTS CONFONDUS AVEC CEUX
C       DETECTES DANS XCFACE LORSQUE LE PT EST EXACT SUR UNE ARETE
        DO 250 J=1,IPT
          DST=PADIST(NDIM,M,PTINT(NDIM*(J-1)+1))
          IF (DST.LE.R8PREM()) GOTO 200
 250    CONTINUE

C       LONGUEUR CARACTERISTIQUE
        DO 260 I=1,NDIM
          A(I) =  ZR(IGEOM-1+NDIM*(FA(IFQ,1)-1)+I)
          B(I) =  ZR(IGEOM-1+NDIM*(FA(IFQ,2)-1)+I)
          C(I) =  ZR(IGEOM-1+NDIM*(FA(IFQ,3)-1)+I)
 260    CONTINUE
        LONCAR=(PADIST(NDIM,A,B)+PADIST(NDIM,A,C))/2.D0

C       ON AJOUTE A LA LISTE LE POINT M

          CALL XAJPIN(NDIM,PTINT,PTMAX,IPT,IBID,M,LONCAR,AINTER,0,0,
     &                0.D0)

 200  CONTINUE

 9999 CONTINUE
      CALL JEDEMA()
      END
