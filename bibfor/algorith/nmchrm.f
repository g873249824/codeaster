      SUBROUTINE NMCHRM(PHASE ,
     &                  PARMET,METHOD,FONACT,SDDISC,SDDYNA,
     &                  NUMINS,ITERAT,DEFICO,METPRE,METCOR,
     &                  REASMA,REAMOR)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 20/10/2009   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*10 PHASE      
      REAL*8       PARMET(*)
      CHARACTER*16 METHOD(*)
      CHARACTER*16 METCOR,METPRE
      CHARACTER*19 SDDISC,SDDYNA
      INTEGER      NUMINS,ITERAT
      CHARACTER*24 DEFICO
      LOGICAL      FONACT(*) 
      LOGICAL      REASMA,REAMOR    
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (CALCUL)
C
C CHOIX DE REASSEMBLAGE DE LA MATRICE GLOBALE
C      
C ----------------------------------------------------------------------
C
C
C IN  PHASE  : PHASE DE CALCUL
C                'PREDICTION'
C                'CORRECTION' 
C                'FORCES_INT' 
C IN  FONACT : FONCTIONNALITES ACTIVEES (vOIR NMFONC)
C IN  METHOD : INFORMATIONS SUR LES METHODES DE RESOLUTION
C                 VOIR DETAIL DES COMPOSANTES DANS NMLECT
C IN  PARMET : PARAMETRES DES METHODES DE RESOLUTION
C                 VOIR DETAIL DES COMPOSANTES DANS NMLECT
C IN  SDDISC : SD DISC_INST
C IN  SDDYNA : SD DYNAMIQUE
C IN  NUMINS : NUMERO D'INSTANT
C IN  ITERAT : NUMERO D'ITERATION
C IN  DEFICO : SD DEF. CONTACT
C OUT METCOR : TYPE DE MATRICE DE CORRECTION
C OUT METPRE : TYPE DE MATRICE DE PREDICTION
C OUT REASMA  : .TRUE. SI ASSEMBLAGE MATRICE GLOBALE
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      REAL*8       INSTAM,INSTAP,DIINST,PASMIN,R8BID,DELTAT
      INTEGER      REINCR,REITER
      INTEGER      IBID,MATTAN,IRET
      CHARACTER*24 K24BLA,K24BID
      LOGICAL      MATFRO,NDYNLO,ISFONC,LEXPL,LCTCC,LXFCM,LTFCM,LPRMO
      LOGICAL      PREMIE,LDYNA,LAMOR,LCHOC
      INTEGER      IFM,NIV
C      
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE><CALC> CHOIX D''ASSEMBLAGE DE '//
     &                'MATRICE GLOBALE' 
      ENDIF          
C
C --- INITIALISATIONS
C      
      REASMA = .FALSE.
      K24BLA = ' '
C
C --- PARAMETRES
C
      REINCR = NINT(PARMET(1))
      REITER = NINT(PARMET(2))
      METCOR = METHOD(2)
      METPRE = METHOD(5)
      INSTAM = DIINST(SDDISC, NUMINS-1)
      INSTAP = DIINST(SDDISC, NUMINS  )
      DELTAT = INSTAP-INSTAM
      PASMIN = PARMET(3)
C
C --- PREMIER INSTANT ?
C
      CALL DIBCLE(SDDISC,'PREMIE','L',IRET  )
      PREMIE = IRET.EQ.0      
C
C --- FONCTIONNALITES ACTIVEES
C
      LEXPL  = NDYNLO(SDDYNA,'EXPLICITE')
      LDYNA  = NDYNLO(SDDYNA,'DYNAMIQUE')
      LAMOR  = NDYNLO(SDDYNA,'MAT_AMORT')
      REAMOR = NDYNLO(SDDYNA,'RAYLEIGH_KTAN')      
      LCTCC  = ISFONC(FONACT,'CONT_CONTINU')
      LXFCM  = ISFONC(FONACT,'CONT_XFEM') 
      LCHOC  = ISFONC(FONACT,'DIS_CHOC') 
      LPRMO  = NDYNLO(SDDYNA,'PROJ_MODAL')             
C
C --- MATRICE DE FROTTEMENT ?
C
      CALL CFDISC(DEFICO,' ',IBID,IBID,IBID,MATTAN)
      MATFRO = (MATTAN.EQ.1) .OR. (LCHOC)         
C      
C --- PASSAGE A LA MATRICE ELASTIQUE EN-DESSOUS DE PAS_MINI_ELAS   
C   
      IF (ABS(DELTAT) .LT. PASMIN) THEN
        REINCR = 1
        REITER = NINT(PARMET(4))
        METPRE = 'SECANTE'
        METCOR = 'SECANTE'
      END IF
C
C --- REASSEMBLAGE DE LA MATRICE GLOBALE      
C
      IF (PHASE.EQ.'CORRECTION' .OR. PHASE.EQ.'FORCES_INT') THEN
        IF ((METCOR.EQ.'TANGENTE').OR.(METCOR.EQ.'SECANTE')) THEN
          REASMA = .FALSE.
          IF (REITER.NE.0) THEN
            REASMA = MOD(ITERAT+1,REITER) .EQ. 0
          ENDIF  
          IF (MATFRO) THEN
            REASMA = .TRUE.
          ENDIF
        ELSE
          REASMA = .FALSE.
        END IF
      ELSE IF (PHASE.EQ.'PREDICTION') THEN
        IF ((REINCR.EQ.0) .AND. (NUMINS.NE.1)) THEN
          REASMA = .FALSE.
        ENDIF  
        IF (NUMINS.EQ.1) THEN
          REASMA = .TRUE.
        ENDIF  
        IF ((REINCR.NE.0) .AND. (NUMINS.NE.1)) THEN
          REASMA = MOD(NUMINS-1,REINCR) .EQ. 0
        END IF 
      ELSE
        CALL ASSERT(.FALSE.)
      END IF
      
C
C --- DYNAMIQUE: REACTUALISATION DE LA MATRICE 
C --- D AMORTISSEMENT DE RAYLEIGH
C      
      IF (PHASE.EQ.'PREDICTION' .AND. LDYNA .AND. LAMOR) THEN
        IF (PREMIE) THEN
          IF ( .NOT. REAMOR ) REAMOR = .TRUE.
          REASMA = .TRUE.
        ENDIF
      ENDIF
C
C --- EXPLICITE: DEJA CALCULEE DANS NMINIT
C      
      IF (LEXPL) THEN
        REASMA =  .FALSE.
        IF (PHASE.EQ.'CORRECTION') THEN
          CALL ASSERT(.FALSE.)
        ENDIF
        IF (METPRE.NE.'TANGENTE') THEN
          CALL U2MESS('F','MECANONLINE5_1')
        ENDIF
        IF (PREMIE) THEN
          IF (LPRMO) THEN
            REASMA  = .FALSE.
          ELSE  
            REASMA  = .TRUE.
          ENDIF
        ENDIF
      ENDIF    
C
C --- SI XFEM/CONTACT_CONTINU - REASSEMBLAGE OBLIGATOIRE
C 
      IF (LXFCM) THEN
        CALL MMINFP(0    ,DEFICO,K24BLA,'XFEM_GG',
     &              IBID ,R8BID ,K24BID,LTFCM)
        IF (LTFCM) THEN
          IF (.NOT.REASMA) THEN
            CALL U2MESS('A','MECANONLINE5_4')
            REASMA = .TRUE.
          ENDIF
        ENDIF
      ENDIF
      IF (LCTCC) THEN
        IF (.NOT.REASMA) THEN
          CALL U2MESS('A','MECANONLINE5_4')
          REASMA = .TRUE.
        ENDIF
      ENDIF
C
C --- CONTACT DISCRET ET DIS_CHOC - CONTRIBUTION MATRICE TANGENTE
C
      IF (MATFRO) THEN
        IF(.NOT.REASMA) THEN
          CALL  U2MESS('A','MECANONLINE5_5')
          REASMA = .TRUE.
        ENDIF
      ENDIF
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        IF (REASMA) THEN
          WRITE (IFM,*) '<MECANONLINE><CALC> ON ASSEMBLE LA MATRICE'
        ELSE
          WRITE (IFM,*) '<MECANONLINE><CALC> ON N''ASSEMBLE PAS '//
     &                'LA MATRICE'        
        ENDIF      
      ENDIF             
C
      CALL JEDEMA()      
C
      END
