      SUBROUTINE COEIME(MECA  ,IMATE ,NOMAIL,OPTION,RESI  ,RIGI  ,
     &                  NDIM  ,DIMDEF,DIMCON,YAP1  ,YAP2  ,YATE  ,
     &                  ADDEME,ADDEP1,ADDEP2,NBVARI,ADVIME,ADVICO,
     &                  NPG   ,NPI   ,DEFGEP,DEFGEM,SIGM  ,SIGP  ,
     &                  VARIM ,VARIP ,OUVH  ,TLINT ,DRDE  ,KPI   ,
     &                  VICPHI,UNSURN,RETCOM)

C======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/02/2011   AUTEUR GRANET S.GRANET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21 CRS_1404
C ======================================================================

      IMPLICIT NONE

C VARIABLES D'ENTREE
      INTEGER IMATE,NDIM,DIMCON,ADDEME,ADDEP1,ADDEP2,NPG,KPI,NPI
      INTEGER RETCOM,DIMDEF,YAP1,YAP2,YATE,ADVIME,NBVARI,ADVICO,VICPHI
      REAL*8 DEFGEM(DIMDEF),DEFGEP(DIMDEF),SIGM(DIMCON),VARIM(NBVARI)
      CHARACTER*8 NOMAIL
      CHARACTER*16 MECA,OPTION
      LOGICAL RESI,RIGI

C VARIABLES DE SORTIE
      REAL*8 SIGP(DIMCON),VARIP(NBVARI),DRDE(DIMDEF,DIMDEF),TLINT,OUVH

C VARIABLES LOCALES
      INTEGER I,J
      REAL*8 DA(NDIM),DSIDEP(6,6),PARA(2),OUVFIC,UNSURN
      CHARACTER*8 NCRA(2)
      CHARACTER*2 CODRET(18)

C =====================================================================
C.......................................................................
C
C     BUT:  INTEGRATION DE LA LOI DE COMPORTEMENT MECANIQUE ET RENVOI
C           DE LA LOI CUBIQUE
C
C.......................................................................
C =====================================================================
C IN MECA   : COMPORTEMENT MECA
C IN IMATE  : CODE MATERIAU
C IN RESI   : FULL_MECA OU RAPH_MECA
C IN RIGI   : FULL_MECA OU RIGI_MECA
C IN NDIM   : DIMENSION ESPACE
C IN DIMDEF : DIMENSION DEFORMATION GENERALISEE
C IN DIMCON : DIMENSION VECTEUR CONTRAINTES GENERALISEES
C IN YAP1   : SI =1 PRESENCE CONSTITUTANT 1
C IN YAP2   : SI =1 PRESENCE CONSTITUTANT 2
C IN YATE   : SI =1 THERMIQUE
C IN ADDEME : ADRESSE DES DEFORMATIONS MECANIQUES
C IN ADDEP1 : ADRESSE DES DEFORMATIONS PRESSION 1
C IN ADDEP2 : ADRESSE DES DEFORMATIONS PRESSION 2
C IN NBVARI : NOMBRE DE VARIABLES INTERNES
C IN ADVIME : ADRESSE DES VI MECANIQUES
C IN ADVICO : ADRESSE DES VI DE COUPLAGE
C IN NPG    : NOMBRE DE POINTS DE GAUSS
C IN DEFGEP : DEFORMATIONS AU TEMPS PLUS
C IN DERGEM : DEFORMATIONS AU TEMPS MOINS
C IN SIGM   : CONTRAINTES AU TEMPS MOINS
C IN VARIM  : VARIABLES INTERNES AU TEMPS MOINS
C IN KPI    : POINT D'INTEGRATION
C =====================================================================
C OUT SIGP  : CONTRAINTES AU TEMPS PLUS
C OUT VARIP : VARIABLES INTERNES AU TEMPS PLUS
C OUT OUVH  : OUVERTURE NORMALE DU JOINT
C OUT TLINT : PERMEABILITE LONGITUDINALE
C OUT DRDE  : MATRICE DE RIGIDITE
C OUT RETCOM : RETOUR LOI DE COMPORTEMENT
C =====================================================================

      DATA NCRA / 'OUV_FICT','UN_SUR_N' /
      OUVH=0.D0
      TLINT=0.D0

C ====================================================================
C LOI DE COMPORTEMENT JOINT_BANDIS
C ====================================================================

      IF (MECA.EQ.'JOINT_BANDIS') THEN

        CALL LCJOHM(IMATE ,RESI  ,RIGI  ,KPI   ,NPG   ,NOMAIL,
     &              ADDEME,ADVICO,NDIM  ,DIMDEF,DIMCON,NBVARI,
     &              DEFGEM,DEFGEP,VARIM ,VARIP ,SIGM  ,SIGP  ,
     &              DRDE  ,OUVH  ,RETCOM)

         TLINT = OUVH**2/12.D0
         IF (RESI) THEN
           VARIP(ADVIME)=TLINT
           IF (YAP1 .EQ. 1) THEN
             SIGP(1+NDIM)=-DEFGEP(ADDEP1)
           END IF
         END IF
         IF ((RIGI).AND. (KPI .LE. NPG)) THEN
           IF (YAP1 .EQ. 1) THEN
             DRDE(ADDEME,ADDEP1)=-1.D0
           END IF
         END IF
      END IF

C ====================================================================
C LOI DE COMPORTEMENT CZM_LIN_REG
C ====================================================================
      IF (MECA.EQ.'CZM_LIN_REG') THEN

         DO 10 I=1,NDIM
           DA(I) = DEFGEP(I) - DEFGEM(I)
 10      CONTINUE

C - INTEGRATION DE LA LOI DE COMPORTEMENT MECANIQUE

         CALL LCEJLI('RIGI',KPI,1,NDIM,IMATE,OPTION,DEFGEM,DA,SIGP,
     &                  DSIDEP,VARIM(ADVIME),VARIP(ADVIME))

C - RECUPERATION DES PARAMETRES DE COUPLAGE POUR LA POINTE DE FISSURE

         IF (VARIP(ADVIME) .EQ. 2) THEN
           UNSURN=0.D0
         ELSE
           CALL RCVALA(IMATE,' ','THM_RUPT',0,' ', 0.D0,2,
     &                                 NCRA(1),PARA(1),CODRET,'FM')
           OUVFIC    = PARA(1)
           UNSURN    = PARA(2)
         END IF

C - CALCUL DES TERMES MECA ET DE COUPLAGE DE L'OPERATEUR TANGENT

         IF (RIGI) THEN
           IF (KPI .LE. NPG) THEN
             DO 20 I=1,NDIM
               DO 21 J=1,NDIM
                 DRDE(I,J)=DSIDEP(I,J)
 21            CONTINUE
 20          CONTINUE
             IF ((YAP1 .EQ. 1).AND.((VARIP(ADVIME+2) .EQ. 1)
     &           .OR.(VARIP(ADVIME+2).EQ. 2))) THEN
               DRDE(1,ADDEP1)=-1.D0
             END IF
           END IF
           IF ((KPI .GT. NPG) .OR. (NPI .EQ. NPG)) THEN
             DRDE(ADDEP1,ADDEP1)=DRDE(ADDEP1,ADDEP1)-UNSURN
           END IF
C - CALCUL DE L'OUVERTURE HYDRO ET DE LA PERMEABILITE
           OUVH=VARIM(ADVICO+VICPHI)
           IF (VARIM(3).EQ.0) THEN
             OUVH=OUVFIC
           END IF
           TLINT=OUVH**2/12
         END IF

C - CALCUL DES TERMES MECA ET DE COUPLAGE DU VECTEUR FORCES INTERNES

         IF (RESI) THEN
           IF ((YAP1 .EQ. 1).AND.((VARIP(ADVIME+2) .EQ. 1)
     &           .OR.(VARIP(ADVIME+2).EQ. 2))) THEN
             SIGP(1+NDIM)=-DEFGEP(ADDEP1)
           END IF
C - CALCUL DE L'OUVERTURE HYDRO ET DE LA PERMEABILITE
           VARIP(ADVICO+VICPHI)=DEFGEP(1)
           OUVH=VARIP(ADVICO+VICPHI)
C - SI FISSURE FERMEE ALORS ON DONNE UNE OUVERTURE HYDRO FICTIVE
           IF ((VARIP(3).EQ.0)) THEN
             OUVH=OUVFIC
           END IF
           TLINT=OUVH**2/12
           VARIP(ADVICO+VICPHI)=DEFGEP(1)+DEFGEP(ADDEP1)*UNSURN
        END IF
      END IF

C ====================================================================
C LOI DE COMPORTEMENT CZM_EXP_REG
C ====================================================================

      IF (MECA.EQ.'CZM_EXP_REG') THEN

        DO 40 I=1,NDIM
          DA(I) = DEFGEP(I) - DEFGEM(I)
 40     CONTINUE

C - INTEGRATION DE LA LOI DE COMPORTEMENT MECANIQUE

        CALL LCEJEX('RIGI',KPI,1,NDIM,IMATE,OPTION,DEFGEM,DA,SIGP,
     &                  DSIDEP,VARIM(ADVIME),VARIP(ADVIME))

C - RECUPERATION DES PARAMETRES DE COUPLAGE POUR LA POINTE DE FISSURE

        CALL RCVALA(IMATE,' ','THM_RUPT',0,' ', 0.D0,2,
     &                                 NCRA(1),PARA(1),CODRET,'FM')
        OUVFIC    = PARA(1)
        UNSURN    = PARA(2)

C - CALCUL DES TERMES MECA ET DE COUPLAGE DE L'OPERATEUR TANGENT

        IF (RIGI) THEN
          IF (KPI .LE. NPG) THEN
             DO 120 I=1,NDIM
               DO 121 J=1,NDIM
                 DRDE(I,J)=DSIDEP(I,J)
 121            CONTINUE
 120          CONTINUE
             IF ((YAP1 .EQ. 1).AND.(VARIP(ADVIME+2) .EQ. 1)) THEN
               DRDE(1,ADDEP1)=-1.D0
             END IF
           END IF
           IF ((KPI .GT. NPG) .OR. (NPI .EQ. NPG)) THEN
             DRDE(ADDEP1,ADDEP1)=DRDE(ADDEP1,ADDEP1)-UNSURN
           END IF
C - CALCUL DE L'OUVERTURE HYDRO ET DE LA PERMEABILITE
           OUVH=VARIM(ADVICO+VICPHI)
           IF (VARIM(3).EQ.0) THEN
             OUVH=OUVFIC
           END IF
           TLINT=OUVH**2/12
         END IF

C - CALCUL DES TERMES MECA ET DE COUPLAGE DU VECTEUR FORCES INTERNES

         IF (RESI) THEN
           IF ((YAP1 .EQ. 1).AND.(VARIP(ADVIME+2) .EQ. 1))THEN
             SIGP(1+NDIM)=-DEFGEP(ADDEP1)
           END IF
C - CALCUL DE L'OUVERTURE HYDRO ET DE LA PERMEABILITE
           VARIP(ADVICO+VICPHI)=DEFGEP(1)
           OUVH=VARIP(ADVICO+VICPHI)
           IF (VARIP(3).EQ.0) THEN
C - SI FISSURE FERMEE ALORS ON DONNE UNE OUVERTURE HYDRO FICTIVE
             OUVH=OUVFIC
           END IF
           TLINT=OUVH**2/12
           VARIP(ADVICO+VICPHI)=DEFGEP(1)+DEFGEP(ADDEP1)*UNSURN
         END IF
      END IF


      END
