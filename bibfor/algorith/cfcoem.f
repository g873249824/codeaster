      SUBROUTINE CFCOEM(RESOCO,LCTFD ,LCTF3D,POSNOE,ILIAI ,
     &                  NBDDLT,NBNOM ,POSNSM,DDL   ,COEF  ,
     &                  COFX  ,COFY)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*24 RESOCO    
      INTEGER      POSNOE
      INTEGER      ILIAI
      INTEGER      NBDDLT
      INTEGER      NBNOM
      INTEGER      POSNSM(9)
      INTEGER      DDL(30)  
      REAL*8       COEF(30),COFX(30),COFY(30)
      LOGICAL      LCTFD,LCTF3D         
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - APPARIEMENT - MAIT/ESCL)
C
C COEFFICIENTS RELATIONS LINEAIRES APPARIEMENT
C
C ----------------------------------------------------------------------
C
C
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  LCTFD  : FROTTEMENT
C IN  LCTF3D : FROTTEMENT EN 3D
C IN  POSNOE : INDICE DANS CONTNO DU NOEUD ESCLAVE
C IN  ILIAI  : INDICE DE LA LIAISON COURANTE
C IN  NBDDLT : NOMBRE DE DDL NOEUD ESCLAVE+NOEUDS MAITRES
C IN  NBNOM  : NOMBRE DE NOEUDS MAITRES CONCERNES (MAX: 9)
C IN  POSNSM : INDICES DANS CONTNO DES NOEUDS MAITRES
C IN  DDL    : NUMEROS DES DDLS ESCLAVE ET MAITRES CONCERNES
C IN  COEF   : VALEURS EN M DES FONCTIONS DE FORME ASSOCIEES AUX NOEUDS
C IN  COFX   : VALEURS EN M DES FONCTIONS DE FORME ASSOCIEES AUX NOEUDS
C                POUR LA PREMIERE DIRECTION TANGENTE
C IN  COFY   : VALEURS EN M DES FONCTIONS DE FORME ASSOCIEES AUX NOEUDS
C                POUR LA SECONDE DIRECTION TANGENTE
C
C
C
C
      INTEGER      NBDDLE,NBDDLM,NESMAX,CFDISD
      INTEGER      JDECAL,JDECDL,K,INO,POSNOM
      INTEGER      JAPCOE,JAPCOF
      CHARACTER*24 APCOEF,APCOFR
      CHARACTER*24 NBDDL,APDDL
      INTEGER      JNBDDL,JAPDDL
      CHARACTER*24 APPOIN
      INTEGER      JAPPTR
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      NBDDL  = RESOCO(1:14)//'.NBDDL' 
      APDDL  = RESOCO(1:14)//'.APDDL'    
      CALL JEVEUO(NBDDL, 'L',JNBDDL)
      CALL JEVEUO(APDDL ,'E',JAPDDL)   
C      
      APCOEF = RESOCO(1:14)//'.APCOEF'
      APCOFR = RESOCO(1:14)//'.APCOFR'
      APPOIN = RESOCO(1:14)//'.APPOIN'
C      
      CALL JEVEUO(APPOIN,'E',JAPPTR)
      CALL JEVEUO(APCOEF,'E',JAPCOE)
      IF (LCTFD) THEN
        CALL JEVEUO(APCOFR,'E',JAPCOF)
      ENDIF
C 
      NESMAX = CFDISD(RESOCO,'NESMAX')
C
C --- NOMBRE DE DDL POUR LE NOEUD ESCLAVE
C      
      NBDDLE = ZI(JNBDDL+POSNOE) - ZI(JNBDDL+POSNOE-1)
C
C --- NOMBRE TOTAL RELATIONS LINEAIRES
C
      ZI(JAPPTR+ILIAI) = ZI(JAPPTR+ILIAI-1) + NBDDLT       
C 
C --- RELATION DE CONTACT POUR LE NOEUD ESCLAVE
C
      JDECAL = ZI(JAPPTR+ILIAI-1)
      DO 5 K = 1,NBDDLE
        ZR(JAPCOE+JDECAL+K-1) = COEF(K)
        ZI(JAPDDL+JDECAL+K-1) = DDL(K)
   5  CONTINUE
C 
C --- RELATION DE FROTTEMENT POUR LE NOEUD ESCLAVE
C
      IF (LCTFD) THEN
        DO 10 K = 1,NBDDLE
          ZR(JAPCOF+JDECAL+K-1) = COFX(K)
          IF (LCTF3D) THEN
            ZR(JAPCOF+JDECAL+30*NESMAX+K-1) = COFY(K)
          END IF
   10   CONTINUE
      ENDIF
C 
C --- RELATION DE CONTACT POUR LE NOEUD MAITRE
C
      JDECAL = JDECAL + NBDDLE
      JDECDL = NBDDLE 
      DO 50 INO = 1,NBNOM
        POSNOM = POSNSM(INO)
        NBDDLM = ZI(JNBDDL+POSNOM) - ZI(JNBDDL+POSNOM-1)
        DO 40 K = 1,NBDDLM
          ZR(JAPCOE+JDECAL+K-1) = COEF(JDECDL+K)
          ZI(JAPDDL+JDECAL+K-1) = DDL(JDECDL+K)       
   40   CONTINUE
C 
C --- RELATION DE FROTTEMENT POUR LE NOEUD MAITRE
C        
        IF (LCTFD) THEN
          DO 41 K = 1,NBDDLM
            ZR(JAPCOF+JDECAL+K-1) = COFX(JDECDL+K)
            IF (LCTF3D) THEN
              ZR(JAPCOF+JDECAL+30*NESMAX+K-1) = COFY(JDECDL+K)
            ENDIF
   41     CONTINUE
        ENDIF
        JDECAL = JDECAL + NBDDLM
        JDECDL = JDECDL + NBDDLM
   50 CONTINUE
C
      CALL JEDEMA()
      END
