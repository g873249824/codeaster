      SUBROUTINE MDBS32 (NEQGEN,DEPL,VITE,ACCE,FEXT,DT,DTSTO,LFLU,
     &                   NBEXCI,IDESCF,NOMFON,COEFM,LIAD,INUMOR,NBCHOC,
     &                   LOGCHO,DPLMOD,PARCHO,NOECHO,SAUCHO,NBREDE,
     &                   DPLRED,PARRED,FONRED,SAURED,SAREDI,NBREVI,
     &                   DPLREV,FONREV,NOFDEP,NOFVIT,NOFACC,PSIDEL,
     &                   MONMOT,NBRFIS,FK,DFK,ANGINI,FONCP,NBPAL,
     &                   VROTAT,TYPAL, FINPAL,CNPAL,PRDEFF,CONV,FSAUV,
     &                   TYPBAS,PULSA2,MASGEN,DESCMM,RIGGEN,DESCMR,
     &                   LAMOR,DESCMA,WORK1,TEMPS,TOL,DEPLI,VITEI,
     &                   ERDE,ERVI,KDE,KVI,FONCA,FONCV,ISTEP,RIGY,
     &                   AMGY,NBCONV,NBMXCV,VITVAR,GYOGEN,RGYGEN,
     &                   AMOGEN,ERRT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 05/02/2013   AUTEUR ALARCON A.ALARCON 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
C
      CHARACTER*16 TYPBAS
      INTEGER EE,SS,NBCONV,NBMXCV,DESCMM,DESCMR,DESCMA,PALMAX,
     &        IM,IFF,NBREDE,NBREVI,NBRFIS,SAREDI(*),
     &        NETAG,NEQGEN,NBEXCI,IDESCF(*),LIAD(*),INUMOR(*),
     &        NBCHOC,LOGCHO(NBCHOC,*),NBPAL,ISTEP,IER,IND,JM
      PARAMETER (PALMAX=20)
      CHARACTER*3   FINPAL(PALMAX)
      CHARACTER*6   TYPAL(PALMAX)
      CHARACTER*8   CNPAL(PALMAX),NOMFON(*),NOECHO(NBCHOC,*),FONRED(*),
     &              FONREV(*),NOFDEP(*),NOFVIT(*),NOFACC(*),MONMOT,
     &              FK(2),DFK(2),FONCP,FONCA,FONCV,VITVAR

      LOGICAL       LAMOR,LFLU,PRDEFF
      REAL*8 CDP(7),ZERO,TEVAL,DT,COEFM(*),DPLMOD(NBCHOC,NEQGEN,*),
     &       PULSA2(*),MASGEN(*),RIGGEN(*),PARCHO(*),DPLRED(*),
     &       PARRED(*),DPLREV(*),ANGINI,DTSTO,VROTAT,ERRT,ERRD,ERRV,
     &       EDP1,EDP2,EDP3,EDP4,R8BID,PSIDEL(*),
     &       CONV,SKD,SKV,TOL,TEMPS,ADP(6,6),AROT,FSAUV(PALMAX,3),VROT,
     &       GYOGEN(*),RGYGEN(*),AMOGEN(*),SAUCHO(*),SAURED(*),WORK1(*),
     &       AMGY(*),RIGY(*),DEPL(*),VITE(*),ACCE(*),FEXT(*),
     &       DEPLI(*),VITEI(*),ERDE(*),ERVI(*),KDE(*),KVI(*),R8PREM,ATOL
C ======================================================================
C TOLE CRP_21
      ZERO = 0.D0
C     ON UTILISE ATOL POUR EVENTUELLEMENT DONNER UNE TOLERANCE ABSOLUE
C     DONNEE PAR L'UTILISATEUR. ICI ON LA FORCE A ZERO
      ATOL=0.D0
      R8BID = ZERO
C
C     --- COEFICIENTS DE DORMAND PRINCE 
C
      CDP(1)=0.00D0
      CDP(2)=0.50D0
      CDP(3)=0.75D0
      CDP(4)=1.D0
C
      ADP(1,1)=0.50D0
      ADP(2,1)=0.00D0
      ADP(2,2)=0.75D0
      ADP(3,1)=2.D0/9.D0
      ADP(3,2)=1.D0/3.D0
      ADP(3,3)=4.D0/9.D0
C
      EDP1=7.D0/24.D0 - ADP(3,1)
      EDP2=1.D0/4.D0 - ADP(3,2)
      EDP3=1.D0/3.D0 - ADP(3,3) 
      EDP4=1.D0/8.D0 - 0.00D0 

C   TAILLE DE LA BOUCLE EN FONCTION DU SCHEMA      
      NETAG=3
C BOUCLE SUR LES ESTIMATIONS DE Ki
      DO 10 EE = 1,NETAG
CC        --- ESTIMATION DE LA DERIVEE (EULER EXPLICITE)---  
        CALL DCOPY(NEQGEN,VITE,1,KDE((EE-1)*NEQGEN+1),1)
        CALL DCOPY(NEQGEN,ACCE,1,KVI((EE-1)*NEQGEN+1),1)
C       --- CALCUL DE L ETAT A CHAQUE ETAGE POUR ESTIMER L ACCEL
        DO 21 IM = 1,NEQGEN
            DEPL(IM) = DEPLI(IM)
            VITE(IM) = VITEI(IM)
            DO 30 SS=1,EE
                 DEPL(IM)=DEPL(IM)+DT*ADP(EE,SS)*KDE((SS-1)*NEQGEN+IM)
                 VITE(IM)=VITE(IM)+DT*ADP(EE,SS)*KVI((SS-1)*NEQGEN+IM)
 30         CONTINUE
 21      CONTINUE
C      
         TEVAL=TEMPS+DT*CDP(EE+1)
C 
C        LA PARTIE SUIVATE EST NECESSAIRE AU CALCUL DE L'ACCELERATION
C         
         VROT = 0.D0
         AROT = 0.D0
         IF (VITVAR(1:3).EQ.'OUI') THEN
           CALL FOINTE('F ',FONCV,1,'INST',TEVAL,VROT,IER)
           CALL FOINTE('F ',FONCA,1,'INST',TEVAL,AROT,IER)
           DO 115 IM = 1 , NEQGEN
             DO 116 JM = 1 , NEQGEN
               IND = JM + NEQGEN*(IM-1)
               AMGY(IND) = AMOGEN(IND) + VROT * GYOGEN(IND)
               RIGY(IND) = RIGGEN(IND) + AROT * RGYGEN(IND)
 116         CONTINUE
 115       CONTINUE
         ELSE
           DO 119 IM = 1 , NEQGEN
             DO 120 JM = 1 , NEQGEN
               IND = JM + NEQGEN*(IM-1)
               AMGY(IND) = AMOGEN(IND)
               RIGY(IND) = RIGGEN(IND)
 120         CONTINUE
 119       CONTINUE
         ENDIF
C
C        --- FORCES EXTERIEURES ---
C
         DO 40 IFF = 1,NEQGEN
            FEXT(IFF) = ZERO
 40      CONTINUE
C
         IF (NBEXCI.NE.0) THEN
            CALL MDFEXT(TEVAL,R8BID,NEQGEN,NBEXCI,IDESCF,NOMFON,COEFM,
     &                  LIAD,INUMOR,1,FEXT)
         ENDIF
C
         IF (LFLU) THEN
           CALL U2MESS('F','ALGORITH5_21')
         ELSE
C
C        CALCUL CLASSIQUE FORCES NON-LINEAIRES ET ACCELERATIONS
C        --- CONTRIBUTION DES FORCES NON LINEAIRES ---
C
         CALL MDFNLI(NEQGEN,DEPL,VITE,
     &               ACCE,FEXT,R8BID,R8BID,R8BID,
     &               R8BID,NBCHOC,LOGCHO,DPLMOD,PARCHO,NOECHO,
     &               SAUCHO, NBREDE,DPLRED,PARRED,FONRED,
     &               SAURED,SAREDI,NBREVI,DPLREV,FONREV,
     &               TEVAL,NOFDEP,NOFVIT,NOFACC,NBEXCI,PSIDEL,MONMOT,
     &               NBRFIS,FK,DFK,ANGINI,FONCP,
     &               (ISTEP+1),NBPAL,DT,DTSTO,VROTAT,
     &               TYPAL, FINPAL,CNPAL,PRDEFF,CONV,FSAUV)
         IF ((CONV.LE.0.D0) .AND. (NBCONV.GT.NBMXCV)) THEN
           CALL U2MESS('F','EDYOS_46')
         ELSEIF ((CONV.LE.0.D0) .AND. (NBCONV.LE.NBMXCV)) THEN
           NBCONV = NBCONV + 1
         ENDIF
C
C        --- ACCELERATIONS GENERALISEES ---
C
         CALL MDACCE(TYPBAS,NEQGEN,PULSA2,MASGEN,DESCMM,
     &               RIGGEN,DESCMR,FEXT,LAMOR,AMGY,
     &               DESCMA,WORK1,DEPL,VITE,ACCE)
C
         ENDIF
C FIN DE LA BOUCLE SUR LES ETAGES DE RUNGE-KUTTA
 10   CONTINUE   
C
C      --- ESTIMATION ERREUR ---
C
       ERRD=0.D0
       ERRV=0.D0
       ERRT=0.D0
C
      DO 50 IM=1,NEQGEN 
C         POUR LES DEPLACEMENTS
C         NOTER QUE k7 EST DONNEE PAR 
C         LA VALEUR DE LA VITESSE AU QUATRIEME ETAGE DE LA METHODE
          ERDE(IM)= (EDP1*KDE(IM)+EDP2*KDE(NEQGEN+IM)
     &              +EDP3*KDE(2*NEQGEN+IM)+EDP4*VITE(IM))*DT  
C
          SKD=ATOL+TOL*MAX(ABS(DEPL(IM)),ABS(DEPLI(IM)),1.D2*R8PREM())
          ERRD = ERRD+(ERDE(IM)/SKD)**2
C         POUR LES VITESSES
C         NOTER QUE k7 EST DONNEE PAR 
C         LA VALEUR DE L'ACCELERATION AU QUATRIEME ETAGE DE LA METHODE
          ERVI(IM)= (EDP1*KVI(IM)+EDP2*KVI(NEQGEN+IM)
     &              +EDP3*KVI(2*NEQGEN+IM)+EDP4*ACCE(IM))*DT
C     
          SKV=ATOL+TOL*MAX(ABS(VITE(IM)),ABS(VITEI(IM)),1.D2*R8PREM())
          ERRV = ERRV+(ERVI(IM)/SKV)**2
 50   CONTINUE
C
C     POUR EVITER DES PROBLEMES NUMERIQUES ON COMPARE A LA TOL MACHINE
      ERRT = MAX(SQRT((ERRD+ERRV)/(2*NEQGEN)),1.D2*R8PREM())
C
      END
