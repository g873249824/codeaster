      SUBROUTINE NMNEWT(MAILLA,MODELE,NUMINS,NUMEDD,NUMFIX,
     &                  MATE  ,CARELE,COMREF,COMPOR,LISCHA,
     &                  METHOD,FONACT,CARCRI,PARCON,CONV  ,
     &                  PARMET,PARCRI,SDSENS,SDTIME,SDERRO,
     &                  SDIMPR,SDNUME,SDDYNA,SDDISC,SDCRIT,
     &                  SDSUIV,SDPILO,SOLVEU,MAPREC,MATASS,
     &                  VALINC,SOLALG,MEELEM,MEASSE,VEELEM,
     &                  VEASSE,DEFICO,RESOCO,DEFICU,RESOCU,
     &                  ETA   ,NBITER,FINPAS,ACTPAS)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 24/05/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INTEGER      NUMINS
      INTEGER      FONACT(*)
      CHARACTER*16 METHOD(*)
      REAL*8       PARMET(*),PARCRI(*),PARCON(*),CONV(*)
      CHARACTER*24 CARCRI
      CHARACTER*24 SDSENS,SDTIME,SDERRO,SDIMPR
      CHARACTER*19 SDNUME,SDDYNA,SDDISC,SDCRIT
      CHARACTER*19 SDSUIV,SDPILO
      CHARACTER*19 VALINC(*),SOLALG(*)
      CHARACTER*19 MEELEM(*),VEELEM(*)
      CHARACTER*19 MEASSE(*),VEASSE(*)
      CHARACTER*19 LISCHA
      CHARACTER*19 SOLVEU,MAPREC,MATASS
      CHARACTER*24 MODELE,NUMEDD,NUMFIX
      CHARACTER*24 COMREF,COMPOR
      CHARACTER*24 MATE  ,CARELE
      CHARACTER*24 DEFICO,RESOCO,DEFICU,RESOCU
      LOGICAL      FINPAS
      REAL*8       ETA
      INTEGER      ACTPAS,NBITER
      CHARACTER*8  MAILLA
C
C ----------------------------------------------------------------------
C
C OPERATEUR NON-LINEAIRE MECANIQUE
C
C ALGORITHME DE NEWTON (STATIQUE ET DYNAMIQUE)
C
C ----------------------------------------------------------------------
C
C
C IN  MODELE : MODELE
C IN  NUMEDD : NUME_DDL (VARIABLE AU COURS DU CALCUL)
C IN  NUMFIX : NUME_DDL (FIXE AU COURS DU CALCUL)
C IN  MATE   : CHAMP MATERIAU
C IN  CARELE : CARACTERISTIQUES DES ELEMENTS DE STRUCTURE
C IN  COMREF : VARIABLES DE COMMANDE DE REFERENCE
C IN  COMPOR : COMPORTEMENT
C IN  LISCHA : L_CHARGES
C IN  METHOD : INFORMATIONS SUR LES METHODES DE RESOLUTION
C IN  SOLVEU : SOLVEUR
C IN  FONACT : FONCTIONNALITES ACTIVEES (VOIR NMFONC)
C IN  PARMET : PARAMETRES DES METHODES DE RESOLUTION
C I/O CONV   : INFORMATIONS SUR LA CONVERGENCE DU CALCUL
C               1 - RESI_DUAL_ABSO      (LAGRANGIEN AUGMENTE)
C               2 - RESI_PRIM_ABSO      (LAGRANGIEN AUGMENTE)
C               3 - NOMBRE D'ITERATIONS DUAL (LAGRANGIEN AUGMENTE)
C               4 - NUMERO ITERATION BFGS (LAGRANGIEN AUGMENTE)
C              10 - NOMBRE D'ITERATIONS (RECHERCHE LINEAIRE)
C              11 - RHO                 (RECHERCHE LINEAIRE)
C              20 - RESI_GLOB_RELA
C              21 - RESI_GLOB_MAXI
C IN  PARCRI : CRITERES DE CONVERGENCE
C               1 : ITER_GLOB_MAXI
C               2 : RESI_GLOB_RELA
C               3 : RESI_GLOB_MAXI
C               4 : ARRET
C               5 : ITER_GLOB_ELAS
C               6 : RESI_REFE_RELA
C              12 : RESI_COMP_RELA
C IN  CARCRI : PARAMETRES DES METHODES D'INTEGRATION LOCALES
C IN  SDDISC : SD DISC_INST
C IN  SDTIME : SD TIMER
C IN  SDERRO : GESTION DES ERREURS
C IN  NUMINS : NUMERO D'INSTANT
C IN  VALINC : VARIABLE CHAPEAU POUR INCREMENTS VARIABLES
C IN  SOLALG : VARIABLE CHAPEAU POUR INCREMENTS SOLUTIONS
C IN  MEELEM : VARIABLE CHAPEAU POUR NOM DES MATR_ELEM
C IN  MEASSE : VARIABLE CHAPEAU POUR NOM DES MATR_ASSE
C IN  VEELEM : VARIABLE CHAPEAU POUR NOM DES VECT_ELEM
C IN  VEASSE : VARIABLE CHAPEAU POUR NOM DES VECT_ASSE
C IN  DEFICO : SD DEFINITION CONTACT
C IN  RESOCO : SD RESOLUTION CONTACT
C IN  RESOCU : SD RESOLUTION LIAISON_UNILATER
C IN  SDDYNA : SD DYNAMIQUE
C IN  SDSENS : SD SENSIBILITE
C IN  MATASS : NOM DE LA MATRICE DU PREMIER MEMBRE ASSEMBLEE
C IN  MAPREC : NOM DE LA MATRICE DE PRECONDITIONNEMENT (GCPC)
C IN  SDNUME : SD NUMEROTATION
C I/O ETA    : PARAMETRE DE PILOTAGE
C OUT NBITER : NOMBRE D'ITERATIONS DE NEWTON
C OUT FINPAS : .TRUE. SI ON NE FAIT PLUS D'AUTRES PAS DE TEMPS
C OUT ACTPAS : BOUCLE TEMPS -> ACTION POUR LA SUITE
C     1 - BOUCLE TEMPS - PAS SUIVANT
C     2 - BOUCLE TEMPS - ON REFAIT LE PAS
C     3 - BOUCLE TEMPS - ARRET DU CALCUL
C
C ----------------------------------------------------------------------
C
      INTEGER      INCRUN
      INTEGER      NIVEAU,ITERAT,IBID
      LOGICAL      LBID
      LOGICAL      CONVER,ERROR,LNOPRE
      REAL*8       R8BID
      INTEGER      ETAUSR
      LOGICAL      ISFONC,LBOUCL
      LOGICAL      MTCPUI,MAXREL,ITEMAX
      INTEGER      ACTITE,ACTNIV
      CHARACTER*24 K24BID
C
C ----------------------------------------------------------------------
C
C
C --- INITIALISATIONS
C
      INCRUN = 1
      ITERAT = 0
      ACTITE = 0
      ACTNIV = 0
      ACTPAS = 0
      NBITER = 0
      CONVER = .FALSE.
      ERROR  = .FALSE.
      LNOPRE = .FALSE.
      MAXREL = .FALSE.
      ITEMAX = .FALSE.
      MTCPUI = .FALSE.
      CALL NMCRET('INIT','   ',SDERRO,SDDISC,IBID  ,
     &            K24BID)
      CALL NMERGE('INI','   ',SDERRO,LBID  )
C
C --- ACTIVATION BOUCLE CONTACT OU PAS ?
C
      LBOUCL = ISFONC(FONACT,'BOUCLE_EXTERNE')
      IF (LBOUCL) THEN
        NIVEAU = 4
      ELSE
        NIVEAU = -1
      ENDIF
C
C --- DECOUPE INITIALE DU PAS DE TEMPS
C
      CALL NMDCIN(SDDISC,SDCRIT,NUMINS)
C
C --- INITIALISATION DES CHAMPS D'INCONNUES POUR LE NOUVEAU PAS DE TEMPS
C
      CALL NMNPAS(MODELE,MAILLA,MATE  ,CARELE,LISCHA,
     &            FONACT,SDDISC,SDSENS,SDDYNA,SDNUME,
     &            NUMEDD,NUMINS,CONV  ,DEFICO,RESOCO,
     &            VALINC,SOLALG)
C
C --- CALCUL DES CHARGEMENTS CONSTANTS AU COURS DU PAS DE TEMPS
C
      CALL NMCHAR('FIXE',' '   ,
     &            MODELE,NUMEDD,MATE  ,CARELE,COMPOR,
     &            LISCHA,CARCRI,NUMINS,SDDISC,PARCON,
     &            FONACT,DEFICO,RESOCO,RESOCU,COMREF,
     &            VALINC,SOLALG,VEELEM,MEASSE,VEASSE,
     &            SDSENS,SDDYNA)
C
C ======================================================================
C    BOUCLE POUR CONTACT
C ======================================================================
C
  100 CONTINUE
C
      ITERAT = 0
C
      CALL NMIBLE(NIVEAU,
     &            NUMINS,MODELE,MAILLA,DEFICO,RESOCO,
     &            FONACT,NUMEDD,SDDYNA,SDDISC,SDTIME,
     &            ITERAT,VALINC,SOLALG,LNOPRE)
C
C ======================================================================
C     PREDICTION
C ======================================================================
C
C
C --- ON SAUTE LA PREDICTION
C
      IF (LNOPRE) THEN
        GOTO 300
      ENDIF
C
C --- PREDICTION D'UNE DIRECTION DE DESCENTE
C
      CALL NMPRED(MODELE,NUMEDD,NUMFIX,MATE  ,CARELE,
     &            COMREF,COMPOR,LISCHA,METHOD,SOLVEU,
     &            FONACT,PARMET,CARCRI,SDDISC,SDTIME,
     &            SDNUME,SDERRO,NUMINS,VALINC,SOLALG,
     &            MATASS,MAPREC,DEFICO,RESOCO,RESOCU,
     &            SDDYNA,MEELEM,MEASSE,VEELEM,VEASSE,
     &            ERROR )
C
      IF (ERROR) GOTO 315
C
C --- PREMIER INSTANT PASSE
C
      CALL DIBCLE(SDDISC,'PREMIE','E',INCRUN)
C
C ======================================================================
C     BOUCLE SUR LES ITERATIONS DE NEWTON
C ======================================================================
C
 300  CONTINUE
C
      CALL NMTIME('DEBUT','ITE',SDTIME,LBID  ,R8BID )
C
C --- CALCUL PROPREMENT DIT DE L'INCREMENT DE DEPLACEMENT
C --- EN CORRIGEANT LA (LES) DIRECTIONS DE DESCENTE
C --- SI CONTACT OU PILOTAGE OU RECHERCHE LINEAIRE
C
      CALL NMDEPL(MODELE,NUMEDD,MATE  ,CARELE,COMREF,
     &            COMPOR,LISCHA,FONACT,PARMET,CARCRI,
     &            MAILLA,METHOD,NUMINS,ITERAT,MATASS,
     &            SDDISC,SDDYNA,SDNUME,SDPILO,SDTIME,
     &            SDERRO,DEFICO,RESOCO,DEFICU,RESOCU,
     &            VALINC,SOLALG,VEELEM,VEASSE,ETA   ,
     &            CONV  ,ERROR )
C
      IF (ERROR) GOTO 315
C
C --- CALCUL DES FORCES APRES CORRECTION
C
      CALL NMFCOR(MODELE,NUMEDD,MATE  ,CARELE,COMREF,
     &            COMPOR,LISCHA,FONACT,PARMET,CARCRI,
     &            METHOD,NUMINS,ITERAT,SDDISC,SDDYNA,
     &            SDSENS,SDTIME,SDNUME,SDERRO,DEFICO,
     &            RESOCO,RESOCU,PARCON,VALINC,SOLALG,
     &            VEELEM,VEASSE,MEELEM,MEASSE,ERROR )
C
      IF (ERROR) GOTO 315
C
C --- SUIVI DE DDL
C
      CALL NMSUIV(MAILLA,SDSUIV,SDIMPR,SDDYNA,VALINC,
     &            VEASSE,RESOCO)
C
C --- ESTIMATION DE LA CONVERGENCE
C
 315  CONTINUE
      CALL NMCONV(MAILLA,MATE  ,NUMEDD,FONACT,SDDYNA,
     &            SDIMPR,SDDISC,SDCRIT,SDERRO,SDTIME,
     &            PARMET,COMREF,MATASS,NUMINS,ITERAT,
     &            CONV  ,ETA   ,PARCRI,DEFICO,RESOCO,
     &            VALINC,SOLALG,MEASSE,VEASSE,ITEMAX,
     &            CONVER,FINPAS,MAXREL)
C
C --- VERIFICATION DES EVENT-DRIVEN
C
      CALL NMEVDR(SDDISC,SDERRO,CONVER,VALINC,ITEMAX,
     &            ACTITE)
C
C --- EXAMEN NEWTON -> ACTION POUR LA SUITE
C     0 - NEWTON OK   - BOUCLE DE CONTACT SUIVANTE
C     1 - NEWTON NOOK - IL FAUT FAIRE QUELQUE CHOSE
C     2 - NEWTON NCVG - ON CONTINUE NEWTON
C     3 - NEWTON STOP - TEMPS/USR1
C
      IF ((ACTITE.EQ.0).OR.(ACTITE.EQ.1)) THEN
        GOTO 330
      ENDIF
C
C --- ON CONTINUE LES ITERATIONS DE NEWTON : CALCUL DE LA DESCENTE
C
 320  CONTINUE
      CALL NMDESC(MODELE,NUMEDD,NUMFIX,MATE  ,CARELE,
     &            COMREF,COMPOR,LISCHA,RESOCO,METHOD,
     &            SOLVEU,PARMET,CARCRI,FONACT,NUMINS,
     &            ITERAT,SDDISC,SDTIME,SDDYNA,SDNUME,
     &            SDERRO,MATASS,MAPREC,DEFICO,VALINC,
     &            SOLALG,ETA   ,MEELEM,MEASSE,VEASSE,
     &            VEELEM,ERROR )
C
      IF (ERROR) THEN
        ACTITE = 1
      ELSE
        ACTITE = 2
        ITERAT = ITERAT + 1
      ENDIF
C
C --- VERIFICATION SI INTERRUPTION DEMANDEE PAR SIGNAL USR1
C
      IF (ETAUSR().EQ.1) THEN
        ACTITE = 3
      ENDIF
C
C --- TEMPS DISPONIBLE POUR FAIRE UNE NOUVELLE ITERATION DE NEWTON ?
C
      CALL NMTIME('MES','ITE',SDTIME,MTCPUI,R8BID )
      IF (MTCPUI) THEN
        CALL NMERGE('SET','TIN',SDERRO,MTCPUI)
        ACTITE = 3
      ENDIF
C
 330  CONTINUE
C
C --- TEMPS CPU ITERATION DE NEWTON
C
      CALL NMTIME('FIN','ITE',SDTIME,LBID  ,R8BID )
C
C --- ON CONTINUE NEWTON ?
C
      IF (ACTITE.EQ.2) GOTO 300
C
C ======================================================================
C     FIN DES ITERATIONS DE NEWTON
C ======================================================================
C
C
C --- GESTION DES ACTIONS SUITE A UN EVENEMENT DANS NEWTON
C
      CALL NMACTN(SDDISC,PARCRI,ITERAT,NUMINS,ITEMAX,
     &            ACTITE,ACTNIV)
C
C --- EXAMEN BOUCLE CONTACT -> ACTION
C     0 - BOUCLE CONTACT SUIVANTE
C     1 - BOUCLE CONTACT STOP - ON REFAIT LE PAS
C     2 - BOUCLE CONTACT STOP - ON FAIT DES ITERATIONS EN PLUS
C     3 - BOUCLE CONTACT STOP - ARRET DU CALCUL
C
      IF ((ACTNIV.EQ.1).OR.(ACTNIV.EQ.3).OR.(ACTNIV.EQ.4)) THEN
        GOTO 120
      ELSEIF (ACTNIV.EQ.2) THEN
        CALL NMTIME('DEBUT','ITE',SDTIME,LBID  ,R8BID )
        CALL NMERGE('SET','ITX',SDERRO,ITEMAX)
        GOTO 320
      ENDIF
C
C --- BOUCLE POUR CONTACT METHODE CONTINUE
C
      CALL NMTBLE(NIVEAU,
     &            MODELE,MAILLA,MATE  ,DEFICO,RESOCO,
     &            FONACT,MAXREL,SDIMPR,SDTIME,SDDYNA,
     &            VALINC)
C
C --- GESTION DES ACTIONS SUITE A UN EVENEMENT DANS BOUCLE CONTACT
C
 120  CONTINUE
      CALL NMACTC(NIVEAU,ACTNIV,ACTPAS)
      IF (ACTPAS.EQ.0) THEN
        GOTO 100
      ENDIF
C
C ======================================================================
C  FIN BOUCLE POUR CONTACT METHODE CONTINUE
C ======================================================================
C
      IF ((ACTPAS.LT.1).OR.(ACTPAS.GT.4)) CALL ASSERT(.FALSE.)
      NBITER = ITERAT
C

      END
