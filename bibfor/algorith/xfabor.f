      SUBROUTINE XFABOR(NOMA,CNXINV,NUNOA,NUNOB,NUNOC,FABORD)
      IMPLICIT NONE
      INTEGER         NUNOA,NUNOB,NUNOC
      CHARACTER*8     NOMA
      CHARACTER*19    CNXINV
      LOGICAL         FABORD

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C     ------------------------------------------------------------------
C
C      ON VERIFIE SI LES TROIS POINTS APPARTIENNENT A UNE FACE DE BORD
C       DANS LE CADRE DE XFEM (UNIQUEMENT APPELEE EN 3D)
C
C  ENTREES :
C     NOMA     :   NOM DU CONCEPT MAILLAGE
C     CNXINV   :   CONNECTIVITE INVERSE
C     NUNOA    :   ADRESSE DU NOEUD A
C     NUNOB    :   ADRESSE DU NOEUD B
C     NUNOC    :   ADRESSE DU NOEUD C
C
C  SORTIES :
C     FABORD   :   TRUE SSI LA FACE A LAQUELLE APPARTIENNENT LES 3
C                   POINTS EST UNE FACE DE BORD
C
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16             ZK16
      CHARACTER*24                      ZK24
      CHARACTER*32                               ZK32
      CHARACTER*80                                        ZK80
      COMMON  /KVARJE/ ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
      CHARACTER*32    JEXNUM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      INTEGER         JMA,NMANOA,NMANOB,NMANOC,JMANOA,JMANOB,JMANOC
      INTEGER         IMAA,IMAB,IMAC,ITYPMA,NUMAA,NUMAB,NUMAC,NBMACO
      INTEGER         NDIME,JTMDIM
      CHARACTER*19    MAI
      CHARACTER*8     K8B,TYPMA
C ----------------------------------------------------------------------

      CALL JEMARQ()

      MAI=NOMA//'.TYPMAIL'
      CALL JEVEUO(MAI,'L',JMA)
      CALL JEVEUO('&CATA.TM.TMDIM','L',JTMDIM)

C     RECUPERATION DES MAILLES CONTENANT LE NOEUD A
      CALL JELIRA(JEXNUM(CNXINV,NUNOA),'LONMAX',NMANOA,K8B)
      CALL JEVEUO(JEXNUM(CNXINV,NUNOA),'L',JMANOA)

C     RECUPERATION DES MAILLES CONTENANT LE NOEUD B
      CALL JELIRA(JEXNUM(CNXINV,NUNOB),'LONMAX',NMANOB,K8B)
      CALL JEVEUO(JEXNUM(CNXINV,NUNOB),'L',JMANOB)

C     RECUPERATION DES MAILLES CONTENANT LE NOEUD C
      CALL JELIRA(JEXNUM(CNXINV,NUNOC),'LONMAX',NMANOC,K8B)
      CALL JEVEUO(JEXNUM(CNXINV,NUNOC),'L',JMANOC)

C     ON COMPTE LE NBRE DE MAILLES VOLUMIQUES COMMUNES AUX 3 NOEUDS :
      NBMACO=0

C     BOUCLE SUR LES MAILLES CONTENANT LE NOEUD A
      DO 100 IMAA=1,NMANOA
        NUMAA = ZI(JMANOA-1+IMAA)

        ITYPMA = ZI(JMA-1+NUMAA)
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYPMA),TYPMA)

C       NDIME : DIMENSION TOPOLOGIQUE DE LA MAILLE
        NDIME= ZI(JTMDIM-1+ITYPMA)

C       SI MAILLE NON VOLUMIQUE ON CONTINUE À 100
        IF (NDIME.NE.3) GOTO 100

C       BOUCLE SUR LES MAILLES CONTENANT LE NOEUD B
        DO 110 IMAB=1,NMANOB
          NUMAB = ZI(JMANOB-1+IMAB)

          ITYPMA = ZI(JMA-1+NUMAB)
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYPMA),TYPMA)

          NDIME= ZI(JTMDIM-1+ITYPMA)

C         SI MAILLE NON VOLUMIQUE ON CONTINUE À 110
          IF (NDIME.NE.3) GOTO 110

C         SI LA MAILLE EST EN COMMUN AUX NOEUDS A ET B,
C         ON BOUCLE SUR LES MAILLES CONTENANT LE NOEUD C
          IF (NUMAA.EQ.NUMAB) THEN
            DO 120 IMAC=1,NMANOC
              NUMAC = ZI(JMANOC-1+IMAC)

              ITYPMA = ZI(JMA-1+NUMAC)
              CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYPMA),TYPMA)

              NDIME= ZI(JTMDIM-1+ITYPMA)

C             SI MAILLE NON VOLUMIQUE ON CONTINUE À 120
              IF (NDIME.NE.3) GOTO 120

C             SI LA MAILLE EST EN COMMUN AUX NOEUDS B ET C (ET A),
C             ON A DECOUVERT UNE MAILLE QUE LES TROIS NOEUDS
C             ONT EN COMMUN
              IF (NUMAB.EQ.NUMAC)    NBMACO=NBMACO+1

 120        CONTINUE
          ENDIF
 110    CONTINUE
 100  CONTINUE

      CALL ASSERT(NBMACO.GT.0)

      FABORD=.FALSE.
      IF (NBMACO.EQ.1)  FABORD=.TRUE.

      CALL JEDEMA()
      END
