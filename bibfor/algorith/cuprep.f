      SUBROUTINE CUPREP(NEQ,DEFICU,RESOCU,DEPTOT,INST)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 14/03/2006   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT     NONE
      INTEGER      NEQ
      CHARACTER*24 DEFICU      
      CHARACTER*24 RESOCU
      CHARACTER*24 DEPTOT
      REAL*8       INST
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : NMUNIL
C ----------------------------------------------------------------------
C
C CETTE ROUTINE PREPARE LE CAS DES RELATIONS UNILATERALES QUI
C NE SONT PAS DES DEPLACEMENTS
C - CALCUL DU "JEU" (MEMBRE DE DROITE)
C - CALCUL DES COEFFICIENTS DE LA RELATION LINEAIRE (MEMBRE DE GAUCHE) 
C
C IN  NEQ    : NOMBRE D'EQUATIONS DU SYSTEME
C IN  DEFICU : SD DE DEFINITION (ISSUE D'AFFE_CHAR_MECA)
C IN  RESOCU : SD DE TRAITEMENT NUMERIQUE 
C IN  DEPTOT : DEPLACEMENT TOTAL OBTENU A L'ISSUE DE L'ITERATION
C               DE NEWTON PRECEDENTE
C IN  INST   : INSTANT COURANT
C
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      ICMP
      REAL*8       COEF,VAL,SIGN
      CHARACTER*24 APCOEF,APJEU,DIMECU,COEFD,COEFG,POINOE,APDDL
      INTEGER      JAPCOE,JAPJEU,JDIM,JCOEFD,JCOEFG,JPOI,JAPDDL,JDEPP
      CHARACTER*24 CMPGCU
      INTEGER      JCMPG
      INTEGER      NBNOE,NBGAU,INOE,TYPCOE,NBDDL,JDECAL
      INTEGER      IFM,NIV      
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ
C ----------------------------------------------------------------------
      CALL INFNIV(IFM,NIV)      
C
C --- LECTURE DES SD 
C
      APCOEF = RESOCU(1:14) // '.APCOEF'
      APJEU  = RESOCU(1:14) // '.APJEU'
      DIMECU = DEFICU(1:16) // '.DIMECU'
      APDDL  = RESOCU(1:14)//'.APDDL'            
      CALL JEVEUO(APJEU, 'E',JAPJEU)
      CALL JEVEUO(DIMECU,'L',JDIM) 
      CALL JEVEUO(APCOEF,'E',JAPCOE)
      CALL JEVEUO(APDDL ,'L',JAPDDL)      
C
C --- NOMBRE TOTAL DE DDLS ET NOMBRE TOTAL DE NOEUDS
C    
      NBNOE  = ZI(JDIM)
      NBGAU  = ZI(JDIM+1)
C      
C --- TYPE DES COEFFICIENTS: FONCTION (2) OU CONSTANTE (1)
C
      TYPCOE = ZI(JDIM+2)     
C
C --- EVALUATION DU MEMBRE DE DROITE (PSEUDO-JEU)
C
      COEFD  = DEFICU(1:16)//'.COEFD'      
      CALL JEVEUO(COEFD,'L',JCOEFD) 
      
      DO 10 INOE = 1,NBNOE    
        CALL CUCOEF(JCOEFD,INOE,TYPCOE,INST,
     &              COEF)
        ZR(JAPJEU+INOE-1) = COEF      
 10   CONTINUE
C
C --- EVALUATION DU MEMBRE DE GAUCHE (COEFFICIENTS DE LA REL. LIN.)
C   
      COEFG  = DEFICU(1:16)//'.COEFG'      
      CALL JEVEUO(COEFG,'L',JCOEFG) 
      CMPGCU = DEFICU(1:16)//'.CMPGCU'
      CALL JEVEUO(CMPGCU,'L',JCMPG)
      
      DO 20 ICMP = 1,NBGAU    
        CALL CUSIGN(JCMPG,ICMP,SIGN)  
        CALL CUCOEF(JCOEFG,ICMP,TYPCOE,INST,
     &              COEF)       
        ZR(JAPCOE+ICMP) = SIGN*COEF   
 20   CONTINUE      
C 
C --- CALCUL DE -A.DEPTOT ET RANGEMENT DANS APJEU
C 
      POINOE = DEFICU(1:16)//'.POINOE'
      CALL JEVEUO(POINOE,'L',JPOI)
      CALL JEVEUO(DEPTOT(1:19)//'.VALE','E',JDEPP)

      DO 30 INOE = 1,NBNOE
        JDECAL = ZI(JPOI+INOE-1)
        NBDDL  = ZI(JPOI+INOE) - ZI(JPOI+INOE-1)
        CALL CALADU(NEQ,NBDDL,ZR(JAPCOE+JDECAL),ZI(JAPDDL+JDECAL),
     &             ZR(JDEPP),VAL)      
        ZR(JAPJEU+INOE-1) = ZR(JAPJEU+INOE-1) - VAL
   30 CONTINUE

C ----------------------------------------------------------------------
      CALL JEDEMA()

      END
