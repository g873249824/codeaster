      SUBROUTINE NMCPL1(COMPOR,TYPMOD,OPTION,VIMP,DEPS,OPTIO2,CP,NVV)
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      IMPLICIT NONE
C ----------------------------------------------------------------------
C MODIF ALGORITH  DATE 08/03/2002   AUTEUR JMBHH01 J.M.PROIX 
C     CONTRAINTES PLANES PAR LA METHODE DE BORST / CONDENSATION STATIQUE
C     POUR LES COMPORTEMENTS QUI N'INTEGRENT PAS LES CONTRAINTES PLANES
C     ATTENTION : POUR BIEN CONVERGER, IL FAUT REACTUALISER LA MATRICE
C     TANGENTE. DE PLUS, IL FAUT AJOUTER 4 VARIABLES INTERNEW
C
C IN  TYPMOD  : TYPE DE MODELISATION
C     COMPOR  : COMPORTEMENT :  (1) = TYPE DE RELATION COMPORTEMENT
C     COMPOR  : COMPORTEMENT :  (3) = 'COMP_INCR_CP' SI 'C_PLAN_DEBORST'
C     OPTION  : OPTION DEMANDEE : RIGI_MECA_TANG , FULL_MECA , RAPH_MECA
C     VIMP    : VARIABLES INTERNES
C               IN  : ESTIMATION (ITERATION PRECEDENTE OU LAG. AUGM.)
C     DEPS    : INCREMENT DE DEFORMATION TOTALE :
C               DEPS(T) = DEPS(MECANIQUE(T)) + DEPS(DILATATION(T))
C OUT OPTIO2  : OPTION MODIFIEE POUR TOUJOURS CALCULER K TANGENT
C     CP      : LOGIQUE POUR NMCPL2
C OUT NVV     : NOMBRE DE VRAIES VARIABLES INTERNES
C ----------------------------------------------------------------------
C
      CHARACTER*8        TYPMOD(*)
      CHARACTER*16       OPTION,OPTIO2
      CHARACTER*16       COMPOR(*)
      REAL*8             VIMP(*),DEPZZ,DEPS(*),RAC2
      LOGICAL CP
      INTEGER NVV,I,NBVARI

      CP = .FALSE.
      NVV=0
      RAC2 = SQRT(2.D0)
      
      IF (TYPMOD(1).EQ.'C_PLAN') THEN
         IF (COMPOR(5)(1:7).EQ.'DEBORST') THEN
            IF (COMPOR(3).EQ.'SIMO_MIEHE') THEN
                CALL UTMESS ('F','NMCPL1','C_PLAN METHODE DE BORST'
     &       // 'ET SIMO_MIEHE INCOMPATIBLES')
            ENDIF
            READ (COMPOR(2),'(I16)') NBVARI
            NVV=NBVARI-4
            WRITE (COMPOR(2),'(I16)') NVV
            CP =  .TRUE.
         ENDIF
      ENDIF
      
      IF (CP) THEN
         TYPMOD(1)='AXIS'
         OPTIO2=OPTION
         IF (OPTIO2.EQ.'RAPH_MECA') THEN
            OPTION='FULL_MECA'
         ENDIF
         IF (OPTION.EQ.'FULL_MECA') THEN
            DEPZZ=VIMP(NVV+1)
     &           -VIMP(NVV+2)*DEPS(1)-VIMP(NVV+3)*DEPS(2)
     &           -VIMP(NVV+4)*DEPS(4)/RAC2
            DEPS(3)=DEPZZ
         ENDIF
      ENDIF

      END
