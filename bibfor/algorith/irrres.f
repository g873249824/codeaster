      SUBROUTINE IRRRES ( FAMI, KPG, KSP, MOD, NMAT, MATERD, MATERF,
     &                    YD ,  YF,   DEPS,   DY,  R )


      IMPLICIT NONE

      CHARACTER*8 MOD
      CHARACTER*(*) FAMI
      INTEGER     NMAT,KPG,KSP
      REAL*8      MATERD(NMAT,2) ,MATERF(NMAT,2)
      REAL*8      YD(*),YF(*),DEPS(6),DY(*),R(*)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/05/2006   AUTEUR MJBHHPE J.L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

      REAL*8    DFDS(6),ID3D(6),SF,A,B
      REAL*8    IRRAD,IRRAF,DPHI,SIGD(6),SIGF(6),DKOOH(6,6)
      REAL*8    FKOOH(6,6),HOOKF(6,6),K,N,P0,AI0,ETAIS,AG,P,ZETA
      REAL*8    DP,DETAI,DPI,DG,ETAIF,S,EPSEF(6),EPSD(6),PE,KAPPA,R02
      REAL*8    DEPSA(6),DEPSG(6),DEV(6),EPSED(6),LCNRTS
      REAL*8    RS(6),RP,RE,RPI,RG,QF,DSIG(6),R8PREM
      REAL*8    PK,PENPE,SPE,SD,DEVD(6)
      REAL*8    AGD,ZETAD,AG2,ZETA2,ETAID,XXX,DPHI2,YYY
      INTEGER   NDT, NDI,IRET,II
C     ----------------------------------------------------------------
      COMMON /TDIM/   NDT , NDI
C     ----------------------------------------------------------------
      
      DATA ID3D /1.D0, 1.D0, 1.D0, 0.D0, 0.D0, 0.D0/

      CALL LCEQVN ( NDT , YF(1), SIGF)
      CALL LCEQVN ( NDT , YD(1), SIGD)
      CALL LCOPIL  ( 'ISOTROPE' , MOD , MATERD(1,1) , DKOOH )
      CALL LCOPIL  ( 'ISOTROPE' , MOD , MATERF(1,1) , FKOOH )
      CALL LCOPLI  ( 'ISOTROPE' , MOD , MATERF(1,1) , HOOKF )
      
C     RECUPERATION DES CARACTERISTIQUES MATERIAUX A t+
      AI0   = MATERF(4,2)
      ETAIS = MATERF(5,2)
      AG    = MATERF(6,2)
      K     = MATERF(7,2)
      N     = MATERF(8,2)
      P0    = MATERF(9,2)
      KAPPA = MATERF(10,2)
      R02   = MATERF(11,2)
      ZETA  = MATERF(12,2)
      PENPE = MATERF(13,2)
      PK    = MATERF(14,2)
      PE    = MATERF(15,2)
      SPE   = MATERF(16,2)
C     RECUPERATION DES CARACTERISTIQUES MATERIAUX A t-
      AGD   = MATERD(6,2)
      ZETAD = MATERD(12,2)
C     RECUPERATION DES CARACTERISTIQUES MATERIAUX A t- + dt/2
      AG2   = MATERF(17,2)
      ZETA2 = MATERF(18,2)


C     RECUPERATION DES VARIABLES INTERNES A t+
      P     = YF(NDT+1)
      ETAIF = YF(NDT+2)
C     RECUPERATION DES VARIABLES INTERNES A t-
      ETAID = YD(NDT+2)

C     RECUPERATION DES INCREMENTS DES VARIABLES INTERNES
      DP    = DY(NDT+1)
      DETAI = DY(NDT+2)
      DPI   = DY(NDT+3)
      DG    = DY(NDT+4)

      CALL RCVARC('F','IRRA','-',FAMI,KPG,KSP,IRRAD,IRET)
      CALL RCVARC('F','IRRA','+',FAMI,KPG,KSP,IRRAF,IRET)
      DPHI=IRRAF-IRRAD

      CALL LCDEVI(SIGF,DEV)
      S = LCNRTS (DEV)
      IF ( S.EQ.0.D0) THEN
        CALL LCINVE (0.D0,DFDS)
      ELSE
        CALL LCPRSV ( 1.5D0 / S , DEV , DFDS )
      ENDIF
      CALL LCPRMV(FKOOH,SIGF,EPSEF)
      CALL LCPRMV(DKOOH,SIGD,EPSED)
      CALL LCPRSV((DP+DPI),DFDS,DEPSA)
      CALL LCPRSV(DG,ID3D,DEPSG)

C   RESIDU EN SIGMA
      CALL LCDIVE(EPSEF,EPSED,RS)
      CALL LCSOVE(RS,DEPSA,RS)
      CALL LCSOVE(RS,DEPSG,RS)
      CALL LCDIVE(RS,DEPS,RS)
      CALL LCPRSV(-1.D0,RS,RS)

C  RESIDU EN DEFORMATION PLASTIQUE
      IF      ( P .LT. PK ) THEN
         SF = KAPPA*R02
      ELSE IF ( P .LT. PE ) THEN
         SF = PENPE*(P - PE) + SPE
      ELSE
         SF = K*(P+P0)**N
      ENDIF
      IF (((S.GE.SF).AND.(DP.GE.0.D0)).OR.(DP.GT.R8PREM())) THEN
         RP = -(S-SF)/HOOKF(1,1)
      ELSE
         RP = -DP
      ENDIF

C     CONTRAINTE EQUIVALENTE A t-
      CALL LCDEVI(SIGD,DEVD)
      SD = LCNRTS(DEVD)

C  RESIDU PAR RAPPORT A ETA
C      XXX =  (ZETAD*SD + ZETA*S)/2.0
      YYY =  (ZETAD*SD + ZETA2*(SD+S)*2.0D0 + ZETA*S)/6.0D0
C      write(*,'(7E15.6)') XXX,YYY,S,SD,ZETAD,ZETA2,ZETA
      RE  = -( DETAI - YYY*DPHI )/HOOKF(1,1)


C  RESIDU EN DEFORMATION D IRRADIATION
C      IF ( ETAIF .GT. ETAIS ) THEN
C         RPI= -( DPI - AI0*DETAI )
C      ELSE
C         RPI= -DPI
C      ENDIF

      IF ( ETAIF .GT. ETAIS ) THEN
         IF ( ETAID .LT. ETAIS) THEN
            DETAI = DETAI - (ETAIS - ETAID)
         ENDIF
         RPI = -( DPI - AI0*DETAI )
      ELSE
         RPI = - DPI
      ENDIF

C  RESIDU PAR RAPPORT AU GONFLEMENT
      IF ( DPHI .GT. 0.0D0 ) THEN
C        EVALUATION NIVEAU 1   
C         RG= -( DG - AG*DPHI)
C        EVALUATION NIVEAU 2
C         RG= -( DG - AG2*DPHI )
C        EVALUATION NIVEAU 4
         RG= -( DG - (AGD+4.0D0*AG2+AG)*DPHI/6.0D0 )
      ELSE
         RG = - DG
      ENDIF

C  RESIDU PAR RAPPORT A LA DEFORMATION ( C_PLAN )
      IF ( MOD(1:6) .EQ. 'C_PLAN' ) THEN
         QF = (-HOOKF(3,3)*EPSEF(3)
     &         -HOOKF(3,1)*EPSEF(1)
     &         -HOOKF(3,2)*EPSEF(2)
     &         -HOOKF(3,4)*EPSEF(4))/HOOKF(1,1)
      ENDIF
      
      CALL LCEQVN ( NDT , RS     , R(1) )
      R(NDT+1)=RP
      R(NDT+2)=RE
      R(NDT+3)=RPI
      R(NDT+4)=RG
      
      IF ( MOD(1:6).EQ.'C_PLAN' ) R(NDT+5) = QF
      END
