      SUBROUTINE EDGANI (DIM,Y,PM,DVSITR,EQSITR,MU,ANI,GAMMA,M,N,
     1                    G,MAXG,DGDY)
      IMPLICIT REAL*8 (A-H,O-Z)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 19/02/2008   AUTEUR CANO V.CANO 
C RESPONSABLE CANO V.CANO

      INTEGER  DIM
      REAL*8   Y(DIM),PM,DVSITR(DIM-1),EQSITR
      REAL*8   MU,ANI(6,6),GAMMA(3),M(3),N(3)
      REAL*8   G(DIM),MAXG,DGDY(DIM,DIM)
                  
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C ----------------------------------------------------------------------
C     MODELE VISCOPLASTIQUE SANS SEUIL DE EDGAR
C     CALCUL DE LA FONCTION G DE SA DERIVEE ET DU CRITERE D ARRET  
C  IN  DIM   : DIMENSION DU SYSTEME A RESOUDRE
C              DIM=NDIM+1 AVEC NDIM=6 EN 3D ET 4 EN 2D
C  IN  Y    :  Y(1)=DVEPSEL(1)
C              Y(2)=DVEPSEL(2)
C              Y(3)=DVEPSEL(3) 
C              Y(4)=DVEPSEL(4)
C              Y(5)=DVEPSEL(5) (EN 3D)
C              Y(6)=DVEPSEL(6) (EN 3D)
C              Y(DIM) = DP
C  IN  PM      : DEFORMATION PLASTIQUE CUMULEE A L INSTANT MOINS
C  IN  DVSITR : CONTRAINTE DEVIATORIQUE ESSAI CONNUE
C  IN  MU      : COEFFICIENT DE MATERIAU ELASTIQUE
C  IN  ANI     : MATRICE DE HILL
C  IN  GAMMA   : COEFFICIENT VISQUEUX
C  IN  M       : COEFFICIENT VISQUEUX
C  IN  N       : COEFFICIENT VISQUEUX

C  OUT G       : POUR I ET J = 1 A NDIM
C       G(I)=Y(I)+Y(DIM)*ANI(I,J)*Y(J)/EQEPEL-DVEPSTR(I)
C       G(DIM)=EQEPEL-GAMMA(K)*((PM+Y(DIM))**M(K))*(Y(DIM)**N(K))
C  OUT MAXG    : MAXIMUM DE G
C  OUT DGDY    : DERIVEE DE G (TROP LONG VOIR DOC R POUR EXPRESSION)

      INTEGER    I,J,K,NDIM
      REAL*8     DP,EQEPEL,VECT(DIM-1)
      REAL*8     EDGEQU,PDTSCA(6)
      DATA       PDTSCA/1.D0,1.D0,1.D0,2.D0,2.D0,2.D0/

C 1 - CALCUL DE CERTAINS PARAMETRES
C 1.1 - DIMENSION

      NDIM=DIM-1
      DP=Y(DIM)
      
C 1.2 - DEFORMATION ELASTIQUE EQUIVALENTE
      
      EQEPEL = EDGEQU (NDIM,Y,ANI)            

C 1.3 - VECTEUR ANI(I,J)*Y(J)

      DO 5 I=1,NDIM
        VECT(I)=0.D0
        DO 10 J=1,NDIM
          VECT(I)=VECT(I)+ANI(I,J)*PDTSCA(J)*Y(J)
 10     CONTINUE
 5    CONTINUE

C 2 - VECTEUR G

      DO 15 I=1,NDIM
        G(I)=Y(I)-DVSITR(I)/(2.D0*MU)+DP*VECT(I)/EQEPEL
        G(I)=-G(I)
 15   CONTINUE
      
      G(DIM)= EQEPEL
      DO 20 K=1,3
        G(DIM)=G(DIM)-GAMMA(K)*((PM+DP)**M(K))*(DP**N(K))
 20   CONTINUE
      G(DIM)=-G(DIM)
      
C 3 - MAX DE G POUR CRITERE REMIS EN CONTRAINTE
      
      MAXG=ABS(G(1))
      DO 25 I=2,DIM
        IF (ABS(G(I)).GT.MAXG) MAXG=ABS(G(I))  
 25   CONTINUE
      MAXG=2.D0*MU*MAXG

C 2 - DERIVEE DU VECTEUR G
C     = [DG(1)/DVEPSEL(1)... .DG(1)/DVEPSEL(NDIM)    - DG(1)/DP   ]
C       [.......................................    .- ...... ....]
C       [DG(NDIM)/DVEPSEL(1)..DG(NDIM)/DVEPSEL(NDIM) - DG(NDIM)/DP]
C       [---------------------------------------------------------]
C       [DG(DIM)/DVEPSEL(1)..DG(DIM)/DVEPSEL(NDIM)   - DG(DIM)/DP] 

      DO 30 I=1,NDIM
        DO 35 J=1,NDIM
          DGDY(I,J)=DP*ANI(I,J)/EQEPEL
     1            -DP*VECT(I)*VECT(J)/(EQEPEL**3.D0)
          IF (I.EQ.J) THEN
            IF (I.LE.3) DGDY(I,J)=DGDY(I,J)+1.D0
            IF (I.GE.4) DGDY(I,J)=DGDY(I,J)+0.5D0
          ENDIF
          IF (J.GE.4) DGDY(I,J)=2.D0*DGDY(I,J)
 35     CONTINUE 
 30   CONTINUE

      DO 40 I=1,NDIM
        DGDY(I,DIM)=VECT(I)/EQEPEL
 40   CONTINUE 

      DO 45 J=1,NDIM
        DGDY(DIM,J)=VECT(J)/EQEPEL
        IF (J.GE.4) DGDY(DIM,J)=2.D0*DGDY(DIM,J)
 45   CONTINUE

      DGDY(DIM,DIM)=0.D0
      DO 50 K=1,3
        DGDY(DIM,DIM)=DGDY(DIM,DIM)
     1       -GAMMA(K)*M(K)*((PM+DP)**(M(K)-1.D0))*(DP**N(K))
     2       -GAMMA(K)*N(K)*((PM+DP)**M(K))*(DP**(N(K)-1.D0))
 50   CONTINUE

      END
