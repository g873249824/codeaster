        SUBROUTINE LCMMON( FAMI,KPG,KSP,COMP,NBCOMM,CPMONO,NMAT,NVI,
     &                     VINI,X,  DTIME, E,NU,ALPHA,PGL,MOD,COEFT,
     &                     SIGI,EPSD, DETOT,
     &                     COEL,DVIN, HSR,ITMAX,TOLER,IRET )
        IMPLICIT NONE
        INTEGER KPG,KSP,NMAT,NBCOMM(NMAT,3),NVI,ITMAX,IRET
        REAL*8 VINI(*),DVIN(*),NU,E,ALPHA,X,DTIME,COEFT(NMAT),COEL(NMAT)
        REAL*8 SIGI(6),EPSD(6),DETOT(6),PGL(3,3),HSR(5,30,30),TOLER
        CHARACTER*(*) FAMI
        CHARACTER*16 COMP(*)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 03/10/2011   AUTEUR PROIX J-M.PROIX 
C RESPONSABLE JMBHH01 J.M.PROIX
C TOLE CRP_21
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21
C ======================================================================
C       IN FAMI     :  FAMILLE DE POINT DE GAUSS (RIGI,MASS,...)
C         KPG,KSP   :  NUMERO DU (SOUS)POINT DE GAUSS
C          COMP     :  NOM DU MODELE DE COMPORTEMENT
C           MOD     :  TYPE DE MODELISATION
C           IMAT    :  ADRESSE DU MATERIAU CODE
C         NBCOMM :  NOMBRE DE COEF MATERIAU PAR FAMILLE
C         CPMONO :  NOMS DES LOIS MATERIAU PAR FAMILLE
C           PGL   : MATRICE DE PASSAGE GLOBAL LOCAL
C           NVI     :  NOMBRE DE VARIABLES INTERNES
C           VINI    :  VARIABLES INTERNES A T
C           X       :  INTERVALE DE TEMPS ADAPTATIF
C           DTIME   :  INTERVALE DE TEMPS
C           COEFT   :  COEFFICIENTS MATERIAU INELASTIQUE A T
C           SIGI    :  CONTRAINTES A L'INSTANT COURANT
C           EPSD    :  DEFORMATION TOTALE A T
C           DETOT   :  INCREMENT DE DEFORMATION TOTALE
C     OUT:
C           DVIN    :  DERIVEES DES VARIABLES INTERNES A T
C INTEGRATION DES LOIS MONOCRISTALLINES PAR UNE METHODE DE RUNGE KUTTA
C
C     CETTE ROUTINE FOURNIT LA DERIVEE DE L ENSEMBLE DES VARIABLES
C     INTERNES DU MODELE
C
C     ----------------------------------------------------------------
      CHARACTER*8 MOD
      CHARACTER*16 NOMFAM,NECOUL,NECRIS,NECRCI,CPMONO(5*NMAT+1)
      REAL*8 VIS(3),DT,DY(30),EXPBP(30),CRIT,SGNS
      REAL*8 EVI(6),RP
      REAL*8 DEVI(6),MS(6),NG(3),TAUS,DGAMMA,DALPHA,DP
      REAL*8 Q(3,3),LG(3)
      INTEGER ITENS,NBFSYS,I,NUVI,IFA,NBSYS,IS,IV,NUMS,NSFV
      INTEGER IEXP,IFL,NUECOU
C     ----------------------------------------------------------------
C --  VARIABLES INTERNES
C
      DO 5 ITENS=1,6
        EVI(ITENS) = VINI(ITENS)
        DEVI(ITENS) = 0.D0
    5 CONTINUE
      DO 15 I=1,30
         DY(I)=0.D0
 15   CONTINUE

      CALL CALSIG(FAMI,KPG,KSP,EVI,MOD,E,NU,ALPHA,X,DTIME,EPSD,
     &              DETOT,NMAT,COEL,SIGI)


      NBFSYS=NBCOMM(NMAT,2)

      NUVI=6
      NUMS=0
C     NSFV : debut de la famille IFA dans les variables internes
      NSFV=6

      DO 6 IFA=1,NBFSYS
            IFL=NBCOMM(IFA,1)
            NUECOU=NINT(COEFT(IFL))

         NOMFAM=CPMONO(5*(IFA-1)+1)
C         NMATER=CPMONO(5*(IFA-1)+2)
         NECOUL=CPMONO(5*(IFA-1)+3)
         NECRIS=CPMONO(5*(IFA-1)+4)
         NECRCI=CPMONO(5*(IFA-1)+5)

         CALL LCMMSG(NOMFAM,NBSYS,0,PGL,MS,NG,LG,0,Q)

         IF (NBSYS.EQ.0) CALL U2MESS('F','ALGORITH_70')

         DO 7 IS=1,NBSYS
            NUMS=NUMS+1

C           VARIABLES INTERNES DU SYST GLIS
            DO 8 IV=1,3
               NUVI=NUVI+1
               VIS(IV)=VINI(NUVI)
  8         CONTINUE

C           CALCUL DE LA SCISSION REDUITE =
C           PROJECTION DE SIG SUR LE SYSTEME DE GLISSEMENT
C           TAU      : SCISSION REDUITE TAU=SIG:MS
            CALL LCMMSG(NOMFAM,NBSYS,IS,PGL,MS,NG,LG,0,Q)

            TAUS=0.D0
            DO 10 I=1,6
               TAUS=TAUS+SIGI(I)*MS(I)
 10         CONTINUE
C
C           ECROUISSAGE ISOTROPE
C
C            IF (NECOUL.NE.'KOCKS_RAUCH') THEN
            IF (NUECOU.NE.4) THEN
               IEXP=0
               IF (IS.EQ.1) IEXP=1
               CALL LCMMFI(COEFT,IFA,NMAT,NBCOMM,NECRIS,
     &        IS,NBSYS,VINI(NSFV+1),DY(1),HSR,IEXP,EXPBP,RP)
            ENDIF

C           ECOULEMENT VISCOPLASTIQUE:
C           ROUTINE COMMUNE A L'IMPLICITE (PLASTI-LCPLNL)
C           ET L'EXPLICITE (NMVPRK-GERPAS-RK21CO-RDIF01)
C           CAS IMPLCITE : IL FAUT PRENDRE EN COMPTE DTIME
C           CAS EXPLICITE : IL NE LE FAUT PAS (ON CALCULE DES VITESSES)
C           D'OU :
            DT=1.D0
C
            CALL LCMMFE( TAUS,COEFT,COEL,IFA,
     &      NMAT,NBCOMM,NECOUL,IS,NBSYS,VINI(NSFV+1),DY(1),
     &      RP,VIS(1),VIS(2),DT,DALPHA,DGAMMA,DP,CRIT,SGNS,HSR,IRET)
C            IF (NECOUL.NE.'KOCKS_RAUCH') THEN
            IF ((NUECOU.NE.4).AND.(NUECOU.NE.5)) THEN
C              ECROUISSAGE CINEMATIQUE
               CALL LCMMEC( COEFT,IFA,NMAT,NBCOMM,NECRCI,
     &           ITMAX, TOLER,VIS(1),DGAMMA,DALPHA, IRET)
               IF (IRET.NE.0) GOTO 9999
            ENDIF

            DO 9 ITENS=1,6
               DEVI(ITENS)=DEVI(ITENS)+MS(ITENS)*DGAMMA
  9         CONTINUE

            DVIN(NUVI-2)=DALPHA
            DVIN(NUVI-1)=DGAMMA
            DVIN(NUVI  )=DP
  7     CONTINUE

        NSFV=NSFV+NBSYS*3

  6   CONTINUE
C
C --    DERIVEES DES VARIABLES INTERNES
C
      DO 30 ITENS=1,6
        DVIN(ITENS)= DEVI(ITENS)
   30 CONTINUE

 9999 CONTINUE
      END
