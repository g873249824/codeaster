      SUBROUTINE CFGCIN(RESOCO,MATASS,SOLVEU,NEQ   ,NBLIAI)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 07/11/2011   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT     NONE
      CHARACTER*24 RESOCO
      INTEGER      NEQ,NBLIAI
      CHARACTER*19 MATASS,SOLVEU
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (RESOLUTION - GCP)
C
C INITIALISATION DE L'ALGORITHME
C
C ----------------------------------------------------------------------
C
C
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  SOLVEU : SD SOLVEUR
C IN  MATASS : NOM DE LA MATRICE DU PREMIER MEMBRE ASSEMBLEE
C IN  NBLIAI : NOMBRE DE LIAISONS DE CONTACT
C IN  NEQ    : NOMBRE D'EQUATIONS
C
C --------------- DEBUT DECLARATIONS NORMALISEES JEVEUX ---------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      REAL*8       NORME2,DDOT
      INTEGER      ILIAI,JDECAL,NBDDL
      CHARACTER*19 MU
      INTEGER      JMU
      CHARACTER*24 APCOEF,APDDL,APPOIN
      INTEGER      JAPCOE,JAPDDL,JAPPTR
      CHARACTER*24 SECMBR,CNCIN0
      INTEGER      JSECMB
      CHARACTER*19 DDEPLC,DDELT
      INTEGER      JDDEPC,JDDELT
      CHARACTER*8  K8BID
      COMPLEX*16   C16BID
      REAL*8       R8BID
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      APPOIN = RESOCO(1:14)//'.APPOIN'
      APCOEF = RESOCO(1:14)//'.APCOEF'
      APDDL  = RESOCO(1:14)//'.APDDL'
      MU     = RESOCO(1:14)//'.MU'
      CALL JEVEUO(APPOIN,'L',JAPPTR)
      CALL JEVEUO(APCOEF,'L',JAPCOE)
      CALL JEVEUO(APDDL ,'L',JAPDDL)
      CALL JEVEUO(MU    ,'L',JMU   )
C
C --- ACCES AUX CHAMPS DE TRAVAIL
C --- DDEPLC: INCREMENT DE SOLUTION APRES CORRECTION DU CONTACT
C --- DDELT : INCREMENT DE SOLUTION ITERATION DE CONTACT
C
      DDEPLC = RESOCO(1:14)//'.DELC'
      DDELT  = RESOCO(1:14)//'.DDEL'
      CNCIN0 = RESOCO(1:14)//'.CIN0'
      SECMBR = RESOCO(1:14)//'.SECM'
      CALL JEVEUO(DDEPLC(1:19)//'.VALE','E',JDDEPC)
      CALL JEVEUO(SECMBR(1:19)//'.VALE','E',JSECMB)
C
C --- INITIALISATION A PARTIR DU CHAMP DE MULTIPLICATEURS INITIAL MU
C --- S'IL EST NON-NUL
C
      NORME2 = DDOT(NBLIAI,ZR(JMU),1,ZR(JMU),1)
C
      IF (NORME2.NE.0.D0) THEN
C
C ----- CALCUL DU SECOND MEMBRE
C
        CALL R8INIR(NEQ,0.D0,ZR(JSECMB),1)
        DO 10 ILIAI = 1,NBLIAI
          JDECAL = ZI(JAPPTR+ILIAI-1)
          NBDDL  = ZI(JAPPTR+ILIAI) - ZI(JAPPTR+ILIAI-1)
          CALL CALATM(NEQ,NBDDL,ZR(JMU+ILIAI-1),ZR(JAPCOE+JDECAL),
     &                ZI(JAPDDL+JDECAL),ZR(JSECMB))
  10    CONTINUE
C
C ----- RESOLUTION
C
        CALL RESOUD(MATASS,K8BID ,SECMBR,SOLVEU,CNCIN0,
     &              'V'   ,DDELT ,K8BID ,0     ,R8BID ,
     &              C16BID,.TRUE.)
C
C ----- U = U + (-DELTA)
C
        CALL JEVEUO(DDELT(1:19) //'.VALE','L',JDDELT)
        CALL DAXPY(NEQ,-1.D0,ZR(JDDELT),1,ZR(JDDEPC),1)
      ENDIF
C
      CALL JEDEMA()
C
      END
