      SUBROUTINE LCUMSD(VARI,NVARI,CMAT,NMAT,NSTRS,ISPH,
     $                 TDT,HINI,HFIN,
     $                 AFP,BFP,CFP,CFPS,CFPD)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/01/2003   AUTEUR YLEPAPE Y.LEPAPE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE YLEPAPE Y.LEPAPE
C
C LCUMSD     SOURCE    BENBOU   01/03/26
C
C_______________________________________________________________________
C
C ROUTINE QUI CALCUL LES MATRICES DE DEFORMATION 
C  DE FLUAGE PROPRE SPHERIQUE ET DEVIATORIQUE
C   D APRES LE MODELE UMLV
C
C     EQUATION (3.5-1)
C
C      POUR LES DEFORMATIONS
C         1 : REVERSIBLE
C         2 : IRREVERSIBLE
C         3 : TOTAL (1 + 2)
C IN  VARI     : VARIABLES INTERNES INITIALES
C IN  NVARI    : DIMENSION DES VECTEURS VARIABLES INTERNES
C IN  CMAT     : VECTEUR DE PARAMETRES (MATERIAU ET AUTRE)
C IN  NMAT     : DIMENSION DE CMAT
C IN  NSTRS    : DIMENSION DES VECTEURS CONTRAINTE ET DEFORMATION
C IN  ISPH     : MODE DE CALCUL DE LA PARTIE SPHERIQUE
C IN  TDT      : PAS DE TEMPS
C IN  HINI     : HUMIDITE INITIALE
C IN  HFIN     : HUMIDITE FINALE
C OUT AFP      : VECTEUR RELATIF A LA DEFORMATION DE FLUAGE PROPRE
C OUT BFP      : MATRICE RELATIVE A LA DEFORMATION DE FLUAGE PROPRE
C OUT CFP      : MATRICE RELATIVE A LA DEFORMATION DE FLUAGE PROPRE
C OUT CFPS     : COEFFICICIENT DE CN SPHERIQUE    (MATRICE TANGENTE)
C OUT CFPD     : COEFFICICIENT DE CN DEVIATORIQUE (MATRICE TANGENTE)
C_______________________________________________________________________
C
      IMPLICIT NONE
      INTEGER I,IFOU,ISPH,NMAT,NSTRS,NVARI
      REAL*8  VARI(NVARI),CMAT(NMAT)
C MODIFI DU 6 JANVIER 2003 - YLP SUPPRESSION DES DECLARATIONS
C IMPLICITES DES TABLEAUX
C     REAL*8  AFP(NSTRS),BFP(NSTRS,NSTRS),CFP(NSTRS,NSTRS)
      REAL*8  AFP(6),BFP(6,6),CFP(6,6)
      REAL*8  AFPD(6)
      REAL*8  AFPS,BFPD,BFPS,CFPD,CFPS,HINI,HFIN,TDT
C
C RECUPERATION DES VALEURS DES PARAMETRES MATERIAU
C
      IFOU = CMAT(12)
C
C INITIALISATION DES VARIABLES
C

      DO 9 I=1,3
        AFPD(I)   = 0.D0
9     CONTINUE
C
C CALCUL DE LA MATRICE DES DEFORMATIONS DE FLUAGE PROPRE SPHERIQUE
C          INCREMENTALES
C
       CALL LCUMFS(VARI,NVARI,CMAT,NSTRS,NMAT,0,ISPH,
     &             TDT,HINI,HFIN,
     &             AFPS,BFPS,CFPS)

C
C CALCUL DE LA MATRICE DES DEFORMATIONS DE FLUAGE PROPRE DEVIATORIQUE
C          INCREMENTALES
C

      CALL LCUMFD(VARI,NVARI,NSTRS,CMAT,NMAT,0,TDT,HINI,HFIN,
     $            AFPD,BFPD,CFPD)

C
C CONSTRUCTION DE LA MATRICE DES DEFORMATIONS DE FLUAGE PROPRE
C          INCREMENTALES
C
C   EQUATION (3.5-2)
C
      DO 10 I=1,2
        AFP(I) = AFPS + AFPD(I)
        BFP(I,I) = (BFPS + 2.D0 * BFPD)/3.D0
        CFP(I,I) = (CFPS + 2.D0 * CFPD)/3.D0
  10  CONTINUE
      BFP(1,2) = (BFPS - BFPD) / 3.D0
      BFP(2,1) = BFP(1,2)
      CFP(1,2) = (CFPS - CFPD) / 3.D0
      CFP(2,1) = CFP(1,2)
C
      IF ((IFOU.EQ.0).OR.(IFOU.EQ.-1).OR.(IFOU.EQ.2)) THEN
        AFP(3)   = AFPS + AFPD(3)
        AFP(4)   = AFPD(4)
        BFP(3,3) = BFP(1,1)
        BFP(1,3) = BFP(1,2)
        BFP(2,3) = BFP(1,2)
        BFP(3,1) = BFP(1,2)
        BFP(3,2) = BFP(1,2)
        BFP(4,4) = BFPD
        CFP(3,3) = CFP(1,1)
        CFP(1,3) = CFP(1,2)
        CFP(2,3) = CFP(1,2)
        CFP(3,1) = CFP(1,2)
        CFP(3,2) = CFP(1,2)
        CFP(4,4) = CFPD
C MODIFI DU 6 JANVIER 2003 - YLP AJOUT DES AFFECTATIONS
        AFP(5)   = 0.0D0
        AFP(6)   = 0.0D0
        BFP(5,5) = 0.0D0
        BFP(6,6) = 0.0D0
        CFP(5,5) = 0.0D0
        CFP(6,6) = 0.0D0
        IF (IFOU.EQ.2) THEN
          AFP(5)   = AFPD(5)
          AFP(6)   = AFPD(6)
          BFP(5,5) = BFPD
          BFP(6,6) = BFPD
          CFP(5,5) = CFPD
          CFP(6,6) = CFPD
        ENDIF
      ELSE
        AFP(3)   = AFPD(3)
        BFP(3,3) = BFPD
        CFP(3,3) = CFPD
      ENDIF
C
      END
