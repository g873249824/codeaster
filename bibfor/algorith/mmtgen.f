      SUBROUTINE MMTGEN(NOMA  ,DEFICO,RESOCO,NEWGEO,ITEMAX,
     &                  EPSMAX)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 22/12/2009   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8   NOMA
      CHARACTER*24  DEFICO,RESOCO,NEWGEO
      INTEGER       ITEMAX
      REAL*8        EPSMAX
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (TOUTES METHODES - APPARIEMENT)
C
C CALCUL DES VECTEURS TANGENTS EN CHAQUE NOEUD POUR CHAQUE ELEMENT
C      
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : NOUVELLE GEOMETRIE (AVEC DEPLACEMENT GEOMETRIQUE)
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  ITEMAX : NOMBRE MAXI D'ITERATIONS DE NEWTON POUR LA PROJECTION
C IN  EPSMAX : RESIDU POUR CONVERGENCE DE NEWTON POUR LA PROJECTION
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32  JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      CFDISI,NMACO,NDIMG
      INTEGER      IFM,NIV
      INTEGER      INO,IMA,I,NIVERR,IZONE,INDMAE
      INTEGER      POSMAI,NUMMAI,POSNNO(9)
      INTEGER      POSNO
      INTEGER      IBID
      REAL*8       TAU1(3),TAU2(3)
      REAL*8       COORNO(3),R8MAEM
      INTEGER      CFMMVD,ZMAES,ZTYPM
      CHARACTER*24 NOZOCO,CONTNO,TGELNO,MAESCL,TYPEMA
      INTEGER      JZOCO,JNOCO,JTGELN,JMAESC,JTYPMA
      CHARACTER*4  TYPNOE
      LOGICAL      LPOINT,LPOUTR,LSSFR,CFCALD
      REAL*8       COORMA(27)
      INTEGER      NBNO,NDIM,NBNOM
      CHARACTER*8  ALIAS,NOMMAI,NOMNO,K8BID
      INTEGER      NBEXFR,NDEXFR(3)
      CHARACTER*4  TYPMAI
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> ... TANGENTES SUR' //
     &  ' LES NOEUDS PAR ELEMENT (ELNO)' 
      ENDIF
C
C --- RECUPERATION DE QUELQUES DONNEES
C
      NOZOCO = DEFICO(1:16)//'.NOZOCO'
      CONTNO = DEFICO(1:16)//'.NOEUCO'
      TGELNO = RESOCO(1:14)//'.TGELNO'
      MAESCL = DEFICO(1:16)//'.MAESCL'
      TYPEMA = DEFICO(1:16)//'.TYPEMA'
      CALL JEVEUO(NOZOCO,'L',JZOCO )
      CALL JEVEUO(CONTNO,'L',JNOCO )
      CALL JEVEUO(MAESCL,'L',JMAESC)
      CALL JEVEUO(TYPEMA,'L',JTYPMA )
      ZMAES  = CFMMVD('ZMAES')
      ZTYPM  = CFMMVD('ZTYPM')    
C
C --- INITIALISATIONS
C
      NMACO     = CFDISI(DEFICO,'NMACO')
      NDIMG     = CFDISI(DEFICO,'NDIM' )
      COORNO(1) = 0.D0
      COORNO(2) = 0.D0
      COORNO(3) = 0.D0  
      DO 5 I = 1,27
        COORMA(I) = 0.D0
 5    CONTINUE                                    
C
C --- BOUCLE SUR LES MAILLES
C
      DO 20 IMA = 1,NMACO
C
C --- MAILLE COURANTE
C      
        POSMAI = IMA  
C
C --- CARACTERISTIQUES DE LA MAILLE
C
        CALL CFCARM(NOMA  ,DEFICO,NEWGEO,POSMAI,TYPMAI,
     &              NUMMAI,ALIAS ,NOMMAI,NDIM  ,NBNO  ,
     &              COORMA)        
        CALL CFPOSN(NOMA  ,DEFICO,POSMAI,POSNNO,NBNO  )
        CALL JELIRA(JEXNUM(NOMA//'.CONNEX',NUMMAI),'LONMAX',
     &              NBNOM ,K8BID )      
C
C --- TYPE DE MAILLE
C 
        LPOUTR = (ALIAS(1:2).EQ.'SE').AND.(NDIMG.EQ.3)
        LPOINT = ALIAS.EQ.'PO1' 
C
C --- Y-A-T-IL DES NOEUDS DE TYPE SANS_NOEUD_FR DANS LA MAILLE ?
C
        IF (TYPMAI.EQ.'ESCL') THEN 
          INDMAE = ZI(JTYPMA+ZTYPM*(POSMAI-1)+2-1)
          LSSFR  = ZI(JMAESC+ZMAES*(INDMAE-1)+5-1).NE.0
          CALL ASSERT(ZI(JMAESC+ZMAES*(INDMAE-1)+1-1).EQ.POSMAI)
          IF (LSSFR) THEN
            CALL MMFREX(NOMA  ,DEFICO,POSMAI,NBEXFR,NDEXFR)
          ELSE
            NBEXFR = 0    
          ENDIF
        ELSE
          NBEXFR = 0  
        ENDIF    
C
C --- ACCES MAILLE COURANTE
C        
        CALL JEVEUO(JEXNUM(TGELNO,POSMAI),'E',JTGELN)
C
C --- BOUCLE SUR LES NOEUDS DE LA MAILLE
C
        DO 10 INO = 1,NBNO  
C
C --- NUMERO DU NOEUD DANS SD CONTACT ET DANS MAILLAGE
C
          POSNO  = POSNNO(INO)  
          TAU1(1) = R8MAEM() 
          TAU1(2) = R8MAEM() 
          TAU1(3) = R8MAEM() 
          TAU2(1) = R8MAEM() 
          TAU2(2) = R8MAEM() 
          TAU2(3) = R8MAEM() 
C
C --- INFOS SUR LE NOEUD 
C           
          IZONE   = ZI(JZOCO+POSNO-1)        
          CALL CFCARN(NOMA  ,DEFICO,RESOCO,NEWGEO,POSNO ,
     &                IBID  ,COORNO,TYPNOE,NOMNO )
          IF (.NOT.CFCALD(DEFICO,IZONE ,TYPMAI)) THEN
            GOTO 16        
          ENDIF
C
C --- CALCUL DES TANGENTES EN CE NOEUD SI ELEMENT POINT
C        
          IF (LPOINT) THEN
            CALL MMCPOI(NOMA  ,DEFICO,NDIMG ,IZONE ,NUMMAI,
     &                  TYPMAI,TAU1  ,TAU2  ) 
            GOTO 15
          ENDIF  
C
C --- CALCUL DES TANGENTES EN CE NOEUD SI SANS_NOEUD_FR
C      
          IF (NBEXFR.NE.0) THEN
            CALL MMTGEX(NOMA  ,NDIMG ,DEFICO,IZONE ,POSMAI,
     &                  INO   ,NBEXFR,NDEXFR,TAU1  ,TAU2  )
            GOTO 15
          ENDIF 
C
C --- CALCUL DES TANGENTES EN CE NOEUD SI ELEMENT POUTRE
C              
          IF (LPOUTR) THEN
            CALL MMCPOU(NOMA  ,DEFICO,IZONE  ,NUMMAI,TYPNOE,
     &                  TAU1  ,TAU2  )
            GOTO 15
          ENDIF 
C
C --- CALCUL DES TANGENTES EN CE NOEUD
C          
          CALL MMCTAN(NOMMAI,ALIAS ,NBNOM ,NDIMG ,COORMA,
     &                COORNO,ITEMAX,EPSMAX,TAU1  ,TAU2  )  
  15      CONTINUE                     
C
C --- NORMALISATION DES TANGENTES
C
          CALL MMTANN(NDIMG ,TAU1  ,TAU2  ,NIVERR)
          IF (NIVERR.EQ.1) THEN  
            CALL U2MESS('I','CONTACT3_33')
            CALL U2MESG('F','CONTACT3_14',1,NOMMAI,0,0,3,COORNO)
          ENDIF    
C
C --- STOCKAGE
C    
  16      CONTINUE
          ZR(JTGELN+6*(INO-1)+1 -1) = TAU1(1)
          ZR(JTGELN+6*(INO-1)+2 -1) = TAU1(2)
          ZR(JTGELN+6*(INO-1)+3 -1) = TAU1(3)                     
          ZR(JTGELN+6*(INO-1)+4 -1) = TAU2(1)
          ZR(JTGELN+6*(INO-1)+5 -1) = TAU2(2)
          ZR(JTGELN+6*(INO-1)+6 -1) = TAU2(3) 
        
 10     CONTINUE                
 20   CONTINUE
C
      CALL JEDEMA()
      END
