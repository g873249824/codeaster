      SUBROUTINE NMDCAE(SDDISC,ITERAT,TYPDEC,NBRPAS,RATIO ,
     &                  OPTDEC,RETDEC)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*19 SDDISC
      INTEGER      ITERAT,NBRPAS,RETDEC
      REAL*8       RATIO
      CHARACTER*16 OPTDEC
      CHARACTER*4  TYPDEC
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (GESTION DES EVENEMENTS - DECOUPE)
C
C PARAMETRES DE DECOUPE - EXTRAPOLATION DES RESIDUS
C
C ----------------------------------------------------------------------
C
C
C IN  SDDISC : SD DISCRETISATION TEMPORELLE
C IN  ITERAT : NUMERO D'ITERATION DE NEWTON
C OUT RATIO  : RATIO DU PREMIER PAS DE TEMPS
C OUT TYPDEC : TYPE DE DECOUPE
C              'SUBD' - SUBDIVISION PAR UN NOMBRE DE PAS DONNE
C              'DELT' - SUBDIVISION PAR UN INCREMENT DONNE
C OUT NBRPAS : NOMBRE DE PAS DE TEMPS
C OUT OPTDEC : OPTION DE DECOUPE
C     'UNIFORME'   - DECOUPE REGULIERE ET UNIFORME
C     'PROGRESSIF' - DECOUPE EN DEUX ZONES, UN PAS LONG+ UNE SERIE
C                    DE PAS UNIFORMES
C     'DEGRESSIF'  - DECOUPE EN DEUX ZONES, UNE SERIE DE PAS
C                    UNIFORMES + UN PAS LONG
C OUT RETDEC : CODE RETOUR DECOUPE
C     0 - ECHEC DE LA DECOUPE
C     1 - ON A DECOUPE
C     2 - PAS DE DECOUPE
C
C
C
C
      REAL*8       UN
      LOGICAL      LEXTRA
      REAL*8       VALEXT(4)
      REAL*8       XXBB,XA0,XA1,XDET,CRESI,CIBLEN
      REAL*8       R8BID,R8PREM
      INTEGER      MNITER,MXITER
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      UN     = 1.D0
      RETDEC = 0
      RATIO  = 0.D0
      NBRPAS = -1
      OPTDEC = 'UNIFORME'
      TYPDEC = ' '
C
C --- LECTURE DES INFOS SUR LES CONVERGENCES
C
      CALL NMLERR(SDDISC,'L','MNITER',R8BID ,MNITER)
      CALL NMLERR(SDDISC,'L','MXITER',R8BID ,MXITER)
C
C --- EXTRAPOLATION LINEAIRE DES RESIDUS
C
      CALL NMACEX(SDDISC,ITERAT,LEXTRA,VALEXT)
      XA0    = VALEXT(1)
      XA1    = VALEXT(2)
      XDET   = VALEXT(3)
      CRESI  = VALEXT(4)
C
C --- CALCUL DU RATIO
C
      IF (.NOT.LEXTRA) THEN
        CALL U2MESS('I','EXTRAPOLATION_10')
        RETDEC = 0
      ELSE
        CALL U2MESS('I','EXTRAPOLATION_11')
        NBRPAS = 4
        CIBLEN = (XA0 + XA1*LOG(CRESI) )/XDET
        IF (XDET.LE.R8PREM()) THEN
          RATIO    = 24.0D0/((3.0D0*NBRPAS+UN)**2 - UN)
        ELSE
          IF ( (CIBLEN*1.20D0) .LT. MNITER ) THEN
            RATIO    = 24.0D0/((3.0D0*NBRPAS+UN)**2 - UN)
          ELSE
            IF (XA1.LE.R8PREM()) THEN
              RATIO    = 24.0D0/((3.0D0*NBRPAS+UN)**2 - UN)
            ELSE
              IF ( (CIBLEN-MXITER) .LE. (-10.0D0*XA1/XDET) ) THEN
                RATIO = EXP( (CIBLEN-MXITER)*XDET/XA1 )
              ELSE
                RATIO = EXP( -10.0D0 )
              ENDIF
              RATIO  = 0.48485D0*RATIO
              XXBB   = ( -UN + (UN+24.0D0/RATIO)**0.5D0 )/3.0D0
              IF ( XXBB .LT. 2.0D0 ) THEN
                NBRPAS = 2
                RATIO  = 0.5D0
              ELSE
                NBRPAS = NINT( XXBB )
              ENDIF
            ENDIF
          ENDIF
        ENDIF
        RETDEC = 1
        OPTDEC = 'PROGRESSIF'
        TYPDEC = 'SUBD'
      ENDIF
C
      CALL JEDEMA()
      END
