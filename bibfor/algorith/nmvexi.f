      SUBROUTINE NMVEXI( SIGI, GRJ2, DJ2DS, NB, MATE, NMAT, XHI, DXHIDS)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 11/04/2002   AUTEUR DURAND C.DURAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
      IMPLICIT NONE
C
      INTEGER      NB, NMAT
C
      REAL*8       MATE(NMAT,2), SIGI(NB)
      REAL*8       XHI,  DXHIDS(NB), GRJ2, DJ2DS(NB)
C-----------------------------------------------------------------------
C     MODELE VISCOPLASTIQUE A ECROUISSAGE ISOTRROPE COUPLE A DE
C     L ENDOMMAGEMENT ISOTROPE
C
C    CALCUL DE LA CONTRAINTE EQUIVALENTE D ENDOMMAGEMENT VISCOPLASTIQUE
C    ET DE SA DERIVEE PAR RAPPORT A LA CONTRAINTE ASSOCIEE
C-----------------------------------------------------------------------
C     IN   SIGI(NB)     : INCONNUES ASSOCIEES AUX CONTRAINTES
C          GRJ2         : CONTRAINTE EQUIVALENTE DE VON MISES
C          DJ2DS(NB)    : DERIVEE DE LA CONTRAINTE DE VON MISES
C          NB           : NOMBRE D'INCONNUES ASSOCIEES AUX CONTRAINTES
C          MATE(*,2)    : PARAMETRES MATERIAUX ELASTQUES ET PLASTIQUES
C          NMATE        : NOMBRE DE PARAMETRES MATERIAUX 
C     OUT  XHI          : CONTRAINTE EQUIVALENTE D'ENDOMMAGEMENT 
C                         VISCOPLASTIQUE
C          DXHIDS(NB)   : DERIVEE DE XHI/SIGI
C-----------------------------------------------------------------------
      INTEGER      ITENS, IVEC
      REAL*8       TRSIG, KRON(6), TOLE, DJ0DS(6), DJ1DS(6)
      REAL*8       GRJ0, GRJ1, VALP(3) 
      REAL*8       SEDVP1, SEDVP2, ZERO, R8GAEM
C
      DATA KRON  /1.D0, 1.D0, 1.D0, 0.D0, 0.D0, 0.D0/
C-----------------------------------------------------------------------
C-- 1- INITIALISATIONS:
      TOLE   = 1.D0 / R8GAEM()
      ZERO  = 0.D0
      CALL R8INIR (NB,0.D0,DJ0DS,1)
      CALL R8INIR (NB,0.D0,DJ1DS,1)
C-- COEFFICIENTS MATERIAU
      SEDVP1 = MATE(2,2)
      SEDVP2 = MATE(3,2)
C     ----------------------------------------------------------------
C     KD VA SERA CALCULEE PLUS LOIN UNE FOIS QUE XHI
C     (CONTRAINTE EQUIVALENTE D ENDOMMAGEMENT VISCOPLASTIQUE)
C     SERA CALCULEE
C     ----------------------------------------------------------------
      TRSIG=(SIGI(1)+SIGI(2)+SIGI(3))
C
C-- CALCUL DE GRJ0(SIGI) : MAX DES CONTRAINTES PRINCIPALES
      IF (SEDVP1.LE.TOLE) THEN
         GRJ0   =ZERO
         VALP(1)=ZERO
         VALP(2)=ZERO
         VALP(3)=ZERO
      ELSE
         CALL CALCJ0(SIGI, GRJ0, VALP)
      ENDIF
C
C-- CALCUL DE GRJ1(SIGI) : PREMIER INVARIANT (TRACE)
      GRJ1= TRSIG
C
C-- CALCUL DE LA CONTRAINTE EQUIVALENTE DE FLUAGE
      XHI=SEDVP1*GRJ0+SEDVP2*GRJ1+(1-SEDVP1-SEDVP2)*GRJ2
C
C-- DERIVEE DE LA CONTRAINTE EQUIVALENTE DE FLUAGE / CONTRAINTE
      DO 10 IVEC= 1,3
        IF (VALP(IVEC).EQ.GRJ0) DJ0DS(IVEC) = 1.D0
 10   CONTINUE
C
      DO 20 ITENS= 1,NB
        DJ1DS(ITENS) = KRON(ITENS)
        DXHIDS(ITENS)=SEDVP1*DJ0DS(ITENS)+SEDVP2*DJ1DS(ITENS)+
     &                (1-SEDVP1-SEDVP2)*DJ2DS(ITENS)
 20   CONTINUE
      END
