      SUBROUTINE XMVEC1(NDIM,NNE,NNES,NNC,NNM,HPG,NFAES,
     &                 FFPC,FFES,FFMA,JACOBI,LAMBD,
     &                 IAINES,CFACE,COEFCA,JEU,NORM,TYPMA,
     &                 SINGE,SINGM,RRE,RRM,NDLS,VTMP)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/12/2009   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C
      IMPLICIT NONE
      INTEGER  NDIM,NNE,NNES,NNM,NNC,NFAES
      INTEGER  IAINES,CFACE(3,5),SINGE,SINGM,NDLS
      REAL*8   HPG,FFPC(8),JACOBI,FFES(8),FFMA(8)
      REAL*8   LAMBD,JEU,NORM(3),COEFCA,RRE,RRM
      REAL*8   VTMP(168)
      CHARACTER*8  TYPMA
C
C ----------------------------------------------------------------------
C ROUTINE APPELLEE PAR : TE0367
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C VECTEUR SECOND MEMBRE SI CONTACT AVEC COMPLIANCE (XFEM)
C
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  NNE    : NOMBRE DE NOEUDS DE LA MAILLE ESCLAVE
C IN  NNES   : NOMBRE DE NOEUDS SOMMETS DE LA MAILLE ESCLAVE
C IN  NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C IN  HPG    : POIDS DU POINT INTEGRATION DU POINT DE CONTACT
C IN  NFAES  : NUMERO DE LA FACETTE DE CONTACT ESCLAVE
C IN  FFPC   : FONCTIONS DE FORME DU POINT DE CONTACT DANS ELC
C IN  FFES   : FONCTIONS DE FORME DU POINT DE CONTACT DANS ESC
C IN  FFMA   : FONCTIONS DE FORME DE LA PROJECTION DU PTC DANS MAIT
C IN  JACOBI : JACOBIEN DE LA MAILLE AU POINT DE CONTACT
C IN  LAMBD  : LAGRANGE DE CONTACT AU POINT D'INTÈGRATION
C IN  IAINES : POINTEUR VERS LE VECT DES ARRETES ESCLAVES INTERSECTEES
C IN  CFACE  : MATRICE DE CONECTIVITE DES FACETTES DE CONTACT
C IN  COEFCA : COEF_REGU_CONT
C IN  JEU    : VALEUR DU JEU
C IN  NORM   : VALEUR DE LA NORMALE AU POINT DE CONTACT
C IN  SINGE  : NOMBRE DE FONCTION SINGULIERE ESCLAVE
C IN  SINGM  : NOMBRE DE FONCTION SINGULIERE MAITRE
C IN  RRE    : SQRT LST ESCLAVE
C IN  RRM    : SQRT LST MAITRE
C I/O VTMP   : VECTEUR SECOND MEMBRE ELEMENTAIRE DE CONTACT/FROTTEMENT
C ----------------------------------------------------------------------
      INTEGER I,J,K,II,IN,PL,XOULA
      REAL*8  VV
C ----------------------------------------------------------------------
C
C --------------------- CALCUL DE [L1_CONT]-----------------------------
C
      IF (NNM.NE.0) THEN
C
      DO 10 J = 1,NDIM
        DO 20 I = 1,NNES
C --- BLOCS ES,CL ; ES,EN ; (ES,SI)
          VV = HPG*JACOBI*(LAMBD-COEFCA*JEU) *
     &          FFES(I)*NORM(J)
          II = NDLS*(I-1)+J
          VTMP(II) = -VV
          II = II + NDIM
          VTMP(II) =  VV
          DO 25 K = 1,SINGE
            II = II + NDIM
            VTMP(II) = RRE * VV
   25     CONTINUE
   20   CONTINUE
        DO 30 I = 1,NNM
C --- BLOCS MA,CL ; MA,EN ; (MA,SI)
          VV = HPG*JACOBI*(LAMBD-COEFCA*JEU) *
     &          FFMA(I)*NORM(J)
          II = NDLS*NNES+NDIM*(NNE-NNES) +
     &          (2+SINGM)*NDIM*(I-1)+J
          VTMP(II) = VV
          II = II + NDIM
          VTMP(II) = VV
          DO 35 K = 1,SINGM
            II = II + NDIM
            VTMP(II) = RRM * VV
   35     CONTINUE
   30   CONTINUE
   10 CONTINUE
      ELSE
      DO 50 J = 1,NDIM
        DO 60 I = 1,NNES
C --- BLOCS ES,SI
          VV = HPG*JACOBI*(LAMBD-COEFCA*JEU) *
     &          FFES(I)*NORM(J)
          II = NDLS*(I-1)+J
          VTMP(II) = RRE * VV
   60   CONTINUE
   50 CONTINUE
      ENDIF
C
C --------------------- CALCUL DE [L2]----------------------------------
C
      DO 40 I = 1,NNC
        IN=XOULA(CFACE,NFAES,I,IAINES,TYPMA)
        CALL XPLMA2(NDIM,NNE,NNES,NDLS,IN,PL)
        VTMP(PL) = -HPG*JACOBI*JEU*FFPC(I)
   40 CONTINUE
C
      END
