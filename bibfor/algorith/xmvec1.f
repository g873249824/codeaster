      SUBROUTINE XMVEC1(NDIM,NNE,NNES,NNC,NNM,HPG,NFAES,
     &                  FFPC,FFES,FFMA,JACOBI,DEPLE,DEPLME,
     &                  IAINES,CFACE,COEFCA,JEU,NORM,ESQ,VTMP)
     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/10/2007   AUTEUR NISTOR I.NISTOR 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
      IMPLICIT NONE
      INTEGER  NDIM,NNE,NNES,NNM,NNC,NFAES
      INTEGER  IAINES,CFACE(3,5)
      REAL*8   HPG,FFPC(9),JACOBI,FFES(9),FFMA(9)
      REAL*8   DEPLE(6),DEPLME(6),JEU,NORM(3),COEFCA
      REAL*8   VTMP(81)
      CHARACTER*8  ESQ
C
C ----------------------------------------------------------------------
C ROUTINE APPELLEE PAR : TE0367
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C VECTEUR SECOND MEMBRE SI CONTACT AVEC COMPLIANCE (XFEM)
C
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  NNE    : NOMBRE DE NOEUDS DE LA MAILLE ESCLAVE
C IN  NNES   : NOMBRE DE NOEUDS SOMMETS DE LA MAILLE ESCLAVE
C IN  NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C IN  HPG    : POIDS DU POINT INTEGRATION DU POINT DE CONTACT
C IN  NFAES : NUMERO DE LA FACETTE DE CONTACT ESCLAVE
C IN  FFPC   : FONCTIONS DE FORME DU POINT DE CONTACT DANS ELC
C IN  FFES   : FONCTIONS DE FORME DU POINT DE CONTACT DANS ESC
C IN  FFMA   : FONCTIONS DE FORME DE LA PROJECTION DU PTC DANS MAIT
C IN  JACOBI : JACOBIEN DE LA MAILLE AU POINT DE CONTACT
C IN  DEPLE  : DEPLACEMENTS DE LA SURFACE ESCLAVE
C IN  IAINES : POINTEUR VERS LE VECT DES ARRETES ESCLAVES INTERSECTEES
C IN  CFACE  : MATRICE DE CONECTIVITE DES FACETTES DE CONTACT
C IN  COEFCA : COEF_REGU_CONT
C IN  JEU    : VALEUR DU JEU
C IN  NORM   : VALEUR DE LA NORMALE AU POINT DE CONTACT
C I/O VTMP   : VECTEUR SECOND MEMBRE ELEMENTAIRE DE CONTACT/FROTTEMENT
C
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER I,J,II,IN,PL,XOULA,E
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
      E=1
C --- DDL DE DEPLACEMENT DE LA SURFACE CINEMATIQUE
C-----A) PARTIE "CLASSIQUE"

      DO 10 I = 1,NNES
        DO 20 J = 1,NDIM
          II = (3*NDIM)*(I-1)+J
           VTMP(II) = -HPG*JACOBI*(DEPLE(2*NDIM+1)-
     &                COEFCA*JEU)*FFES(I)*NORM(J)
   20   CONTINUE
   10 CONTINUE
C
C-----B) PARTIE "ENRICHIE"

      DO 30 I = 1,NNES
        DO 40 J = 1,NDIM
          II = (3*NDIM)*(I-1)+NDIM+J
          VTMP(II) = +HPG*JACOBI*(DEPLE(2*NDIM+1)-
     &                COEFCA*JEU)*FFES(I)*NORM(J)
   40   CONTINUE
   30 CONTINUE
C

C --- DDL DES DEPLACEMENTS DE LA SURFACE GEOMETRIQUE
C-----A) PARTIE "CLASSIQUE"

        DO 50 I = 1,NNM
          DO 60 J = 1,NDIM
            II = NNES*3*NDIM+(NNE-NNES)*NDIM+2*NDIM*(I-1)+J
            VTMP(II) = +HPG*JACOBI*(DEPLE(2*NDIM+1)-
     &                 COEFCA*JEU)*FFMA(I)*NORM(J)
   60     CONTINUE
   50   CONTINUE
  
C-----A) PARTIE "ENRICHIE"

        DO 70 I = 1,NNM
          DO 80 J = 1,NDIM
            II = NNES*3*NDIM+(NNE-NNES)*NDIM+2*NDIM*(I-1)+NDIM+J
            VTMP(II) = +HPG*JACOBI*(DEPLE(2*NDIM+1)-
     &                 COEFCA*JEU)*FFMA(I)*NORM(J)     
   80     CONTINUE
   70   CONTINUE   
     
C
C --- DDL DES MULTIPLICATEURS DE CONTACT (DE LA SURFACE CINEMATIQUE)
        DO 90 I = 1,NNC
          IN=XOULA(CFACE,NFAES,I,IAINES,ESQ)
          CALL XPLMA2(NDIM,NNE,NNES,IN,PL)
          VTMP(PL) = -HPG*JACOBI*JEU*FFPC(I)*E
   90   CONTINUE
C
C
      CALL JEDEMA()
C
      END
