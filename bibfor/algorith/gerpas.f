      SUBROUTINE GERPAS( FAMI,KPG,KSP,
     &                   COMP,MOD,IMAT,MATCST,NBCOMM,CPMONO,NBPHAS,NVI,
     &                   NMAT,Y,PAS, ITMAX,EPS,TOLY,COTHE,COEFF,
     &                   DCOTHE,DCOEFF,COEL,PGL,ANGMAS,
     &                   NEPS,EPSD,DETOT,X,NFS,NSG,NHSR,NUMHSR,HSR,IRET)
      IMPLICIT NONE
C     ================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 18/12/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21 CRS_1404
C
C     INTEGRATION DE LOIS DE COMPORTEMENT ELASTO-VISCOPLASTIQUE
C     PAR UNE METHODE DE RUNGE KUTTA
C
C     GESTION AUTOMATIQUE DES PAS DE TEMPS (REDECOUPAGE SI NON CV)
C     -
C       IN   FAMI :  FAMILLE DE POINT DE GAUSS (RIGI,MASS,...)
C         KPG,KSP :  NUMERO DU (SOUS)POINT DE GAUSS
C         LOI     :  NOM DU MODELE DE COMPORTEMENT
C         MOD     :  TYPE DE MODELISATION
C         IMAT    :  CODE DU MATERIAU CODE
C         MATCST  : 'OUI' SI MATERIAU CST ENTRE T ET T+DT
C                   'NAP' SI LE PARAMETRE K_D EST UNE NAPPE
C                   'NON' SINON
C         N       :  NOMBRE DE VARIABLES INTERNES
C         NMAT    :  NOMBRE DE PARAMETRES MATERIAU
C     VAR Y       :  VARIABLES INTERNES (VIND AU DEPART, SOLU A LA FIN)
C         PAS     :  INTERVALLE DE TEMPS TF-TD = DTIME
C         EPS     :  PARAMETRE DE CONVERGENCE TOLER=CRIT(3) 
C         TOLY    :  POUR CALCUL ERREUR RELATIVE=YMFS 
C         COTHE   :  COEFFICIENTS MATERIAU ELAS A T
C         COEFF   :  COEFFICIENTS MATERIAU INELAS A T
C         DCOTHE  :  COEFFICIENTS MATERIAU ELAS A T+DT
C         DCOEFF  :  COEFFICIENTS MATERIAU INELAS A T+DT
C         SIGI    :  CONTRAINTES A L'INSTANT COURANT
C         EPSD    :  DEFORMATION TOTALE A T
C         DETOT   :  INCREMENT DE DEFORMATION TOTALE
C         NFS     :  NOMBRE MAX DE FAMILLES DE SYSTEMES DE GLISSEMENT
C         NSG     :  NOMBRE MAX DE DE SYSTEMES DE GLISSEMENT MONOCRISTAL
C     OUT X       :  INSTANT COURANT
C     -
      INTEGER  NMAT,IMAT,NBCOMM(NMAT,3),NE,NY,NA,NVI,KPOK,IP,I,NEPS
      INTEGER  NBPHAS,NFS,KPG,KSP,ITMAX,IRET,NSG,NHSR,NUMHSR(*),IROTA
      CHARACTER*16 LOI,COMP(*)
      CHARACTER*24 CPMONO(5*NMAT+1)
      CHARACTER*8  MOD
      CHARACTER*3  MATCST
      CHARACTER*(*) FAMI
      REAL*8 COEL(NMAT),HSR(NSG,NSG,NHSR),X,PAS,H,TOLY,XR,W,WZ,EPS
      REAL*8 COTHE(NMAT),DCOTHE(NMAT),COEFF(NMAT),DCOEFF(NMAT)
      REAL*8 EPSD(NEPS),DETOT(NEPS),PGL(3,3),ANGMAS(3)
C     TABLEAUX AUTOMATIQUES F90
      REAL*8 Y(NVI),WK(3*NVI),YMFS(NVI)
C      POUR GAGNER EN TEMPS CPU. ATTENTION TABLEAU POUVANT ETRE GROS
C      UTILISE SEULEMENT POUR POLYCRISTAL LCMMOP
      REAL*8 TOUTMS(NBPHAS*NFS*NSG*7)
C
      LOI=COMP(1)
      IF (LOI(1:8).EQ.'POLYCRIS') THEN
         CALL CALCMS( NBPHAS,NBCOMM,CPMONO,NMAT,PGL,
     &                COEFF,ANGMAS,NFS,NSG,TOUTMS )
      ENDIF
      IF (LOI(1:8).EQ.'MONOCRIS') THEN
         IROTA=0
         CALL CALCMM(NBCOMM,CPMONO,NMAT,PGL,NFS,NSG,TOUTMS,
     &                 COMP,NVI,Y,IROTA)
      ENDIF
C
      IRET=0
C
      NE=0
      NY=NVI
      NA=NY+NVI
      KPOK=1
      X=0.0D0
      
      H=PAS
      
      IP=0

      DO 10 I=1,NVI
        YMFS(I)=MAX(TOLY,ABS(Y(I)))
   10 CONTINUE

   40 CONTINUE
      IF ((X+H).GE.PAS) THEN
        H=PAS-X
        IP=1
      ENDIF
      
C     WK(3*NVI) CONTIENT EE, PUIS Y, PUIS A=F(Y)
      DO 50 I=1,NVI
        WK(NY+I)=Y(I)
   50 CONTINUE

      XR=X
   60 CONTINUE

C     
      CALL RK21CO(FAMI,KPG,KSP,
     &            COMP,MOD,IMAT,MATCST,NBCOMM,CPMONO,NFS,NSG,
     &            TOUTMS,NVI,NMAT,Y,KPOK,WK(NE+1),WK(NA+1),H,PGL,NBPHAS,
     &            COTHE,COEFF,DCOTHE,DCOEFF,COEL,X,PAS,NEPS,EPSD,
     &            DETOT,NHSR,NUMHSR,HSR,ITMAX,EPS,IRET)
      IF (IRET.GT.0) THEN
          GOTO 9999
      ENDIF

      W=ABS(WK(1))/YMFS(1)
      DO 70 I=2,NVI
        WZ=ABS(WK(I))/YMFS(I)
        IF (WZ.GT.W) W=WZ
   70 CONTINUE

      IF (W.LE.EPS) THEN
C        CONVERGENCE DU PAS DE TEMPS COURANT
         KPOK=1
         IF (IP.EQ.1) THEN
C           PAS DE TEMPS FINAL ATTEINT, SOLUTION OK 
            GOTO 9999
         ELSE
C           CALCUL DU NOUVEAU PAS DE TEMPS H (AUGMENTATION)
            CALL RKCAH1( COMP,Y,PAS,NVI,W,WK,H,EPS,IRET)
            IF (IRET.GT.0) THEN
               GOTO 9999
            ELSE
               GOTO 40
            ENDIF
         ENDIF
      ELSE
C        W.GT.EPS : NON CV
         KPOK=0
C        ON REPART DE LA SOLUTION Y PRECEDENTE         
         DO 80 I=1,NVI
           Y(I)=WK(NY+I)
   80    CONTINUE
         X=XR
         IP=0
C        CALCUL DU NOUVEAU PAS DE TEMPS H (DIMINUTION)
         CALL RKCAH2( COMP,Y,PAS,NVI,W,WK,H,EPS,IRET)
         IF (IRET.GT.0) THEN
            GOTO 9999
         ELSE
            GOTO 60
         ENDIF

      ENDIF

 9999 CONTINUE

      END
