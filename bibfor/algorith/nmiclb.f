      SUBROUTINE NMICLB(OPTION,COMPOR,E,ALPHA,XLONG0,A,TMOINS,TPLUS,
     &                   XLONGM,DLONG0,DSDE,SIGY,EFFNOM,TEMPM,TEMPP,
     &                   TREF,VIM,EFFNOP,VIP,KLV,FONO)
C ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 12/05/98   AUTEUR UFBHHLL C.CHAVANT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C TOLE CRP_21
C ------------------------------------------------------------------
      IMPLICIT REAL*8 (A-H,O-Z)
      PARAMETER   (NEQ = 6,NBT = 21,NVAR=2)

      CHARACTER*(*) COMPOR,OPTION
      REAL*8        XLONG0,A
      REAL*8        E,ALPHA,DSDE,SIGY
      REAL*8        TMOINS,TPLUS,XLONGM,DLONG0
      REAL*8        TEMPM,TEMPP,TREF,EFFNOM,VIM(NVAR)
      REAL*8        EFFNOP,VIP(NVAR),FONO(NEQ),KLV(NBT)
C -------------------------------------------------------------------
C
C    TRAITEMENT DE LA RELATION DE COMPORTEMENT -ELASTOPLASTICITE-
C    ECROUISSAGE ISOTROPE ET CINEMATIQUE- LINEAIRE - VON MISES-
C    POUR UN MODELE BARRE ELEMENT MECA_BARRE
C
C -------------------------------------------------------------------
C IN  : E      : MODULE D'YOUNG
C       COMPOR : LOI DE COMPORTEMENT
C       XLONG0 : LONGUEUR DE L'ELEMENT DE BARRE AU REPOS
C       A      : SECTION DE LA BARRE
C       ALPHA  : COEFFICIENT DE DILATATION THERMIQUE
C       DSDE   : ET
C       SIGY   : LIMITE ELASTIQUE
C       TMOINS : INSTANT PRECEDENT
C       TPLUS  : INSTANT COURANT
C       XLONGM : LONGEUR DE L'ELEMENT AU TEMPS MOINS
C       DLONG0 : INCREMENT D'ALLONGEMENT DE L'ELEMENT
C       EFFNOM : EFFORT NORMAL PRECEDENT
C       TREF   : TEMPERATURE DE REFERENCE
C       TEMPM  : TEMPERATURE IMPOSEE A L'INSTANT PRECEDENT
C       TEMPP  : TEMPERATURE IMPOSEE A L'INSTANT COURANT
C       OPTION : OPTION DEMANDEE (R_M_T,FULL OU RAPH_MECA)
C
C OUT : EFFNOP : CONTRAINTE A L'INSTANT ACTUEL
C       VIP    : VARIABLE INTERNE A L'INSTANT ACTUEL
C       FONO   : FORCES NODALES COURANTES
C       KLV    : MATRICE TANGENTE
C
C----------VARIABLES LOCALES
C
      REAL*8      SIGM,DEPS
      REAL*8      SIGP,XRIG
      LOGICAL ISOT,CINE
C
C
C----------INITIALISATIONS
C
      ISOT = .FALSE.
      CINE = .FALSE.
      IF (COMPOR.EQ.'VMIS_ISOT_LINE') THEN
            ISOT = .TRUE.
      ELSE IF (COMPOR.EQ.'VMIS_CINE_LINE') THEN
            CINE = .TRUE.
      ENDIF
C
      CALL R8INIR (NBT,0.D0,KLV,1)
      CALL R8INIR (NEQ,0.D0,FONO,1)
C
C----------RECUPERATION DES CARACTERISTIQUES
C
      DEPS = DLONG0/XLONG0
      SIGM    =EFFNOM/A
C
C
C
      IF ( ISOT) THEN
       CALL  NM1DIS(TEMPM,TEMPP,E,DSDE,ALPHA,SIGY,
     >                  SIGM,DEPS,VIM(1),VIM(2),
     >                  SIGP,VIP(1),VIP(2),DSDEM,DSDEP)
      ELSE IF ( CINE) THEN
       CALL  NM1DCI(TEMPM,TEMPP,E,DSDE,ALPHA,SIGY,
     >                  SIGM,DEPS,VIM(1),VIM(2),
     >                  SIGP,VIP(1),VIP(2),DSDEM,DSDEP)
      ELSE
       CALL UTMESS('F','NMICLB_1','LOI DE COMPORTEMENT INEXISTANTE')
      ENDIF
      EFFNOP=SIGP*A
C
C --- CALCUL DU COEFFICIENT NON NUL DE LA MATRICE TANGENTE
C
      IF ( OPTION(1:14) .EQ. 'RIGI_MECA_TANG'.OR.
     &     OPTION(1:9)  .EQ. 'FULL_MECA'         ) THEN
C
         IF ( OPTION(1:14) .EQ. 'RIGI_MECA_TANG' ) THEN
           XRIG= DSDEM*A/XLONG0
         ELSE
           XRIG= DSDEP*A/XLONG0
         ENDIF
            KLV(1)  =  XRIG
            KLV(7)  = -XRIG
            KLV(10)  = XRIG
      ENDIF
C
C --- CALCUL DES FORCES NODALES
C
      FONO(1) = -EFFNOP
      FONO(4) =  EFFNOP
C
C -------------------------------------------------------------
C
      END
