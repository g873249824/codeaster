      SUBROUTINE NMICLB(OPTION,COMPOR,IMATE,XLONG0,A,DLONG0,EFFNOM,
     &                  TEMPM,TEMPP,TREF,VIM,EFFNOP,VIP,KLV,FONO,EPSM,
     &                  CORRM,CORRP)     
C ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/04/2004   AUTEUR JMBHH01 J.M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21
C ------------------------------------------------------------------
      IMPLICIT NONE
      INTEGER IMATE,NEQ,NBT,NBPAR
      PARAMETER (NEQ=6,NBT=21)

      CHARACTER*16 COMPOR(*),OPTION
      REAL*8 XLONG0,A
      REAL*8 EM,EP,ALPHAM,ALPHAP
      REAL*8 DLONG0
      REAL*8 TEMPM,TEMPP,TREF,EFFNOM,VIM(*)
      REAL*8 EFFNOP,VIP(*),FONO(NEQ),KLV(NBT)
      REAL*8 VALPAR,DSDE,EPSM,CORRM,CORRP
      CHARACTER*2 BL2,FB2,CODRES
      CHARACTER*8 NOMPAR
C -------------------------------------------------------------------

C    TRAITEMENT DE LA RELATION DE COMPORTEMENT -ELASTOPLASTICITE-
C    ECROUISSAGE ISOTROPE ET CINEMATIQUE- LINEAIRE - VON MISES-
C    POUR UN MODELE BARRE ELEMENT MECA_BARRE

C -------------------------------------------------------------------
C IN  : IMATE : POINTEUR MATERIAU CODE
C       COMPOR : LOI DE COMPORTEMENT
C       XLONG0 : LONGUEUR DE L'ELEMENT DE BARRE AU REPOS
C       A      : SECTION DE LA BARRE
C       TMOINS : INSTANT PRECEDENT
C       TPLUS  : INSTANT COURANT
C       DLONG0 : INCREMENT D'ALLONGEMENT DE L'ELEMENT
C       EFFNOM : EFFORT NORMAL PRECEDENT
C       TREF   : TEMPERATURE DE REFERENCE
C       TEMPM  : TEMPERATURE IMPOSEE A L'INSTANT PRECEDENT
C       TEMPP  : TEMPERATURE IMPOSEE A L'INSTANT COURANT
C       OPTION : OPTION DEMANDEE (R_M_T,FULL OU RAPH_MECA)
C       CORRM  : CORROSION A L'INSTANT MOINS
C       CORRP  : CORROSION A L'INSTANT PLUS
C OUT : EFFNOP : CONTRAINTE A L'INSTANT ACTUEL
C       VIP    : VARIABLE INTERNE A L'INSTANT ACTUEL
C       FONO   : FORCES NODALES COURANTES
C       KLV    : MATRICE TANGENTE

C----------VARIABLES LOCALES

      REAL*8 SIGM,DEPS,DEPSTH
      REAL*8 SIGP,XRIG
      LOGICAL ISOT,CINE,ELAS,CORR

      BL2 = '  '
      FB2 = 'FM'

C----------INITIALISATIONS

      ELAS = .FALSE.
      ISOT = .FALSE.
      CINE = .FALSE.
      CORR = .FALSE.      
      IF (COMPOR(1).EQ.'ELAS') THEN
        ELAS = .TRUE.
      ELSE IF ((COMPOR(1).EQ.'VMIS_ISOT_LINE') .OR.
     &         (COMPOR(1).EQ.'VMIS_ISOT_TRAC')) THEN
        ISOT = .TRUE.
      ELSE IF (COMPOR(1).EQ.'VMIS_CINE_LINE') THEN
        CINE = .TRUE.
      ELSE IF (COMPOR(1).EQ.'CORR_ACIER') THEN
        CORR = .TRUE.
      END IF

      CALL R8INIR(NBT,0.D0,KLV,1)
      CALL R8INIR(NEQ,0.D0,FONO,1)

C----------RECUPERATION DES CARACTERISTIQUES

      DEPS = DLONG0/XLONG0
      SIGM = EFFNOM/A
      NBPAR = 1
      NOMPAR = 'TEMP'

C --- CARACTERISTIQUES ELASTIQUES A TMOINS

      VALPAR = TEMPM
      CALL RCVALA(IMATE,' ','ELAS',NBPAR,NOMPAR,VALPAR,1,'E',EM,
     &       CODRES,FB2)
      CALL RCVALA(IMATE,' ','ELAS',NBPAR,NOMPAR,VALPAR,1,'ALPHA',ALPHAM,
     &            CODRES,BL2)
      IF (CODRES.NE.'OK') ALPHAM = 0.D0

C --- CARACTERISTIQUES ELASTIQUES A TPLUS

      VALPAR = TEMPP
      CALL RCVALA(IMATE,' ','ELAS',NBPAR,NOMPAR,VALPAR,1,'E',EP,
     &              CODRES,FB2)
      CALL RCVALA(IMATE,' ','ELAS',NBPAR,NOMPAR,VALPAR,1,'ALPHA',ALPHAP,
     &            CODRES,BL2)
      IF (CODRES.NE.'OK') ALPHAP = 0.D0


      IF (ISOT) THEN
        CALL NM1DIS(IMATE,TEMPM,TEMPP,TREF,EM,EP,ALPHAM,ALPHAP,SIGM,
     &              DEPS,VIM,OPTION,COMPOR,SIGP,VIP,DSDE)
      ELSE IF (CINE) THEN
        CALL NM1DCI(IMATE,TEMPM,TEMPP,TREF,EM,EP,ALPHAM,ALPHAP,SIGM,
     &              DEPS,VIM,OPTION,SIGP,VIP,DSDE)
      ELSE IF (ELAS) THEN
        DSDE = EP
        VIP(1) = 0.D0
        DEPSTH = ALPHAP* (TEMPP-TREF) - ALPHAM* (TEMPM-TREF)
        SIGP = EP* (SIGM/EM+DEPS-DEPSTH)
      ELSE IF (CORR) THEN
        CALL NM1DCO(OPTION,IMATE,TEMPM,TEMPP,EP,SIGM,EPSM,DEPS,VIM,
     &              SIGP,VIP,DSDE,CORRM,CORRP)  
      ELSE
        CALL UTMESS('F','NMICLB_1','LOI DE COMPORTEMENT INEXISTANTE')
      END IF

C --- CALCUL DU COEFFICIENT NON NUL DE LA MATRICE TANGENTE

      IF (OPTION(1:10).EQ.'RIGI_MECA_' .OR.
     &    OPTION(1:9).EQ.'FULL_MECA') THEN

        XRIG = DSDE*A/XLONG0
        KLV(1) = XRIG
        KLV(7) = -XRIG
        KLV(10) = XRIG
      END IF

C --- CALCUL DES FORCES NODALES

      IF (OPTION(1:14).EQ.'RAPH_MECA' .OR.
     &    OPTION(1:9).EQ.'FULL_MECA') THEN
        EFFNOP = SIGP*A
        FONO(1) = -EFFNOP
        FONO(4) = EFFNOP
      END IF

C -------------------------------------------------------------

      END
