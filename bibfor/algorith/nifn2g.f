       SUBROUTINE NIFN2G(NNO1  , NNO2  , NPG   , POIDSG, VFF1  , VFF2  ,
     &                   DFDE1 , DFDK1 , DFDI ,GEOM , AXI , SIG,
     &                   DEPLM  , GONFLM  , FINTU , FINTA )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 11/08/2003   AUTEUR SMICHEL S.MICHEL-PONNELLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
       IMPLICIT NONE
       LOGICAL       AXI
       INTEGER       NNO1,NNO2, NPG
       REAL*8        DFDE1(*),DFDK1(*)
       REAL*8        POIDSG(NPG), VFF1(NNO1,NPG),VFF2(NNO2,NPG)
       REAL*8        GEOM(2,NNO1),DFDI(NNO1,2),SIG(5,NPG)
       REAL*8        DEPLM(2,NNO1),GONFLM(2,NNO2),FINTU(2,9), FINTA(2,4)

C......................................................................
C
C     BUT:  CALCUL  DE L'OPTION FORC_NODA EN QUASI INCOMPRESSIBLE
C              DEFORMATION = 'SIMO_MIEHE'
C     APPELEE PAR  TE0480.F 
C......................................................................
C IN  NNO1    : NOMBRE DE NOEUDS DE L'ELEMENT LIES AUX DEPLACEMENTS
C IN  NNO2    : NOMBRE DE NOEUDS DE L'ELEMENT LIES A LA PRESSION
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  POIDSG  : POIDS DES POINTS DE GAUSS
C IN  VFF1    : VALEUR  DES FONCTIONS DE FORME LIES AUX DEPLACEMENTS
C IN  VFF2    : VALEUR  DES FONCTIONS DE FORME LIES A LA PRESSION
C IN  DFDE1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDK1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  GEOM    : COORDONEES DES NOEUDS
C IN  AXI     : CALCUL AXISYMETRIQUE OU NON
C IN  DEPLM   : DEPLACEMENTS A L'INSTANT PRECEDENT
C IN  GONFLM  : VARIABLES LIEES AU GONFLEMENT A L'INSTANT PRECEDENT
C IN  SIG     : CONTRAINTES A L'INSTANT PRECEDENT
C OUT DFDI    : DERIVEE DES FONCTIONS DE FORME  AU DERNIER PT DE GAUSS
C OUT  FINTU  : VECTEUR DES FORCES INTERNES (COMPOSANTES U)
C OUT  FINTA  : VECTEUR DES FORCES INTERNES (COMPOSANTES P et G)
C......................................................................

      LOGICAL      GRAND
      INTEGER      KPG,N,I,IND(3,3)
      REAL*8       TMP,RM,POIDS,POIDS0,GM,JM
      REAL*8       GEOMM(2,9),EPSBID(6),FM(3,3),FBID(3,3),SIGDF(2,9)
      DATA    IND / 1, 4, 5,
     &              4, 2, 6,
     &              5, 6, 3 /
C -------------------------------------------------------------------
C - PRE REQUIS
      IF (NNO1 .GT. 9) CALL UTMESS('F','NIFN2D','DVP : TROP DE NOEUDS')

C - INITIALISATION
      CALL R8INIR(18, 0.D0, FINTU,1)
      CALL R8INIR( 8, 0.D0, FINTA,1)
      GRAND = .TRUE.      
C    REACTUALISATION DE LA GEOMETRIE
      DO 5 N = 1,NNO1
        DO 6 I = 1,2
          GEOMM(I,N) = GEOM(I,N) + DEPLM(I,N)
 6      CONTINUE
 5    CONTINUE
 
C - CALCUL POUR CHAQUE POINT DE GAUSS

      DO 800 KPG = 1,NPG
      
C - CALCUL DU GONFLEMENT
        GM = 0.D0
        DO 1 N = 1, NNO2
          GM = GM + VFF2(N,KPG)*GONFLM(2,N)
 1      CONTINUE
       
C - CALCUL DES ELEMENTS GEOMETRIQUES

C   CALCUL DU POIDS DU PT DE GAUSS CONFIG INITIALE ET FM
        CALL NMGEOM(2,NNO1,AXI,GRAND,GEOM,KPG,POIDSG(KPG),
     &              VFF1(1,KPG),DFDE1,TMP,DFDK1,DEPLM,POIDS0,DFDI,
     &              FM,EPSBID,RM)

C     CALCUL DE DFDI SUR LA CONFIGURATION ACTUELLE
        CALL NMGEOM(2,NNO1,AXI,GRAND,GEOMM,KPG,POIDSG(KPG),
     &              VFF1(1,KPG),DFDE1,TMP,DFDK1,DEPLM,POIDS,DFDI,
     &              FBID,EPSBID,RM)


C   CALCUL DU GRADIENT DE LA TRANSFORMATION POUR LA LOI DE COMPORTEMENT
      JM=FM(1,1)*(FM(2,2)*FM(3,3)-FM(2,3)*FM(3,2))
     &  -FM(2,1)*(FM(1,2)*FM(3,3)-FM(1,3)*FM(3,2))
     &  +FM(3,1)*(FM(1,2)*FM(2,3)-FM(1,3)*FM(2,2))

C        CALCUL DE FINT_U
        DO 500 N = 1,NNO1
          DO 510 I = 1,2
            SIGDF(I,N) = SIG(IND(I,1),KPG)*DFDI(N,1)
     &                 + SIG(IND(I,2),KPG)*DFDI(N,2)
 510      CONTINUE
 500    CONTINUE
        DO 3 N=1,NNO1
          DO 2 I=1,2
            FINTU(I,N) = FINTU(I,N) + POIDS * SIGDF(I,N)
 2        CONTINUE
C        CORRECTION POUR LE CAS AXISYMETRIQUE
           IF (AXI) THEN
             FINTU(1,N) = FINTU(1,N) +  
     &             POIDS * SIG(3,KPG) * VFF1(N,KPG)/RM
           ENDIF
 3      CONTINUE

C        CALCUL DE FINT_P ET FINT_G
        DO 4 N = 1, NNO2
          TMP = (JM - 1.D0 - GM)*VFF2(N, KPG)
          FINTA(1,N) = FINTA(1,N) + TMP*POIDS0

          TMP = SIG(5,KPG)*VFF2(N,KPG)
          FINTA(2,N) = FINTA(2,N) + TMP*POIDS0
4      CONTINUE

 800  CONTINUE
      END
