      SUBROUTINE CFJEIN(NOMA  ,DEFICO,RESOCO,DEPDEL)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 12/09/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO,RESOCO
      CHARACTER*19 DEPDEL
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE DISCRETE - ALGORITHME)
C
C CALCUL DES JEUX INITIAUX AJEU+ = AJEU/I/N - A.DDEPLA
C
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE DEFINITION DU CONTACT
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  DEPDEL : INCREMENT DE DEPLACEMENT CUMULE DEPUIS DEBUT DU PAS
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      IFM,NIV
      INTEGER      NBDDL,JDECAL
      INTEGER      ILIAI,IALARM
      CHARACTER*19 DDEPL0
      INTEGER      JDDEP0,JDEPDE
      CHARACTER*24 JEUITE,JEUX
      INTEGER      JJEUIT,JJEUX
      CHARACTER*24 APDDL,APCOEF,APPOIN
      INTEGER      JAPDDL,JAPCOE,JAPPTR      
      CHARACTER*24 APCOFR
      INTEGER      JAPCOF
      CHARACTER*24 CLREAC
      INTEGER      JCLREA
      LOGICAL      REAPRE
      INTEGER      CFDISD,NBLIAI,NEQ,NDIMG,NESMAX
      LOGICAL      CFDISL,LGLISS,LCTFD,LLAGRF
      REAL*8       CFDISR,ALJEU
      REAl*8       JEUOLD,JEUINI,JEXINI,JEYINI
      REAL*8       VAL1,VAL2,VAL
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> ......... CALCUL DES JEUX INITIAUX'
      ENDIF

C --- PARAMETRES
C
      NDIMG  = CFDISD(RESOCO,'NDIM'  )
      NBLIAI = CFDISD(RESOCO,'NBLIAI')
      NEQ    = CFDISD(RESOCO,'NEQ'   )
      NESMAX = CFDISD(RESOCO,'NESMAX')
      LCTFD  = CFDISL(DEFICO,'FROT_DISCRET')
      LLAGRF = CFDISL(DEFICO,'FROT_LAGR'   )
      LGLISS = CFDISL(DEFICO,'CONT_DISC_GLIS')
      ALJEU  = CFDISR(DEFICO,'ALARME_JEU'  )
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      APPOIN = RESOCO(1:14)//'.APPOIN'
      APDDL  = RESOCO(1:14)//'.APDDL'
      APCOEF = RESOCO(1:14)//'.APCOEF'
      CALL JEVEUO(APPOIN,'L',JAPPTR)
      CALL JEVEUO(APDDL ,'L',JAPDDL)
      CALL JEVEUO(APCOEF,'L',JAPCOE)
C
      JEUITE = RESOCO(1:14)//'.JEUITE'
      JEUX   = RESOCO(1:14)//'.JEUX'
      CALL JEVEUO(JEUITE,'L',JJEUIT)
      CALL JEVEUO(JEUX  ,'E',JJEUX )
C
      IF (LCTFD) THEN
        APCOFR = RESOCO(1:14)//'.APCOFR'
        CALL JEVEUO(APCOFR,'L',JAPCOF)
      ENDIF
C
      CLREAC = RESOCO(1:14)//'.REAL'
      CALL JEVEUO(CLREAC,'L',JCLREA)
C
C --- PARAMETRES DE REACTUALISATION
C
      REAPRE = ZL(JCLREA+3-1)
C
C --- ACCES AUX CHAMPS DE TRAVAIL
C --- DDEPL0: INCREMENT DE SOLUTION SANS CORRECTION DU CONTACT
C
      DDEPL0 = RESOCO(1:14)//'.DEL0'
      CALL JEVEUO(DDEPL0(1:19)//'.VALE','L',JDDEP0)
C
C --- INCREMENT DE DEPLACEMENT DEPUIS LE DEBUT DU PAS DE TEMPS
C
      CALL JEVEUO(DEPDEL(1:19)//'.VALE','L',JDEPDE)
C
C --- ON CALCULE LE NOUVEAU JEU : AJEU+ = AJEU/I/N - A.DDEPLA
C
      DO 10 ILIAI = 1,NBLIAI
C
C ----- ACCES TABLEAU LIAISONS
C
        JDECAL = ZI(JAPPTR+ILIAI-1)
        NBDDL  = ZI(JAPPTR+ILIAI)  - ZI(JAPPTR+ILIAI-1)
C
C ----- INCR. DE JEU SANS CORRECTION [A].{DDEPL0}
C        
        CALL CALADU(NEQ,NBDDL,ZR(JAPCOE+JDECAL),ZI(JAPDDL+JDECAL),
     &              ZR(JDDEP0),VAL   )
C
C ----- JEU AVANT L'ITERATION DE NEWTON {JEU(DEPTOT)}
C
        JEUOLD = ZR(JJEUIT+3*(ILIAI-1)+1-1)
C
C ----- JEU SANS CORRECTION DU CONTACT: {JEU(DEPTOT)} - [A].{DDEPL0}
C
        JEUINI = JEUOLD - VAL 
C
C ----- SAUVEGARDES
C
        ZR(JJEUX+3*(ILIAI-1)+1-1) = JEUINI
C
C ----- JEUX TANGENTS
C       
        IF (LCTFD) THEN
C
C ------- INCR. JEUX TANGENTS SANS CORRECTION
C
          CALL CALADU(NEQ,NBDDL,ZR(JAPCOF+JDECAL),ZI(JAPDDL+JDECAL),
     &                ZR(JDDEP0),VAL1  )        
C
C ------- INCR. JEUX TANGENTS DEPUIS LE DEBUT DU PAS DE TEMPS
C
          CALL CALADU(NEQ,NBDDL,ZR(JAPCOF+JDECAL),ZI(JAPDDL+JDECAL),
     &                ZR(JDEPDE),VAL2  )
C
C ------- INCR. DE JEU DEPUIS LE DEBUT DU PAS DE TEMPS SANS CORR.
C
          JEXINI = VAL1 + VAL2     
C
          IF ((NDIMG.EQ.2).AND.LLAGRF.AND.REAPRE) THEN
            ZR(JJEUIT+3*(ILIAI-1)+2-1) = 0.D0
          ENDIF
C
          ZR(JJEUX+3*(ILIAI-1)+2-1) = JEXINI
C
          IF (NDIMG.EQ.3) THEN
C
C --------- INCR. DE JEU SANS CORRECTION
C
            CALL CALADU(NEQ,NBDDL,
     &                  ZR(JAPCOF+JDECAL+30*NESMAX),ZI(JAPDDL+JDECAL),
     &                  ZR(JDDEP0),VAL1  )        
C
C --------- INCR. DE JEU DEPUIS LE DEBUT DU PAS DE TEMPS
C
            CALL CALADU(NEQ,NBDDL,
     &                  ZR(JAPCOF+JDECAL+30*NESMAX),ZI(JAPDDL+JDECAL),
     &                  ZR(JDEPDE),VAL2  )
C
C --------- INCR. DE JEU DEPUIS LE DEBUT DU PAS DE TEMPS SANS CORR.
C
            JEYINI = VAL1 + VAL2
            ZR(JJEUX+3*(ILIAI-1)+3-1) = JEYINI
          ENDIF     
        ENDIF
C
   10 CONTINUE
C
C --- ALARME SI DECOLLEMENT ALORS QUE GLISSIERE
C
      DO 15 ILIAI = 1,NBLIAI
        IF (LGLISS) THEN
          JEUINI = ZR(JJEUX+3*(ILIAI-1)+1-1)
          IF (JEUINI.GT.ALJEU) THEN
            IALARM = IALARM+1
            IF (IALARM.EQ.1) THEN
              CALL U2MESS('A','CONTACT_9')
            ENDIF
            CALL CFIMP2(DEFICO,RESOCO,NOMA  ,ILIAI,'C0'  ,
     &                  'ALJ' )
          ENDIF
        ENDIF
   15 CONTINUE
C
      CALL JEDEMA()
C
      END
