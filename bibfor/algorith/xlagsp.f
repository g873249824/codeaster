      SUBROUTINE XLAGSP(ALGOLA,NDIM,NOMA,CNSLT,CNSLN,GRLN,GRLT,FISS)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 02/05/2006   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT

      IMPLICIT NONE

      CHARACTER*8   ALGOLA,NOMA,FISS
      CHARACTER*19  CNSLT,CNSLN,GRLN,GRLT
      INTEGER       NDIM
C
C   CHOIX DE L'ESPACE DES LAGRANGES POUR LE CONTACT
C                   (VOIR BOOK VI 15/07/05) :
C           - DETERMINATION DES NOEUDS
C           - CREATION DES RELATIONS DE LIAISONS ENTRE LAGRANGE
C
C
C   IN
C       ALGOLA : ALGORITHME UTILISE ('VERSION1' OU 'VERSION2')
C       NDIM   : DIMENSION DE L'ESPACE
C       NOMA   : NOM DE L'OBJET MAILLAGE
C       CNSLT  : CHAM_NO_S DE LA LEVEL SET TANGENTE
C       CNSLT  : CHAM_NO_S DE LA LEVEL SET NORMALE
C       GRLT   : CHAM_NO_S DU GRADIENT DE LA LEVEL SET TANGENTE
C       GRLT   : CHAM_NO_S DU GRADIENT DE LA LEVEL SET NORMALE
C
C   OUT
C      FISS   : SD FISS_XFEM AVEC LISTE DES RELATIONS ENTRE LAGRANGE

      INTEGER      NBMA,IRET,IMA,JMA,AR(12,2),NBAR,NA,NB,NMIL,NUNOA,MXAR
      INTEGER      NUNOB,NUNOM,NOMIL,JLTSV,JLNSV,JCONX1,JCONX2,NBARTO
      INTEGER      JTAB1,JTAB2,JCOOR,NBPINO,JPINO,NBNO,IA,I,JGRLT,JGRLN
      INTEGER      JCNSV,JCNSL
      REAL*8       LSNA,LSNB,LSTA,LSTB,A(3),B(3),C(3),S
      CHARACTER*8  K8BID,TYPMA,LICMPR(12)
      CHARACTER*19 MAI,CNSBAS


C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM,JEXATR
C---------------- FIN COMMUNS NORMALISES  JEVEUX  ----------------------

      CALL JEMARQ()

C     RECUPERATION DE DONNEES RELATIVES AU MAILLAGE
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8BID,IRET)
      CALL DISMOI('F','NB_NO_MAILLA',NOMA,'MAILLAGE',NBNO,K8BID,IRET)
      MAI=NOMA//'.TYPMAIL'
      CALL JEVEUO(MAI,'L',JMA)
      CALL JEVEUO(NOMA//'.CONNEX','L',JCONX1)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',JCONX2)
      CALL JEVEUO(NOMA//'.COORDO    .VALE','L',JCOOR)

C     RECUPERATION DE DONNEES RELATIVES AUX LEVEL SETS ET AUX GRADIENTS
      CALL JEVEUO(CNSLT//'.CNSV','L',JLTSV)
      CALL JEVEUO(CNSLN//'.CNSV','L',JLNSV)
      CALL JEVEUO(GRLT//'.CNSV','L',JGRLT)
      CALL JEVEUO(GRLN//'.CNSV','L',JGRLN)

      LICMPR(1)='X1'
      LICMPR(2)='X2'
      LICMPR(3)='X3'
      LICMPR(4)='X4'
      LICMPR(5)='X5'
      LICMPR(6)='X6'
      LICMPR(7)='X7'
      LICMPR(8)='X8'
      LICMPR(9)='X9'
      LICMPR(10)='X10'
      LICMPR(11)='X11'
      LICMPR(12)='X12'

C     CREATION DU CHAM_NO_S DE LA BASE COVARIANTE (POUR LE MOMENT,
C     SEULEMENT POUR LES POINTS DE CONTACT SUR LES ARETES)
      CNSBAS='&&XLAGSP.CNSBAS'
      CALL CNSCRE(NOMA,'NEUT_R',12,LICMPR,'V',CNSBAS)
      CALL JEVEUO(CNSBAS//'.CNSV','E',JCNSV)
      CALL JEVEUO(CNSBAS//'.CNSL','E',JCNSL)

C     ------------------------------------------------------------------
C     CREATION DE LA LISTE DES ARETES COUPEES
C     ------------------------------------------------------------------

C     DIMENTIONNEMENT DU NOMBRE MAXIMUM D'ARETES COUPEES PAR LA FISSURE
C     PAR LE NOMBRE DE NOEUDS DU MAILLAGE (À AUGMENTER SI NÉCESSAIRE)
      MXAR=NBNO
      CALL WKVECT('&&XLAGSP.TABNO','V V I',3*MXAR,JTAB1)
      
      CALL WKVECT('&&XLAGSP.TABINT','V V R',NDIM*MXAR,JTAB2)

C     TABNO  : COL 1,2     : NOEUDS EXTREMITE
C            : COL 3       : NOEUD MILIEU
C     TABINT : COL 1,2(,3) : COORDONNEES DU POINT D'INTERSECTION

      NBARTO=0

C     BOUCLE SUR TOUTES LES MAILLES DU MAILLAGE
      DO 100 IMA=1,NBMA
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(JMA-1+IMA)),TYPMA)
C       SI MAILLE NON VOLUMIQUE OU SURFACIQUE ON CONTINUE ï¿½100
        IF (TYPMA(1:4).NE.'HEXA'.AND.TYPMA(1:5).NE.'PENTA'.
     &       AND.TYPMA(1:5).NE.'TETRA'.AND.TYPMA(1:5).NE.'TRIA6'.
     &       AND.TYPMA(1:5).NE.'QUAD8') GOTO 100
C       BOUCLE SUR LES ARETES DE LA MAILLE VOLUMIQUE
        CALL CONARE(TYPMA,AR,NBAR)
        DO 110 IA=1,NBAR
          NA=AR(IA,1)
          NB=AR(IA,2)
          NUNOA=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+NA-1)
          NUNOB=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+NB-1)

C         SI CETTE ARETE N'EST PAS COUPEE PAR LA FISSURE, ON SORT
          LSNA=ZR(JLNSV-1+(NUNOA-1)+1)
          LSNB=ZR(JLNSV-1+(NUNOB-1)+1)
          IF (LSNA*LSNB.GE.0.D0) GOTO 110
          LSTA=ZR(JLTSV-1+(NUNOA-1)+1)
          LSTB=ZR(JLTSV-1+(NUNOB-1)+1)
          IF (LSTA.GE.0.D0.OR.LSTB.GE.0.D0) GOTO 110

          NMIL=NOMIL(TYPMA,IA)
          NUNOM=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+NMIL-1)

C         EST-CE QUE L'ARETE EST DEJA VUE ?
          DO 120 I = 1,NBARTO
C           ARETE DEJA VUE (ON A DEJA SON NOEUD MILIEU EN STOCK)
            IF (NUNOM.EQ.ZI(JTAB1-1+3*(I-1)+3)) GOTO 110
 120      CONTINUE

C         NOUVELLE ARETE
          NBARTO=NBARTO+1
          IF (NBARTO.GE.MXAR) CALL UTMESS('F','XLAGSP','LE NOMBRE  '//
     &       'D''ARETES COUPEES PAR LA FISSURE EST SUPERIEUR AU '//
     &       'CRITERE DE DIMENTIONEMENT INITIALLEMENT PREVU. IL FAUT '//
     &       'AUGMENTER LE PARAMETRE MXAR DANS LA ROUTINE XLAGSP.')
          ZI(JTAB1-1+3*(NBARTO-1)+1)=NUNOA
          ZI(JTAB1-1+3*(NBARTO-1)+2)=NUNOB
          ZI(JTAB1-1+3*(NBARTO-1)+3)=NUNOM
          DO 130 I=1,NDIM
              A(I)=ZR(JCOOR-1+3*(NUNOA-1)+I)
              B(I)=ZR(JCOOR-1+3*(NUNOB-1)+I)
              S=-LSNA/(LSNB-LSNA)
              C(I)=A(I)+S*(B(I)-A(I))
              ZR(JTAB2-1+NDIM*(NBARTO-1)+I)=C(I)
 130      CONTINUE

C         ENREGISTREMENT POUR CE POINT MILIEU DE LA BASE COVARIANTE
C         (POUR LE MOMENT, CA FAIT DOUBLON AVEC LE TE0510....)
          CALL XBASCO(NDIM,NUNOA,NUNOB,NUNOM,A,B,C,S,JGRLN,JGRLT,
     &                                                     JCNSV,JCNSL)

 110    CONTINUE
 100  CONTINUE

C      WRITE(6,*)'NBARTO ',NBARTO
C      DO 610 I=1,NBARTO
C        IA=-1+3*(I-1)
C        WRITE(6,*)' ',ZR(JTAB2+IA+1),ZR(JTAB2+IA+2),ZR(JTAB2+IA+3)
C 610  CONTINUE

      IF (NBARTO.GT.0) CALL CNSCNO(CNSBAS,' ','NON','G',FISS//'.BASCO')
      CALL DETRSD('CHAM_NO_S',CNSBAS)

C      CALL IMPRSD('CHAMP',FISS//'.BASCO',6,'FISS.BASCO')

C     ---------------------------------------------------------------
C     CREATION DES LISTES DES RELATIONS DE LIAISONS ENTRE LAGRANGE
C     ---------------------------------------------------------------

      IF (ALGOLA.EQ.'VERSION1') THEN

C       VERSION N°1 DE L'ALGORITHME
C       ---------------------------

C       SELECTION DES NOEUDS
        CALL WKVECT('&&XLAGSP.PICKNO','V V I',MXAR,JPINO)
        CALL XSELLA(ZR(JLNSV),NBNO,NBARTO,ZI(JTAB1),ZI(JPINO),
     &              NBPINO)

C       CREATION DES LISTES DES RELATIONS DE LIAISONS ENTRE LAGRANGE
        CALL XRELL1(ZI(JTAB1),NDIM,NBARTO,ZR(JTAB2),ZI(JPINO),NBPINO,
     &              FISS)

        CALL JEDETR('&&XLAGSP.PICKNO')

      ELSEIF (ALGOLA.EQ.'VERSION2') THEN

C       VERSION N°2 DE L'ALGORITHME
C       ---------------------------

C       CREATION DES LISTES DES RELATIONS DE LIAISONS ENTRE LAGRANGE
        CALL XRELL2(NBARTO,NDIM,ZI(JTAB1),ZR(JTAB2),FISS)
C        CALL XRELL3(NBARTO,NDIM,ZI(JTAB1),ZR(JTAB2),FISS)

      ENDIF

C     DESTRUCTION DES OBJETS TEMPORAIRES
      CALL JEDETR('&&XLAGSP.TABNO')
      CALL JEDETR('&&XLAGSP.TABINT')

      CALL JEDEMA()
      END
