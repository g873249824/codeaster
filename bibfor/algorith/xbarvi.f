      SUBROUTINE XBARVI(NOMA,NOMO,FISS,FACLON,AINTER)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXATR
      CHARACTER*8   NOMA,NOMO,FISS
      CHARACTER*19  FACLON,AINTER
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 19/12/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C
C ROUTINE XFEM
C
C C'EST ICI QUE L'ON REMPLIT LA 5EME COMPOSANTES DE TOPOFAC.AI
C DANS L'ELEMENT DE CONTACT XFEM
C
C CETTE COMPOSANTE VAUT :
C       1 SI L'ARETE INTERSECTÉE EST VITALE
C       0 SINON
C
C ON CRÉE AUSSI LA SD FISS(1:8)//'.CONTACT.CNCTE QUI LISTE ET RENSEIGNE
C LES ARETES APARTENANTES A UN GROUPE D'ARETES VITALES CONNECTÉES,
C IL Y A 4 COMPOSANTES PAR ARETES :
C      1 : NUMERO D'ARETE DANS LE GROUPE
C      2 : NUMERO DE GROUPE
C      3 : NUMERO DE MAILLE
C      4 : NUMERO LOCAL DE L'ARETE DANS LA MAILLE
C
C TRAVAIL EFFECTUE EN COLLABORATION AVEC L'I.F.P.
C
C ----------------------------------------------------------------------
C
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NOMO   : NOM DU MODELE
C IN  FISS   : NOM DE LA FISSURE EN COURS
C IN FACLON  : TOPOFAC.LO SIMPLIFIÉ
C IN/OUT AINTER : TOPOFAC.AI SIMPLIFIÉ
C
C
C
C
      INTEGER JCSD1,JCSL1,JCSV1,JCSD2,JCSL2,JCSV2,JCONX1,JCONX2
      INTEGER JLIS1,IMA,JMA,PIN,IAD,IRET,NINTER,IFISS
      INTEGER AR(12,3),I,IA,NBAR,NLOC(2),NGLO(2),NUNO(2),NEQ,NO
      CHARACTER*24  GRP(3)
      CHARACTER*19 NLISEQ
      CHARACTER*8 TYPMA,K8BID
      INTEGER   ZXAIN,XXMMVD,IN,JN,IAC,IER,NCTA,NCTE
      INTEGER   JCNTAN,JCNTES,JCNTE2,JARCON,NARCON,JNBPT
      LOGICAL   LMULTI,LCONNE
      INTEGER   KK,JGRP,IENR,NMAENR
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      ZXAIN = XXMMVD('ZXAIN')
      NARCON = 0
      IAC = 0
C
C --- LE MULTI-HEAVISIDE EST-IL ACTIF ?
C
      CALL JEEXIN(NOMO//'.FISSNO    .CELD',IER)
      IF (IER.EQ.0) THEN
        LMULTI = .FALSE.
        IFISS = 1
      ELSE
        LMULTI = .TRUE.
        CALL JEVEUO('&&XCONTA.NBSP','L',JNBPT)
      ENDIF
C
C --- ON RECUPERE LA LISTE DES RELATIONS D'EGALITÉS
C
      NLISEQ = FISS(1:8)//'.LISEQ'
      CALL JEEXIN(NLISEQ,IER)
      IF (IER.EQ.0) THEN
        NEQ = 0
      ELSE
        CALL JEVEUO(NLISEQ,'L',JLIS1)
        CALL JELIRA(NLISEQ,'LONMAX',NEQ,K8BID)
      ENDIF
C
C --- Y A T-IL DES ARETES CONNECTÉES ?
C
      CALL JEEXIN(FISS(1:8)//'.CONNECTANT',IER)
      IF (IER.EQ.0) THEN
        LCONNE = .FALSE.
        NCTA = 0
      ELSE
        LCONNE = .TRUE.
        CALL JEVEUO(FISS(1:8)//'.CONNECTANT','L',JCNTAN)
        CALL JELIRA(FISS(1:8)//'.CONNECTANT','LONMAX',
     &                 NCTA,K8BID)
        CALL JEVEUO(FISS(1:8)//'.CONNECTES ','L',JCNTES)
      ENDIF
C
C --- ON RECUPERE DES INFOS GLOBALES SUR LE MAILLAGE
C
      CALL JEVEUO(NOMA(1:8)//'.TYPMAIL','L',JMA)
      CALL JEVEUO(NOMA(1:8)//'.CONNEX','L',JCONX1)
      CALL JEVEUO(JEXATR(NOMA(1:8)//'.CONNEX','LONCUM'),'L',JCONX2)
C
C --- ACCES AUX CHAM_ELEM_S
C   
      CALL JEVEUO(FACLON//'.CESD','L',JCSD1)
      CALL JEVEUO(FACLON//'.CESV','L',JCSV1)
      CALL JEVEUO(FACLON//'.CESL','L',JCSL1)
      CALL JEVEUO(AINTER//'.CESD','L',JCSD2)
      CALL JEVEUO(AINTER//'.CESL','E',JCSL2)
      CALL JEVEUO(AINTER//'.CESV','E',JCSV2)
C
C --- RECUPERATION DES GROUPES
C
      GRP(1) = FISS//'.MAILFISS.HEAV'
      GRP(2) = FISS//'.MAILFISS.CTIP'
      GRP(3) = FISS//'.MAILFISS.HECT'
C
C --- PREMIÈRE PASSE POUR DIMENSIONER LE VECT DES ARETES CONNECTÉES
C
      IF ((.NOT.LCONNE)) GOTO 41
C
C --- BOUCLE SUR LES GRP
C
      DO 81 KK = 1,3
        CALL JEEXIN(GRP(KK),IRET)
        IF (IRET.EQ.0) GOTO 81
        CALL JEVEUO(GRP(KK),'L',JGRP)
        CALL JELIRA(GRP(KK),'LONMAX',NMAENR,K8BID)
C
C --- BOUCLE SUR LES MAILLES DU GROUPE
C
        DO 11 IENR = 1,NMAENR
          IMA = ZI(JGRP-1+IENR)
          IF (LMULTI) IFISS = ZI(JNBPT-1+IMA)
          IF (IFISS.EQ.0) GOTO 11
          CALL CESEXI('C',JCSD1,JCSL1,IMA,1,IFISS,1,IAD)
          IF (IAD.EQ.0) GOTO 11
          CALL ASSERT(IAD.GT.0)
          NINTER = ZI(JCSV1-1+IAD)
          IF (NINTER.EQ.0) GOTO 11
          CALL ASSERT(NINTER.GT.0)
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(JMA-1+IMA)),TYPMA)
          CALL CONARE(TYPMA,AR,NBAR)
          DO 21 PIN = 1,NINTER
C
C --- NUMERO DE L'ARETE INTERSECTÉ
C
            CALL CESEXI('S',JCSD2,JCSL2,IMA,1,IFISS,ZXAIN*(PIN-1)+1,IAD)
            CALL ASSERT(IAD.GT.0)
            IA=NINT(ZR(JCSV2-1+IAD))
            IF (IA.EQ.0) GOTO 21
            CALL ASSERT(IA.GT.0)
C
C --- S IL S'AGIT D'UNE ARRETE, RECUP DES NUM GLOBAUX DE SES NOEUDS
C
            DO 31 I=1,2
              NLOC(I) = AR(IA,I)
              NGLO(I) = ZI(JCONX1-1+ZI(JCONX2+IMA-1)+NLOC(I)-1)
 31         CONTINUE
C
C --- COMPARAISON AVEC LES NOEUDS DE LA LISTE DE NOEUDS CONNECTANTS
C
            DO 51 IN=1,NCTA/3
              NUNO(1) = ZI(JCNTAN-1+3*(IN-1)+1)
              IF (NUNO(1).EQ.NGLO(1).OR.NUNO(1).EQ.NGLO(2)) THEN
                NCTE   = ZI(JCNTAN-1+3*(IN-1)+2)
                JCNTE2 = ZI(JCNTAN-1+3*(IN-1)+3)
                DO 61 JN=1,NCTE       
                  NUNO(2)  = ZI(JCNTES-1 + JCNTE2 + JN)
                  IF (NUNO(2).EQ.NGLO(1).OR.NUNO(2).EQ.NGLO(2)) THEN
                    NARCON = NARCON + 1
                    GOTO 21
                  ENDIF
 61             CONTINUE
              ENDIF
 51         CONTINUE
 21       CONTINUE
 11     CONTINUE
 81   CONTINUE
      CALL ASSERT(NARCON.GT.0)
      CALL WKVECT('&&XBARVI.ARCON','V V I',4*NARCON,JARCON)
C
 41   CONTINUE
C
C --- BOUCLE SUR LES GRP
C
      DO 80 KK = 1,3
        CALL JEEXIN(GRP(KK),IRET)
        IF (IRET.EQ.0) GOTO 80
        CALL JEVEUO(GRP(KK),'L',JGRP)
        CALL JELIRA(GRP(KK),'LONMAX',NMAENR,K8BID)
C
C --- BOUCLE SUR LES MAILLES DU GROUPE
C
        DO 10 IENR = 1,NMAENR
          IMA = ZI(JGRP-1+IENR)
          IF (LMULTI) IFISS = ZI(JNBPT-1+IMA)
          IF (IFISS.EQ.0) GOTO 10
          CALL CESEXI('C',JCSD1,JCSL1,IMA,1,IFISS,1,IAD)
          IF (IAD.EQ.0) GOTO 10
          CALL ASSERT(IAD.GT.0)
          NINTER = ZI(JCSV1-1+IAD)
          IF (NINTER.EQ.0) GOTO 10
          CALL ASSERT(NINTER.GT.0)
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(JMA-1+IMA)),TYPMA)
          CALL CONARE(TYPMA,AR,NBAR)
          DO 20 PIN = 1,NINTER
C
C --- NUMERO DE L'ARETE INTERSECTÉE
C
            CALL CESEXI('S',JCSD2,JCSL2,IMA,1,IFISS,ZXAIN*(PIN-1)+1,IAD)
            CALL ASSERT(IAD.GT.0)
            IA=NINT(ZR(JCSV2-1+IAD))
            CALL CESEXI('S',JCSD2,JCSL2,IMA,1,IFISS,ZXAIN*(PIN-1)+2,IAD)
            CALL ASSERT(IAD.GT.0)
            NO=NINT(ZR(JCSV2-1+IAD))
            CALL ASSERT(NO.GE.0)
            CALL CESEXI('S',JCSD2,JCSL2,IMA,1,IFISS,ZXAIN*(PIN-1)+5,IAD)
            IF(IAD.LE.0) IAD = -IAD
C
C --- S IL S'AGIT D'UN NOEUD, ALORS IL EST VITAL
C
            IF (NO.GT.0) THEN
              ZR(JCSV2-1+IAD)=1
              ZL(JCSL2-1+IAD)=.TRUE.
            ENDIF
            IF (IA.GT.0) THEN
C
C --- S IL S'AGIT D'UNE ARRETE, RECUP DES NUM GLOBAUX DE SES NOEUDS
C
              DO 30 I=1,2
                NLOC(I) = AR(IA,I)
                NGLO(I) = ZI(JCONX1-1+ZI(JCONX2+IMA-1)+NLOC(I)-1)
 30           CONTINUE
C
C --- COMPARAISON AVEC LES COUPLES DE NO DE LA LISTE DE RELAT D'EGALITÉS
C
              DO 50 I=1,NEQ/2
                NUNO(1)  = ZI(JLIS1-1+2*(I-1)+1)
                NUNO(2)  = ZI(JLIS1-1+2*(I-1)+2)
                IF (NUNO(1).EQ.NGLO(1).AND.NUNO(2).EQ.NGLO(2).OR.
     &               NUNO(1).EQ.NGLO(2).AND.NUNO(2).EQ.NGLO(1)) THEN
C
C --- SI C EGAL, ON EST SUR UNE ARETE VITALE, ON MET LE STATUT À 1
C
                  ZR(JCSV2-1+IAD)=1
                  ZL(JCSL2-1+IAD)=.TRUE.
C
C --- COMPARAISON AVEC LES NOEUDS DE LA LISTE DE NOEUDS CONNECTANTS
C
                  DO 60 IN=1,NCTA/3
                    NUNO(1) = ZI(JCNTAN-1+3*(IN-1)+1)
                    IF (NUNO(1).EQ.NGLO(1).OR.NUNO(1).EQ.NGLO(2)) THEN
C
C --- SI C EGALE, ON RECUPERE LA LISTE DES NOEUDS CONNECTES
C
                      NCTE   = ZI(JCNTAN-1+3*(IN-1)+2)
                      JCNTE2 = ZI(JCNTAN-1+3*(IN-1)+3)
                      DO 70 JN=1,NCTE
C
C --- ON COMPARE LES NUMEROS DE CETTE LISTE À CELLE DE L'ARETE
C        
                        NUNO(2)  = ZI(JCNTES-1 + JCNTE2 + JN)
                        IF(NUNO(2).EQ.NGLO(1).OR.NUNO(2).EQ.NGLO(2))THEN
C
C --- SI C EGALE, ON NOTE LE NUMERO DE MAILLE, LE NUMERO LOCAL DE
C --- L'ARETE ET ON SORT
C
                          IAC = IAC + 1
                          CALL ASSERT(IAC.LE.NARCON)
                          ZI(JARCON-1+4*(IAC-1)+1) = IN
                          ZI(JARCON-1+4*(IAC-1)+2) = IMA
                          ZI(JARCON-1+4*(IAC-1)+3) = IA
                          ZI(JARCON-1+4*(IAC-1)+4) = JN
                          GOTO 20
                        ENDIF
 70                   CONTINUE
                    ENDIF
 60               CONTINUE
                  GOTO 20
                ELSE
                  ZR(JCSV2-1+IAD)=0
                  ZL(JCSL2-1+IAD)=.TRUE.                 
                ENDIF
C
 50           CONTINUE
            ENDIF

 20       CONTINUE
 10     CONTINUE
 80   CONTINUE
C
      CALL ASSERT(IAC.EQ.NARCON)
      IF (NARCON.GT.0) THEN
C
C --- ON ECRIT LE VECTEUR DES ARETES CONECTÉES
C
        CALL WKVECT(FISS(1:8)//'.CNCTE     ','G V I',4*NARCON,JCNTES)
        NCTE = 0
C
C --- ON BOUCLE SUR TOUT LES NOEUDS CONNECTANT, CHAQUE NOEUD 
C --- CONNECTANT DEFINI UN GROUPE
C
        DO 90 IN=1,NCTA/3
C
C --- ON BOUCLE SUR TOUTES LES ARETES CONNECTEES
C
          DO 100 IAC=1,NARCON
            IF (ZI(JARCON-1+4*(IAC-1)+2).EQ.IN) THEN
              NCTE = NCTE+1
C
C --- DES QU'IL Y EN A UNE QUI CORRESPOND AU GROUPE EN COURS,
C --- ON LE STOQUE DANS CE GROUPE 
C
              ZI(JCNTES-1+4*(NCTE-1)+1) = ZI(JARCON-1+4*(IAC-1)+4)
              ZI(JCNTES-1+4*(NCTE-1)+2) = IN
              ZI(JCNTES-1+4*(NCTE-1)+3) = ZI(JARCON-1+4*(IAC-1)+2)
              ZI(JCNTES-1+4*(NCTE-1)+4) = ZI(JARCON-1+4*(IAC-1)+3)
            ENDIF
100       CONTINUE
 90     CONTINUE
C
C --- MENAGE
C
        CALL JEDETR(FISS(1:8)//'.CONNECTANT')
        CALL JEDETR(FISS(1:8)//'.CONNECTES ')
        CALL JEDETR('&&XBARVI.ARCON')
      ENDIF
C
      CALL JEDEMA()
      END
