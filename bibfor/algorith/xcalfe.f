      SUBROUTINE  XCALFE(XYZ,HE,LSNG,LSTG,BASLOG,FE,DGDGL,IRET)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 27/10/2009   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
       IMPLICIT NONE
C
       REAL*8        XYZ(3),HE,LSNG,LSTG,BASLOG(9),FE(4),DGDGL(4,3)
       INTEGER       IRET
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      INTEGER  ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
C
C     BUT:  CALCUL DES FONCTIONS D'ENRICHISSEMENT EN UN POINT DE GAUSS
C
C IN  XYZ     : COORDONNÉES DU POINT DE GAUSS CONSIDÉRÉ
C IN  HE      : VALEUR DE LA FONCTION HEAVYSIDE CSTE LE SS-ÉLT
C IN  LSNG    : VALEUR DE LA LEVEL SET NORMALE AU POINT DE GAUSS
C IN  LSTG    : VALEUR DE LA LEVEL SET TANGENTE AU POINT DE GAUSS
C IN  BASLOG  : BASE LOCALE AU FOND DE FISSURE AU POINT DE GAUSS

C OUT FE      : VALEURS DES FONCTIONS D'ENRICHISSEMENT
C OUT DGDGL   : DÉRIVÉES DES FONCTIONS D'ENRICHISSEMENT
C OUT IRET    : CODE RETOUR VALANT 0 SI ON SE TROUVE SUR LE FOND DE
C               FISSURE (RG=0).
C               LES DÉRIVÉES DES FONCTIONS SINGULIÈRES (DGDGL)
C               NE SONT ALORS PAS CALCULÉES (CAR EN 1/SQRT(RG)).
C
C----------------------------------------------------------------

      INTEGER   I,J,K
      REAL*8    E1(3),E2(3),E3(3),P(3,3),INVP(3,3),NORME
      REAL*8    RG,TG,DGDPO(4,2),DGDLO(4,3),R8PREM

C     RECUPERATION DE LA BASE LOCALE ASSOCIÉE AU PT
C     (E1=GRLT,E2=GRLN,E3=E1^E2)
      DO 123 I=1,3
C         A(I)  = BASLOG(I)
         E1(I) = BASLOG(I+3)
         E2(I) = BASLOG(I+6)
 123  CONTINUE
C     NORMALISATION DE LA BASE
      CALL NORMEV(E1,NORME)
      CALL NORMEV(E2,NORME)
      CALL PROVEC(E1,E2,E3)

C     CALCUL DE LA MATRICE DE PASSAGE P TQ 'GLOBAL' = P * 'LOCAL'
      DO 124 I=1,3
        P(I,1)=E1(I)
        P(I,2)=E2(I)
        P(I,3)=E3(I)
 124  CONTINUE

C     CALCUL DE L'INVERSE DE LA MATRICE DE PASSAGE : INV=TRANSPOSE(P)
      DO 125 I=1,3
        DO 126 J=1,3
          INVP(I,J)=P(J,I)
 126    CONTINUE
 125  CONTINUE
C
C     COORDONNÉES POLAIRES DU POINT
      RG=SQRT(LSNG**2+LSTG**2)

      IF (RG.GT.R8PREM()) THEN
C       LE POINT N'EST PAS SUR LE FOND DE FISSURE
        TG = HE * ABS(ATAN2(LSNG,LSTG))
        IRET=1
      ELSE
C       LE POINT EST SUR LE FOND DE FISSURE :
C       L'ANGLE N'EST PAS DÉFINI, ON LE MET À ZÉRO
C       ON NE FERA PAS LE CALCUL DES DÉRIVÉES
        TG=0.D0
        IRET=0
      ENDIF
C
C     FONCTIONS D'ENRICHISSEMENT
      FE(1)=SQRT(RG)*SIN(TG/2.D0)
      FE(2)=SQRT(RG)*COS(TG/2.D0)
      FE(3)=SQRT(RG)*SIN(TG/2.D0)*SIN(TG)
      FE(4)=SQRT(RG)*COS(TG/2.D0)*SIN(TG)

      IF (IRET.EQ.0) GOTO 9999
C
C     CALCUL DES DÉRIVÉES
C     -------------------

C     DÉRIVÉES DES FONCTIONS D'ENRICHISSEMENT DANS LA BASE POLAIRE
      DGDPO(1,1)=1.D0/(2.D0*SQRT(RG))*SIN(TG/2.D0)
      DGDPO(1,2)=SQRT(RG)/2.D0*COS(TG/2.D0)
      DGDPO(2,1)=1.D0/(2.D0*SQRT(RG))*COS(TG/2.D0)
      DGDPO(2,2)=-SQRT(RG)/2.D0*SIN(TG/2.D0)
      DGDPO(3,1)=1.D0/(2.D0*SQRT(RG))*SIN(TG/2.D0)*SIN(TG)
      DGDPO(3,2)=SQRT(RG) *
     &          (COS(TG/2.D0)*SIN(TG)/2.D0 + SIN(TG/2.D0)*COS(TG))
      DGDPO(4,1)=1.D0/(2.D0*SQRT(RG))*COS(TG/2.D0)*SIN(TG)
      DGDPO(4,2)=SQRT(RG) *
     &          (-SIN(TG/2.D0)*SIN(TG)/2.D0 + COS(TG/2.D0)*COS(TG))

C     DÉRIVÉES DES FONCTIONS D'ENRICHISSEMENT DANS LA BASE LOCALE
      DO 131 I=1,4
        DGDLO(I,1)=DGDPO(I,1)*COS(TG)-DGDPO(I,2)*SIN(TG)/RG
        DGDLO(I,2)=DGDPO(I,1)*SIN(TG)+DGDPO(I,2)*COS(TG)/RG
        DGDLO(I,3)=0.D0
 131  CONTINUE

C     DÉRIVÉES DES FONCTIONS D'ENRICHISSEMENT DANS LA BASE GLOBALE
      DO 132 I=1,4
        DO 133 J=1,3
          DGDGL(I,J)=0.D0
          DO 134 K=1,3
            DGDGL(I,J)=DGDGL(I,J)+DGDLO(I,K)*INVP(K,J)
 134      CONTINUE
 133    CONTINUE
 132  CONTINUE

 9999 CONTINUE

      END
