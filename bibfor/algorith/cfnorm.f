      SUBROUTINE CFNORM(NDIM,NNOCO,NOMA,NEWGEO,RESOCO,DEFICO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/12/2004   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT     NONE
      INTEGER      NDIM
      INTEGER      NNOCO
      CHARACTER*8  NOMA
      CHARACTER*24 NEWGEO
      CHARACTER*24 RESOCO
      CHARACTER*24 DEFICO
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : RECHCO
C ----------------------------------------------------------------------
C
C ACTUALISATION DES NORMALES AUX NOEUDS POTENTIELS DE CONTACT.
C
C IN  NDIM   : DIMENSION DE L'ESPACE (2 OU 3)
C IN  NNOCO  : NOMBRE DE NOEUDS POTENTIELS DE CONTACT
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : COORDONNEES ACTUALISEES DES NOEUDS DU MAILLAGE
C IN  NOZOCO : TABLEAU DONNANT LE NUMERO DE ZONE POUR CHAQUE NOEUD DE
C              CONTACT (DIM NNOCO)
C IN  CONTMA : LISTE DES MAILLES DE CONTACT
C IN  MANOCO : TABLEAU INVERSE NOEUDS->MAILLES
C IN  PMANO  : POINTEUR DE CE TABLEAU NOEUDS->MAILLES
C OUT NORINI : NORMALE EN TOUS LES NOEUDS DE CONTACT
C IN  DIRCO  : DANS LE CAS D'UN APPARIEMENT DANS UNE DIRECTION FIXE
C              (PRESENCE DU MOT-CLE FACTEUR DIRECTION), CONTIENT POUR
C              CHAQUE ZONE LES 3 COMPOSANTES DEFINISSANT LA DIRECTION
C              UTILISEE POUR APPARIEMENT ET ECRITURE DE LA RELATION DE
C              NON PENETRATION.
C IN  METHCO : TABLEAU DONNANT LES CARACTERISTIQUES DE LA METHODE
C              DE TRAITEMENT DU CONTACT UNILATERAL
C              POUR LA ZONE N : METHCO(1+10*(N-1)+1) = 4 SI
C              APPARIEMENT NODAL DANS UNE DIRECTION FIXEE DANS LE
C              FICHIER DE COMMANDES
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32 JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      ZMETH
      PARAMETER    (ZMETH=8)
      INTEGER      JCOOR,JDES
      INTEGER      POSNO,POSMA,N1,NO(9),NUMA,IZONE
      INTEGER      NUTYP
      INTEGER      INO,J,JDEB,JFIN,K
      REAL*8       RBID,XNORM(3),VECSEG(3),COORMA(27),XTANG(6),VECNOR(3)
      CHARACTER*1  K1BID
      CHARACTER*24 NOZOCO,CONTMA,MANOCO,PMANO,DIRCO,METHCO
      INTEGER      JZOCO,JMANO,JMACO,JPOMA,JDIR,JMETH
      CHARACTER*24 NORINI,TANDEF    
      INTEGER      JNRINI,JTGDEF 
      CHARACTER*4  MATYP
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()

C --- LECTURE DES SD POUR LE CONTACT POTENTIEL

      METHCO = DEFICO(1:16)//'.METHCO'
      TANDEF = DEFICO(1:16)//'.TANDEF'
      CONTMA = DEFICO(1:16)//'.MAILCO'
      MANOCO = DEFICO(1:16)//'.MANOCO'
      PMANO  = DEFICO(1:16)//'.PMANOCO'
      DIRCO  = DEFICO(1:16)//'.DIRCO'
      NOZOCO = DEFICO(1:16)//'.NOZOCO'

      CALL JEVEUO(NEWGEO(1:19)//'.VALE','L',JCOOR)
      CALL JEVEUO(CONTMA,'L',JMACO)
      CALL JEVEUO(MANOCO,'L',JMANO)
      CALL JEVEUO(PMANO, 'L',JPOMA)
      CALL JEVEUO(NOZOCO,'L',JZOCO)
      CALL JEVEUO(DIRCO, 'L',JDIR )
      CALL JEVEUO(METHCO,'L',JMETH)
C
C ----- MISE A JOUR DES SD POUR LE CONTACT
C ----- CAS DES VECTEURS NORMAL ET TANGENT
C
      NORINI = RESOCO(1:14)//'.NORINI'
      CALL JEVEUO(NORINI,'E',JNRINI)

C ----- BOUCLE SUR LES NOEUDS POTENTIELS DE CONTACT

      DO 50 POSNO = 1,NNOCO

C --- ZONE DE CONTACT CORRESPONDANTE

        IZONE = ZI(JZOCO+POSNO-1)

C --- UTILISATION D'UNE DIRECTION FIXE SI NECESSAIRE

        IF (ZI(JMETH+ZMETH* (IZONE-1)+1).EQ.4) THEN

          XNORM(1) = ZR(JDIR+3* (IZONE-1))
          XNORM(2) = ZR(JDIR+3* (IZONE-1)+1)
          XNORM(3) = ZR(JDIR+3* (IZONE-1)+2)

        ELSE

C --- SINON CALCUL DES NOUVELLES COMPOSANTES DE LA NORMALE

          XNORM(1) = 0.D0
          XNORM(2) = 0.D0
          XNORM(3) = 0.D0

          JDEB = ZI(JPOMA+POSNO-1) + 1
          JFIN = ZI(JPOMA+POSNO)

          DO 40 J = JDEB,JFIN

            POSMA = ZI(JMANO+J-1)
            NUMA  = ZI(JMACO+POSMA-1)

            CALL TYPEMA(NOMA,NUMA,NUTYP,MATYP)

            CALL JELIRA(JEXNUM(NOMA//'.CONNEX',NUMA),'LONMAX',N1,K1BID)
            CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',NUMA),'L',JDES)

            DO 10 INO = 1,N1
              NO(INO) = ZI(JDES+INO-1)
   10       CONTINUE

            DO 20 K = 1,N1
              COORMA(3* (K-1)+1) = ZR(JCOOR+3* (NO(K)-1))
              COORMA(3* (K-1)+2) = ZR(JCOOR+3* (NO(K)-1)+1)
              COORMA(3* (K-1)+3) = ZR(JCOOR+3* (NO(K)-1)+2)
   20       CONTINUE

C - ON MET LE VECTEUR NUL POUR LA NORMALE D'UN NOEUD APPARTENANT
C - A UN POI1 EN 3D
C - ON LA CALCULE PAR PRODUIT VECTORIEL POUR UN NOEUD DE SEG2 OU
C - SEG3 EN 3D

C   ---   VALEUR DE VECT_Y
            IF (ZI(JMETH+ZMETH*(IZONE-1)+2).EQ.1) THEN
              CALL JEVEUO(TANDEF,'L',JTGDEF)
              XTANG(1) = ZR(JTGDEF+3* (IZONE-1))
              XTANG(2) = ZR(JTGDEF+3* (IZONE-1)+1)
              XTANG(3) = ZR(JTGDEF+3* (IZONE-1)+2)
            END IF

C   ---   CALCUL DES NORMALES
            IF ((NDIM.EQ.3) .AND. (MATYP.EQ.'POI1')) THEN
              XNORM(1) = 0.D0
              XNORM(2) = 0.D0
              XNORM(3) = 0.D0
            ELSE IF ((NDIM.EQ.3) .AND. ((MATYP.EQ.'SEG2').OR.
     &               (MATYP.EQ.'SEG3'))) THEN
              CALL CANORM(COORMA,VECSEG,NDIM,NUTYP,1)
              XNORM(1) = XNORM(1) + VECSEG(2)*XTANG(3) -
     &                   VECSEG(3)*XTANG(2)
              XNORM(2) = XNORM(2) + VECSEG(3)*XTANG(1) -
     &                   VECSEG(1)*XTANG(3)
              XNORM(3) = XNORM(3) + VECSEG(1)*XTANG(2) -
     &                   VECSEG(2)*XTANG(1)
            ELSE
              CALL CANORM(COORMA,VECNOR,NDIM,NUTYP,1)
              DO 30 K = 1,3
                XNORM(K) = XNORM(K) + VECNOR(K)
   30         CONTINUE
            END IF

   40     CONTINUE

        END IF

        CALL NORMEV(XNORM,RBID)
        CALL DCOPY(3,XNORM,1,ZR(JNRINI + 3* (POSNO-1)),1)

   50 CONTINUE

C ----------------------------------------------------------------------

      CALL JEDEMA()
      END
