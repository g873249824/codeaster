      SUBROUTINE CALGF(NDIM,NNO,AXI,NPG,GEOMI,G,IW,VFF,IDFF,
     &                 DEPLM,DEPLT,GRAND,ALPHA,R,W,DFF,FM,FMP,
     &                 FMA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/12/2009   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
      IMPLICIT NONE

      LOGICAL AXI, GRAND
      INTEGER NDIM,NNO,G,IW,IDFF,I,J,NPG
      REAL*8  GEOMI(NDIM,NNO),VFF(NNO,NPG)
      REAL*8  DEPLM(NNO*NDIM),DEPLT(NNO*NDIM)
      REAL*8 ALPHA 

C ---------------------------------
C   NDIM    : DIMENSION DE L'ESPACE
C   NNO     : NOMBRE DE NOEUDS (FAMILLE E-BARRE)
C   AXI     : .TRUE. SI AXISYMETRIQUE
C   GRAND   : .TRUE.  --> CALCUL DE F(3,3)
C             .FALSE. --> CALCUL DE EPS(6)
C   GEOMI   : COORDONNEES DES NOEUDS A L'INSTANT T-
C   G       : NUMERO DU POINT DE GAUSS COURANT
C   IW      : ACCES AUX POIDS DES POINTS DE GAUSS DE REFERENCE
C   VFF     : VALEURS DES FONCTIONS DE FORME (POINT DE GAUSS COURANT)
C   IDFF    : DERIVEES DES FONCTIONS DE FORME DE REFERENCE
C   R       : RAYON DU POINT DE GAUSS COURANT (EN AXI)
C   W       : POIDS DU POINT DE GAUSS COURANT (Y COMPRIS R EN AXI)
C   DFF     : DERIVEE DES FONCTIONS DE FORME (POINT DE GAUSS COURANT)
C   DEPLM   : DEPLACEMENT EN T-
C   DEPLT   : INCREMENT DE DEPLACEMENT ENTRE T- ET T+
C   ALPHA   : PARAMETRE D INTEGRATION COMPRIS ENTRE 0 ET 1 


C   SORTIE:
C   FM      : TENSEUR DE DEFORMATION ENTRE CONFIGURATION INITIALE ET 
C             CONFIGURATION A T-
C   FMP     : TENSEUR DE DEFORMATION ENTRE CONFIGURATION T- ET 
C             CONFIGURATION A T+
C   FMA     : TENSEUR DE DEFORMATION ENTRE CONFIGURATION T- ET 
C             CONFIGURATION A T_(N+ALPHA)
C ----------------------------
C
C  CALCUL DES TENSEURS DE DEFORMATIONS ENTRE CONFIGURATION DE REFERENCE
C  ET LES CONFIGURATIONS A L INSTANTS T-, T+ ET INTERMEDIAIRE

C----------------------------------------------------------------------


      REAL*8 FM(3,3),FMP(3,3),FMA(3,3),POIDS,EPS(6),X,IVF
      REAL*8 R,W,DFF(NNO,NDIM),TBID(6)
      
C----------------------INTIALISATION DES MATRICES ---------------------
      CALL R8INIR(9,0.D0,FM,1)
      CALL R8INIR(9,0.D0,FMP,1)
      CALL R8INIR(9,0.D0,FMA,1)
      CALL R8INIR(6,0.D0,EPS,1)
      CALL R8INIR(6,0.D0,TBID,1)
      CALL R8INIR(NNO*NDIM,0.D0,DFF,1)
      R=0.D0
      W=0.D0
      
C--CALCUL DE F_N, F_N+1 ET F_(N+ALPHA) PAR RAPPORT A GEOMI GEOM INITIAL

C     CALCUL DE FM EN SORTIE FM = F_N      
      CALL DFDMIP(NDIM,NNO,AXI,GEOMI,G,IW,VFF(1,G),IDFF,R,W,DFF)
      CALL NMEPSI(NDIM,NNO,AXI,GRAND,VFF(1,G),R,DFF,DEPLM,FM,TBID)
        
C     CALCUL DE FMP EN SORTIE FMP = F_N+1
      CALL NMEPSI(NDIM,NNO,AXI,GRAND,VFF(1,G),R,DFF,DEPLT,FMP,TBID)
      
C     CALCUL DE F_(N+ALPHA)
      DO 11 I = 1,3
       DO 20 J = 1,3
        FMA(I,J) = FM(I,J) + ALPHA*(FMP(I,J)-FM(I,J))
 20    CONTINUE
 11   CONTINUE
      END
