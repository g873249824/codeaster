      SUBROUTINE CFGCCJ(RESOCO,NBLIAI,CONJUG)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*24 RESOCO
      INTEGER      NBLIAI
      LOGICAL      CONJUG
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (RESOLUTION - GCP)
C
C CONJUGAISON
C
C ----------------------------------------------------------------------
C
C ON FAIT LA CONJUGAISON DE POLAK-RIBIERE :
C     - SI L'ETAT DE CONTACT EST LE MEME D'UNE ITERATION SUR L'AUTRE
C     - TOUTES LES FREQ ITERATIONS
C     - SI DIRECT EST UNE DIRECTION DE DESCENTE I.E.
C                                                  (DIRECT' SGRAD+)>0
C  NB1 :  FORMULE DE CONJUGAISON EN PRESENCE DE PRECONDITIONNEUR :
C         GAMMA = (SGRADP' (SGRPRP - SGRPRM)) / (SGRADM' SGRPRM))
C  NB2 : LA CONJUGAISON DE FLETCHER-REEVES EST : GAMMA = NUMER/DENOM
C
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NBLIAI : NOMBRE DE LIAISONS DE CONTACT
C I/O CONJUG : DIRECTIONS CONJUGUEES
C
C
C
C
C
      INTEGER      IFM,NIV
      REAL*8       DDOT,NUMER,NUMER2,DENOM,GAMMA
      CHARACTER*19 SGRADM,SGRADP,SGRPRM,SGRPRP,DIRECT
      INTEGER      JSGRAM,JSGRAP,JSGPRM,JSGPRP,JDIREC
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- INITIALISATIONS
C
      GAMMA = 0.D0
C
C --- ACCES VECTEURS DE TRAVAIL
C
      SGRADM = RESOCO(1:14)//'.SGDM'
      SGRADP = RESOCO(1:14)//'.SGDP'
      SGRPRM = RESOCO(1:14)//'.SGPM'
      SGRPRP = RESOCO(1:14)//'.SGPP'
      DIRECT = RESOCO(1:14)//'.DIRE'
      CALL JEVEUO(SGRADM,'L',JSGRAM)
      CALL JEVEUO(SGRADP,'L',JSGRAP)
      CALL JEVEUO(SGRPRM,'L',JSGPRM)
      CALL JEVEUO(SGRPRP,'L',JSGPRP)
      CALL JEVEUO(DIRECT,'E',JDIREC)
C
C --- COEFFICIENT DE DIRECTION
C
      IF (CONJUG) THEN
        NUMER  = DDOT(NBLIAI,ZR(JSGRAP),1,ZR(JSGPRP),1)
        NUMER2 = DDOT(NBLIAI,ZR(JSGRAP),1,ZR(JSGPRM),1)
        DENOM  = DDOT(NBLIAI,ZR(JSGRAM),1,ZR(JSGPRM),1)
        GAMMA  = (NUMER-NUMER2)/DENOM
      ENDIF
C
C --- MISE A JOUR DIRECTION
C
      CALL DSCAL(NBLIAI,GAMMA,ZR(JDIREC),1)
      CALL DAXPY(NBLIAI,1.D0,ZR(JSGPRP),1,ZR(JDIREC),1)
C
C --- AFFICHAGE
C
      IF (CONJUG) THEN
        IF (NIV.EQ.2) THEN
          WRITE (IFM,*) '<CONTACT><CALC> CONJUGAISON DES DIRECTIONS '//
     &      'DE RECHERCHE, GAMMA=',GAMMA
        ENDIF
      ENDIF
C
      CALL JEDEMA()
C
      END
