      SUBROUTINE CFIMP3(DEFICO,RESOCO,NOMA  ,IFM   ,NUMORD,
     &                  INSTAP,NBLIAI,NBLIAC,JCNSVR)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 23/09/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*24 DEFICO,RESOCO
      INTEGER      IFM
      INTEGER      NUMORD
      INTEGER      NBLIAI
      INTEGER      NBLIAC
      INTEGER      JCNSVR
      CHARACTER*8  NOMA
      REAL*8       INSTAP
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE DISCRETE - APPARIEMENT - UTILITAIRE)
C
C IMPRESSION DES INFOS SUR LES LIAISONS DE CONTACT/FROTTEMENT
C
C ----------------------------------------------------------------------
C
C
C IN  IFM    : UNITE D'IMPRESSION DU MESSAGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NUMORD : NUMERO D'INSTANT
C IN  INSTAP : INSTANT DE CALCUL
C IN  NBLIAI : NOMBRE DE LIAISONS DE CONTACT POSSIBLES
C IN  NBLIAC : NOMBRE DE LIAISONS DE CONTACT ACTIVES
C IN  JCNSVR : POINTEUR SUR OBJET CHAM_NO VALE_CONT
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      CFMMVD,ZAPPA,ZRESU
      INTEGER      ILIAI,POSNOE,POSMAI
      CHARACTER*8  NOMNOE,NOMMAI,NOMENT
      INTEGER      CODRET,NUMNOE    
      CHARACTER*4  TYPE2
      CHARACTER*24 APPARI,APJEU
      INTEGER      JAPPAR,JAPJEU
      REAL*8       RN,R,COE,PROD,VARC,AJEUFT
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ ()
C
C --- ACCES SD CONTACT
C
      APPARI = RESOCO(1:14)//'.APPARI'
      APJEU  = RESOCO(1:14)//'.APJEU'
      CALL JEVEUO(APPARI,'L',JAPPAR)
      CALL JEVEUO(APJEU, 'L',JAPJEU)
      ZAPPA  = CFMMVD('ZAPPA')
      ZRESU  = CFMMVD('ZRESU')
C
C --- PREMIERS AFFICHAGES      
C  
      WRITE(IFM,1000)
      WRITE(IFM,1001) NUMORD
      WRITE(IFM,1002) INSTAP
      WRITE(IFM,1003) NBLIAI
      WRITE(IFM,1004) NBLIAC
C
C --- BOUCLE SUR LES LIAISONS
C
      DO 120 ILIAI = 1,NBLIAI
C
C --- REPERAGE DE L'ESCLAVE
C
        POSNOE = ZI(JAPPAR+ZAPPA*(ILIAI-1)+1)
C
C --- NOM DU NOEUD ESCLAVE
C      
        CALL CFNOMM(NOMA  ,DEFICO,'NOEU',POSNOE,NOMENT,
     &              CODRET)       
        IF (CODRET.GE.0) THEN
          NOMNOE = NOMENT
        ELSE
          NOMNOE = 'ERREUR'
        ENDIF        
C
C --- NUMERO ABSOLU DU NOEUD ESCLAVE
C                
        CALL CFPOSM(NOMA  ,DEFICO,'NOEU',1     ,POSNOE,
     &              NUMNOE,CODRET)  
        IF (CODRET.LT.0) THEN
          WRITE (IFM,2002)
          GOTO 999 
        ENDIF    
C
C --- REPERAGE DU MAITRE
C
        POSMAI = ZI(JAPPAR+ZAPPA*(ILIAI-1)+2)        
C
C --- NOM ET TYPE DU MAITRE
C
        IF (POSMAI.LT.0) THEN
          CALL CFNOMM(NOMA  ,DEFICO,'NOEU',POSMAI,NOMENT,
     &              CODRET)
          IF (CODRET.LT.0) THEN
            TYPE2  = ' '
            NOMMAI = 'ERREUR'          
          ELSE   
            TYPE2  = '/ND '
            NOMMAI = NOMENT   
          ENDIF         
        ELSEIF (POSMAI.GT.0) THEN
          CALL CFNOMM(NOMA  ,DEFICO,'MAIL',POSMAI,NOMENT,
     &                CODRET)
          IF (CODRET.LT.0) THEN
            TYPE2  = ' '
            NOMMAI = 'ERREUR'          
          ELSE
            TYPE2  = '/EL '
            NOMMAI = NOMENT   
          ENDIF    
        ELSE  
          TYPE2  = ' NON'
          NOMMAI = ' APPARIE'               
        ENDIF 
C
C --- VALEURS
C
        RN = ZR(JCNSVR-1+(NUMNOE-1)*ZRESU+3 )
        R  = ZR(JCNSVR-1+(NUMNOE-1)*ZRESU+19)

        COE = 0.0D0
        IF ( RN.NE.0.D0 ) THEN
          PROD = (ABS(R)-ABS(RN))
          COE = PROD/RN
        ENDIF

        VARC   = ZR(JCNSVR-1+ (NUMNOE-1)*ZRESU+1 )
        AJEUFT = ZR(JCNSVR-1+ (NUMNOE-1)*ZRESU+9 )
        IF (VARC.NE.0.0D0) THEN
          WRITE (IFM,2000) ILIAI,' (ND ',NOMNOE,TYPE2,NOMMAI,
     &                     ') * JEU:',ZR(JAPJEU+ILIAI-1),' * RN:',RN,
     &                     ' * GLI:',AJEUFT,' * R:',R,
     &                     ' * RT/RN:',COE
        ELSE
          WRITE (IFM,2001) ILIAI,' (   ',NOMNOE,TYPE2,NOMMAI,
     &                     ') * JEU:',ZR(JAPJEU+ILIAI-1),' * RN:',RN,
     &                     ' * GLI:',AJEUFT,' * R:',R,
     &                     ' * RT/RN:',COE
        ENDIF
 120  CONTINUE

 999  CONTINUE
 
      
1000  FORMAT (' <CONTACT> LISTE DES LIAISONS DE CONTACT')
1001  FORMAT (' <CONTACT>   NUMERO D''ORDRE  : ',I6)
1002  FORMAT (' <CONTACT>   INSTANT          : ',1PE12.5)
1003  FORMAT (' <CONTACT>   LIAISONS         : ',I6)
1004  FORMAT (' <CONTACT>   LIAISONS ACTIVES : ',I6)
2000  FORMAT (' <CONTACT>   * LIAISON ACTIVE   ',I6,A5,A8,A4,A8,
     &        A8,1PE12.5,A6,1PE12.5,
     &        A7,1PE12.5,A5,1PE12.5,
     &        A9,1PE12.5)
2001  FORMAT (' <CONTACT>   * LIAISON INACTIVE ',I6,A5,A8,A4,A8,
     &        A8,1PE12.5,A6,1PE12.5,
     &        A7,1PE12.5,A5,1PE12.5,
     &        A9,1PE12.5)
2002  FORMAT (' <CONTACT>   * LIAISON INCONNUE ')     
C
      CALL JEDEMA()
      END
