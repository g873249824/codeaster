        SUBROUTINE LCMMLC(COMP,NMAT,NBCOMM,CPMONO,PGL,TOUTMS,HSR,NSFV,
     &  NSFA,IFA,NBSYS,IS,DT,NVI,VIND,SIGF,FETFE6,YD,DY,
     &  ITMAX,TOLER,MATERF,EXPBP,NUECOU,
     &  TAUS,DALPHA,DGAMMA,DP,CRIT,SGNS,RP,MSNS,MS,IRET)
        IMPLICIT NONE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 14/06/2011   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C       ----------------------------------------------------------------
C     MONOCRISTAL  : CALCUL DE L'ECOULEMENT VISCOPLASTIQUE
C     IN  COMP   :  NOM COMPORTEMENT
C         NMAT   :  DIMENSION MATER
C         NBCOMM :  INCIDES DES COEF MATERIAU
C         CPMONO :  NOM DES COMPORTEMENTS
C         PGL    :  MATRICE DE PASSAGE
C         TOUTMS :  TENSEURS D'ORIENTATION
C         HSR    :  MATRICE D'INTERACTION
C         NVI    :  NOMBRE DE VARIABLES INTERNES
C         VIND   :  VARIABLES INTERNES A L'INSTANT PRECEDENT
C         ITMAX  :  ITER_INTE_MAXI
C         TOLER  :  RESI_INTE_RELA
C         DT     :  ACCROISSEMENT DE TEMPS
C         YD     :  VARIABLES A T       
C         DY     :  SOLUTION ESSAI      
C     OUT IRET   :  CODE RETOUR
C     ----------------------------------------------------------------
      INTEGER NMAT, NVI, NSFV, IRET
      INTEGER I,NUVI,IFA,NBSYS,IS,ITMAX,IEXP,J
      INTEGER NBCOMM(NMAT,3),NSFA,NUECOU
C
      REAL*8 SIGF(6), MSNS(3,3),DT,FETFE6(6), VIND(NVI)
      REAL*8 MATERF(NMAT*2),DY(*),YD(*),TOLER
      REAL*8 MS(6),NG(3),TAUS,DGAMMA,DALPHA,DP,RP
      REAL*8 TOUTMS(5,24,6), HSR(5,24,24),Q(3,3),LG(3)
      REAL*8 PGL(3,3),DGAMM1,ALPHAM
      REAL*8 CRIT,ALPHAP,SGNS,GAMMAP,EXPBP(24)
      CHARACTER*16 CPMONO(5*NMAT+1),COMP(*)
      CHARACTER*16 NOMFAM,NECOUL,NECRIS,NECRCI
C     ----------------------------------------------------------------
C
      NOMFAM=CPMONO(5*(IFA-1)+1)
      NECOUL=CPMONO(5*(IFA-1)+3)
      NECRIS=CPMONO(5*(IFA-1)+4)
      NECRCI=CPMONO(5*(IFA-1)+5)

      IF (COMP(3)(1:5).EQ.'PETIT') THEN
C        CALCUL DE LA SCISSION REDUITE =
C        PROJECTION DE SIG SUR LE SYSTEME DE GLISSEMENT
C        TAU      : SCISSION REDUITE TAU=SIG:MS
         DO 101 I=1,6
            MS(I)=TOUTMS(IFA,IS,I)
 101      CONTINUE
         TAUS=0.D0
         DO 10 I=1,6
            TAUS=TAUS+SIGF(I)*MS(I)
 10      CONTINUE
      ELSE

C     A FAIRE POUR GANGER DU TEMPS
C         DO 109 I=1,3
C            LG(I)=TOUTMS(IFA,IS,I)
C            NG(I)=TOUTMS(IFA,IS,I+3)
C 109     CONTINUE
         CALL LCMMSG(NOMFAM,NBSYS,IS,PGL,MS,NG,LG,0,Q)
         DO 110 I=1,3
         DO 110 J=1,3
            MSNS(I,J)=LG(I)*NG(J)
 110     CONTINUE
         CALL CALTAU(SIGF,MSNS,FETFE6,TAUS)
      ENDIF
      
      NUVI=NSFV+3*(IS-1)

C     ECROUISSAGE CINEMATIQUE - CALCUL DE DALPHA-SAUD MODELES DD
      IF ((NUECOU.NE.4).AND.(NUECOU.NE.5)) THEN

          DGAMM1=DY(NSFA+IS)
          ALPHAM=VIND(NUVI+1)
          CALL LCMMFC( MATERF(NMAT+1),IFA,NMAT,NBCOMM,NECRCI,
     &      ITMAX, TOLER,ALPHAM,DGAMM1,DALPHA, IRET)

          ALPHAP=ALPHAM+DALPHA
          GAMMAP=YD(NSFA+IS)+DGAMM1
      ELSE
C        POUR KOCKS-RAUCH, DD_CFC ALPHA est la variable principale
         ALPHAP=YD(NSFA+IS)+DY(NSFA+IS)
      ENDIF
      
      IF (NUECOU.NE.4) THEN
C        ECROUISSAGE ISOTROPE : CALCUL DE R(P)
         IEXP=0
         IF (IS.EQ.1) IEXP=1
         CALL LCMMFI(MATERF(NMAT+1),IFA,NMAT,NBCOMM,NECRIS,IS,
     &               NBSYS,VIND(NSFV+1),DY(NSFA+1),HSR,IEXP,
     &               EXPBP,RP)
      ENDIF
      
C     ECOULEMENT VISCOPLASTIQUE
C     ROUTINE COMMUNE A L'IMPLICITE (PLASTI-LCPLNL)
C     ET L'EXPLICITE (NMVPRK-GERPAS-RK21CO-RDIF01)
C     CAS IMPLICITE : IL FAUT PRENDRE EN COMPTE DT
C     CAS EXPLICITE : IL NE LE FAUT PAS (C'EST FAIT PAR RDIF01)
C
      CALL LCMMFE( TAUS,MATERF(NMAT+1),MATERF(1),IFA,NMAT,NBCOMM,
     &             NECOUL,IS,NBSYS,VIND(NSFV+1),DY(NSFA+1),
     &   RP,ALPHAP,GAMMAP,DT,DALPHA,DGAMMA,DP,CRIT,SGNS,HSR,IRET)
     
      END
