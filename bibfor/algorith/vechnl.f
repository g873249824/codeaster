      SUBROUTINE VECHNL(MODELE,CHARGE,INFCHA,CARELE,INST,CHTN,
     &                  LVECHN)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 12/02/2013   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C CALCUL DES VECTEURS ELEMENTAIRES TERMES D EVOLUTION
C POUR LES CHARGES NON LINEAIRES ( FLUXNL, RAYONNEMENT, SOUR_NL )
C IN  MODELE : NOM DU MODELE
C IN  CHARGE : LISTE DES CHARGES
C IN  INFCHA : INFORMATIONS SUR LES CHARGES
C IN  CARELE : CHAMP DE CARA_ELEM
C IN  INST   : CARTE CONTENANT LA VALEUR DU TEMPS ET AUTRES PARAMETRES
C IN  CHTN   : ITERE A L INSTANT PRECEDENT DU CHAMP DE TEMPERATURE
C OUT LVECHN : VECT_ELEM
C----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE

C 0.1. ==> ARGUMENTS

      INCLUDE 'jeveux.h'
      CHARACTER*24 MODELE,CHARGE,INFCHA,CARELE,INST,CHTN,LVECHN

C 0.2. ==> COMMUNS

C 0.3. ==> VARIABLES LOCALES
      CHARACTER*1 C1
      CHARACTER*8 LPAIN(7),LPAOUT(1),K8BID,NEWNOM,NOMCHA,NOMCH2
      CHARACTER*16 OPTION
      CHARACTER*24 LIGRMO,LCHIN(7),LCHOUT(1),CHGEOM,CHCARA(18)
      INTEGER IRET,IFM,NIV,EXICHA,NCHIN,I,NCHAR,JCHAR,JINF,
     &        ICHA,IBID,JLVN
      LOGICAL EXICAR

C====
C 1.1 PREALABLES LIES AUX OPTIONS
C====
      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)

      DO 10 I = 1,6
        LPAIN(I) = '        '
        LCHIN(I) = '                        '
   10 CONTINUE
C====
C 1.2 PREALABLES LIES AUX CHARGES
C====

      NEWNOM = '.0000000'
      LIGRMO = MODELE(1:8)//'.MODELE'
C TEST D'EXISTENCE DE L'OBJET JEVEUX CHARGE
      CALL JEEXIN(CHARGE,IRET)
      IF (IRET.NE.0) THEN
C LECTURE DU NBRE DE CHARGE NCHAR DANS L'OBJET JEVEUX CHARGE
        CALL JELIRA(CHARGE,'LONMAX',NCHAR,K8BID)
C LECTURE DES ADRESSES JEVEUX DES CHARGES ET DES INFOS AFFERENTES
        CALL JEVEUO(CHARGE,'L',JCHAR)
        CALL JEVEUO(INFCHA,'L',JINF)
      ELSE
        NCHAR = 0
      END IF

C====
C 2.1 PREPARATION DES CALCULS ELEMENTAIRES
C====

      CALL MEGEOM(MODELE,CHGEOM)
      CALL MECARA(CARELE,EXICAR,CHCARA)
      CALL JEEXIN(LVECHN,IRET)
      IF (IRET.EQ.0) THEN
        CALL WKVECT(LVECHN,'V V K24',2*NCHAR,JLVN)
        CALL JEECRA(LVECHN,'LONUTI',0,K8BID)
      END IF
C
C PAS DE CHARGE ==> EXIT
      IF (NCHAR.EQ.0) GO TO 70

C CHAMP LOCAL RESULTAT
      LPAOUT(1) = 'PVECTTR'
      LCHOUT(1) = '&&VECHNL.???????'
C ... LA CARTE DES NOEUDS (X Y Z)
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM
C ... LA CARTE DES INSTANTS (INST DELTAT THETA KHI  R RHO)
      LPAIN(3) = 'PTEMPSR'
      LCHIN(3) = INST
C ... LE CHAM_NO T- OU (DT/DS)-
      LPAIN(4) = 'PTEMPER'
      LCHIN(4) = CHTN

C====
C 2.2 IMPRESSIONS NIVEAU 2 POUR DIAGNOSTICS...
C====

      IF (NIV.EQ.2) THEN
        WRITE (IFM,*) '*******************************************'
        WRITE (IFM,*) ' CALCUL DE SECOND MEMBRE THERMIQUE: VECHNL'
        WRITE (IFM,*)
        WRITE (IFM,*) ' LIGREL/MODELE    :',LIGRMO
        WRITE (IFM,*) ' OBJ JEVEUX CHARGE:',CHARGE
        WRITE (IFM,*) '            INFOCH:',INFCHA
        WRITE (IFM,*) ' NBRE DE CHARGES  :',NCHAR
        WRITE (IFM,*) ' BOUCLE SUR LES CHARGES DE TYPE NEUMANN NON-LIN'
      END IF

C====
C 3. BOUCLE SUR LES AFFE_CHAR_THER ==================================
C====

      IF (NCHAR.GT.0) THEN
        DO 60 ICHA = 1,NCHAR

C NOM DE LA CHARGE
          NOMCHA = ZK24(JCHAR+ICHA-1) (1:8)
          IF (NIV.EQ.2) THEN
            WRITE (IFM,*) ' '
            WRITE (IFM,*) '   CHARGE         :',NOMCHA
          END IF

C ON CONSTRUIT LES SECONDS MEMBRES ELEM
          EXICHA = 0
          NOMCH2 = NOMCHA

          IF (EXICHA.EQ.0) THEN

C====
C 3.2 TEST D'EXISTENCE DE LA CL DE FLUX NON-LINEAIRE
C====
            IRET = 0
            LPAIN(2) = 'PFLUXNL'
            LCHIN(2) = NOMCH2(1:8)//'.CHTH.FLUNL.DESC'
            CALL JEEXIN(LCHIN(2),IRET)

            IF (IRET.NE.0) THEN
              OPTION = 'CHAR_THER_FLUNL'
C CALCUL DU SECOND MEMBRE DU PB STD
              NCHIN = 4
C====
C 3.2.1 PRETRAITEMENTS POUR TENIR COMPTE DE FONC_MULT
C====
              CALL GCNCO2(NEWNOM)
              LCHOUT(1) (10:16) = NEWNOM(2:8)
              CALL CORICH('E',LCHOUT(1),-1,IBID)

C====
C 3.2.2 LANCEMENT DES CALCULS ELEMENTAIRES
C====
              IF (NIV.EQ.2) THEN
                WRITE (IFM,*) '     OPTION         :',OPTION
                DO 20 I = 1,NCHIN
                  WRITE (IFM,*) '     LPAIN/LCHIN    :',LPAIN(I),' ',
     &              LCHIN(I)
   20           CONTINUE
              END IF
              CALL CALCUL('S',OPTION,LIGRMO,NCHIN,LCHIN,LPAIN,1,LCHOUT,
     &                    LPAOUT,'V','OUI')

C INCREMENTATION DE LONUTI ET STOCKAGE DU RESULTAT
              CALL REAJRE(LVECHN,LCHOUT(1),'V')
C FIN DU IF IRET POUR FLUX_NL
            END IF

C====
C 3.3 TEST D'EXISTENCE DE LA CL DE RAYONNEMENT
C====
            IRET = 0
            LPAIN(2) = 'PRAYONF'
            LCHIN(2) = NOMCH2(1:8)//'.CHTH.RAYO .DESC'
            CALL JEEXIN(LCHIN(2),IRET)

            IF (IRET.NE.0) THEN
C CALCUL DU SECOND MEMBRE
              NCHIN = 4
              C1 = 'R'
              IF (ZI(JINF+NCHAR+ICHA).GT.1) C1 = 'F'
              OPTION = 'CHAR_THER_RAYO_'//C1
              LPAIN(2) = 'PRAYON'//C1
              LCHIN(2) = NOMCH2(1:8)//'.CHTH.RAYO'

C====
C 3.3.1 PRETRAITEMENTS POUR TENIR COMPTE DE FONC_MULT
C====
              CALL GCNCO2(NEWNOM)
              LCHOUT(1) (10:16) = NEWNOM(2:8)
              CALL CORICH('E',LCHOUT(1),-1,IBID)

C====
C 3.3.2 LANCEMENT DES CALCULS ELEMENTAIRES
C====
              IF (NIV.EQ.2) THEN
                WRITE (IFM,*) '     OPTION         :',OPTION
                DO 30 I = 1,NCHIN
                  WRITE (IFM,*) '     LPAIN/LCHIN    :',LPAIN(I),' ',
     &              LCHIN(I)
   30           CONTINUE
              END IF
              CALL CALCUL('S',OPTION,LIGRMO,NCHIN,LCHIN,LPAIN,1,LCHOUT,
     &                    LPAOUT,'V','OUI')

C INCREMENTATION DE LONUTI ET STOCKAGE DU RESULTAT
              CALL REAJRE(LVECHN,LCHOUT(1),'V')
C FIN DU IF IRET POUR RAYONNEMENT
            END IF


C====
C 3.4 TEST D'EXISTENCE DE LA CHARGE DE SOURCE NON LINEAIRE
C====
            IRET = 0
            LPAIN(2) = 'PSOURNL'
            LCHIN(2) = NOMCH2(1:8)//'.CHTH.SOUNL.DESC'
            CALL JEEXIN(LCHIN(2),IRET)

            IF (IRET.NE.0) THEN
              OPTION = 'CHAR_THER_SOURNL'
C CALCUL DU SECOND MEMBRE DU PB STD
              NCHIN = 4
C====
C 3.4.1 PRETRAITEMENTS POUR TENIR COMPTE DE FONC_MULT
C====
              CALL GCNCO2(NEWNOM)
              LCHOUT(1) (10:16) = NEWNOM(2:8)
              CALL CORICH('E',LCHOUT(1),-1,IBID)

C====
C 3.4.2 LANCEMENT DES CALCULS ELEMENTAIRES
C====
              IF (NIV.EQ.2) THEN
                WRITE (IFM,*) '     OPTION         :',OPTION
                DO 25 I = 1,NCHIN
                  WRITE (IFM,*) '     LPAIN/LCHIN    :',LPAIN(I),' ',
     &              LCHIN(I)
   25           CONTINUE
              END IF
              CALL CALCUL('S',OPTION,LIGRMO,NCHIN,LCHIN,LPAIN,1,LCHOUT,
     &                    LPAOUT,'V','OUI')

C INCREMENTATION DE LONUTI ET STOCKAGE DU RESULTAT
              CALL REAJRE(LVECHN,LCHOUT(1),'V')
C FIN DU IF IRET POUR SOUR_NL
            END IF

C FIN DU IF EXICHA
          END IF

C 5. FIN DE BOUCLE SUR LES CHARGES
C====
   60   CONTINUE
C FIN DU IF NCHAR
      END IF

C SORTIE DE SECOURS EN CAS D'ABSENCE DE CHARGE
   70 CONTINUE
C FIN ------------------------------------------------------------------
      CALL JEDEMA()
      END
