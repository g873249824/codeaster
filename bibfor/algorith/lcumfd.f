      SUBROUTINE LCUMFD(VARI,NVARI,NSTRS,CMAT,NMAT,
     &                  IFLU,TDT,HINI,HFIN,
     &                  AFPD,BFPD,CFPD)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 24/03/2009   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE YLEPAPE Y.LEPAPE
C
C ROUTINE APPELE DANS LCUMSD
C LCUMFD     SOURCE    BENBOU   01/03/26
C_______________________________________________________________________
C
C ROUTINE QUI CALCUL LES MATRICES DE DEFORMATION
C   DE FLUAGE PROPRE DEVIATORIQUE
C   D APRES LE MODELE UMLV
C
C     IFLU = O => SORTIE DES VECTEURS TOTAUX : EQUATION (3.4-8)
C     IFLU = 1 => SORTIE DES VECTEURS REVERSIBLES : EQUATION (3.4-9)
C     IFLU = 2 => SORTIE DES VECTEURS IRREVERSIBLES : EQUATION (3.4-10)
C
C
C   DFPRODEV(N+1) = AFPD + BFPD * SIGMA(N) + CFPD * SIGMA(N+1)
C
C        => EQUATION (3.4-1)
C
C IN  VARI     : VARIABLES INTERNES INITIALES
C IN  NVARI    : DIMENSION DES VECTEURS VARIABLES INTERNES
C IN  NSTRS    : DIMENSION DES VECTEURS CONTRAINTE ET DEFORMATION
C IN  CMAT     : VECTEUR DE PARAMETRES (MATERIAU ET AUTRE)
C IN  NMAT     : DIMENSION DE CMAT
C IN  IFLU     : CF COMMENTAIRES CI-DESSUS
C IN  TDT      : PAS DE TEMPS
C IN  HINI     : HUMIDITE INITIALE
C IN  HFIN     : HUMIDITE FINALE
C OUT AFPD     : CF. EQUATION CI-DESSUS
C OUT BFPD     : IDEM
C OUT CFPD     : IDEM
C_______________________________________________________________________
C
C      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT NONE
      INTEGER I,IFLU,IFPO,NSTRS,NVARI,NMAT
      REAL*8 VARI(NVARI),CMAT(NMAT)
      REAL*8 AFPD(6)
      REAL*8 EPSDVR(6),EPSDVI(6)
      REAL*8 ADI,ADR,BDI,BDR,BFPD,CDI,CDR,CFPD
      REAL*8 HFIN,HINI,HVFIN,HVINI,RRDV,TDEV         
      REAL*8 TDEXP,TDT,VIDV,VRDV
C
C RECUPERATION DES VALEURS DES PARAMETRES MATERIAU
C
      RRDV = CMAT(7) 
      VRDV = CMAT(8) 
      VIDV = CMAT(9) 
      IFPO = NINT(CMAT(13))
C
      HVINI = HINI
      HVFIN = HFIN
C
C TEST SI L HUMIDITE RELATIVE DOIT ETRE PRISE EN COMPTE OU NON
C
      IF (IFPO.EQ.1) THEN
        HINI = 1.D0
        HFIN = 1.D0
      ENDIF
C
C TEST SI TDT = 0
C
      IF (TDT.EQ.0.D0) THEN
        DO 11 I=1,NSTRS
          AFPD(I)    = 0.D0
11      CONTINUE
        BFPD = 0.D0
        CFPD = 0.D0
        GOTO 20
      ENDIF
C
C RECUPERATION DES VARIABLES INTERNES INITIALES
C
C    REMPLISSAGE DES VECTEURS DE DEFORMATION DE FLUAGE DEVIATOIRE
C
C         REVERSIBLE ET IRREVERSIBLE
C
      EPSDVR(1) = VARI(3)
      EPSDVI(1) = VARI(4)
      EPSDVR(2) = VARI(5)
      EPSDVI(2) = VARI(6)
      EPSDVR(3) = VARI(7)
      EPSDVI(3) = VARI(8)
      EPSDVR(4) = VARI(12)
      EPSDVI(4) = VARI(13)
      EPSDVR(5) = VARI(14)
      EPSDVI(5) = VARI(15)
      EPSDVR(6) = VARI(16)
      EPSDVI(6) = VARI(17)
C
C CONSTRUCTION DE LA MATRICE DEVIATOIRE REVERSIBLE
C
C  MODELE => EQUATION (3.4-2)
C  DISCRETISATION => EQUATION (3.4-3)
C
C  VALEUR DES PARAMETRES => EQUATION (3.4-4)
C
      TDEV = VRDV / RRDV
      TDEXP = EXP(-TDT/TDEV)
      ADR = TDEXP - 1.D0
      BDR = 1.D0/RRDV*( TDEXP*(-HINI*(2*TDEV/TDT+1.D0) 
     $      + HFIN*TDEV/TDT)
     $      + HINI*(2.D0*(TDEV-TDT)/TDT+1.D0) 
     $      - HFIN*(TDEV-TDT)/TDT )
      CDR = HINI/(TDT*RRDV)*( TDEV*TDEXP - (TDEV - TDT) )
C
C CONSTRUCTION DE LA MATRICE DEVIATOIRE IRREVERSIBLE
C
C  MODELE => EQUATION (3.4-5)
C  DISCRETISATION => EQUATION (3.4-6)
C
C  VALEUR DES PARAMETRES => EQUATION (3.4-7)
C
      ADI = 0.D0
      BDI = HFIN*TDT/(2.D0*VIDV)
      CDI = HINI*TDT/(2.D0*VIDV)
C
C SAUVEGARDE DES VALEURS SUIVANT LA VALEUR DE IFLU
C
C
C        CONSTRUTION DE LA MATRICE TOTALE DEVIATOIRE
C
C
C  CONSTRUCTION DE LA MATRICE TOTAL :
C
C  * PARTIE REVERSIBLE   : ADR  BDR  CDR  (IFLU = 0)
C          => EQUATION (3.4-8)
C             +
C  * PARTIE IRREVERSIBLE : ADI  BDI  CDI  (IFLU = 1)
C          => EQUATION (3.4-9)
C             =
C  * TOTAL               : AFPD BFPD CFPD (IFLU = 2)
C          => EQUATION (3.4-10)
C
      DO 10 I=1,6
        IF (IFLU.EQ.0) THEN
C
          AFPD(I) = ADR * EPSDVR(I) + ADI * EPSDVI(I)
          BFPD    = BDR + BDI
          CFPD    = CDR + CDI
C
        ELSEIF (IFLU.EQ.1) THEN
C
          AFPD(I) = ADR * EPSDVR(I)
          BFPD    = BDR
          CFPD    = CDR
C
        ELSEIF (IFLU.EQ.2) THEN
C
          AFPD(I) = ADI * EPSDVI(I)
          BFPD    = BDI
          CFPD    = CDI
        ENDIF
C
  10  CONTINUE
C
  20  CONTINUE
C
      HINI = HVINI
      HFIN = HVFIN
C
      END
