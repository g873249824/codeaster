      SUBROUTINE RADIAL(NBSIG,SIGM,SIGP,INDM,INDP,ICINE,XM,XP,NORMDN)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/01/2012   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C     BUT:
C       CALCUL DE L'INDICATEUR LOCAL DE RADIALITE 
C       NORME DE LA DIFFERENCE DES NORMALES
C
C     ARGUMENTS:
C     ----------
C
C      ENTREE :
C-------------
C IN   NBSIG    : CDIMENSION DES VECTEURS CONTRAINTES
C IN   SIGM     : CONTRAINTES INSTANT + AVEC RAC2
C IN   SIGP     : CONTRAINTES INSTANT - AVEC RAC2
C IN   INDM     : INDICATEUR PLASTICITE INSTANT -
C IN   INDP     : INDICATEUR PLASTICITE INSTANT +
C IN   ICINE    : INDICATEUR ECROUISSAGE CINAMATIQUE
C IN   XM       : VARIABLES INTERNES X INSTANT - AVEC RAC2
C IN   XP       : VARIABLES INTERNES X INSTANT + AVEC RAC2
C
C      SORTIE :
C-------------
C OUT  NORMDN   : INDICATEUR DE PERTE DE RADIALITE=NORME DE DN
C
C ......................................................................
      IMPLICIT NONE
      INTEGER NBSIG,ICINE,I
      REAL*8 N1(6),N2(6),XM(6),XP(6),INDM,INDP
      REAL*8 TENSM(6),TENSP(6),NORMDN,SIGM(NBSIG),SIGP(NBSIG)
      REAL*8 ZERNOR,R8PREM,DEVM(6),DEVP(6),SMEQ,SPEQ
      REAL*8 DN1N2(6),NDN,PRECI,DDOT,TRT1,TRT2
C ......................................................................

      IF ((NINT(INDM).GT.0.D0). AND.(NINT(INDP).GT.0.D0)) THEN
      
         CALL DCOPY(NBSIG,SIGM,1,TENSM,1)
         CALL DCOPY(NBSIG,SIGP,1,TENSP,1)

         ZERNOR = R8PREM()
         IF (ICINE.EQ.1) THEN
           
            CALL DAXPY(NBSIG,-1.D0,XM,1,TENSM,1)
            CALL DAXPY(NBSIG,-1.D0,XP,1,TENSP,1)

         ENDIF
         
C        PART HYDROSTATIQUE DES CONTRAINTES
         TRT1 = ( TENSM(1)+TENSM(2)+TENSM(3))/3.D0
         TRT2 = ( TENSP(1)+TENSP(2)+TENSP(3))/3.D0
C        PART DEVIATORIQUE DES CONTRAINTES

         CALL DCOPY(NBSIG,TENSM,1,DEVM,1)
         CALL DCOPY(NBSIG,TENSP,1,DEVP,1)
         DO 10 I=1,3
            DEVM(I)=DEVM(I)-TRT1
            DEVP(I)=DEVP(I)-TRT2
10       CONTINUE
C         CALL DSCAL(NBSIG-3,SQRT(2.D0),DEVM(4),1)
C         CALL DSCAL(NBSIG-3,SQRT(2.D0),DEVP(4),1)
         SMEQ=SQRT(DDOT(NBSIG,DEVM,1,DEVM,1))
         SPEQ=SQRT(DDOT(NBSIG,DEVP,1,DEVP,1))
         PRECI=1.D3*ZERNOR*MAX(SMEQ,SPEQ)
 
C ----      DANS LE CAS OU NORME(TENSM) = 0  OU NORME(DTENSMA) = 0 :
C ----      ON MET L'INDICATEUR A 0 :
C           -----------------------
         IF (SMEQ.LE.PRECI .OR. SPEQ.LE.PRECI) THEN
           NORMDN  = 0.D0
         ELSE
           CALL DCOPY(NBSIG,DEVM,1,N1,1)
           CALL DCOPY(NBSIG,DEVP,1,N2,1)
           CALL DSCAL(NBSIG,1.D0/SMEQ,N1,1)
           CALL DSCAL(NBSIG,1.D0/SPEQ,N2,1)
           CALL DCOPY(NBSIG,N1,1,DN1N2,1)
 
C          CALCUL DE LA DIFFERENCE N1 - N2

           CALL DAXPY(NBSIG,-1.D0,N2,1,DN1N2,1)
           NDN=SQRT(DDOT(NBSIG,DN1N2,1,DN1N2,1))
           NORMDN  = NDN/2.D0
         END IF
         
      ELSE
C        PAS D'INDICATEUR. ON NE FAIT RIEN      
         NORMDN = 0.D0
         
      END IF
      
      END
