      SUBROUTINE  REFE81 (NOMRES,BASMOD,RAIDF,MASSF,AMORF,MAILLA,OPT)
      IMPLICIT REAL*8 (A-H,O-Z)
C MODIF ALGORITH  DATE 12/09/2005   AUTEUR NICOLAS O.NICOLAS 
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C***********************************************************************
C  P. RICHARD     DATE 13/07/90
C-----------------------------------------------------------------------
C  BUT : < CREATION DU REFE ET DU DESC POUR OP0081 >
C
C        - RECUPERER LES NOMS UTILISATEUR DES CONCEPTS ASSOCIES AUX
C          MATRICES ASSEMBLEES ET BASE MODALE CONSIDEREES
C        - EFFECTUER QUELQUES CONTROLES ET DETERMINER
C          OPTION DE CALCUL MATRICES PROJETEES
C-----------------------------------------------------------------------
C
C NOMRES /I/ : NOM UTILISATEUR DU RESULTAT
C BASMOD /O/ : NOM UT DE LA BASE MODALE DE PROJECTION
C RAIDF  /O/ : NOM UT DE LA MATRICE RAIDEUR A PROJETER
C MASSEF /O/ : NOM UT DE LA MATRICE DE MASSE A PROJETER
C AMORF  /O/ : NOM UT DE LA MATRICE D'AMORTISSEMENT A PROJETER
C MAILLA /O/ : NOM UT DU MAILLAGE EN AMONT
C OPT    /O/ : K9 OPTION DE CALCUL MATRICES PROJETEES
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C----------  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      CHARACTER*6  PGC
      CHARACTER*8  NOMRES,MAILLA,BASMOD,MAILLB,AMOR,BLANC,LINTF,K8BID
      CHARACTER*9  CAL,ORT,OPT
      CHARACTER*14 NUMDDL,NUMBIS,NUMTER
      CHARACTER*19 RAID,RAIDB,MASS,MASSB,MASSF,RAIDF,AMORF,AMORB
C
C-----------------------------------------------------------------------
      DATA PGC           /'REFE81'/
      DATA BLANC,CAL,ORT /'        ','RITZ','CLASSIQUE'/
C-----------------------------------------------------------------------
C
C --- RECUPERATION EVENTUELLE MATRICE RAIDEUR EN ARGUMENT
C
      CALL JEMARQ()
      CALL GETVID(BLANC,'MATR_RIGI',1,1,0,K8BID,IOC)
      IOC=-IOC
      IF(IOC.EQ.0) THEN
        RAID=BLANC
      ELSEIF(IOC.EQ.1) THEN
        CALL GETVID(BLANC,'MATR_RIGI',1,1,1,RAID,L)
      ELSE
        CALL UTDEBM('F',PGC,'ARRET SUR MATRICE RAIDEUR NON UNIQUE')
        CALL UTFINM
      ENDIF
C
C --- RECUPERATION EVENTUELLE MATRICE MASSE EN ARGUMENT
C
      CALL GETVID(BLANC,'MATR_MASS',1,1,0,K8BID,IOC)
      IOC=-IOC
      IF(IOC.EQ.0) THEN
        MASS=BLANC
      ELSEIF(IOC.EQ.1) THEN
        CALL GETVID(BLANC,'MATR_MASS',1,1,1,MASS,L)
      ELSE
        CALL UTDEBM('F',PGC,'ARRET SUR MATRICE MASSE NON UNIQUE')
        CALL UTFINM
      ENDIF
C
C --- RECUPERATION EVENTUELLE MATRICE AMORTISSEMENT EN ARGUMENT
C
      CALL GETVID(BLANC,'MATR_AMOR',1,1,0,K8BID,IOC)
      IOC=-IOC
      IF(IOC.EQ.0) THEN
        AMOR=BLANC
      ELSEIF(IOC.EQ.1) THEN
        CALL GETVID(BLANC,'MATR_AMOR',1,1,1,AMOR,L)
      ELSE
        CALL UTDEBM('F',PGC,
     &     'ARRET SUR MATRICE AMORTISSEMENT NON UNIQUE EN ARGUMENT')
        CALL UTFINM
      ENDIF
C
C --- RECUPERATION BASE MODALE OBLIGATOIRE
C
      RAIDB=BLANC
      MASSB=BLANC
      AMORB=BLANC
      CALL GETVID(BLANC,'BASE_MODALE',1,1,1,BASMOD,NBVAL)
      CALL JEVEUO(BASMOD//'           .REFD','L',LLREFB)
      RAIDB=ZK24(LLREFB)(1:8)
      MASSB=ZK24(LLREFB+1)(1:8)
      AMORB=ZK24(LLREFB+2)(1:8)
      NUMDDL=ZK24(LLREFB+3)(1:14)
      LINTF=ZK24(LLREFB+4)(1:8)
C
C
C --- RECUPERATION MAILLAGE
C
      IF (LINTF.NE.BLANC) THEN
        CALL JEVEUO(LINTF//'.IDC_REFE','L',LLREF)
        MAILLA=ZK24(LLREF)
      ELSE
        CALL DISMOI('F','NOM_MAILLA',NUMDDL,'NUME_DDL',IBID,
     &   MAILLA,IRET)
      ENDIF
C
C --- RECUPERATION DU TYPE DE BASE MODALE
C
      CALL JEVEUO(BASMOD//'           .UTIL','L',LLDESC)
      ITYBA=ZI(LLDESC)
C
C --- CAS DE LA DONNEE DES MATRICE DE LA BASE
C
      IF(MASS.EQ.MASSB) MASS=BLANC
      IF(RAID.EQ.RAIDB) RAID=BLANC
      IF(AMOR.EQ.AMORB) AMOR=BLANC
C
C --- RECUPERATION DU TYPE DE CALCUL DE MACRO ELEMENT DESIRE
C
      CALL GETVTX('     ','OPTION',1,1,1,OPT,L)
C
C --- CAS BASE MODALE CLASSIQUE
C
      IF((ITYBA.EQ.1).OR.(ITYBA.EQ.4)) THEN
C
C --- CAS DE LA DONNEE DE MASSE ET OPTION CLASSIQUE (INCOMPATIBLES)
C
        IF(OPT.EQ.ORT.AND.MASS.NE.BLANC) THEN
            CALL UTDEBM('A',PGC,
     &'DONNEE DE MATRICE MASSE ET OPTION CLASSIQUE INCOMPATIBLES')
            CALL UTIMPK('L','MATRICE MASSE:',1,MASS)
            CALL UTIMPK('L','OPTION AUTOMATIQUEMENT MISE A',1,CAL)
            CALL UTFINM
            OPT=CAL
        ENDIF
C
C --- CAS DE LA DONNEE DE RAIDEUR ET OPTION CLASSIQUE (INCOMPATIBLES)
C
        IF(OPT.EQ.ORT.AND.RAID.NE.BLANC) THEN
            CALL UTDEBM('A',PGC,
     &'DONNEE DE MATRICE RAIDEUR ET OPTION CLASSIQUE INCOMPATIBLES')
            CALL UTIMPK('L','MATRICE RAIDEUR:',1,RAID)
            CALL UTIMPK('L','OPTION AUTOMATIQUEMENT MISE A',1,CAL)
            CALL UTFINM
        ENDIF
C
C --- TRAITEMENT DU NOM FINAL MASSE ET RAIDEUR
C
        IF(MASS.NE.BLANC)THEN
          MASSF=MASS
        ELSE
          MASSF=MASSB
        ENDIF
        IF(RAID.NE.BLANC)THEN
          RAIDF=RAID
        ELSE
          RAIDF=RAIDB
        ENDIF
        IF(AMOR.NE.BLANC)THEN
          AMORF=AMOR
        ELSE
          AMORF=AMORB
        ENDIF
C          AMORF=AMOR
      ENDIF
C
C --- CAS BASE MODALE RITZ
C
      IF(ITYBA.EQ.3) THEN
        IF(OPT.EQ.ORT) THEN
            CALL UTDEBM('A',PGC,
     &'BASE MODALE RITZ ET OPTION CLASSIQUE INCOMPATIBLES')
            CALL UTIMPK('L','OPTION AUTOMATIQUEMENT MISE A',1,CAL)
            CALL UTFINM
            OPT=CAL
        ENDIF
        IF(MASS.EQ.BLANC.OR.RAID.EQ.BLANC) THEN
            CALL UTDEBM('F',PGC,
     &'BASE MODALE RITZ: IL FAUT DONNER LES MATRICE A PROJETER')
            CALL UTIMPK('L','MATRICE MASSE DONNEE:',1,MASS)
            CALL UTIMPK('L','MATRICE RAIDEUR DONNEE:',1,RAID)
            CALL UTFINM
        ENDIF
C
C --- TRAITEMENT DU NOM FINAL MASSE ET RAIDEUR
C
        MASSF=MASS
        RAIDF=RAID
        AMORF=AMOR
      ENDIF
C
C --- TRAITEMENT COHERENCE MATRICE ASSEMBLEES
C     SI MASSF ET RAIDF NON BLANC
C
C
      CALL DISMOI('F','NOM_NUME_DDL',RAIDF,'MATR_ASSE',
     &IBID,NUMDDL,IRET)
C
      CALL DISMOI('F','NOM_MAILLA',NUMDDL,'NUME_DDL',IBID,
     &MAILLB,IRET)
C
      CALL DISMOI('F','NOM_NUME_DDL',MASSF,'MATR_ASSE',IBID,
     &NUMBIS,IRET)
C
      IF (AMORF.NE.BLANC) THEN
         CALL DISMOI('F','NOM_NUME_DDL',AMORF,'MATR_ASSE',IBID,
     &               NUMTER,IRET)
      ENDIF
C
C --- CONTROLE DE LA COHERENCE DES MATRICES ASSEMBLEES
C
      IF(NUMDDL.NE.NUMBIS) THEN
         CALL UTDEBM('F',PGC,
     &  'LES MATRICES ASSEMBLEES N''ONT PAS LA MEME NUMEROTATION')
         CALL UTIMPK('L','MASSE=',1,MASS)
         CALL UTIMPK('L','RAIDEUR=',1,RAID)
         CALL UTFINM
      ENDIF
C
      IF((AMOR.NE.BLANC).AND.(NUMDDL.NE.NUMTER)) THEN
         CALL UTDEBM('F',PGC,
     &  'LES MATRICES ASSEMBLEES N''ONT PAS LA MEME NUMEROTATION')
         CALL UTIMPK('L','AMORTISSEMENT=',1,AMOR)
         CALL UTIMPK('L','RAIDEUR=',1,RAID)
         CALL UTFINM
      ENDIF
C
      IF(MAILLA.NE.MAILLB) THEN
        CALL UTDEBM('F',PGC,'LES MATRICES ASSEMBLEES ET LA BASE MODALE
     &  N''ONT PAS LE MEME MAILLAGE INITIAL')
        CALL UTIMPK('L','MAILLAGE MATRICE:',1,MAILLB)
        CALL UTIMPK('L','MAILLAGE BASE MODALE:',1,MAILLA)
        CALL UTFINM
      ENDIF
C
C --- REMPLISSAGE DU .REFE
C
      CALL WKVECT(NOMRES//'.MAEL_REFE','G V K24',2,IADREF)
      ZK24(IADREF)=BASMOD
      ZK24(IADREF+1)=MAILLA
C
C --- REMPLISSAGE DU .DESC
C
      CALL WKVECT(NOMRES//'.MAEL_DESC','G V I',3,LDDESC)
      IF (LINTF.NE.BLANC) THEN
       CALL JEVEUO(LINTF//'.IDC_DESC','L',LLDESC)
       ZI(LDDESC)=ZI(LLDESC+1)
       ZI(LDDESC+1)=ZI(LLDESC+2)
       ZI(LDDESC+2)=ZI(LLDESC+3)
      ENDIF
C
 9999 CONTINUE
      CALL JEDEMA()
      END
