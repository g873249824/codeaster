      SUBROUTINE XDECQU(NNOSE,IT,NDIM,CNSET,JLSN,JGRLSN,IGEOM,PINTER,
     &               NINTER,NPTS,AINTER,PMILIE,NMILIE,MFIS)
      IMPLICIT NONE

      INCLUDE 'jeveux.h'
      INTEGER       NNOSE,IT,NDIM,CNSET(*),NINTER,IGEOM,NPTS,NMILIE,MFIS
      INTEGER       JGRLSN,JLSN
      REAL*8        PINTER(*),AINTER(*),PMILIE(*)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 04/03/2013   AUTEUR MARTIN A.MARTIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRS_1404
C                      TROUVER LES PTS D'INTERSECTION ENTRE LES ARETES
C                      ET LE PLAN DE FISSURE
C
C     ENTREE
C       NNOSE    : NOMBRE DE NOEUDS DU SOUS TETRA
C       IT       : INDICE DU TETRA EN COURS
C       CNSET    : CONNECTIVITÉ DES NOEUDS DU TETRA
C       LSN      : VALEURS DE LA LEVEL SET NORMALE
C       IGEOM    : ADRESSE DES COORDONNÉES DES NOEUDS DE L'ELT PARENT
C       JGRLSN   : VALEURS DU GRADIENT DE LA LEVEL SET NORMALE
C
C     SORTIE
C       PINTER   : COORDONNÉES DES POINTS D'INTERSECTION
C       NINTER   : NB DE POINTS D'INTERSECTION
C       NPTS     : NB DE PTS D'INTERSECTION COINCIDANT AVEC UN
C                  NOEUD SOMMET
C       AINTER   : INFOS ARETE ASSOCIÉE AU POINTS D'INTERSECTION
C       PMILIE   : COORDONNEES DES POINTS MILIEUX
C       NMILIE   : NB DE POINTS MILIEUX
C       MFIS     : NB DE POINTS MILIEUX SUR LA FISSURE
C     ----------------------------------------------------------------
C
      REAL*8          A(NDIM),B(NDIM),C(NDIM),M(NDIM),LSNA,LSNB,LSNM
      REAL*8          ALPHA,PADIST,LONGAR,LONREF,TAMPOR(4),TABCO(81)
      REAL*8          MILFI(NDIM),MILAC(NDIM),MILARA(NDIM),MILARB(NDIM)
      REAL*8          TEMP(NDIM),X(NDIM),XLSN,D,CRILSN,R8PREM,VAL,RBID
      REAL*8          TABLS(27),TABAR(9)
      INTEGER         AR(12,3),NBAR,NTA,NTB,NA,NB,INS
      INTEGER         IA,I,IPI,IPM,IBID,PP,PD,K,TABDIR(4)
      INTEGER         NDIME
      INTEGER         J,R,IP,IPP,A1,A2,NX
      INTEGER         PTMAX,PMMAX(2)
      INTEGER         NTM,NM,INM,NPTM,NNOP
      INTEGER         ZXAIN,XXMMVD
      CHARACTER*8     TYPMA,ELREFE,ELRESE(2),TYMASE(2),ELREFP
      LOGICAL         CUT

      PARAMETER       (CRILSN=1.D-4)
      PARAMETER       (PTMAX=3)
      DATA            ELRESE /'SE3','TR6'/
      DATA            TYMASE /'SEG3','TRIA6'/
      DATA            PMMAX  / 2,  6 /
C --------------------------------------------------------------------

      CALL JEMARQ()

      ZXAIN = XXMMVD('ZXAIN')
      CALL ELREF1(ELREFP)
      CALL ELREF4(' ','RIGI',NDIME,NNOP,IBID,IBID,IBID,IBID,IBID,IBID)

C     VECTEUR REEL A 4 COMPOSANTES, POUR CHAQUE PT D'INTER :
C     - NUMERO ARETE CORRESPONDANTE (0 SI C'EST UN NOEUD SOMMET)
C     - VRAI NUMERO NOEUD CORRESPONDANT (SERT QUE POUR NOEUD SOMMET)
C     - LONGUEUR DE L'ARETE
C     - POSITION DU PT SUR L'ARETE

      ELREFE=ELRESE(NDIME)
      TYPMA =TYMASE(NDIME)

C     CALCUL DES COORDONNEES ET LSN DU NOEUD 9
      IF (NNOP.EQ.8) CALL NDCENT(IGEOM,ZR(JLSN),X,XLSN)

      NX=0
C     TABLEAU DES COORDONNEES DES NOEUDS DE L'ELEMENT ENFANT
      DO 50 J=1,NNOSE
        DO 51 I=1,NDIM
          IF(CNSET(NNOSE*(IT-1)+J).EQ.9) THEN
            NX=J
            VAL=X(I)
          ELSE
            VAL=ZR(IGEOM-1+NDIM*(CNSET(NNOSE*(IT-1)+J)-1)+I)
          ENDIF
          TABCO(NDIM*(J-1)+I)=VAL
 51     CONTINUE
 50   CONTINUE

C     TABLEAU DES LSN DES NOEUDS DE L'ELEMENT ENFANT
      DO 40 J=1,NNOSE
        IF(CNSET(NNOSE*(IT-1)+J).EQ.9) THEN
          VAL=XLSN
        ELSE
          VAL=ZR(JLSN-1+CNSET(NNOSE*(IT-1)+J))
        ENDIF
        TABLS(J)=VAL
 40   CONTINUE

C     L'ELEMENT EST IL TRAVERSE PAR LA FISSURE?
      CUT=.FALSE.
      I=1
C     (1) RECHERCHE D'UN NOEUD PIVOT (LSN NON NULLE)
  1   CONTINUE
      IF (I.LT.NNOSE) THEN
        IF (I.EQ.NX.OR.TABLS(I).EQ.0.D0) THEN
          I=I+1
          GOTO 1
        END IF
      END IF
C     (2) PRODUIT DE CE PIVOT PAR LES AUTRES LSN      
      K=I+1
 30   CONTINUE
      IF (K.LE.NNOSE) THEN      
        IF (TABLS(I)*TABLS(K).LT.0.D0) THEN
          CUT=.TRUE.
        ELSE
          K=K+1
          GOTO 30
        END IF
      END IF

      CALL CONARE(TYPMA,AR,NBAR)
C     COMPTEUR DE POINT INTERSECTION = NOEUD SOMMET
      INS=0
C     COMPTEUR DE POINT INTERSECTION = NOEUD MILIEU
      INM=0
C     COMPTEUR DE POINT INTERSECTION = TOUS TYPES CONFONDUS
      IPI=0
C     LONGUEUR D'ARETE MAXIMALE DE L'ELEMENT (DU SE3 OU TR6)
      LONREF=0.D0

C     BOUCLE SUR LES ARETES POUR DETERMINER LES POINTS D'INTERSECTION
      DO 100 IA=1,NBAR

C       RECUPERATION NUM ENFANT DES NOEUDS DE L'ARETE
        NTA=AR(IA,1)
        NTB=AR(IA,2)
        NTM=AR(IA,3)

C       RECUPERATION NUM PARENT DES NOEUDS DE L'ARETE
        NA=CNSET(NNOSE*(IT-1)+NTA)
        NB=CNSET(NNOSE*(IT-1)+NTB)
        NM=CNSET(NNOSE*(IT-1)+NTM)

C       RECUPERATION LSN DES NOEUDS EXTREMITE DE L'ARETE
        LSNA=TABLS(NTA)
        LSNB=TABLS(NTB)
        LSNM=TABLS(NTM)

        CALL VECINI(NDIM,0.D0,A)
        CALL VECINI(NDIM,0.D0,B)
        CALL VECINI(NDIM,0.D0,M)

C       RECUPERATION COORDONNEES DES NOEUDS EXTREMITE DE L'ARETE
        DO 101 I=1,NDIM
          A(I)=TABCO(NDIM*(NTA-1)+I)
          B(I)=TABCO(NDIM*(NTB-1)+I)
          M(I)=TABCO(NDIM*(NTM-1)+I)
 101    CONTINUE

C     BLINDAGE PARTIEL : FISSURE RENTRANTE SUR UNE ARETE
        IF (LSNA*LSNM.LT.0 .AND. LSNB*LSNM.LT.0) THEN
          CALL U2MESS('F','XFEM_63')
        ENDIF

C       LONGUEUR DE L'ARETE
        LONGAR=PADIST(NDIM,A,B)

C DEBUT RECHERCHE COORDONNEES DES POINTS D'INTERSECTION
C UN SEUL POINT INTER NON NOEUD PAR ARETE!

C       SI LA FISSURE COUPE L'ARETE
        IF ((LSNA*LSNB).LE.0) THEN

C         SI LA FISSURE COUPE L'EXTREMITE A
          IF (LSNA.EQ.0) THEN
C           ON AJOUTE A LA LISTE LE POINT A
            CALL XAJPIN(NDIM,PINTER,PTMAX,IPI,INS,A,LONGAR,
     &                                      AINTER,0,NA,0.D0)
          END IF

C         SI LA FISSURE COUPE L'EXTREMITE B
          IF (LSNB.EQ.0) THEN
C           ON AJOUTE A LA LISTE LE POINT B
            CALL XAJPIN(NDIM,PINTER,PTMAX,IPI,INS,B,LONGAR,
     &                                    AINTER,0,NB,LONGAR)
          END IF

C         SI LA FISSURE COUPE LE MILIEU M
C         PETITE TOLERANCE SUR LSNM CAR VALEUR CALCULEE
          D=999.D0
          IF (ABS(LSNM-LSNB).GT.R8PREM() ) D=LSNM/(LSNM-LSNB)

          IF (ABS(LSNM-LSNB).LE.R8PREM().OR.ABS(D).LE.CRILSN) THEN
            ALPHA=PADIST(NDIM,A,M)

            IF (CUT) THEN
              CALL XAJPIN(NDIM,PINTER,PTMAX,IPI,INM,M,LONGAR,
     &                                     AINTER,IA,0,ALPHA)
            ELSEIF (.NOT.CUT) THEN
              CALL XAJPIN(NDIM,PINTER,PTMAX,IPI,INM,M,LONGAR,
     &                                     AINTER,0,NM,ALPHA)
            ENDIF
          END IF

C         SI LA FISSURE COUPE AILLEURS
          IF (LSNA.NE.0.AND.LSNB.NE.0.AND.LSNM.NE.0) THEN
C           INTERPOLATION DES COORDONNEES DE C
            CALL XINTAR(ELREFE,NDIM,IA,TABCO,TABLS,C)
C           POSITION DU PT D'INTERSECTION SUR L'ARETE
            ALPHA=PADIST(NDIM,A,C)
C           ON AJOUTE A LA LISTE LE POINT C
            CALL XAJPIN(NDIM,PINTER,PTMAX,IPI,IBID,C,LONGAR,
     &                                      AINTER,IA,0,ALPHA)
          END IF

        END IF

 100  CONTINUE

C       NB DE POINTS D'INTERSECTION
        NINTER=IPI
C       NB DE POINTS D'INTERSECTION = NOEUD SOMMET
        NPTS  =INS
C       NB DE POINTS D'INTERSECTION = NOEUD MILIEU
        NPTM  =INM

C     TRI DES POINTS D'INTERSECTION PAR ORDRE CROISSANT DES ARETES
      DO 200 PD=1,NINTER-1
         PP=PD
         DO 201 I=PP,NINTER
           IF (AINTER(ZXAIN*(I-1)+1).LT.AINTER(ZXAIN*(PP-1)+1))
     &       PP=I
 201     CONTINUE
         DO 202 K=1,4
          TAMPOR(K)=AINTER(ZXAIN*(PP-1)+K)
          AINTER(ZXAIN*(PP-1)+K)=AINTER(ZXAIN*(PD-1)+K)
          AINTER(ZXAIN*(PD-1)+K)=TAMPOR(K)
 202     CONTINUE
         DO 203 K=1,NDIM
           TAMPOR(K)=PINTER(NDIM*(PP-1)+K)
           PINTER(NDIM*(PP-1)+K)=PINTER(NDIM*(PD-1)+K)
           PINTER(NDIM*(PD-1)+K)=TAMPOR(K)
 203     CONTINUE
 200   CONTINUE

C DEBUT RECHERCHE COORDONNEES DES POINTS MILIEUX
      IF (.NOT.CUT) GOTO 999

C ON NE DEVRAIT AVOIR QUE
C       TR6    NINTER=2 NPTS=0         ---> NSE=3
C       TR6    NINTER=2 NPTS=1         ---> NSE=2
C       TR6    NINTER=3 NPTS=1 NPTM=1  ---> NSE=3
C       SE3    NINTER=1 NPTS=0         ---> NSE=2
      CALL XERFIS(NDIME,NINTER,NPTS,NPTM)

C     COMPTEUR DES POINTS INTERSECTION NON CONFONDUS AVEC ND SOMMET
      IP=0
C     COMPTEUR DE POINTS MILIEUX
      IPM=0
      INM=0
C     CALCUL D'UNE LONGUEUR CARACTERISTIQUE DE L'ELEMENT
      CALL LONCAR(NDIM,TYPMA,TABCO,LONREF)

      DO 300 R = 1, NINTER
        A2=NINT(AINTER(ZXAIN*(R-1)+1))
        IF (A2.NE.0) THEN
          IP = IP+1
          NM=AR(A2,3)
          IF (IP.EQ.1) THEN
            CALL INFOAR(NDIM,AR,A2,1,TABCO,TABLS,A,B,M,RBID,RBID,RBID)
          ELSEIF(IP.GT.1)THEN
            DO 301 I=1,2
              DO 302 J=1,2
                IF (AR(A2,I).EQ.AR(A1,J)) THEN
                  CALL INFOAR(NDIM,AR,A2,I,TABCO,TABLS,A,B,M,
     &                                            RBID,RBID,RBID)
                  TABDIR(1)=AR(A2,I)
                  TABDIR(2)=AR(A1,3-J)
                  TABDIR(3)=AR(A2,3-I)
                  TABDIR(4)=AR(6-(A1+A2),3)
                  IF (J.EQ.2) THEN
                    DO 304 K=1,NDIM
                      TEMP(K)=PMILIE((IP-2)*NDIM+K)
                      PMILIE((IP-2)*NDIM+K)=PMILIE((IP-1)*NDIM+K)
                      PMILIE((IP-1)*NDIM+K)=TEMP(K)
 304                CONTINUE
                  END IF
                END IF
 302          CONTINUE
 301        CONTINUE
          END IF

          CALL VECINI(NDIM,0.D0,MILARA)
          CALL VECINI(NDIM,0.D0,MILARB)
          IF(NM.EQ.NX) THEN
            DO 306 K=1,NDIM
              MILARA(K)=(PINTER(NDIM*(R-1)+K)+B(K))/2
              MILARB(K)=(PINTER(NDIM*(R-1)+K)+A(K))/2
 306        CONTINUE
          ELSE
C         TABLEAU DES COORDONNEES DES NOEUDS DE L'ARETE AB : B,A,E
            DO 307 I=1,NDIM
              TABAR(I)=B(I)
              TABAR(NDIM+I)=A(I)
              TABAR(2*NDIM+I)=M(I)
 307        CONTINUE
C           INTERPOLATION DES COORDONNEES DES POINTS MILIEUX MA ET MB
            CALL XMILAR(NDIM,PINTER,TABAR,R,MILARA,MILARB)
          END IF

C         STOCKAGE PMILIE
          CALL XAJPMI(PMILIE,PMMAX(NDIM),IPM,INM,MILARA,LONREF)
          CALL XAJPMI(PMILIE,PMMAX(NDIM),IPM,INM,MILARB,LONREF)

          IF (R.EQ.NINTER .AND. R.GT.1) THEN
            IPP=R-1
C           CALCUL DU POINT MILIEU DE 101-102
            CALL XMILFI(ELREFP,NDIM,NNOP,PINTER,IGEOM,JLSN,
     &                                              IPP,R,MILFI)
            MFIS=MFIS+1
C           STOCKAGE PMILIE
            CALL XAJPMI(PMILIE,PMMAX(NDIM),IPM,INM,MILFI,LONREF)
          END IF

          IF (IP.EQ.2) THEN
C           CALCUL DU POINT MILIEU DE 101-C
            CALL XMILAC(NDIM,IGEOM,PINTER,
     &               TABCO,TABDIR,JGRLSN,IP,R,PMILIE,MILAC)

C           STOCKAGE PMILIE
            CALL XAJPMI(PMILIE,PMMAX(NDIM),IPM,INM,MILAC,LONREF)
          END IF

          A1=A2

        END IF
 300  CONTINUE

C     NB DE POINTS MILIEUX
      NMILIE = IPM

 999  CONTINUE

      CALL JEDEMA()
      END
