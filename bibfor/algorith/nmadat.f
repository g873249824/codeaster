      SUBROUTINE NMADAT(SDDISC,NUMINS,NBITER,VALINC)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*19 VALINC(*)
      CHARACTER*19 SDDISC
      INTEGER      NUMINS,NBITER
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME)
C
C GESTION DE L'ADAPTATION DE PAS DE TEMPS
C   CALCUL DE DTPLUS
C
C ----------------------------------------------------------------------
C
C
C IN  SDDISC : SD DISCRETISATION TEMPORELLE
C IN  NUMINS : NUMERO D'INSTANT
C IN  NBITER : NOMBRE D'ITERATIONS DE NEWTON
C IN  VALINC : VARIABLE CHAPEAU POUR INCREMENTS VARIABLES
C
C
C
C
      INTEGER      IFM,NIV
      INTEGER      IBID,NADAPT,IADAPT,JDT
      CHARACTER*19 METLIS,MODETP,DTPLUS
      CHARACTER*8  K8BID
      REAL*8       R8BID,R8MAEM,DT,MIN,PASMIN,PASMAX,DIINST,DTM,JALON
      REAL*8       NEWINS,NEWDT,DELTAC
      REAL*8       INST,R8VIDE,PREC,VALR(2)
      REAL*8       INSFIN,INSREF
      LOGICAL      LADAP,DIADAP,UNCROK
      CHARACTER*24 TPSITE
      INTEGER      JITER
      INTEGER      NBINI,NMAX,INSPAS
      LOGICAL      COMPR8
      CHARACTER*24 TPSEXT
      INTEGER      JTPSEX
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- INITIALISATIONS
C
      DTPLUS = '&&NMADAP.DTPLUS'
C
C --- PARAMETRES
C
      CALL UTDIDT('L'   ,SDDISC,'LIST',IBID  ,'PAS_MINI',
     &            PASMIN,IBID  ,K8BID )
      CALL UTDIDT('L'   ,SDDISC,'LIST',IBID  ,'PAS_MAXI',
     &            PASMAX,IBID  ,K8BID )
      CALL UTDIDT('L'   ,SDDISC,'LIST',IBID  ,'METHODE' ,
     &            R8BID ,IBID  ,METLIS)
      CALL UTDIDT('L'   ,SDDISC,'LIST',IBID  ,'NBINST'  ,
     &            R8BID ,NBINI ,K8BID )
      CALL UTDIDT('L'   ,SDDISC,'LIST',IBID  ,'NB_PAS_MAXI',
     &            R8BID ,NMAX  ,K8BID )
C
C --- NOM SDS DE LA SDDISC
C
      TPSITE = SDDISC(1:19)//'.ITER'
C
C --- PRECISION SUR LES INSTANTS
C --- (LIEE A CELLE DE VAL_MIN DE PAS_MINI DANS DEFI_LIST_INST.CAPY)
C
      PREC   = 1.D-12
C
C --- ON NE FAIT DE L'ADAPTATION DE PAS DE TEMPS QU'EN GESTION AUTO
C
      IF (METLIS.EQ.'MANUEL') THEN
        GOTO 9999
      ENDIF
C
C --- INSTANT COURANT
C
      INST   = DIINST(SDDISC,NUMINS)
C
C --- PROCHAIN INSTANT DE PASSAGE OBLIGATOIRE (JALON) ?
C
      CALL NMJALO(SDDISC,INST  ,PREC  ,JALON )
      IF (JALON.EQ.R8VIDE()) GOTO 9999
C
C --- NOMBRE DE SCHEMAS D'ADAPTATION : NADAPT
C
      CALL UTDIDT('L'   ,SDDISC,'LIST',IBID  ,'NADAPT',
     &            R8BID ,NADAPT,K8BID )
C
C --- LISTE DES NADAPT PAS DE TEMPS POSSIBLES
C
      CALL WKVECT(DTPLUS,'V V R',NADAPT,JDT)
C
C --- PAS DE TEMPS PAR DEFAUT (LE DERNIER, SAUF SI JALON) : DTM
C
      CALL UTDIDT('L'   ,SDDISC,'LIST',IBID  ,'DT-',
     &            DTM   ,IBID  ,K8BID )
C
C --- STOCKAGE DU NOMBRE D'ITERATIONS DE NEWTON ET EXTENSION
C
      CALL JEVEUO(TPSITE,'L',JITER)
      ZI(JITER-1+NUMINS) = NBITER
      CALL JUVECA(TPSITE,NBINI+1)
C
C ----------------------------------------------------------------------
C    CALCUL DU PAS DE TEMPS
C ----------------------------------------------------------------------
C
      CALL U2MESS('I','ADAPTATION_1')
C
      DO 100 IADAPT = 1,NADAPT
C
        CALL UTDIDT('L'   ,SDDISC,'ADAP',IADAPT,'METHODE',
     &              R8BID ,IBID  ,MODETP)
C
        ZR(JDT-1+IADAPT) = R8VIDE()
C
C ----- DOIT-ON ADAPTER ?
C
        LADAP  = DIADAP(SDDISC,IADAPT)
        IF (LADAP) THEN
          CALL NMCADT(SDDISC,IADAPT,NUMINS,VALINC,ZR(JDT-1+IADAPT))
        ENDIF
        NEWDT  = ZR(JDT-1+IADAPT)
C
C ----- AFFICHAGE
C
        IF (NEWDT .NE. R8VIDE() ) THEN
          CALL U2MESG('I','ADAPTATION_2',1,MODETP,0,0,1,NEWDT)
        ELSE
          CALL U2MESK('I','ADAPTATION_3',1,MODETP)
        ENDIF
 100  CONTINUE
C
C --- ON CHOISIT LE PLUS PETIT DT PARMI LES NADAPT PAS DE TEMPS
C --- POSSIBLES
C --- SI AUCUN CRITERE N'EST VERIFIE, ON PREND LE PAS DE TEMPS "MOINS"
C
      DT     = R8MAEM()
      UNCROK = .FALSE.
      DO 200 IADAPT = 1,NADAPT
        NEWDT  = ZR(JDT-1+IADAPT)
        IF (NEWDT .NE. R8VIDE() ) THEN
          DT     = MIN(DT,NEWDT)
          UNCROK = .TRUE.
        ENDIF
 200  CONTINUE
C
      IF (UNCROK) THEN
        CALL U2MESR('I','ADAPTATION_5',1,DT)
      ELSE
        DT = DTM
        CALL U2MESR('I','ADAPTATION_4',1,DT)
      ENDIF
C
C --- PROJECTION SUR LA BORNE SUP (POUR TOUTES LES METHODES)
C
      IF (DT.GT.PASMAX) THEN
C       EMISSION DU MESSAGE D'INFO (SAUF POUR IMPLEX)
        IF (MODETP(1:6).NE.'IMPLEX') THEN
          VALR(1) = DT
          VALR(2) = PASMAX
          CALL U2MESR('I','ADAPTATION_12',2,VALR)
        ENDIF
        DT = PASMAX
      ENDIF
C
C --- PROJECTION SUR LA BORNE INF POUR IMPLEX
C --- (ATTENTION : A FAIRE AVANT L'AJUSTEMENT / JALON)
C
      IF ((MODETP.EQ.'IMPLEX').OR.(MODETP.EQ.'IMPLEX2')) THEN
        IF (DT.LT.PASMIN) DT = PASMIN
      ENDIF
C
C --- LA DECOUPE DU PAS DE TEMPS PEUT DONNER UN DELTAT MAXI A RESPECTER
C
      TPSEXT = SDDISC(1:19)//'.AEXT'
      CALL JEVEUO(TPSEXT,'E',JTPSEX)
      INSREF = ZR(JTPSEX-1+1)
      DELTAC = ZR(JTPSEX-1+2)
      INSFIN = ZR(JTPSEX-1+3)
      IF (INSREF.NE.R8VIDE()) THEN
        IF (INST.LE.INSFIN) THEN
          DT = DELTAC
          CALL U2MESR('I','ADAPTATION_10',1,DT)
        ELSE
          ZR(JTPSEX-1+1) = R8VIDE()
        ENDIF
      ENDIF
C
C --- AJUSTEMENT DE DT EN FONCTION DU PROCHAIN JALON
C
      IF ( COMPR8(INST+DT ,'GT',JALON,PREC,1) ) THEN
C       LE NOUVEAU PAS DEPASSE LE PROCHAIN IPO :
C       ON FORCE A Y PASSER ET ON N'ENREGISTRE PAS DT
        DT = JALON-INST
      ELSEIF ( COMPR8(INST+DT ,'GT',JALON-PASMIN,PREC,1) ) THEN
C       NOUVEAU DE PAS INFERIEUR A JALON, MAIS TROP PROCHE DE JALON :
C       ON FORCE A Y PASSER ET ON ENREGISTRE DT
        DT = JALON-INST
        CALL UTDIDT('E',SDDISC,'LIST',IBID,'DT-',DT,IBID,K8BID)
      ELSE
C       NOUVEAU PAS DE TEMPS OK
C       ON ENREGISTRE DT
        CALL UTDIDT('E',SDDISC,'LIST',IBID,'DT-',DT,IBID,K8BID)
      ENDIF
C
      CALL U2MESR('I','ADAPTATION_6',1,DT)
C
C --- ON VERIFIE LES GARDE FOUS
C
      IF (MODETP(1:6).NE.'IMPLEX') THEN
        IF (DT.LT.PASMIN) CALL U2MESR('F','ADAPTATION_11',1,DT)
      ENDIF
      IF (NUMINS.GT.NMAX) CALL U2MESS('F','ADAPTATION_13')
C
C --- INSERTION DU NOUVEL INSTANT
C
      INSPAS = 1
      NEWINS = INST+DT
      CALL NMDCEI(SDDISC,NUMINS,NEWINS,NBINI ,INSPAS,
     &            'ADAP',R8BID )
C
 9999 CONTINUE
C
      CALL JEDETR(DTPLUS)
      CALL JEDEMA()
      END
