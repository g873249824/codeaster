      SUBROUTINE NMTAXI(NDIMSI,CRIT,MAT,SIGEL,VIM,EPM,DP,SP,XI,G,IRET)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/09/2005   AUTEUR LEBOUVIE F.LEBOUVIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================

      IMPLICIT NONE
      INTEGER  NDIMSI, IRET
      REAL*8   CRIT(3),MAT(14),SIGEL(*),VIM(9),EPM(*),DP,SP
      REAL*8   XI,G
C ----------------------------------------------------------------------
C TAHERI :  RESOLUTION DE L'EQUATION SCALAIRE G(P,SP,XI) = 0 PAR RT A SP
C ----------------------------------------------------------------------
C IN  NDIMSI DIMENSION DES TENSEURS
C IN  CRIT   CRITERES DE CONVERGENCE LOCAUX
C IN  MAT    TABLEAU DES CONSTANTES MATERIAUX
C IN  SIGEL  DEVIATEUR DES CONTRAINTES ELASTIQUES
C IN  VIM    VARIABLES INTERNES EN T-
C IN  EPM    DEFORMATION PLASTIQUE EN T-
C IN  DP     INCREMENT DE DEFORMATION PLASTIQUE CUMULEE
C IN  SP     CONTRAINTE DE PIC
C OUT XI     PILOTAGE DE EPN               TQ    F(P,SP,XI) = 0
C OUT G      VALEUR DU CRITERE                   G(P,SP,XI)
C OUT IRET   CODE RETOUR DE LA RESOLUTION DE L'EQUATION SCALAIRE
C               IRET=0 => PAS DE PROBLEME
C               IRET=1 => ECHEC
C ----------------------------------------------------------------------

      INTEGER NITER
      REAL*8  F,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG(6),TANG(6,6)
      REAL*8  X(4),Y(4),GG(4)


C    EXAMEN DE LA SOLUTION XI = 1 (PLASTIQUE)

      XI = 1.D0
      CALL NMTACR(1,NDIMSI,MAT,SIGEL,VIM,EPM,DP,SP,XI,
     &                  F,G,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)
      IF (F.GT.0.D0) GOTO 9999
      X(1)  = XI
      Y(1)  = F
      GG(1) = G


C    EXAMEN DE LA SOLUTION XI = 0 (ELASTIQUE)

      XI = 0.D0
      CALL NMTACR(1,NDIMSI,MAT,SIGEL,VIM,EPM,DP,SP,XI,
     &                F,G,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)
      IF (F.LE.0.D0)
     &  CALL UTMESS('F','NMTAXI','MAUVAISE ESTIMATION DE F')
      X(2)  = XI
      Y(2)  = F
      GG(2) = G

C    CALCUL DE XI : EQUATION SCALAIRE F=0 AVEC  0 < XI < 1

      X(3)  = X(1)
      Y(3)  = Y(1)
      GG(3) = GG(1)
      X(4)  = X(2)
      Y(4)  = Y(2)
      GG(4) = GG(2)

      DO 100 NITER = 1,INT(CRIT(1))
        IF ( ABS(Y(4))/MAT(4).LT.CRIT(3) .AND. X(4).NE.0.D0) GOTO 110
        CALL ZEROCO(X,Y)
        CALL NMTACR(1,NDIMSI,MAT,SIGEL,VIM,EPM,DP,SP,X(4),
     &                Y(4),GG(4),FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)
 100  CONTINUE
      IRET = 1
      GOTO 9999
 110  CONTINUE
      XI = X(4)
      G  = GG(4)

 9999 CONTINUE
      END
