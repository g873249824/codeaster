      SUBROUTINE CGLECO(RESU,MODELE,IORD0,TYPFIS,COMPOR,INCR)
      IMPLICIT NONE

      INTEGER      IORD0
      CHARACTER*8  RESU,MODELE,TYPFIS
      CHARACTER*24 COMPOR
      LOGICAL      INCR

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 17/12/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C
C     SOUS-ROUTINE DE L'OPERATEUR CALC_G
C
C     BUT : LECTURE DE LA CARTE DE COMPORTEMENT UTILISEE DANS LE CALCUL
C
C  IN :
C     RESU   : MOT-CLE RESULTAT
C     MODELE : MODELE ASSOCIE AU RESULTAT
C     IORD0  : PREMIER NUME_ORDRE
C     TYPFIS : TYPE D'OBJET POUR DECRIRE LE FOND DE FISSURE
C              'FONDFISS' OU 'FISSURE' OU 'THETA'
C  OUT :
C     COMPOR : CARTE DU COMPORTEMENT
C     INCR   : .TRUE. SI COMPORTEMENT INCREMENTAL
C ======================================================================
C
      INTEGER      NBCOMP,NTMP,IER,I,IBID
      CHARACTER*16 MOCLEF(2)
      CHARACTER*24 K24B,REPK
      LOGICAL      LIMPEL

      CALL JEMARQ()

C     RECUPERATION DE LA CARTE DE COMPORTEMENT UTILISEE DANS LE CALCUL
      MOCLEF(1) = 'COMP_INCR'
      MOCLEF(2) = 'COMP_ELAS'

C     NOMBRE MAX D'OCCURENCES DE COMP_ELAS/INCR DANS CALC_G
      NBCOMP=0
      DO 10 I=1,2
        CALL GETFAC(MOCLEF(I),NTMP)
        NBCOMP=MAX(NBCOMP,NTMP)
10    CONTINUE

      LIMPEL=.FALSE.

C
C     1) RECUP OU CREATION DE COMPOR
C     ------------------------------

      IF (NBCOMP.EQ.0) THEN

C       COMP_ELAS/INCR N'EST PAS RENSEIGNE DANS CALC_G ALORS
C       ON VA CHERCHE LE COMPORTEMENT ASSOCIE AU RESU (1ER NUME_ORDRE)
        CALL RSEXCH(' ',RESU,'COMPORTEMENT',IORD0,COMPOR,IER)

        IF (IER.NE.0) THEN
C         PROBLEME  DANS LA RECUPERATION DU COMPORTEMENT DANS RESU
C         (CAS MECA_STATIQUE PAR EXEMPLE)
C         --> ON VA IMPOSER DANS CALC_G COMP_ELAS
          LIMPEL=.TRUE.
          CALL NMDORC(MODELE,COMPOR,K24B)
        ENDIF

      ELSE

C       COMP_ELAS/INCR EST RENSEIGNE DANS CALC_G ALORS ON LE PREND
        CALL NMDORC(MODELE,COMPOR,K24B)

      ENDIF

C
C     2) RECUPERATION DE INCR
C     -----------------------

      IF (LIMPEL) THEN
C       C'EST FACILE, ON A IMPOSE COMP_ELAS DONC INCR EST FAUX
        INCR = .FALSE.
      ELSE
C       SOIT COMP_ELAS/INCR EST IMPOSE DANS CALC_G
C       SOIT ON A PRIS LE COMPOR DE RESU
        CALL DISMOI('F','ELAS_INCR',COMPOR,'CARTE_COMPOR',IBID,REPK,IER)
        IF (REPK.EQ.'ELAS') THEN
          INCR = .FALSE.
        ELSEIF (REPK.EQ.'INCR'.OR.REPK.EQ.'MIXTE') THEN
          INCR = .TRUE.
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF
      ENDIF

C
C     3) VERIFS
C     ---------

C     VERIF COHERENCE (CAS OU COMP_ELAS/INCR EST IMPOSE DANS CALC_G)
      IF (NBCOMP.NE.0) CALL GVERLC(RESU,COMPOR,IORD0)

C     X-FEM N'EST PAS ENCORE DEVELOPPE POUR GTP (G EN INCREMENTAL)
      IF (INCR.AND.TYPFIS.EQ.'FISSURE') CALL U2MESS('F','RUPTURE1_43')

      CALL JEDEMA()

      END
