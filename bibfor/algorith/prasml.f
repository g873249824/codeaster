      SUBROUTINE  PRASML (OPTION,NUGENE,TMINBL,NOMPRN,MODGEN,
     &TMNOBL,TMADBL,KNOMBL,INUMBL,CONLEQ,CONLBL)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
C
C***********************************************************************
C    P. RICHARD     DATE 13/10/92
C-----------------------------------------------------------------------
C  BUT:      < PREPARATION ASSEMBLAGE MATRICE LIAISONS >
C
C  PREPARER L'ASSEMBLAGE POUR UN LIGREL CORRESPONDANT AUX MATRICES
C   DES LIAISONS
C   ON CONSIDERE POUR L'ASSEMBLAGE UN LISTE GENERALE DES BLOC
C   ELEMENTAIRES A ASSEMBLER DANS UNE MATRICE STOCKEE PROFIL BLOC
C   (EN GENERAL MATRICE PROJETEE=1BLOC,MATRICE DE LIAISON=NBLOCS)
C    CHAQUE LIAISON CORRESPOND A 2 MATRICES DE LIAISON
C  PLUS UNE MATRICE DE LAGRANGE-LAGRANGE AUTANT DE FOIS QU'IL Y A DE
C     LIGNE DANS LA MATRICE DE LIAISON
C
C   NOEUD TARDIF = NOEUD FICTIF SUPPORTANT UNE MATRICE DE LIAISON
C
C   ON REMPLIT TMNOBL TMADBL KNOMBL INUMBL CONLEC CONLBL
C
C-----------------------------------------------------------------------
C
C NOM----- / /:
C
C OPTION   /I/: NOM K11 DE L'OPTION D'ASSEMBLAGE
C NUGENE   /I/: NOM K14 DE LA NUMEROTATION GENERALISEE
C NOMPRN   /I/: NOM K8 DU LIGREL COURANT A TRAITER
C TMINBL   /I/: NOM K24 DE LA FAMILLE NOMMEE AU NOM DES LIGRELS
C               ET DONNANT POUR CHAQUE NOEUD TARDIF DU LIGREL
C               LE NUMERO DE SON 1 BLOC DANS LA LISTE GENERALE ET
C               LE NOMBRE DE BLOC, LES DEUX NOEUDS TARDIF D'UNE LIAISON
C               SONT CONSECUTIFS
C MODGEN   /I/: NOM K8 MODELE_GENERALISE AMONT
C TMNOBL   /I/: NOM K24 DE LA FAMILLE NUMEROTE DONNANT POUR CHAQUE
C               TERME D'UN BLOC ELEMENTAIRE LE NUMERO DU BLOC ASSEMBLE
C               D'ARRIVE
C TMADBL   /I/: NOM K24 DE LA FAMILLE NUMEROTE DONNANT POUR CHAQUE
C               TERME D'UN BLOC ELEMENTAIRE LE RANG D'ARRIVEE
C               DANS LE  BLOC ASSEMBLE
C KNOMBL   /M/: VECTEUR DES NOM K24 DES OBJETS OU FAMILLE CONTENANT
C               LES BLOCS ELEMENTAIRES
C INUMBL   /M/: VECTEUR NUMERO  BLOCS ELEMNTAIRE DANS LEUR FAMILLE OU 0
C               LES BLOCS ELEMENTAIRES
C CONLEQ   /M/: VECTEUR REEL DES COEF DE CONDITIONNEMENT  AFFECTE
C               AUX EQUATION
C CONLBL   /M/: VECTEUR REEL DES COEF DE CONDITIONNEMENT  AFFECTE
C               AUX BLOCS ELEMNTAIRE
C
C
C
      INCLUDE 'jeveux.h'
C
C
      CHARACTER*8 MODGEN,SST(2),NOMPRN
      CHARACTER*14 NUGENE
      CHARACTER*19 PRGENE,STOLCI
      CHARACTER*9  RIGOPT,KSST
      CHARACTER*11 OPTION,RICOPT
      CHARACTER*24 TMADBL,TMNOBL,TMINBL
      CHARACTER*24  NOMLIA,KNOMBL(*)
      REAL*8        ZERO,CONLBL(*),CONLEQ(*)
      INTEGER      INUMBL(*),IBL(3)
      CHARACTER*1 K1BID
C
C-----------------------------------------------------------------------
      DATA RIGOPT,RICOPT/'RIGI_GENE','RIGI_GENE_C'/
      DATA ZERO / 0.0D+00 /
      DATA KSST /'&SOUSSTR'/
C-----------------------------------------------------------------------
C
C    TEST SI ON EST SUR LES LAGRANGES ET SI OPTION=RIGI_GENE(_C)
C
      CALL JEMARQ()

      IF(NOMPRN.EQ.KSST.OR.(OPTION.NE.RIGOPT.AND.
     &                     OPTION.NE.RICOPT)) GOTO 9999

C------------------RECUPERATION DU NOMBRE DE SOUS-STRUCTURE-------------
      PRGENE=NUGENE//'.NUME'
      STOLCI=NUGENE//'.SLCS'

      CALL JENONU(JEXNOM(PRGENE//'.LILI',KSST),IBID)
      CALL JELIRA(JEXNUM(PRGENE//'.PRNO',IBID),'LONMAX',NBSST,K1BID)
      NBSST=NBSST/2
C
C--------------------RECUPERATION DES CARACTERISTIQUES BLOCS------------
C
C
C---------------------REMPLISSAGE DES OBJETS DE TRAVAIL-----------------
C
      CALL JEVEUO(PRGENE//'.NUEQ','L',LLNUEQ)
      CALL JEVEUO(STOLCI//'.SCDI','L',LLADIA)
      CALL JEVEUO(STOLCI//'.SCIB','L',LLIABL)
C
      CALL JENONU(JEXNOM('&&ASSGEN.REP.NOM.PROF',NOMPRN),IBID)
      CALL JEVEUO(JEXNUM(TMINBL,IBID),'L',LTINBL)
      CALL JELIRA(JEXNUM(TMINBL,IBID),'LONMAX',NTPRNO,K1BID)
      NTPRNO=NTPRNO/3
C
      CALL JENONU(JEXNOM(PRGENE//'.LILI',NOMPRN),IBID)
      CALL JEVEUO(JEXNUM(PRGENE//'.ORIG',IBID),'L',LLORL)
      CALL JENONU(JEXNOM(PRGENE//'.LILI',KSST),IBID)
      CALL JEVEUO(JEXNUM(PRGENE//'.ORIG',IBID),'L',LLORS)
      CALL JENONU(JEXNOM(PRGENE//'.LILI',NOMPRN),IBID)
      CALL JEVEUO(JEXNUM(PRGENE//'.PRNO',IBID),'L',LLPRL)
      CALL JENONU(JEXNOM(PRGENE//'.LILI',KSST),IBID)
      CALL JEVEUO(JEXNUM(PRGENE//'.PRNO',IBID),'L',LLPRS)
      CALL JEVEUO(MODGEN//'      .MODG.LIPR','L',LLPROF)
C
      NOMLIA=MODGEN//'      .MODG.LIMA'
C
C     BOUCLE SUR LES ELEMENTS DU LIGREL
C
      DO 10 J=1,NTPRNO
C       NUMERO DE LA LIAISON
        NULIA=ZI(LLORL+J-1)
C       RECUPERATION DU PROFIL BLOC ELEMENTAIRE
        IBL(1)=ZI(LTINBL+(J-1)*3)
        IBL(2)=ZI(LTINBL+(J-1)*3+1)
        IBL(3)=ZI(LTINBL+(J-1)*3+2)
C  RECUPERATION NOM SOUS-STRUCTURE MISE EN JEU
        CALL JEVEUO(JEXNUM(MODGEN//'      .MODG.LIDF',NULIA),
     &   'L',LLDEFL)
        SST(1)=ZK8(LLDEFL)
        SST(2)=ZK8(LLDEFL+2)
        CALL JELIBE(JEXNUM(MODGEN//'      .MODG.LIDF',NULIA))
C  RECUPERATION DIMENSIONS ET NUMERO PREMIERE EQUATION DANS NUEQ
        NBLIG=ZI(LLPRL+(J-1)*2+1)
        INUL=ZI(LLPRL+(J-1)*2)
C
C   TRAITEMENT DES BLOCS DES MATRICES DE  LIAISON
C
C  BOUCLE SUR LES BLOCS ELEMENTAIRE (CHAQUE BLOC CORRESPOND
C  A LA MATRICE DE LIAISON SUR UNE SOUS-STRUCTURE
C  SI LA MATRICE CONTIENT PLUSIEURS BLOCS FAIRE UNE BOUCLE SUR LES BLOCS
C
C   BOUCLE SUR LES DEUX MATRICE DE LIAISON DE LA LIAISON
        DO 20 K=1,2
C  NUMERO BLOC ELEMENTAIRE COURANT
          IBLC=IBL(K)
          CALL JENONU(JEXNOM(MODGEN//'      .MODG.SSNO',SST(K)),
     &    NUSST)
C  ECRITURE NOM DU BLOC
          KNOMBL(IBLC)=NOMLIA
          NUBLO=ZI(LLPROF+(NULIA-1)*9+(K-1)*3+2)
          INUMBL(IBLC)=NUBLO
C   RECUPERATION DU NUMERO TARDIF DE LA SOUS-STRUCTURE
          DO 30 L=1,NBSST
            IF(ZI(LLORS+L-1).EQ.NUSST) NUTARS=L
30        CONTINUE
          NBCOL=ZI(LLPRS+(NUTARS-1)*2+1)
          INUC=ZI(LLPRS+(NUTARS-1)*2)
          NTAIL=NBLIG*NBCOL
C
          CALL JECROC(JEXNUM(TMADBL,IBLC))
          CALL JEECRA(JEXNUM(TMADBL,IBLC),'LONMAX',NTAIL,' ')
          CALL JEVEUO(JEXNUM(TMADBL,IBLC),'E',LTADBL)
          CALL JECROC(JEXNUM(TMNOBL,IBLC))
          CALL JEECRA(JEXNUM(TMNOBL,IBLC),'LONMAX',NTAIL,' ')
          CALL JEVEUO(JEXNUM(TMNOBL,IBLC),'E',LTNOBL)
C DETERMINATION MAX DU BLOC
          SCONL=ZERO
          CALL MAXBLO(JEXNUM(NOMLIA,NUBLO),SCONL)
C     BOUCLE SUR LES TERMES DU BLOC ELEMENTAIRE
          DO 40 LL=1,NBLIG
C  NUMERO D'EQUATION LIGNE
            IEQL=ZI(LLNUEQ+(INUL-1)+(LL-1))
            CONLEQ(IEQL)=MAX(CONLEQ(IEQL),SCONL)
            CONLBL(IBLC)=MAX(CONLBL(IBLC),SCONL)
            DO 50 LC=1,NBCOL
C  ADRESSE DU TERME DANS LE BLOC ELEMENTAIRE
              IAD=NBLIG*(LC-1)+LL
C  NUMERO D'EQUATION COLONNE
              IEQC=ZI(LLNUEQ+(INUC-1)+(LC-1))
C
C QUI DU TERME OU DE SON TRANSPOSE ARRIVE DANS LE TRIANGLE SUP ?
              IVL=MIN(IEQL,IEQC)
              IVC=MAX(IEQL,IEQC)
C
              ZI(LTNOBL+IAD-1)=ZI(LLIABL+IVC-1)
              ZI(LTADBL+IAD-1)=ZI(LLADIA+IVC-1)-(IVC-IVL)
50          CONTINUE
40        CONTINUE
          CALL JELIBE(JEXNUM(TMADBL,IBLC))
          CALL JELIBE(JEXNUM(TMNOBL,IBLC))
20      CONTINUE
C
C   TRAITEMENT DES BLOCS LAGRANGES LAGRANGES
C
C RECUPERATION NOEUD TARDIF ANTAGONISTE
C
        DO 60 L=1,NTPRNO
          IF(ZI(LLORL+L-1).EQ.NULIA.AND.L.NE.J) NUANT=L
60      CONTINUE
          IBLC=IBL(3)
          KNOMBL(IBLC)=NOMLIA
          NUBLO=ZI(LLPROF+(NULIA-1)*9+6+2)
          INUMBL(IBLC)=NUBLO
C DETERMINATION MAX DU BLOC
          SCONL=ZERO
          CALL MAXBLO(JEXNUM(NOMLIA,NUBLO),SCONL)
          CONLBL(IBLC)=MAX(CONLBL(IBLC),SCONL)
          CALL JECROC(JEXNUM(TMNOBL,IBLC))
          CALL JEECRA(JEXNUM(TMNOBL,IBLC),'LONMAX',NBLIG*2,' ')
          CALL JEVEUO(JEXNUM(TMNOBL,IBLC),'E',LTNOBL)
          CALL JECROC(JEXNUM(TMADBL,IBLC))
          CALL JEECRA(JEXNUM(TMADBL,IBLC),'LONMAX',NBLIG*2,' ')
          CALL JEVEUO(JEXNUM(TMADBL,IBLC),'E',LTADBL)
        DO 70 K=1,NBLIG
          IEQL=ZI(LLNUEQ+(INUL-1)+(K-1))
          INUC=ZI(LLPRL+(NUANT-1)*2)
          IEQC=ZI(LLNUEQ+(INUC-1)+(K-1))
          CONLEQ(IEQL)=MAX(CONLEQ(IEQL),SCONL)
          IAD=K
          ZI(LTNOBL+IAD-1)=ZI(LLIABL+IEQL-1)
          ZI(LTADBL+IAD-1)=ZI(LLADIA+IEQL-1)
C QUI DU TERME OU DE SON TRANSPOSE ARRIVE DANS LE TRIANGLE SUP ?
          IVL=MIN(IEQL,IEQC)
          IVC=MAX(IEQL,IEQC)
C
          IAD=NBLIG+K
          ZI(LTNOBL+IAD-1)=ZI(LLIABL+IVC-1)
          ZI(LTADBL+IAD-1)=ZI(LLADIA+IVC-1)-(IVC-IVL)
70        CONTINUE
          CALL JELIBE(JEXNUM(TMADBL,IBLC))
          CALL JELIBE(JEXNUM(TMNOBL,IBLC))
10    CONTINUE
C
      CALL JELIBE(PRGENE//'.NUEQ')
      CALL JELIBE(STOLCI//'.SCDI')
      CALL JELIBE(STOLCI//'.SCIB')
      CALL JELIBE(MODGEN//'      .MODG.LIPR')
C
 9999 CONTINUE
      CALL JEDEMA()
      END
