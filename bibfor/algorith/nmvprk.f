         SUBROUTINE NMVPRK (FAMI,KPG,KSP,NDIM,TYPMOD,IMAT,COMP,CRIT,
     &                       TIMED, TIMEF,
     &                       EPSDT, DEPST, SIGD,  VIND,  OPT,
     &                       ANGMAS, SIGF,  VINF,  DSDE)
        IMPLICIT NONE
C       ================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 28/03/2007   AUTEUR PELLET J.PELLET 
C RESPONSABLE JMBHH01 J.M.PROIX
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C       ================================================================
C       INTEGRATION DE LOIS DE COMPORTEMENT ELASTO-VISCOPLASTIQUE
C       PAR UNE METHODE DE RUNGE KUTTA
C               AVEC    .N VARIABLES INTERNES
C
C       INTEGRATION DES CONTRAINTES           = SIG(T+DT)
C       INTEGRATION DES VARIABLES INTERNES    = VIN(T+DT)
C
C       CETTE METHODE NE FOURNIT PAS DE MATRICE TANGENTE POUR LA
C       RESOLUTION GLOBALE.
C       ON FOURNIT DONC LA MATRICE DSDE OBTENUE EN ELASTICITE
C       ================================================================
C     ---------------------------ATTENTION----------------------------
C     IMPLANTATION D UNE LOI DE COMPORTEMENT VISCOPLASTIQUE COUPLEE
C     A DE L ENDOMMAGEMENT ISOTROPE (MODELE DE CHABOCHE) VENDOCHAB
C     ---------------------------ATTENTION----------------------------
C
C       ARGUMENTS
C
C       IN      FAMI    FAMILLE DE POINT DE GAUSS (RIGI,MASS,...)
C               KPG,KSP NUMERO DU (SOUS)POINT DE GAUSS
C               NDIM   DIMENSION DE L ESPACE (3D=3,2D=2,1D=1)
C               TYPMOD TYPE DE MODELISATION
C               IMAT   ADRESSE DU MATERIAU CODE
C               COMP   COMPORTEMENT DE L ELEMENT
C                      COMP(1) = RELATION DE COMPORTEMENT (CHABOCHE...)
C                      COMP(2) = NB DE VARIABLES INTERNES
C                      COMP(3) = TYPE DE DEFORMATION (PETIT,JAUMANN...)
C               OPT    OPTION DE CALCUL A FAIRE
C                              'RIGI_MECA_TANG'> DSDE(T)
C               CRIT   CRITERES  LOCAUX
C                      CRIT(1) = NOMBRE D ITERATIONS MAXI A CONVERGENCE
C                                (ITER_INTE_MAXI == ITECREL)
C                      CRIT(3) = CRITERE DE PRECISION POUR L INTEGRATION
C                                PAR LA METHODE DE RUNGE KUTTA
C                      CRIT(6) = TYPE D INTEGRATION LOCALE POUR
C                                LA LOI DE COMPORTEMENT
C                                (RESO_LOCA  == INTLOC)
C                                0 = IMPLICITE
C                                1 = RUNGE_KUTTA_2
C               TIMED   INSTANT T
C               TIMEF   INSTANT T+DT
C               EPSDT   DEFORMATION TOTALE A T
C               DEPST   INCREMENT DE DEFORMATION TOTALE
C               SIGD    CONTRAINTE A T
C               VIND    VARIABLES INTERNES A T    + INDICATEUR ETAT T
C      ANGMAS  : LES TROIS ANGLES DU MOT_CLEF MASSIF (AFFE_CARA_ELEM)
C       OUT     SIGF    CONTRAINTE A T+DT
C               VINF    VARIABLES INTERNES A T+DT + INDICATEUR ETAT T+DT
C               DSDE    MATRICE DE COMPORTEMENT TANGENT ELASTIQUE
C       ----------------------------------------------------------------
C       INFO    MATERD        (*,1) = CARACTERISTIQUES ELASTIQUES A T
C                             (*,2) = CARACTERISTIQUES PLASTIQUES A T
C               MATERF        (*,1) = CARACTERISTIQUES ELASTIQUES A T+DT
C                             (*,2) = CARACTERISTIQUES PLASTIQUES A T+DT
C               MATCST        'OUI' SI MATERIAU CST ENTRE T ET T+DT
C                             'NAP' SI LE PARAMETRE K_D EST UNE NAPPE
C                             'NON' SINON
C               NDT            NB DE COMPOSANTES TOTALES DES TENSEURS
C                                      = 6  3D
C                                      = 4  AXIS  C_PLAN  D_PLAN
C               NDI            NB DE COMPOSANTES DIRECTES DES TENSEURS
C               NVI            NB DE VARIABLES INTERNES
C               NR             NB EQUATIONS SYSTEME INTEGRE A RESOUDRE
C       ----------------------------------------------------------------
C       ROUTINE LC....UTILITAIRES POUR INTEGRATION LOI DE COMPORTEMENT
C       ----------------------------------------------------------------
C       ORDRE DES TENSEURS      3D      XX YY ZZ XY XZ YZ
C                               DP      XX YY ZZ XY
C                               AX      RR ZZ TT RZ
C       ----------------------------------------------------------------
C       ATTENTION
C       SI OPT = 'RIGI_MECA_TANG' NE PAS TOUCHER AUX VARIABLES SIGF,VINF
C       QUI N ONT PAS DE PLACE MEMOIRE ALLOUEE
C
C       SIG EPS DEPS  ONT DEJA LEURS COMPOSANTES DE CISAILLEMENT
C       MULTIPLIES PAR RACINE DE 2 > PRISE EN COMPTE DES DOUBLES
C       PRODUITS TENSORIELS ET CONSERVATION DE LA SYMETRIE
C
C       ----------------------------------------------------------------
C TOLE CRP_21
        INTEGER         IMAT,   NDIM,   NDT, NDI, NR, NVI, KPG, KSP
C
        INTEGER         I, NCHAI, ICP, NBPHAS
C
        INTEGER         ITMAX,  ICOMP, IREP
        INTEGER         NMAT,   IOPTIO, IDNR, NBMAT
        REAL*8          TOLER,  YMFS
        REAL*8          CRIT(*)
        REAL*8          VIND(*),     VINF(*)
        REAL*8          TIMED,       TIMEF,     TEMPD,    TEMPF  , TREF
        REAL*8          DEPS(6),     EPSF(6)
        REAL*8          EPSDT(6),    DEPST(6)
        CHARACTER*(*)   FAMI
C
        REAL*8            ENDOC, MAXDOM
C
        REAL*8          SIGD(6),     SIGF(6)
C
        REAL*8          DSDE(6,6), WORK(6,6), ANGMAS(*),REPERE(7)
C
        PARAMETER     ( MAXDOM = 0.99D0 )
C
C       POUR POLYCRISTAL, POUR POUVOIR STOCKER JUSQU'A 1000 PHASES
        PARAMETER       ( NMAT = 6000     )
C
        REAL*8          MATERD(NMAT,2) , MATERF(NMAT,2),R8VIDE
        INTEGER         NBCOMM(NMAT,3)
C
        CHARACTER*7     ETATD  ,     ETATF
        CHARACTER*8     NBITER,      MOD,      TYPMA,  TYPMOD(*)
        CHARACTER*16    COMP(*),     OPT,      LOI,CPMONO(5*NMAT+1)
C
        CHARACTER*3     MATCST
C
        CHARACTER*11    METING
        CHARACTER*16    CHAI
C
        REAL*8          COTHE(NMAT),DCOTHE(NMAT),PGL(3,3),PASSAG(6,6)
        REAL*8          COEFF(NMAT),DCOEFF(NMAT),COEL(NMAT),XYZ(3)
        REAL*8          SIGI(6),EPSD(6),DETOT(6)
        REAL*8          NU,DTIME,E,ALPHA,X
        REAL*8          EPSEQ,RBID
        REAL*8          HSR(5,24,24),TOUTMS(5,24,6)
C
        COMMON /TDIM/   NDT,    NDI
        COMMON /OPTI/   IOPTIO, IDNR
        COMMON /METI/   METING
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      CHARACTER*32       JEXNUM , JEXNOM , JEXR8 , JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
C --    INITIALISATION DES PARAMETRES DE CONVERGENCE ET ITERATIONS
C
      TOLER    = CRIT(3)
      METING = 'RUNGE_KUTTA'
      LOI  = COMP(1)
      MOD  = TYPMOD(1)
C
C     YMFS EST UTILISE LORS DU CALCUL D ERREUR COMME MINIMUM DE
C     CHAQUE COMPOSANTE DE VINT. L IDEAL SERAIT DE RENTRER CE
C     PARAMETRE EN DONNEE POUR CHAQUE VARIABLE INTERNE
C
      YMFS     = 0.0001D0
C     INITIALISATION DE NBPHAS SI LCMATE NE LE FAIT PAS
      NBCOMM(1,1)=1
C
C
C --    RECUPERATION COEF(TEMP(T))) LOI ELASTO-PLASTIQUE A T ET/OU T+DT
C                    NB DE CMP DIRECTES/CISAILLEMENT + NB VAR. INTERNES
C
      CALL LCMATE (FAMI,KPG,KSP,COMP,MOD,IMAT,NMAT,RBID,RBID,1,
     &             TYPMA,HSR,MATERD,MATERF,MATCST,
     &    NBCOMM,CPMONO,ANGMAS,PGL,0,TOLER,NDT,NDI,NR,NVI,VIND,TOUTMS)
C
      IF (OPT.EQ.'RIGI_MECA_TANG') THEN
          CALL LCINMA(0.D0,DSDE)
          IF (MATERD(NMAT,1).EQ.0) THEN
             CALL LCOPLI('ISOTROPE',MOD,MATERD(1,1),DSDE)
          ELSEIF (MATERD(NMAT,1).EQ.1) THEN
             CALL LCOPLI('ORTHOTRO',MOD,MATERD(1,1),DSDE)
          ENDIF


C --      DEBUT TRAITEMENT DE VENDOCHAB --
          IF (LOI(1:9).EQ.'VENDOCHAB') THEN
             CALL U2MESS('F','ALGORITH8_91')
          ENDIF
C --      FIN   TRAITEMENT DE VENDOCHAB --

          GOTO 9999
      END IF
C
C
      DO 1 ICP=1,6
        DETOT(ICP)=DEPST(ICP)
        EPSD(ICP)=EPSDT(ICP)
    1 CONTINUE
      DTIME=TIMEF-TIMED
C
C --  INITIALISATION DES VARIABLES INTERNES A T
C
      DO 10 I=1,NMAT
        COTHE(I)=MATERD(I,1)
        DCOTHE(I)=-COTHE(I)+MATERF(I,1)
   10 CONTINUE
C
      DO 11 I=1,NMAT
        COEFF(I)=MATERD(I,2)
        DCOEFF(I)=-COEFF(I)+MATERF(I,2)
   11 CONTINUE


C --    DEBUT TRAITEMENT DE VENDOCHAB --
C     ROUTINE DE DECROISSANCE DES CONTRAINTES QUAND D>MAXDOM
      IF (LOI(1:9).EQ.'VENDOCHAB') THEN
C
        IF (VIND(9).GE.MAXDOM) THEN
C
          IF (VIND(9).EQ.1.0D0) THEN
            DO 4 ICP=1,2*NDIM
              SIGF(ICP)=SIGD(ICP)*(0.01D0)
    4       CONTINUE
            MATERD(1,1)=0.01D0*MATERD(1,1)
            CALL LCOPLI('ISOTROPE',MOD,MATERD(1,1),DSDE)
          ELSE
            DO 5 ICP=1,2*NDIM
               SIGF(ICP)=SIGD(ICP)*(0.1D0)
    5       CONTINUE
            ENDOC=(1.0D0-MAX(MAXDOM,VIND(9)))*0.1D0
            MATERD(1,1)=ENDOC*MATERD(1,1)
            CALL LCOPLI('ISOTROPE',MOD,MATERD(1,1),DSDE)
            MATERD(1,1)=MATERD(1,1)/ENDOC
          ENDIF
          DO 6 ICP=1,NVI
             VINF(ICP)=VIND(ICP)
    6     CONTINUE
          VINF(9)=1.0D0
          GOTO 9999
        ENDIF
      ENDIF
C --  FIN   TRAITEMENT DE VENDOCHAB --


      DO 13 ICP=1,NVI
        VINF(ICP)=VIND(ICP)
   13 CONTINUE

C --  DEBUT TRAITEMENT DE VENDOCHAB --
C     INITIALISATION PARTICULIERE SUIVANT LA LOI
C     ICI-> INITIALISATION DE VINF(8) A UNE VALEUR NON NULLE
C     POUR EVITER LES 1/0 DANS RKDVEC
      IF (LOI(1:9).EQ.'VENDOCHAB') THEN
        IF (VINF(8).LE.(1.0D-8)) THEN
          VINF(8)=1.0D-8
        ENDIF
      ENDIF
C --  FIN   TRAITEMENT DE VENDOCHAB --

C      POUR POLYCRISTAL

      NBPHAS=NBCOMM(1,1)

      IF (LOI(1:8).EQ.'MONOCRIS')  NVI = NVI-2

      CALL GERPAS(  FAMI,KPG,KSP,
     &              COMP,MOD,IMAT,MATCST,NBCOMM,CPMONO,NBPHAS,
     &              NVI,NMAT,VINF,DTIME,TOLER,YMFS,COTHE,
     &              COEFF,DCOTHE,DCOEFF,E,NU,ALPHA,COEL,PGL,ANGMAS,
     &              SIGI,EPSD,DETOT,X)
      IF (LOI(1:8).EQ.'MONOCRIS')  NVI = NVI +2


C --    DEBUT TRAITEMENT DE VENDOCHAB --
C --    CALCUL DES CONTRAINTES SUIVANT QUE LE MATERIAU EST
C --    ENDOMMAGE OU PAS
      IF (LOI(1:9).EQ.'VENDOCHAB')  E=E*(1.0D0-VINF(9))


C --  CALCUL DES CONTRAINTES
C
      CALL CALSIG(FAMI,KPG,KSP,VINF,MOD,E,NU,ALPHA,X,DTIME,EPSD,
     &             DETOT,NMAT, COEL, SIGI)

      DO 14 ICP=1,2*NDIM
        SIGF(ICP)=SIGI(ICP)
   14 CONTINUE

      CALL LCINMA(0.D0,DSDE)

      IF ((LOI(1:8).EQ.'MONOCRIS').OR.(LOI(1:8).EQ.'POLYCRIS')) THEN

        CALL LCDPEQ ( VIND , VINF, EPSEQ)
        VINF (NVI-1) = VIND (NVI-1) + EPSEQ

        IF (EPSEQ.EQ.0.D0) THEN
        VINF (NVI) = 0.D0
        ELSE
        VINF (NVI) = 1.D0
        ENDIF

      ENDIF

      IF (LOI(1:9).EQ.'VENDOCHAB') THEN
C --    DEBUT TRAITEMENT DE VENDOCHAB --
C --    CALCUL DE DSDE SUIVANT QUE LE MATERIAU EST
C --    ENDOMMAGE OU PAS
        ENDOC=(1.0D0-VINF(9))
        MATERF(1,1)=MATERF(1,1)*ENDOC
        CALL LCOPLI('ISOTROPE',MOD,MATERF(1,1),DSDE)
        MATERF(1,1)=MATERF(1,1)/ENDOC
C --    FIN   TRAITEMENT DE VENDOCHAB --
      ELSE

        IF (MATERF(NMAT,1).EQ.0) THEN

          CALL LCOPLI('ISOTROPE',MOD,MATERF(1,1),DSDE)

        ELSEIF (MATERF(NMAT,1).EQ.1) THEN

          CALL LCOPLI('ORTHOTRO',MOD,MATERF(1,1),DSDE)

        ENDIF

      ENDIF
C
 9999 CONTINUE
      END
