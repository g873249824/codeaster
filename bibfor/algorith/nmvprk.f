      SUBROUTINE NMVPRK(FAMI,KPG,KSP,NDIM,TYPMOD,IMAT,COMP,CRIT,
     &                  TIMED, TIMEF,NEPS,EPSDT, DEPST,SIGD,VIND, 
     &                  OPT,ANGMAS,SIGF,VINF,DSDE,IRET)
      IMPLICIT NONE
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/07/2012   AUTEUR PROIX J-M.PROIX 
C TOLE CRP_21
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PROIX J.M.PROIX
C     ================================================================
C     INTEGRATION DE LOIS DE COMPORTEMENT ELASTO-VISCOPLASTIQUE
C     PAR UNE METHODE DE RUNGE KUTTA
C             AVEC    .N VARIABLES INTERNES
C
C     INTEGRATION DES CONTRAINTES           = SIG(T+DT)
C     INTEGRATION DES VARIABLES INTERNES    = VIN(T+DT)
C
C     CETTE METHODE NE FOURNIT PAS DE MATRICE TANGENTE POUR LA
C     RESOLUTION GLOBALE.
C     ON FOURNIT DONC LA MATRICE DSDE OBTENUE EN ELASTICITE
C     ================================================================
C
C     ARGUMENTS
C
C     IN      FAMI    FAMILLE DE POINT DE GAUSS (RIGI,MASS,...)
C           KPG,KSP NUMERO DU (SOUS)POINT DE GAUSS
C           NDIM   DIMENSION DE L ESPACE (3D=3,2D=2,1D=1)
C           TYPMOD TYPE DE MODELISATION
C           IMAT   ADRESSE DU MATERIAU CODE
C           COMP   COMPORTEMENT DE L ELEMENT
C                  COMP(1) = RELATION DE COMPORTEMENT (CHABOCHE...)
C                  COMP(2) = NB DE VARIABLES INTERNES
C                  COMP(3) = TYPE DE DEFORMATION (PETIT,JAUMANN...)
C           OPT    OPTION DE CALCUL A FAIRE
C                          'RIGI_MECA_TANG'> DSDE(T)
C           CRIT   CRITERES  LOCAUX
C                  CRIT(1) = NOMBRE D ITERATIONS MAXI A CONVERGENCE
C                            (ITER_INTE_MAXI == ITECREL)
C                  CRIT(3) = CRITERE DE PRECISION POUR L INTEGRATION
C                            PAR LA METHODE DE RUNGE KUTTA
C                  CRIT(6) = TYPE D INTEGRATION LOCAL POUR LA LOI DE
C                            COMPORTEMENT (ALGO_INTE)
C           TIMED   INSTANT T
C           TIMEF   INSTANT T+DT
C           EPSDT   DEFORMATION TOTALE A T
C           DEPST   INCREMENT DE DEFORMATION TOTALE
C           SIGD    CONTRAINTE A T
C           VIND    VARIABLES INTERNES A T    + INDICATEUR ETAT T
C           ANGMAS  3 ANGLES DU MOT_CLEF MASSIF (AFFE_CARA_ELEM)
C     OUT   SIGF    CONTRAINTE A T+DT
C           VINF    VARIABLES INTERNES A T+DT + INDICATEUR ETAT T+DT
C           DSDE    MATRICE DE COMPORTEMENT TANGENT ELASTIQUE
C     ----------------------------------------------------------------
C     INFO  MATERD        (*,1) = CARACTERISTIQUES ELASTIQUES A T
C                         (*,2) = CARACTERISTIQUES PLASTIQUES A T
C           MATERF        (*,1) = CARACTERISTIQUES ELASTIQUES A T+DT
C                         (*,2) = CARACTERISTIQUES PLASTIQUES A T+DT
C           MATCST        'OUI' SI MATERIAU CST ENTRE T ET T+DT
C                         'NAP' SI LE PARAMETRE K_D EST UNE NAPPE
C                         'NON' SINON
C           NDT            NB DE COMPOSANTES TOTALES DES TENSEURS
C                                  = 6  3D
C                                  = 4  AXIS  C_PLAN  D_PLAN
C           NDI            NB DE COMPOSANTES DIRECTES DES TENSEURS
C           NVI            NB DE VARIABLES INTERNES
C           NR             NB EQUATIONS SYSTEME INTEGRE A RESOUDRE
C     ----------------------------------------------------------------
C     ROUTINE LC....UTILITAIRES POUR INTEGRATION LOI DE COMPORTEMENT
C     ----------------------------------------------------------------
C     ORDRE DES TENSEURS  3D XX YY ZZ SQRT(2)*XY SQRT(2)*XZ SQRT(2)*YZ
C                         DP XX YY ZZ SQRT(2)*XY
C                         AX RR ZZ TT SQRT(2)*RZ
C     ----------------------------------------------------------------
C     ATTENTION
C     SI OPT = 'RIGI_MECA_TANG' NE PAS TOUCHER AUX VARIABLES SIGF,VINF
C     QUI N ONT PAS DE PLACE MEMOIRE ALLOUEE
C
C     SIG EPS DEPS  ONT DEJA LEURS COMPOSANTES DE CISAILLEMENT
C     MULTIPLIES PAR RACINE DE 2 > PRISE EN COMPTE DES DOUBLES
C     PRODUITS TENSORIELS ET CONSERVATION DE LA SYMETRIE
C
C     ----------------------------------------------------------------

      INCLUDE 'jeveux.h'
      CHARACTER*(*)   FAMI
      INTEGER IMAT,NDIM,NDT,NDI,NR,NVI,KPG,KSP,I,NBPHAS,ITMAX
      INTEGER NMAT,IOPTIO,IDNR,NSG,NFS,NHSR,NEPS
C     POUR POLYCRISTAL, POUR POUVOIR STOCKER JUSQU'A 1000 PHASES
      PARAMETER (NMAT =6000)
C     POUR LCMATE (MONOCRISTAL) DIMENSIONS MAX
C        NSG=NOMBRE DE SYSTEMES DE GLISSEMENT MAXIMUM
C        NFS=NOMBRE DE FAMILLES DE SYSTEMES DE GLISSEMENT MAXIMUM
      PARAMETER (NSG=30)
      PARAMETER (NFS=5)
      PARAMETER (NHSR=5)
      INTEGER   NBCOMM(NMAT,3),NUMHSR(NMAT),IRET
      REAL*8 MATERD(NMAT,2),MATERF(NMAT,2),EPSDT(NEPS),DEPST(NEPS),RBID
      REAL*8 TOLER,YMFS,CRIT(*),VIND(*),VINF(*),TIMED,TIMEF
      REAL*8 SIGD(6),SIGF(6),DSDE(6,6),ANGMAS(*)
      REAL*8 COTHE(NMAT),DCOTHE(NMAT),PGL(3,3),EPSD(9)
      REAL*8 COEFF(NMAT),DCOEFF(NMAT),COEL(NMAT),DTIME,X
C     POUR POLYCRISTAL, 5 MATRICE HSR MAXI. POUR MONOCRISTAL, 1 MAXI
      REAL*8 TOUTMS(NFS,NSG,6),HSR(NSG,NSG,NHSR),DETOT(9)
      CHARACTER*3  MATCST
      CHARACTER*8  MOD,TYPMA,TYPMOD(*)
      CHARACTER*11 METING
      CHARACTER*16 CPMONO(5*NMAT+1),COMP(*),OPT,LOI
      COMMON /TDIM/   NDT,    NDI
      COMMON /OPTI/   IOPTIO, IDNR
      COMMON /METI/   METING
C
C --    INITIALISATION DES PARAMETRES DE CONVERGENCE ET ITERATIONS
C
      ITMAX  = INT(CRIT(1))
      TOLER  = CRIT(3)
      METING = 'RUNGE_KUTTA'
      MOD    = TYPMOD(1)
      LOI    = COMP(1)
C
C     YMFS EST UTILISE LORS DU CALCUL D ERREUR COMME MINIMUM DE
C     CHAQUE COMPOSANTE DE VINT. L IDEAL SERAIT DE RENTRER CE
C     PARAMETRE EN DONNEE POUR CHAQUE VARIABLE INTERNE
C
      YMFS     = 0.0001D0
C
C --  RECUPERATION COEF(TEMP(T))) LOI ELASTO-PLASTIQUE A T ET/OU T+DT
C                    NB DE CMP DIRECTES/CISAILLEMENT + NB VAR. INTERNES
C
      CALL LCMATE(FAMI,KPG,KSP,COMP,MOD,IMAT,NMAT,RBID,RBID,1,
     &            TYPMA,HSR,MATERD,MATERF,MATCST,NBCOMM,CPMONO,
     &            ANGMAS,PGL,0,TOLER,NDT,NDI,NR,CRIT,NVI,VIND,NFS,NSG,
     &            TOUTMS,NHSR,NUMHSR,SIGD)
C
      IF (OPT(1:9).EQ.'RIGI_MECA') GOTO 9000
C
      CALL DCOPY(NEPS,DEPST,1,DETOT,1)
      CALL DCOPY(NEPS,EPSDT,1,EPSD,1)
    
      DTIME=TIMEF-TIMED
C
C --  INITIALISATION DES VARIABLES INTERNES A T
C
      DO 10 I=1,NMAT
        COTHE(I)=MATERD(I,1)
        DCOTHE(I)=-COTHE(I)+MATERF(I,1)
   10 CONTINUE
C
      DO 11 I=1,NMAT
        COEFF(I)=MATERD(I,2)
        DCOEFF(I)=-COEFF(I)+MATERF(I,2)
   11 CONTINUE

C     INITIALISATIONS PARTICULIERES POUR CERTAINES LOIS

      CALL LCRKIN(NDIM,OPT,COMP,MATERF,NBCOMM,CPMONO,NMAT,MOD,
     &            NVI,SIGD,SIGF,VIND,VINF,NBPHAS,IRET)
      IF (IRET.EQ.9) THEN
C        ENDOMMAGEMENT MAXI AU POINT DE GAUSS 
         IRET=0
         GOTO 9999
      ENDIF
     
      CALL GERPAS(  FAMI,KPG,KSP,
     &              COMP,MOD,IMAT,MATCST,NBCOMM,CPMONO,NBPHAS,
     &              NVI,NMAT,VINF,DTIME,ITMAX,TOLER,YMFS,COTHE,
     &              COEFF,DCOTHE,DCOEFF,COEL,PGL,ANGMAS,
     &              NEPS,EPSD,DETOT,X,NFS,NSG,NHSR,NUMHSR,HSR,IRET)
      IF (IRET.NE.0) THEN
         GOTO 9999
      ENDIF

C --  CALCUL DES CONTRAINTES

      IF ((LOI(1:8).EQ.'MONOCRIS').AND.(COMP(3)(1:4).EQ.'SIMO')) THEN
         CALL LCRKSG(COMP,NVI,VINF,EPSD,DETOT,NMAT,COEL,SIGF)
      ELSE
         CALL CALSIG(FAMI,KPG,KSP,VINF,MOD,COMP,VINF,X,DTIME,EPSD,
     &               DETOT,NMAT,COEL,SIGF)
      ENDIF
      
      CALL LCDPEQ (VIND, VINF,COMP,NBCOMM,CPMONO,NMAT,NVI,SIGF,
     &                DETOT,EPSD,MATERF,PGL)
     
 9000 CONTINUE

C     OPERATEUR TANGENT = ELASTIQUE OU SECANT (ENDOMMAGEMENT)
      IF (MATERF(NMAT,1).EQ.0) THEN

        CALL LCOPLI('ISOTROPE',MOD,MATERF(1,1),DSDE)

      ELSEIF (MATERF(NMAT,1).EQ.1) THEN

        CALL LCOPLI('ORTHOTRO',MOD,MATERF(1,1),DSDE)

      ENDIF

C
 9999 CONTINUE
      END
