      SUBROUTINE TANBUL(OPTION,NDIM,G,MATE,COMPOR,RESI,MINI,ALPHA,
     &                  DSBDEP,TREPST)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 18/03/2013   AUTEUR SFAYOLLE S.FAYOLLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE SFAYOLLE S.FAYOLLE
C TOLE CRS_1404
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      LOGICAL      RESI,MINI
      INTEGER      NDIM,G,MATE
      REAL*8       ALPHA,DSBDEP(2*NDIM,2*NDIM),TREPST
      CHARACTER*16 OPTION,COMPOR
C-----------------------------------------------------------------------
C          CALCUL DE LA MATRICE TANGENTE BULLE
C-----------------------------------------------------------------------
C IN  RESI    : CALCUL DES FORCES INTERNES
C IN  MINI    : STABILISATION BULLE - MINI ELEMENT
C IN  OPTION  : OPTION DE CALCUL
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  G       : NUMERO DU POINT DE GAUSS
C IN  MATE    : NUMERO DU MATERIAU
C IN  COMPOR  : NOM DU COMPORTEMENT
C OUT ALPHA   : INVERSE DE KAPPA
C OUT DSBDEP  : MATRICE TANGENTE BULLE
C OUT TREPST  : TRACE DU TENSEUR DES DEFORMATIONS THERMIQUES
C-----------------------------------------------------------------------

      INTEGER      K
      INTEGER      ICODRE(2),ITEMPS,IRET,IEPSV
      REAL*8       E,NU,VALRES(2),VALPAR
      REAL*8       XYZGAU(3),REPERE(7),EPSTH(6),R8MIEM
      REAL*8       COEF,COEF1,COEF2,COEF3
      CHARACTER*4  FAMI
      CHARACTER*8  NOMRES(2),NOMPAR
C-----------------------------------------------------------------------


C - INITIALISATION
      CALL R8INIR(2*NDIM*2*NDIM,0.D0,DSBDEP,1)
      CALL R8INIR(3,0.D0,XYZGAU,1)
      CALL R8INIR(7,0.D0,REPERE,1)
      CALL R8INIR(6,0.D0,EPSTH, 1)

      NOMPAR = 'INST'
      FAMI   = 'RIGI'

C - LA FORMULATION INCO A 2 CHAMPS UP NE FONCTIONNE QU AVEC ELAS OU VMIS
C - POUR L INSTANT.
C - POUR L ANISOTROPIE IL FAUDRAIT CALCULER XYZGAU ET REPERE
      IF (.NOT.( COMPOR(1:4) .EQ. 'ELAS'.OR.
     &     COMPOR(1:9) .EQ. 'VMIS_ISOT' )) THEN
        CALL U2MESK('F','ALGORITH4_50',1,COMPOR)
      ENDIF

C - RECUPERATION DE L INSTANT
      CALL TECACH('NNN','PTEMPSR','L',1,ITEMPS,IRET)
      IF (ITEMPS.NE.0) THEN
        VALPAR = ZR(ITEMPS)
      ELSE
        VALPAR = 0.D0
      ENDIF

C - RECUPERATION DE E ET NU DANS LE FICHIER PYTHON
      NOMRES(1)='E'
      NOMRES(2)='NU'

      CALL RCVALB('RIGI',G,1,'+',MATE,' ','ELAS',1,NOMPAR,VALPAR,2,
     &            NOMRES,VALRES,ICODRE,1)

      E  = VALRES(1)
      NU = VALRES(2)
      ALPHA=(3.D0*(1.D0-2.D0*NU))/E

      IF (MINI) THEN
        COEF  = 1.D0/((1.D0+NU)*(1.D0-2.D0*NU))
        COEF1 = E*(1.D0-NU)*COEF
        COEF2 = E*NU*COEF
        COEF3 = 2.D0*E/(1.D0+NU)

        DSBDEP(1,1) = COEF1
        DSBDEP(1,2) = COEF2
        DSBDEP(1,3) = COEF2

        DSBDEP(2,1) = COEF2
        DSBDEP(2,2) = COEF1
        DSBDEP(2,3) = COEF2

        DSBDEP(3,1) = COEF2
        DSBDEP(3,2) = COEF2
        DSBDEP(3,3) = COEF1

        DSBDEP(4,4) = COEF3
        IF(NDIM.EQ.3)THEN
          DSBDEP(5,5) = COEF3
          DSBDEP(6,6) = COEF3
        ENDIF
      ENDIF

      IF (RESI) THEN
        CALL EPSTMC(FAMI, NDIM,VALPAR,'+',G,1,XYZGAU,REPERE,MATE,
     &              OPTION,EPSTH)

C - TEST DE LA NULLITE DES DEFORMATIONS DUES AUX VARIABLES DE COMMANDE
        IEPSV = 0
        TREPST = 0.D0
        DO 90 K = 1, 6
          IF ( ABS(EPSTH(K)).GT.R8MIEM() ) IEPSV=1
 90     CONTINUE
C - TOUTES DES COMPOSANTES SONT NULLES. ON EVITE DE CALCULER TREPST
        IF (IEPSV.NE.0) THEN
          DO 50 K = 1, 3
            TREPST = TREPST + EPSTH(K)
 50       CONTINUE
        ENDIF
      ENDIF

      END
