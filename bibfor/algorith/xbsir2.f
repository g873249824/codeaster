      SUBROUTINE XBSIR2(ELREF,CONTAC,DDLC  ,DDLM  ,DDLS  ,IGEOM ,
     &                  JFISNO,JLST  ,IVECTU,SINGU ,
     &                  NDDL  ,NDIM  ,NFE   ,NFH   ,NFISS ,
     &                  NNO   ,NNOM  ,NNOS  ,DEPREF,SIGREF,
     &                  NOMTE)

      IMPLICIT   NONE      
      INCLUDE 'jeveux.h'
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 15/01/2013   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C IN ELREF  : ELEMENT DE REF PARENT
C IN CONTAC : DISCRETISATION, 1 POUR P1P1, 3 POUR P2P1
C IN DDLC   : NB DDL DE CONTACT PAR NOEUD SOMMET
C IN DDLM   : NB DDL PAR NOEUD MILIEU
C IN DDLS   : NB DDL TOT PAR NOEUD SOMMET
C IN GEOM   : ADRESSE POUR COORDONNEES NOEUD PARENT
C IN JFISNO : CONNECTIVITE FISSURE/DDLS HEAVISIDE AU NOEUD
C IN JLST   : ADRESSE LST
C IN/OUT IVECTU : VECTEUR RESIDUS DE REF
C IN SINGU
C IN NDDL   : NB TOTAL DDL ELEMENT
C NDIM      : DIMENSION DU MODELE
C NFE       : NB FONCTION ENRICHISSEMENT CTIP
C NFH       : IDEM HEAVISIDE
C NFISS     : NB FISSURES
C NNO       : NB NOEUD ELEM PARENT
C NNOM      : DONT NB NOEUDS MILIEUX
C NNOS      : DONT NB NOEUDS SOMMETS
C DEPREF    : DEPLACEMENT DE REFERENCE
C SIGREF    : CONTRAINTE DE REFERENCE
C NOMTE     : TYPE D ELEMENT
C -------------------
C CALCUL RESIDU DE REFERENCE ELEMENTS COHESIF MIXTE XFEM
C TERMES D INTERFACE
C -------------------   
      INTEGER     CFACE(5,3),CONTAC,DDLC,DDLM,DDLS
      INTEGER     I,IADZI,IAZK24,IB,IBID,IFA,IFISS,IGEOM,IPGF
      INTEGER     IRET,JAINT,JBASEC,JCFACE
      INTEGER     JFISNO,JHEAFA,JHEANO,JLONCH,JLST,JPTINT,JTAB(2)
      INTEGER     IVECTU,LACT(8),SINGU
      INTEGER     NBSPG,NCOMPA,NCOMPB,NCOMPC,NCOMPH,NCOMPP
      INTEGER     NDDL,NDIM,NFACE,NFE,NFH,NFISS,NINTER,NLACT
      INTEGER     NNO,NNOL,NNOM,NNOS,NPGF,NPTF,NSPFIS,PLA(27)
      INTEGER     IDFDEF,IPOIDF,IVFF,J,NNOF
      REAL*8      DEPREF,FFC(8),FFP(27),JAC
      REAL*8      R3BID(3),RR,SIGREF,VTMP(400)
      LOGICAL     LBID
      CHARACTER*8 ELC,ELREF,ELREFC,FPG,TYPMA
      CHARACTER*16 NOMTE
C
C --- INITIALISATIONS
C
      DO 5 I  = 1,8
        LACT(I) = 0
 5    CONTINUE
      CALL VECINI(27,0.D0,FFP)
      CALL VECINI(400,0.D0,VTMP)
      RR = 0.D0
      NCOMPH = 0
      NBSPG = 0
      CALL TECAEL(IADZI,IAZK24)
      TYPMA=ZK24(IAZK24-1+3+ZI(IADZI-1+2)+3)(1:8)
C
C --- ROUTINE SPECIFIQUE P2P1
C
      CALL ELELIN(CONTAC,ELREF,ELREFC,IBID,IBID)
C
C --- ARGUMENTS SUPPLEMENTAIRES NECESSAIRES PAR RAPPORT
C --- AUX ELEMENTS VOLUMIQUES
C
      CALL JEVECH('PPINTER','L',JPTINT)
      CALL JEVECH('PAINTER','L',JAINT)
      CALL JEVECH('PCFACE','L',JCFACE)
      CALL JEVECH('PBASECO','L',JBASEC)
      CALL JEVECH('PLONFA','L',JLONCH)
      IF (NFISS.GT.1) THEN
        CALL JEVECH('PHEAVNO','L',JHEANO)
        CALL JEVECH('PHEAVFA','L',JHEAFA)
        CALL TECACH('OOO','PHEAVFA','L',2,JTAB,IRET)
        NCOMPH = JTAB(2)
      ENDIF
C     DIMENSIONS DES GRANDEURS DANS LA CARTE
      CALL TECACH('OOO','PPINTER','L',2,JTAB,IRET)
      NCOMPP = JTAB(2)
      CALL TECACH('OOO','PAINTER','L',2,JTAB,IRET)
      NCOMPA = JTAB(2)
      CALL TECACH('OOO','PBASECO','L',2,JTAB,IRET)
      NCOMPB = JTAB(2)
      CALL TECACH('OOO','PCFACE','L',2,JTAB,IRET)
      NCOMPC = JTAB(2)
C
C --- BOUCLE SUR LES FISSURES
C
      DO 90 IFISS = 1,NFISS
C
C --- RECUPERATION DIVERSES DONNEES CONTACT
C
        NINTER=ZI(JLONCH+3*(IFISS-1)-1+1)
        IF (NINTER.EQ.0) GOTO 90
C
        FPG = 'FPG2'
C SCHEMA EN DUR POUR LE MOMENT
        IF (NDIM .EQ. 3) THEN
          ELC='TR3'
        ELSEIF (NDIM.EQ.2) THEN
          IF(CONTAC.LE.2) THEN
            ELC='SE2'
          ELSE
            ELC='SE3'
          ENDIF
        ENDIF
C
        CALL ELREF4(ELC,FPG,IBID,NNOF,IBID,NPGF,IPOIDF,
     &              IVFF,IDFDEF,IBID)
         NFACE=ZI(JLONCH+3*(IFISS-1)-1+2)
         NPTF=ZI(JLONCH+3*(IFISS-1)-1+3)
         DO 11 I=1,NFACE
            DO 12 J=1,NPTF
              CFACE(I,J)=ZI(JCFACE-1+NPTF*(I-1)+J)
 12         CONTINUE
 11     CONTINUE
C
        NSPFIS = NPGF*NFACE
C
C
C --- RECUP MULTIPLICATEURS ACTIFS ET LEURS INDICES
C
         CALL XMULCO(CONTAC,DDLC,DDLM,JAINT,IFISS,
     &               JHEANO,IB,LACT,.FALSE.,LBID,NDIM,NFE,
     &               NFH,NFISS,NINTER,NLACT,NNO,
     &               NNOL,NNOM,NNOS,PLA,TYPMA)
C
C --- BOUCLE SUR LES FACETTES
C
        DO 100 IFA=1,NFACE
C
C --- BOUCLE SUR LES POINTS DE GAUSS DES FACETTES
C
          DO 110 IPGF=1,NPGF
C
C --- PREPARATION DU CALCUL
C
          CALL XMPREP(CFACE ,CONTAC,ELREF ,ELREFC,ELC   ,
     &                FFC   ,FFP   ,FPG   ,JAINT ,JBASEC,
     &                JPTINT,IFA   ,IGEOM ,IPGF  ,JAC   ,
     &                JLST  ,LACT  ,R3BID ,NDIM  ,NINTER,
     &                NLACT ,NNO   ,NNOS  ,NPTF  ,IBID  ,
     &                RR    ,SINGU ,R3BID ,R3BID)
C
C --- CALCUL VECTEURS DE REFERENCE POUR LA LOI D INTERFACE
C
          CALL XMVCO3(SIGREF,DEPREF,NDIM  ,NNO   ,NNOL  ,
     &                NNOS  ,PLA   ,LACT  ,NFH   ,DDLS  ,
     &                DDLM  ,NFISS ,IFISS ,JHEAFA,IFA   ,
     &                NCOMPH,JFISNO,JAC   ,FFC   ,FFP   ,
     &                SINGU ,RR    ,VTMP)
C --- FIN DE BOUCLE SUR LES POINTS DE GAUSS
 110      CONTINUE

C --- FIN DE BOUCLE SUR LES FACETTES
 100    CONTINUE
C --- FIN BOUCLE SUR LES FISSURES
        NBSPG  = NBSPG  + NSPFIS
        JBASEC = JBASEC + NCOMPB
        JPTINT = JPTINT + NCOMPP
        JAINT  = JAINT  + NCOMPA
        JCFACE = JCFACE + NCOMPC
  90  CONTINUE
C
C-----------------------------------------------------------------------
C     COPIE DES CHAMPS DE SORTIES ET FIN
C-----------------------------------------------------------------------
C
      DO 900 I=1,NDDL
        ZR(IVECTU-1+I)=ZR(IVECTU-1+I)+VTMP(I)
 900  CONTINUE
C
      END
