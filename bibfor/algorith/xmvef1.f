      SUBROUTINE XMVEF1 (NDIM,NNE,NNES,NNC,NNM,NFAES,CFACE,
     &                   HPG,FFPC,FFES,FFMA,JACOBI,IAINES,    
     &                   LAMBDA,COEFFA,COEFFF,TAU1,TAU2,
     &                   RESE,MPROJ,DEPLE,TYPMA,VTMP)
      IMPLICIT NONE
      INTEGER  NDIM,NNC,NNE,NNES,NNM,NFAES,IAINES,CFACE(5,3)
      REAL*8   HPG,FFPC(9),FFES(9),FFMA(9),JACOBI  
      REAL*8   LAMBDA,COEFFF,COEFFA 
      REAL*8   TAU1(3),TAU2(3),RESE(3),MPROJ(3,3),DEPLE(9),VTMP(120)
      CHARACTER*8  TYPMA  
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 12/05/2009   AUTEUR MAZET S.MAZET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C
C ROUTINE APPELLEE PAR : TE0367
C ----------------------------------------------------------------------
C
C CALCUL DU SECOND MEMBRE POUR LE FROTTEMENT
C CAS AVEC CONTACT
C
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  NNE    : NOMBRE DE NOEUDS DE LA MAILLE ESCLAVE
C IN  NNES   : NOMBRE DE NOEUDS SOMMETS DE LA MAILLE ESCLAVE
C IN  NNC    : NOMBRE DE NOEUDS DE CONTACT
C IN  NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C IN  NFAES  : NUMERO DE LA FACETTE DE CONTACT ESCLAVE
C IN  CFACE  : MATRICE DE CONECTIVITE DES FACETTES DE CONTACT
C IN  HPG    : POIDS DU POINT INTEGRATION DU POINT DE CONTACT
C IN  FFPC   : FONCTIONS DE FORME DU POINT DE CONTACT DANS ELC
C IN  FFES   : FONCTIONS DE FORME DU POINT DE CONTACT DANS ESC
C IN  FFMA   : FONCTIONS DE FORME DE LA PROJECTION DU PTC DANS MAIT
C IN  JACOBI : JACOBIEN DE LA MAILLE AU POINT DE CONTACT
C IN  IAINES : POINTEUR VERS LE VECT DES ARRETES ESCLAVES INTERSECTEES
C IN  LAMBDA : VALEUR DU SEUIL_INIT
C IN  COEFFA : COEF_REGU_FROT
C IN  COEFFF : COEFFICIENT DE FROTTEMENT DE COULOMB
C IN  TAU1   : PREMIERE TANGENTE
C IN  TAU2   : SECONDE TANGENTE
C IN  RESE   : PROJECTION DE LA BOULE UNITE POUR LE FROTTEMENT
C IN  MPROJ  : MATRICE DE L'OPERATEUR DE PROJECTION
C IN  DEPLE  : DEPLACEMENTS DE LA SURFACE ESCLAVE
C IN  TYPMA    : NOM DE LA MAILLE ESCLAVE D'ORIGINE (QUADRATIQUE)
C I/O VTMP   : VECTEUR SECOND MEMBRE ELEMENTAIRE DE CONTACT/FROTTEMENT
C ----------------------------------------------------------------------
      INTEGER   I, J, K, II, INI, PLI, XOULA
      REAL*8    VECTT(3), INTER(2)
C ----------------------------------------------------------------------
C
C --- INITIALISATIONS
C
      DO 150 I = 1,3
        VECTT(I) = 0.D0
 150  CONTINUE
      INTER(1)=0.D0
      INTER(2)=0.D0         
C        
C --- CALCUL DE RESE.C(*,I)
C
      DO 130 I = 1,NDIM
        DO 140 K = 1,NDIM
          VECTT(I) = RESE(K)*MPROJ(K,I) + VECTT(I)
  140   CONTINUE
  130 CONTINUE
C
C --- DDL DE DEPLACEMENT DE LA SURFACE CINEMATIQUE
C-----A) PARTIE "CLASSIQUE"

      DO 10 I = 1,NNES
        DO 20 J = 1,NDIM
          II = (3*NDIM)*(I-1)+J
           VTMP(II) =-JACOBI*HPG*COEFFF*LAMBDA*VECTT(J)*FFES(I)
   20   CONTINUE
   10 CONTINUE
C
C-----B) PARTIE "ENRICHIE"

      DO 30 I = 1,NNES
        DO 40 J = 1,NDIM
          II = (3*NDIM)*(I-1)+NDIM+J
          VTMP(II) = JACOBI*HPG*COEFFF*LAMBDA*VECTT(J)*FFES(I)
   40   CONTINUE
   30 CONTINUE
C
C --- DDL DES DEPLACEMENTS DE LA SURFACE GEOMETRIQUE
C-----A) PARTIE "CLASSIQUE"

        DO 50 I = 1,NNM
          DO 60 J = 1,NDIM
            II = NNES*3*NDIM+(NNE-NNES)*NDIM+2*NDIM*(I-1)+J
            VTMP(II) = JACOBI*HPG*COEFFF*LAMBDA*VECTT(J)*FFMA(I)
   60     CONTINUE
   50   CONTINUE
  
C-----A) PARTIE "ENRICHIE"

        DO 70 I = 1,NNM
          DO 80 J = 1,NDIM
            II = NNES*3*NDIM+(NNE-NNES)*NDIM+2*NDIM*(I-1)+NDIM+J
            VTMP(II) = JACOBI*HPG*COEFFF*LAMBDA*VECTT(J)*FFMA(I)
   80     CONTINUE
   70   CONTINUE   
C
C --- DDL DES MULTIPLICATEURS DE FROTTEMENT (DE LA SURFACE CINEMATIQUE)
C
      IF (NDIM.EQ.2) THEN
        DO 90 I = 1,2
          INTER(1)=(DEPLE(2*NDIM+1+1)*TAU1(I)-RESE(I))*TAU1(I)+INTER(1)
   90   CONTINUE
      ELSE IF (NDIM.EQ.3)THEN
        DO 100 I = 1,3
          INTER(1)=(DEPLE(2*NDIM+1+1)*TAU1(I)+
     &              DEPLE(2*NDIM+1+2)*TAU2(I)-RESE(I))*TAU1(I)+INTER(1)
          INTER(2)=(DEPLE(2*NDIM+1+1)*TAU1(I)+
     &              DEPLE(2*NDIM+1+2)*TAU2(I)-RESE(I))*TAU2(I)+INTER(2)
  100   CONTINUE
      END IF  
      DO 110 I = 1,NNC
        DO 120 J = 1,NDIM-1
          INI=XOULA(CFACE,NFAES,I,IAINES,TYPMA)
          CALL XPLMA2(NDIM,NNE,NNES,INI,PLI)
          II = PLI+J
            VTMP(II) =
     &      JACOBI*HPG*COEFFF*INTER(J)*LAMBDA*FFPC(I)/COEFFA
  120   CONTINUE
  110 CONTINUE
C
      END
