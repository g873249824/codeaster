      SUBROUTINE XMVEF1 (NDIM,NNE,NNES,NNC,NNM,NFAES,CFACE,
     &                  HPG,FFPC,FFES,FFMA,JACOBI,IAINES,    
     &                  LAMBDA,COEFFA,COEFFF,TAU1,TAU2,
     &                  RESE,MPROJ,LAMBD,COEFCA,JEU,TYPMA,
     &                  SINGE,SINGM,RRE,RRM,NVIT,INADH,NDLS,
     &                  VTMP)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/12/2009   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C
      IMPLICIT NONE
      INTEGER  NDIM,NNC,NNE,NNES,NNM,NFAES,IAINES,CFACE(5,3)
      INTEGER  SINGE,SINGM,NVIT,INADH,NDLS
      REAL*8   HPG,FFPC(9),FFES(9),FFMA(9),JACOBI
      REAL*8   LAMBDA,COEFFF,COEFFA,RRE,RRM,COEFCA,JEU
      REAL*8   TAU1(3),TAU2(3),RESE(3),MPROJ(3,3),LAMBD(3),VTMP(168)
      CHARACTER*8  TYPMA
C
C ROUTINE APPELLEE PAR : TE0367
C ----------------------------------------------------------------------
C
C CALCUL DU SECOND MEMBRE POUR LE FROTTEMENT
C CAS AVEC CONTACT
C
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  NNE    : NOMBRE DE NOEUDS DE LA MAILLE ESCLAVE
C IN  NNES   : NOMBRE DE NOEUDS SOMMETS DE LA MAILLE ESCLAVE
C IN  NNC    : NOMBRE DE NOEUDS DE CONTACT
C IN  NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C IN  NFAES  : NUMERO DE LA FACETTE DE CONTACT ESCLAVE
C IN  CFACE  : MATRICE DE CONECTIVITE DES FACETTES DE CONTACT
C IN  HPG    : POIDS DU POINT INTEGRATION DU POINT DE CONTACT
C IN  FFPC   : FONCTIONS DE FORME DU POINT DE CONTACT DANS ELC
C IN  FFES   : FONCTIONS DE FORME DU POINT DE CONTACT DANS ESC
C IN  FFMA   : FONCTIONS DE FORME DE LA PROJECTION DU PTC DANS MAIT
C IN  JACOBI : JACOBIEN DE LA MAILLE AU POINT DE CONTACT
C IN  IAINES : POINTEUR VERS LE VECT DES ARRETES ESCLAVES INTERSECTEES
C IN  LAMBDA : VALEUR DU SEUIL_INIT
C IN  COEFFA : COEF_REGU_FROT
C IN  COEFFF : COEFFICIENT DE FROTTEMENT DE COULOMB
C IN  TAU1   : PREMIERE TANGENTE
C IN  TAU2   : SECONDE TANGENTE
C IN  RESE   : PROJECTION DE LA BOULE UNITE POUR LE FROTTEMENT
C IN  MPROJ  : MATRICE DE L'OPERATEUR DE PROJECTION
C IN  LAMBD  : LAGRANGE DE CONTACT AU POINT D'INTÈGRATION
C IN  TYPMA  : NOM DE LA MAILLE ESCLAVE D'ORIGINE (QUADRATIQUE)
C IN  SINGE  : NOMBRE DE FONCTION SINGULIERE ESCLAVE
C IN  SINGM  : NOMBRE DE FONCTION SINGULIERE MAITRE
C IN  RRE    : SQRT LST ESCLAVE
C IN  RRM    : SQRT LST MAITRE
C IN  NVIT   : POINT VITAL OU PAS
C IN  INADH  : POINT ADHERENT OU PAS
C I/O VTMP   : VECTEUR SECOND MEMBRE ELEMENTAIRE DE CONTACT/FROTTEMENT
C ----------------------------------------------------------------------
      INTEGER   I, J, K, II, INI, PLI, XOULA
      REAL*8    VECTT(3), TT(2), VV, T
C ----------------------------------------------------------------------
C
C --- INITIALISATIONS
C
      DO 100 I = 1,3
        VECTT(I) = 0.D0
 100  CONTINUE
      DO 110 I = 1,2
        TT(I) = 0.D0
 110  CONTINUE       
C        
C --- CALCUL DE RESE.C(*,I)
C
      DO 120 I = 1,NDIM
        DO 130 K = 1,NDIM
          VECTT(I) = RESE(K)*MPROJ(K,I) + VECTT(I)
  130   CONTINUE
  120 CONTINUE
C
C --- CALCUL DE T.(T-P)
C
      DO 140 I = 1,NDIM
        T = LAMBD(1+1)*TAU1(I)+LAMBD(1+2)*TAU2(I)-RESE(I)
        TT(1)= T*TAU1(I)+TT(1)
        IF (NDIM.EQ.3) TT(2)= T*TAU2(I)+TT(2)
  140 CONTINUE
C
C --------------------- CALCUL DE [L1_FROT]-----------------------------
C
      IF (NNM.NE.0) THEN
C
      DO 10 J = 1,NDIM
        DO 20 I = 1,NNES
C --- BLOCS ES,CL ; ES,EN ; (ES,SI)
          VV = JACOBI*HPG*COEFFF*(LAMBD(1)-COEFCA*JEU)*VECTT(J)*FFES(I)
          II = NDLS*(I-1)+J
          VTMP(II) = -VV
          II = II + NDIM
          VTMP(II) = VV
          DO 25 K = 1,SINGE
            II = II + NDIM
            VTMP(II) = RRE * VV
   25     CONTINUE
   20   CONTINUE
        DO 30 I = 1,NNM
C --- BLOCS MA,CL ; MA,EN ; (MA,SI)
          VV = JACOBI*HPG*COEFFF*(LAMBD(1)-COEFCA*JEU)*VECTT(J)*FFMA(I)
          II = NDLS*NNES+NDIM*(NNE-NNES) +
     &          (2+SINGM)*NDIM*(I-1)+J
          VTMP(II) = VV
          II = II + NDIM
          VTMP(II) = VV
          DO 35 K = 1,SINGM
            II = II + NDIM
            VTMP(II) = RRM * VV
   35     CONTINUE
   30   CONTINUE 
   10 CONTINUE
      ELSE
C
      DO 60 J = 1,NDIM
        DO 70 I = 1,NNES
C --- BLOCS ES,SI
          VV = JACOBI*HPG*COEFFF*(LAMBD(1)-COEFCA*JEU)*VECTT(J)*FFES(I)
          II = NDLS*(I-1)+J
          VTMP(II) = RRE * VV
   70   CONTINUE
   60 CONTINUE
      ENDIF
C
C --------------------- CALCUL DE [L3]----------------------------------
C
      IF (NVIT.EQ.1) THEN
      DO 40 I = 1,NNC
        INI=XOULA(CFACE,NFAES,I,IAINES,TYPMA)
        CALL XPLMA2(NDIM,NNE,NNES,NDLS,INI,PLI)
        DO 50 J = 1,NDIM-1
          II = PLI+J
          VTMP(II) = JACOBI*HPG*TT(J)*FFPC(I)/COEFFA
   50   CONTINUE
   40 CONTINUE
      ENDIF
C
      END
