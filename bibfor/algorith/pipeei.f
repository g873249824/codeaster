      SUBROUTINE PIPEEI(NDIM,AXI,NNO1,NNO2,NPG,WREF,VFF1,VFF2,DFFR2,
     &  GEOM,ANG,MAT,COMPOR,LGPG,DDLM,DDLD,DDL0,DDL1,DTAU,VIM,IU,IM,
     &  COPILO)
      
      
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 07/07/2008   AUTEUR LAVERNE J.LAVERNE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21

      IMPLICIT NONE
      LOGICAL AXI
      INTEGER NDIM,NNO1,NNO2,NPG,MAT,LGPG,IU(3,18),IM(3,9)
      REAL*8  VFF1(NNO1,NPG),VFF2(NNO2,NPG),GEOM(NDIM,NNO2),WREF(NPG)
      REAL*8  DDLM(2*NNO1*NDIM+NNO2*NDIM),DDLD(2*NNO1*NDIM+NNO2*NDIM)
      REAL*8  DDL0(2*NNO1*NDIM+NNO2*NDIM),DDL1(2*NNO1*NDIM+NNO2*NDIM)
      REAL*8  VIM(LGPG,NPG),DFFR2(NDIM-1,NNO2,NPG),ANG(*)
      REAL*8  DTAU, COPILO(5,NPG) 
      CHARACTER*16 COMPOR
       
C-----------------------------------------------------------------------
C
C  PILOTAGE PRED_ELAS POUR LES ELEMENTS D'INTERFACE
C    
C-----------------------------------------------------------------------
      INTEGER G,N,I,J,KK
      REAL*8  MUP(3),SUP(3),MUD(3),SUD(3),WG,B(3,3,18)
      REAL*8  R8VIDE
C-----------------------------------------------------------------------

 
      CALL R8INIR(3,0.D0,SUP,1)
      CALL R8INIR(3,0.D0,SUD,1)
      CALL R8INIR(3,0.D0,MUP,1)
      CALL R8INIR(3,0.D0,MUD,1)
      CALL R8INIR(5*NPG,0.D0,COPILO,1)
  
      DO 10 G=1,NPG
              
C -- INITIALISATION DES ELEMENTS CINEMATIQUES

        CALL EICINE(NDIM,AXI,NNO1,NNO2,VFF1(1,G),VFF2(1,G),WREF(G),
     &              DFFR2(1,1,G),GEOM,ANG,WG,B)

        DO 150 I = 1,NDIM
          SUP(I) = 0.D0
          SUD(I) = 0.D0
          DO 160 J = 1,NDIM
          DO 161 N = 1,2*NNO1
            KK = IU(J,N)
            SUP(I) = SUP(I) + B(I,J,N) * (DDLM(KK)+DDLD(KK)+DDL0(KK))
            SUD(I) = SUD(I) + B(I,J,N) * DDL1(KK)
 161      CONTINUE
 160      CONTINUE
 150    CONTINUE
 
        DO 170 I = 1,NDIM
          MUP(I) = 0.D0
          MUD(I) = 0.D0
          DO 180 N = 1,NNO2
            KK = IM(I,N)
            MUP(I) = MUP(I) + VFF2(N,G) * (DDLM(KK)+DDLD(KK)+DDL0(KK))
            MUD(I) = MUD(I) + VFF2(N,G) * DDL1(KK)
 180      CONTINUE
 170    CONTINUE


C -- APPEL DU PILOTAGE PRED_ELAS SPECIFIQUE A LA LOI DE COMPORTEMENT

        COPILO(5,G) = R8VIDE()
        IF (COMPOR.EQ.'CZM_TAC_MIX') THEN
          CALL PIPETC(MAT,SUP,SUD,MUP,MUD,VIM(1,G),DTAU,COPILO(1,G))
        ELSEIF (COMPOR.EQ.'CZM_OUV_MIX') THEN
          CALL PIPEOU(MAT,SUP,SUD,MUP,MUD,VIM(1,G),DTAU,COPILO(1,G))
        ELSE
          CALL U2MESS('F','MECANONLINE_59')   
        END IF

  10  CONTINUE
   
      END
