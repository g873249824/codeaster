      SUBROUTINE XPOSEP(MO,MALINI,MAILN,MAILC,MAILX,
     &                               NSETOT,NNNTOT,NCOTOT )

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 15/05/2007   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT

      IMPLICIT NONE

      CHARACTER*8   MO,MALINI
      CHARACTER*24  MAILN,MAILC,MAILX
C
C   SEPARATION DES MAILLES DE MALINI EN 3 GROUPES
C              - MAILN : MAILLES NON AFFECTEES D'UN MODELE
C              - MAILC : MAILLES NON SOUS-DECOUPEES (CLASSIQUE)
C              - MAILX : MAILLES SOUS-DECOUPEES (X-FEM)
C
C   ET CALCUL DU 
C        - NOMBRE TOTAL DE SOUS-ELEMENT (NSETOT)
C        - NOMBRE TOTAL DE NOUVEAUX NOEUDS (NNNTOT) 
C        - LONGUEUR DE LA CONNECTIVITE DES NOUVEAUX NOEUDS (NCOTOT)
C
C   IN
C       MO     : MODELE FISSURE
C       MALINI : MAILLAGE SAIN
C
C   OUT
C       MAILN  : LISTE DES NUMEROS DES MAILLES NON AFFECTEES D'UN MODELE
C       MAILC  : LISTE DES NUMEROS DES MAILLES NON SOUS-DECOUPEES
C       MAILX  : LISTE DES NUMEROS DES MAILLES SOUS-DECOUPEES
C       NSETOT : NOMBRE TOTAL DE SOUS-ELEMENT
C       NNNTOT : NOMBRE TOTAL DE NOUVEAUX NOEUDS
C       NCOTOT : LONGUEUR DE LA CONNECTIVITE DES NOUVEAUX NOEUDS

      INTEGER       IBID,NBMA,ISEPMA,JCESD,JCESL,IAD,IMA,NIT,JCESV,IT
      INTEGER       NBMAN,NBMAC,NBMAX,NGR,IGR,J1,N1,NBELT,IEL,NSETOT
      INTEGER       IMAN,IMAC,IMAX,JMAN,JMAC,JMAX,NNNTOT,NCOTOT,NSE,N
      INTEGER       JTMDIM,NDIME,JTYPM
      CHARACTER*8   K8B
      CHARACTER*19  CES,LIGREL
      CHARACTER*24  SEPMAI,LIEL

C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM,JEXATR
C---------------- FIN COMMUNS NORMALISES  JEVEUX  ----------------------

      CALL JEMARQ()

      CALL DISMOI('F','NB_MA_MAILLA',MALINI,'MAILLAGE',NBMA,K8B,IBID)

C     TABLEAU D'ENTIERS DIMENSIONNÉ AU NOMBRE DE MAILLE DU MAILLAGE 
C     INITIAL, INDIQUANT L'APPARTENANCE DES MAILLES A UN DES 3 GROUPES :
C         - 0 SI LA MAILLE N'EST PAS AFFECTEE D'UN MODELE -> MAILN
C         - 1 SI LA MAILLE N'EST PAS SOUS-DECOUPEE        -> MAILC
C         - 2 SI LA MAILLE EST SOUS-DECOUPEE              -> MAILX
      SEPMAI = '&XPOSEP.SEPMAI'
      CALL WKVECT( SEPMAI, 'V V I', NBMA, ISEPMA )

C     DANS SEPMAI, MISE A -1 DE TOUTES LES MAILLES DU MODELE
C     LES MAILLES SANS MODELE RESTENT A 0
      LIGREL=MO//'.MODELE'
      LIEL=LIGREL//'.LIEL'
      CALL JELIRA(LIEL,'NMAXOC',NGR,K8B)
      DO 100 IGR=1,NGR
        CALL JEVEUO(JEXNUM(LIEL,IGR),'L',J1)
        CALL JELIRA(JEXNUM(LIEL,IGR),'LONMAX',N1,K8B)
        NBELT=N1-1
        DO 110 IEL=1,NBELT
          IMA=ZI(J1-1+IEL)
          ZI(ISEPMA-1+IMA)=-1
 110    CONTINUE
 100  CONTINUE

      CES = '&&XPOSEP.TOPOSE.LON'
      CALL CELCES(MO//'.TOPOSE.LON','V',CES)
      CALL JEVEUO(CES//'.CESD','L',JCESD)
      CALL JEVEUO(CES//'.CESV','L',JCESV)
      CALL JEVEUO(CES//'.CESL','L',JCESL)

      CALL JEVEUO('&CATA.TM.TMDIM','L',JTMDIM)
      CALL JEVEUO(MALINI//'.TYPMAIL','L',JTYPM)

      NBMAC = 0
      NBMAX = 0
      NSETOT = 0
      NNNTOT = 0
      NCOTOT = 0

C     REMPLISSAGE DE SEPMAI POUR LES MAILLES DU MODELE
C     BOUCLE SUR LES MAILLES
      DO 200 IMA = 1,NBMA
C       ON SE RESTREINT AUX MAILLES DU MODELE
        IF (ZI(ISEPMA-1+IMA).EQ.0) GOTO 200

C       RECUPERATION DE NIT
        CALL CESEXI('C',JCESD,JCESL,IMA,1,1,1,IAD)
        IF (IAD.NE.0) THEN
          NIT=ZI(JCESV-1+IAD)
        ELSE
          NIT=0
        ENDIF
C       SI NIT DIFFERENT DE 0 ALORS MAILLE SOUS DECOUPEE
C       SINON, MAILLE CLASSIQUE
        IF (NIT.NE.0) THEN 
          ZI(ISEPMA-1+IMA)= 2
          NBMAX = NBMAX + 1
C         NOMBRE DE SOUS-ELT SUR LA MAILLE
          NSE = 0
          DO 201 IT=1,NIT
            NSE = NSE + ZI(JCESV-1+IAD+IT)
 201      CONTINUE
C         DIMENSION TOPOLOGIQUE DE LA MAILLE
          NDIME= ZI(JTMDIM-1+ZI(JTYPM-1+IMA))

C         AUGMENTATION DE NSETOT AVEC LE NOMBRE DE NSE SUR LA MAILLE
          NSETOT = NSETOT + NSE
C         AUGMENTATION DU NOMBRE DE NOUVEAUX NOEUDS (NNNTOT)
          NNNTOT = NNNTOT + ZI(JCESV-1+IAD+NIT+2)
C         AUGEMENTATION DU NOMBRE DE NOEUDS DANS LA CONNECTIVITE TOT
          NCOTOT = NCOTOT + NSE * (NDIME + 1)
        ELSE 
          ZI(ISEPMA-1+IMA)= 1
          NBMAC = NBMAC + 1
C         N : NOMBRE DE NOEUDS DE LA MAILLE
          CALL JELIRA(JEXNUM(MALINI//'.CONNEX',IMA),'LONMAX',N,K8B)
          NCOTOT = NCOTOT + N
        ENDIF
 200  CONTINUE

C     NOMBRE DE MAILLES SANS MODELE
      NBMAN = NBMA - NBMAC - NBMAX

      CALL U2MESG('I','XFEM_7',1,'SANS MODELISATION',1,NBMAN,0,0.D0)
      CALL U2MESG('I','XFEM_7',1,'NON SOUS-DECOUPEES',1,NBMAC,0,0.D0)
      CALL U2MESG('I','XFEM_7',1,'SOUS-DECOUPEES',1,NBMAX,0,0.D0)

      IMAN = 0
      IMAC = 0
      IMAX = 0

C     CREATION DES 3 GROUPES DE MAILLES DE SORTIE
      IF (NBMAN.NE.0) CALL WKVECT( MAILN, 'V V I', NBMAN, JMAN )
      IF (NBMAC.NE.0) CALL WKVECT( MAILC, 'V V I', NBMAC, JMAC )
      IF (NBMAX.NE.0) CALL WKVECT( MAILX, 'V V I', NBMAX, JMAX )
      DO 300 IMA = 1,NBMA
        IF (ZI(ISEPMA-1+IMA).EQ.0) THEN
          IMAN = IMAN + 1
          ZI(JMAN-1+IMAN) = IMA
        ELSEIF (ZI(ISEPMA-1+IMA).EQ.1) THEN
          IMAC = IMAC + 1
          ZI(JMAC-1+IMAC) = IMA
        ELSEIF (ZI(ISEPMA-1+IMA).EQ.2) THEN
          IMAX = IMAX + 1
          ZI(JMAX-1+IMAX) = IMA
        ENDIF
 300  CONTINUE

      CALL JEDETR(SEPMAI)
      CALL DETRSD('CHAM_ELEM_S',CES)

      CALL JEDEMA()
      END
