      SUBROUTINE  XPOLSN(ELREFP,ENTITE,N,JLSN,JLST,IMA,J,IAD,IGEOM,
     &                   NDIME,NDIM,JCONX1,JCONX2,CO,LSN,LST,D)
      IMPLICIT NONE

      INTEGER     N,J,JLSN,JLST,NDIM,NDIME
      INTEGER     IAD,IGEOM,IMA,JCONX1,JCONX2
      CHARACTER*8 ENTITE,ELREFP
      REAL*8      D,CO(3),LSN,LST

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 06/07/2010   AUTEUR CARON A.CARON 
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C              RECUPERATION DES COORDONNEES RELLES ET 
C                 CALCUL DES LEVEL SET DE L'ENTITE
C
C   IN
C     ELREFP : ÉLÉMENT DE RÉFÉRENCE PARENT
C     ENTITE : NOEUD OU POINT D'INTERSECTION OU POINT MILIEU
C     JLSN   : ADRESSE DU CHAM_NO_S DE LA LEVEL NORMALE
C     JLST   : ADRESSE DU CHAM_NO_S DE LA LEVEL TANGENTE
C     IGEOM  : COORDONNÉES DES NOEUDS DE L'ÉLÉMENT PARENT
C     NDIME  : DIMENSION TOPOLOGIQUE DE LA MAILLE PARENT
C     NDIM   : DIMENSION DU MAILLAGE
C     IMA    : NUMERO DE MAILLE COURANTE PARENT
C     JCONX1 : ADRESSE DE LA CONNECTIVITE DU MAILLAGE SAIN
C              (CONNECTIVITE QUADRATIQUE SI LAGRANGES DE CONTACT
C              AUX ARETES)
C     JCONX2 : LONGUEUR CUMULEE DE LA CONNECTIVITE DU MAILLAGE SAIN
C              (CONNECTIVITE QUADRATIQUE SI LAGRANGES DE CONTACT 
C              AUX ARETES)
C     JCNSV1 : ADRESSE DU CNSV DU CHAM_NO DE DEPLACEMENT 1
C
C   OUT
C     CO     : COORDONNEES DU NOEUD OU DU POINT
C     LSN    : LEVEL SET NORMALE DU NOEUD OU DU POINT
C     LST    : LEVEL SET TANGENTE DU NOEUD OU DU POINT
C     D      : LEVEL SET NORMALE
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     ----- FIN COMMUNS NORMALISES  JEVEUX  --------------------------
C
      REAL*8       R8PREM,FF(N),D1,D2
      INTEGER      I,INO

      CALL JEMARQ()

      IF (ENTITE.EQ.'MILIEU') THEN 
        DO 10 I=1,NDIM
          CO(I)=ZR(IAD-1+I)
 10     CONTINUE

C       FF : FONCTIONS DE FORMES
        CALL XPOFFO(NDIM,NDIME,ELREFP,N,IGEOM,CO,FF)

        LSN = 0.D0
        LST = 0.D0
        DO 20 I=1,N
          INO=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+I-1)
          LSN = LSN + ZR(JLSN-1+INO) * FF(I)
          LST = LST + ZR(JLST-1+INO) * FF(I)
 20     CONTINUE

C       TRAITEMENT DE LSN (POUR COMPARER AVEC CRILSN)
        D1=0.D0
        DO 22 I=1,N
          INO=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+I-1)
          D2=ABS(LSN-ZR(JLSN-1+INO))
          D1=MAX(D1,D2)
 22     CONTINUE

        IF (D1.GT.R8PREM()) THEN
          D=LSN/D1     
        ELSE
          D=LSN
        ENDIF

      ELSEIF (ENTITE.EQ.'NOEUD') THEN 
        INO=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+J-1)
        CO(1)=ZR(IAD-1+3*(INO-1)+1)
        CO(2)=ZR(IAD-1+3*(INO-1)+2)
        CO(3)=ZR(IAD-1+3*(INO-1)+3)

        LSN = ZR(JLSN-1+INO)
        LST = ZR(JLST-1+INO)
        D=-1.D0

      ELSEIF (ENTITE.EQ.'POINT') THEN 
        DO 11 I=1,NDIM
          CO(I)=ZR(IAD-1+I)
 11     CONTINUE

C       FF : FONCTIONS DE FORMES
        CALL XPOFFO(NDIM,NDIME,ELREFP,N,IGEOM,CO,FF)

        LSN = 0.D0
        LST = 0.D0
        DO 21 I=1,N
          INO=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+I-1)
          LSN = LSN + ZR(JLSN-1+INO) * FF(I)
          LST = LST + ZR(JLST-1+INO) * FF(I)
 21     CONTINUE
        D=-1.D0
      ENDIF

      CALL JEDEMA()

      END
