      SUBROUTINE IRMOPR ( CHAMNS, NBNOEC, LINOEC, NONVPR )
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER LINOEC(*),NBNOEC
      CHARACTER*19 CHAMNS,NONVPR
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 24/09/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C ----------------------------------------------------------------------
C  IMPR_RESU - MODIFICATION DU PROFIL
C  -    -      --              --
C ----------------------------------------------------------------------
C
C  CETTE ROUTINE SERT A MODIFIER LE PROFIL POUR LES CHAMPS AUX NOEUDS
C   DANS LE CAS OU CERTAINS NOEUDS DEVRAIENT ETRE EXCLUS
C   EXEMPLE : POUR LES QUAD9, LE CHAMP DEPL N'A PAS DE VALEUR SUR LE
C             DERNIER NOEUD MILIEU
C
C IN  :
C   CHAMNS  K19  NOM DU CHAM_NO_S A ANALYSER
C   NBNOEC  I    NOMBRE DE NOEUDS DEJA PRESENTS DANS LE PROFIL
C   LINOEC  I*   LISTE DES NOEUDS DANS LE PROFIL
C
C OUT :
C   NONVPR  K19  NOM DE L'OBJET JEVEUX CONTENANT LE PROFIL MODIFIE
C                 EN PREMIERE POSITION ON TROUVE LA TAILLE DU PROFIL
C                 SI ON A RETIRE AUCUN NOEUD PAR RAPPORT AU MAILLAGE
C                 INITIAL ALORS ON N'A PAS BESOIN DE PROFIL
C                 => TAILLE DU PROFIL = 0
C RESPONSABLE SELLENET N.SELLENET
C     ------------------------------------------------------------------
C
      INTEGER NBNOTO,NBCMPT,JCNSL,JCNSD,IAUX,INO,JNVPRO,NBNONP
C
      CALL JEMARQ()
C
      CALL JEVEUO(CHAMNS//'.CNSL','L',JCNSL)
      CALL JEVEUO(CHAMNS//'.CNSD','L',JCNSD)
      NBCMPT=ZI(JCNSD+1)
      IF ( NBNOEC.EQ.0 ) THEN
        NBNOTO=ZI(JCNSD)
      ELSE
        NBNOTO=NBNOEC
      ENDIF
C
      CALL WKVECT(NONVPR,'V V I',NBNOTO+1,JNVPRO)
C
      NBNONP=0
      IF ( NBNOEC.EQ.0 ) THEN
        DO 10, INO=1,NBNOTO
          IF ( ZL(JCNSL+(INO-1)*NBCMPT) ) THEN
            ZI(JNVPRO+NBNONP+1)=INO
            NBNONP=NBNONP+1
          ENDIF
  10    CONTINUE
        CALL ASSERT(NBNONP.LE.NBNOTO)
        IF ( NBNONP.EQ.NBNOTO ) THEN
          ZI(JNVPRO)=0
        ELSE
          ZI(JNVPRO)=NBNONP
        ENDIF
      ELSE
        DO 20, IAUX=1,NBNOTO
          INO=LINOEC(IAUX)
          IF ( ZL(JCNSL+(INO-1)*NBCMPT) ) THEN
            ZI(JNVPRO+NBNONP+1)=INO
            NBNONP=NBNONP+1
          ENDIF
  20    CONTINUE
        CALL ASSERT(NBNONP.LE.NBNOEC)
        ZI(JNVPRO)=NBNONP
      ENDIF
C
      CALL JEDEMA()
C
      END
