      SUBROUTINE ASCNBE(RM, RC, NSEP, COORXG, COORXD, BG, BD, COORYI, 
     +                  COORYS, BI, BS, INDSEX, INDSEY, IABSC1, IORDO1,
     +                  DNX, DNY, NZONEX, NZONEY, NLX, NLY) 
C
      IMPLICIT NONE  
C
      REAL*8  COORXG(*),COORXD(*),BD(*),BG(*),BI(*),BS(*),RM,RC,
     +        COORYI(*),COORYS(*),DNX(2,*),DNY(2,*)
      INTEGER INDSEX(*),INDSEY(*),IABSC1(*),IORDO1(*),NSEP,NZONEX,
     +        NZONEY,NLX(*),NLY(*) 
C      
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 08/03/99   AUTEUR AUBHHMB M.BONNAMY 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C TOLE  CRP_21
C     MACR_ASCOUF_MAIL
C
C CALCUL DU NOMBRE D'ELEMENTS LONGI ET CIRCONF. DANS LES SOUS-EPAISSEURS
C
C-----------------DONNEES FOURNIES PAR L'UTILISATEUR--------------------
C
C     RM    = RAYON MOYEN DU COUDE
C     RC    = RAYON DE CINTRAGE DU COUDE
C     NSEP  = NOMBRE DE SOUS-EPAISSEURS
C     COORXG = ABSCISSE DU BORD GAUCHE DE LA SOUS-EPAISSEUR I
C     COORXD = ABSCISSE DU BORD DROIT DE LA SOUS-EPAISSEUR I
C     BG = ABSCISSE DU BORD GAUCHE DE LA ZONE CIRCONF J
C     BD = ABSCISSE DU BORD DROIT DE LA ZONE CIRCONF J
C     COORYI = ORDONNEE DU BORD INFERIEUR DE LA SOUS-EPAISSEUR I
C     COORYS = ORDONNEE DU BORD SUPERIEUR DE LA SOUS-EPAISSEUR I
C     BI = ORDONNEE DU BORD INFERIEUR DE LA ZONE LONGI J
C     BS = ORDONNEE DU BORD SUPERIEUR DE LA ZONE LONGI J
C     INDSEX = NUMERO DE SOUS-EPAISSEUR CONTENU DANS LA ZONE CIRCONF J
C     INDSEY = NUMERO DE SOUS-EPAISSEUR CONTENU DANS LA ZONE LONGI J
C     IABSC1 = CORRESPONDANCE ABSCISSE CURVILIGNE CIRCONF. SOUS-EP. I
C     IORDO1 = CORRESPONDANCE ABSCISSE CURVILIGNE LONGIT. SOUS-EP. I
C     DNX = DENSITE ET NOMBRE D'ELEMENTS CIRCONF. DE LA ZONE J
C     DNY = DENSITE ET NOMBRE D'ELEMENTS LONGIT. DE LA ZONE J
C     NZONEX = NOMBRE DE ZONES CIRCONFERENTIELLES
C     NZONEY = NOMBRE DE ZONES LONGITUDINALES     
C     
C----------------------DONNEES FOURNIES PAR ASTER-----------------------
C
C     NLX = NOMBRE TOTAL D'ELEMENTS CIRCONF. DE LA SOUS-EPAISSEUR K
C     NLY = NOMBRE TOTAL D'ELEMENTS LONGIT. DE LA SOUS-EPAISSEUR K
C     
C ----------------------------------------------------------------------
C 
      REAL*8  R8PI,PI,RNBEL,RNBEL2
      INTEGER IFM,IUNIFI,L,J,K,NBEL
C      
      PI = R8PI()
      IFM = IUNIFI('MESSAGE')
C      
      DO 10 L=1,NSEP
         NLX(L) = 0
         NLY(L) = 0
 10   CONTINUE
C
      WRITE(IFM,*)   
      WRITE(IFM,*) 'DETERMINATION DU NOMBRE D''ELEMENTS DANS LES SOUS-'
     &            //'EPAISSEURS :'
      WRITE(IFM,*) '--------------------------------------------------'
     &            //'----------'
C
      DO 20 J=1,NZONEX
C
          IF (INDSEX(J).NE.0) THEN
C
C            CALCUL AU PASSAGE DU NOMBRE D'ELEMENTS SUR
C            CHAQUE ZONE CIRCONFERENTIELLEMENT
C          
             RNBEL = (BD(J)-BG(J))*360.D0/(DNX(1,J)*2.D0*PI*RM)
             RNBEL2 = RNBEL - INT(RNBEL)
             IF (RNBEL2.LE.0.5D0) THEN
                 NBEL = INT(RNBEL)
             ELSE
                 NBEL = INT(RNBEL)+1
             END IF
             IF (NBEL.LE.1) NBEL = 2 
C                       
             DO 30 K=1,NSEP
C
C            CALCUL AU PASSAGE DU NOMBRE D'ELEMENTS SUR
C            CHAQUE SOUS-EPAISSEUR CIRCONFERENTIELLEMENT
C
                L = IABSC1(K)
                IF ( ( (COORXG(L).LT.COORXD(L)) .AND. BG(J).GE.COORXG(L)
     &                .AND. BD(J).LE.COORXD(L) ) 
     &        .OR. ( (COORXG(L).GT.COORXD(L)) .AND. ( BG(J).LE.COORXG(L)
     &                .OR. BD(J).GE.COORXD(L) ) ) ) THEN
                   NLX(K) = NLX(K) + NBEL
                   WRITE(IFM,*) 'SOUS-EP NO ',K, ' ZONE CIRC. NO ',J,
     &                         ' NB ELEM. = ',NBEL
C
                END IF
C                
 30          CONTINUE             
             
          END IF
C
 20   CONTINUE
C
      DO 40 J=1,NZONEY
C
           IF (INDSEY(J).NE.0) THEN
C           
C            CALCUL AU PASSAGE DU NOMBRE D'ELEMENTS SUR
C            CHAQUE ZONE LONGITUDINALEMENT
C     
             RNBEL = (BS(J)-BI(J))*360.D0/(DNY(1,J)*2.D0*PI*RC)
             RNBEL2 = RNBEL - INT(RNBEL)
             IF (RNBEL2.LE.0.5D0) THEN
                NBEL = INT(RNBEL)
             ELSE
                NBEL = INT(RNBEL)+1
             END IF
             IF (NBEL.LE.1) NBEL = 2
    
             DO 50 K=1,NSEP
C           
C            CALCUL AU PASSAGE DU NOMBRE D'ELEMENTS SUR
C            CHAQUE ZONE LONGITUDINALEMENT
C
               L = IORDO1(K)
               IF ( (  BI(J).GE.COORYI(L)
     &                .AND. BS(J).LE.COORYS(L) ) ) THEN
                  NLY(K) = NLY(K) + NBEL
                  WRITE(IFM,*) 'SOUS-EP NO ',K, ' ZONE LONGI NO ',J,
     &                        ' NB ELEM. = ',NBEL
               END IF  
C
 50          CONTINUE
C
           END IF
C
 40   CONTINUE
C
      DO 60 K=1,NSEP
C
        WRITE(IFM,*)'SOUS-EP NO ',K, 
     &             ' NBE TOTAL ELEMENTS CIRCONF. : ',NLX(K)
        WRITE(IFM,*)'SOUS-EP NO ',K, 
     &             ' NBE TOTAL ELEMENTS LONGI : ',NLY(K) 
C
C         STOCKAGE DANS UN FICHIER TEMPORAIRE DU NBE DE NOEUDS 
C         LONGI ET CIRC. EN VUE DE POST-TRAITEMENT DANS FICHIER COM3
C
C          IF (.NOT.ZAXI(K)) THEN
C              WRITE(MTPC3,*) 1,2*NLX(K)+1,2*NLY(K)+1
C          ELSE
C              WRITE(MTPC3,*) 0,0,0    
C          END IF
C           
 60   CONTINUE
C
      END
