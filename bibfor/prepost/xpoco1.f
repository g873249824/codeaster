      SUBROUTINE XPOCO1(DIRMA,NBMA,DIRNO,NBNO,MA1,MA2,JNIVGR)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 19/10/2010   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT

      IMPLICIT NONE

      CHARACTER*8   MA1,MA2
      INTEGER       NBMA,DIRMA(NBMA),NBNO,DIRNO(NBNO),JNIVGR
C
C   COPIE DANS LE MAILLAGE MA2 DES MAILLES ET DES NOEUDS DU MAILLAGE MA1
C   CONTENUS DANS LES TABLEAUX D'INDIRECTION DIRMA ET DIRNO
C
C   IN
C       DIRMA : TABLEAU DE CORRESPONDANCE DES NUMEROS DE MAILLES
C       DIRNO : TABLEAU DE CORRESPONDANCE DES NUMEROS DE NOEUDS
C       NBMA  : LONGUEUR DE DIRMA
C       NBNO  : LONGUEUR DE DIRNO
C       MA1   : NOM DU MAILLAGE SAIN
C      JNIVGR : ADRESSE DU VECTEUR DE REMPLISSAGE DES GROUP_MA DE MAXFEM
C
C   OUT
C       MA2   : NOM DU MAILLAGE FISSURE


C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM,JEXATR
C---------------- FIN COMMUNS NORMALISES  JEVEUX  ----------------------

      INTEGER       JTYPM1,JTYPM2,I,J,IBID,L1,L2,INO1,IRET,NBGN,IAGNO
      INTEGER       IGEOMR,IADESC,IAREFE,IACOO1,IACOO2,IACON1,N,IACON2
      INTEGER       INO2,NBGM2,I1,I2,IAGMA1,IAGMA2,N1,N2,IMA
      CHARACTER*8   LICMPR(3),NOMA2,NONO2,K8BID,NOGMA

      CALL JEMARQ()

C     RECUP DES .TYPMAIL, .COORDO DU MAILLAGE 1 ET 2
      CALL JEVEUO(MA1//'.TYPMAIL','L',JTYPM1)
      CALL JEVEUO(MA2//'.TYPMAIL','E',JTYPM2)
      CALL JEVEUO(MA1//'.COORDO    .VALE','L',IACOO1)
      CALL JEVEUO(MA2//'.COORDO    .VALE','E',IACOO2)

      CALL JEEXIN(MA2//'.GROUPENO',IRET)
      NBGN = 0
      IF (IRET.GT.0) CALL JELIRA(MA2//'.GROUPENO','NUTIOC',NBGN,K8BID)

      CALL JEEXIN(MA2//'.GROUPEMA',IRET)
      NBGM2 = 0
      IF (IRET.GT.0) CALL JELIRA(MA2//'.GROUPEMA','NUTIOC',NBGM2,K8BID)

C     ---------------------------------------------------------------
C     COPIE DES VECTEURS
C     ---------------------------------------------------------------

C     .NOMMAI ET .TYPMAIL
      DO 100 I=1,NBMA
        IF (DIRMA(I).NE.0) THEN
          CALL JENUNO(JEXNUM(MA1//'.NOMMAI',DIRMA(I)),NOMA2)
          CALL JECROC(JEXNOM(MA2//'.NOMMAI',NOMA2))
          ZI(JTYPM2-1+DIRMA(I)) = ZI(JTYPM1-1+I)
        ENDIF
 100  CONTINUE

C     .NOMNOE
      DO 200 I=1,NBNO
        IF (DIRNO(I).NE.0) THEN
          CALL JENUNO(JEXNUM(MA1//'.NOMNOE',DIRNO(I)),NONO2)
          CALL JECROC(JEXNOM(MA2//'.NOMNOE',NONO2))
        ENDIF
 200  CONTINUE

C     .COORDO
      DO 300 I=1,NBNO
        IF (DIRNO(I).NE.0) THEN
          DO 310 J=1,3
            ZR(IACOO2-1+3*(DIRNO(I)-1)+J)=ZR(IACOO1-1+3*(I-1)+J)
 310      CONTINUE
        ENDIF
 300  CONTINUE

C     .CONNEX
      DO 400 I=1,NBMA
        IF (DIRMA(I).NE.0) THEN
          CALL JEVEUO(JEXNUM(MA1//'.CONNEX',I),'L',IACON1)
          CALL JELIRA(JEXNUM(MA1//'.CONNEX',I),'LONMAX',N,K8BID)
          CALL JEECRA(JEXNUM(MA2//'.CONNEX',DIRMA(I)),'LONMAX',N,K8BID)
          CALL JEVEUO(JEXNUM(MA2//'.CONNEX',DIRMA(I)),'E',IACON2)
          DO 410 J=1,N
            INO1 = ZI(IACON1-1+J)
            INO2 = DIRNO(INO1)
            ZI(IACON2-1+J)=INO2
 410      CONTINUE
        ENDIF
 400  CONTINUE

C     .GROUPENO
      DO 500 I=1,NBGN
        CALL JEVEUO(JEXNUM(MA2//'.GROUPENO',I),'E',IAGNO)
        CALL JELIRA(JEXNUM(MA2//'.GROUPENO',I),'LONUTI',N,K8BID)
        DO 501 J=1,N
          IF (DIRNO(ZI(IAGNO-1+J)).NE.0) THEN
            ZI(IAGNO-1+J)=DIRNO(ZI(IAGNO-1+J))
          ENDIF
 501    CONTINUE
 500  CONTINUE

C     .GROUPEMA
      DO 600 I2=1,NBGM2
        CALL JENUNO(JEXNUM(MA2//'.GROUPEMA',I2),NOGMA)
        CALL JENONU(JEXNOM(MA1//'.GROUPEMA',NOGMA),I1)
        CALL JEVEUO(JEXNUM(MA1//'.GROUPEMA',I1),'L',IAGMA1)
        CALL JELIRA(JEXNUM(MA1//'.GROUPEMA',I1),'LONUTI',N1,K8BID)
        CALL JEVEUO(JEXNUM(MA2//'.GROUPEMA',I2),'E',IAGMA2)
        CALL JELIRA(JEXNUM(MA2//'.GROUPEMA',I2),'LONUTI',N2,K8BID)
        DO 610 I = 1,N1
          IMA = ZI(IAGMA1-1+I)
          IF (DIRMA(IMA).NE.0)  THEN
C           NIVEAU DE REMPLISSAGE DU GROUP_MA
            ZI(JNIVGR-1+I2) = ZI(JNIVGR-1+I2) + 1
            ZI(IAGMA2-1+ZI(JNIVGR-1+I2)) = DIRMA(IMA)
          ENDIF
 610    CONTINUE
        CALL ASSERT(ZI(JNIVGR-1+I2).LE.N2)
 600  CONTINUE

      CALL JEDEMA()
      END
