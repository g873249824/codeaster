      SUBROUTINE IRCHOR(IOCCUR,LERESU,LRESUL,NCHSYM,NNUORD,
     &                  NLICMP,NOVCMP,NNOPAR,NBNOSY,NBORDR,
     &                  NBRCMP,NBCMDU,NBPARA,CODRET)
      IMPLICIT NONE
      INTEGER       IOCCUR,NBNOSY,NBORDR,NBRCMP,NBCMDU,NBPARA,CODRET
      CHARACTER*8   LERESU
      CHARACTER*(*) NCHSYM,NNUORD,NLICMP,NOVCMP,NNOPAR
      LOGICAL       LRESUL
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE SELLENET N.SELLENET
C ----------------------------------------------------------------------
C  IMPR_RESU - CHAMP, NUMEROS D'ORDRE, ...
C  -    -      --               --
C ----------------------------------------------------------------------
C
C  CETTE ROUTINE SCRUTE LES MOTS-CLES TOUT_CHAM, NOM_CMP, ...
C
C IN  :
C   IOCCUR  I    NUMERO D'OCCURENCE DU MOT CLE FACTEUR
C   LERESU  K8   CHAINE CONTENANT SOIT LE NOM DE LA SD RESULTAT SOIT
C                 LE NOM DU CHAMP A IMPRIMER
C   LRESUL  L    BOOLEEN INDIQUANT SI L'UTILISATEUR IMPRIME UN CHAMP
C                 (FALSE) OU UN RESU (TRUE)
C
C IN/OUT :
C   NCHSYM  K*   NOM DE L'OBJET JEVEUX CONTENANT LE NOM DES CHAMPS
C                 A IMPRIMER
C   NNUORD  K*   NOM DE L'OBJET JEVEUX CONTENANT LES NUMEROS D'ORDRE
C   NLICMP  K*   NOM DE L'OBJET JEVEUX CONTENANT LES COMPOSANTES
C   NOVCMP  K*   NOM DE L'OBJET JEVEUX CONTENANT LES NOMS MED
C   NNOPAR  K*   NOM DE L'OBJET JEVEUX CONTENANT LES PARAMETRES
C
C OUT :
C   NBNOSY  I    NOMBRE DE CHAMPS A IMPRIMER
C   NBORDR  I    NOMBRE DE NUMEROS D'ORDRE A IMPRIMER
C   NBRCMP  I    NOMBRE DE COMPOSANTES
C   NBCMDU  I    NOMBRE DE NOMS MED
C   NPARAM  I    NOMBRE DE PARAMETRES (FORMAT 'RESULTAT')
C   CODRET  I    CODE RETOUR (0 SI OK, 1 SINON)
C
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
C
      INTEGER LXLGUT,JNOSY,JNCMED,JPA,JORDR,IARG,N23,IRET,N21,NVCMP
      INTEGER N22,NNRMED,ISY,NNOCHA,NNOCMP,NCHAR,JVCMP,IBID,NPRECI
      INTEGER VALI,NCRIT,INNOSY,JNORDR,ICMP,NBCMPT,GD,IER,NCMPMX,IAD
      INTEGER NTPARA,NNPARA,JCMP,NPARAM
C
      REAL*8 PREC
C
      CHARACTER*1  K1BID
      CHARACTER*3  TOUPAR,TOUCHA
      CHARACTER*8  K8B,RESMED,CRIT,NOMGD
      CHARACTER*16 K16BID
      CHARACTER*19 NOCH19,KNUM
      CHARACTER*24 VALK(6)
      CHARACTER*64 K64BID
C
      LOGICAL AFAIRE
C
      CALL JEMARQ()
C
      AFAIRE = .FALSE.
      CODRET = 0
C
C     --- ECRITURE D'UN CHAM_GD ---
      IF(.NOT.LRESUL) THEN
        NBNOSY = 1
        CALL WKVECT(NCHSYM,'V V K16',NBNOSY,JNOSY)
        CALL WKVECT(NOVCMP,'V V K80',NBNOSY,JNCMED)
C       --- NOM DU CHAM_GD ---
        ZK16(JNOSY)  = LERESU
        NPARAM = 0
        JPA = 1
        NBORDR = 1
        CALL WKVECT(NNUORD,'V V I',NBORDR,JORDR)
        ZI(JORDR) = 1
        CALL GETVTX('RESU','NOM_CHAM_MED' ,IOCCUR,IARG,0,K64BID,N23)
        NBCMDU = - N23
        CALL GETVTX('RESU','NOM_CHAM_MED',IOCCUR,IARG,NBCMDU,
     &              ZK80(JNCMED),IRET)
C
C     --- ECRITURE D'UN RESULTAT_COMPOSE ---
      ELSE
C       --- ON REGARDE QUELS SONT LES NOM_CHAM A IMPRIMER:
        TOUCHA = 'OUI'
        CALL GETVTX('RESU','TOUT_CHAM',IOCCUR,IARG,1,TOUCHA,N21)
        CALL GETVTX('RESU','NOM_CHAM',IOCCUR,IARG,0,K16BID,N22)
        CALL GETVTX('RESU','NOM_CHAM_MED',IOCCUR,IARG,0,K64BID,N23)
        CALL GETVTX('RESU','NOM_RESU_MED',IOCCUR,IARG,0,K8B,NNRMED)
C       *** N22 EST NEGATIF SI L'UTILISATEUR DONNE UNE LISTE DE NOMS
C           (PAR DEFAUT TOUS LES CHAMPS CAR MOT-CLE FACULTATIF)
        IF(ABS(N21)+ABS(N22).EQ.0) N21=1
        IF(N21.GT.0 .AND. TOUCHA.EQ.'OUI'.AND.NNRMED.EQ.0) THEN
C         - ON RECUPERE LES NOMS (ON IMPRIME TOUS LES CHAMPS)
          CALL JELIRA(LERESU//'           .DESC','NOMUTI',
     &                NBNOSY,K1BID)
          CALL WKVECT(NCHSYM,'V V K16',
     &                NBNOSY,JNOSY)
          DO 12 ISY = 1,NBNOSY
            CALL JENUNO(JEXNUM(LERESU//'           .DESC',ISY),
     &                  ZK16(JNOSY-1+ISY))
   12     CONTINUE
        ELSEIF (N21.GT.0 .AND. TOUCHA.EQ.'NON') THEN
          NBNOSY=0
          JNOSY =1
        ELSEIF(N22.LT.0) THEN
          NBNOSY = - N22
          NBCMDU = - N23
          CALL WKVECT(NCHSYM,'V V K16',
     &                NBNOSY,JNOSY)
          CALL WKVECT(NOVCMP,'V V K80',NBNOSY,JNCMED)

C         - ON RECUPERE LA LISTE DES NOMS DONNEE PAR L'UTILISATEUR
          CALL GETVTX('RESU','NOM_CHAM',IOCCUR,IARG,NBNOSY,
     &                ZK16(JNOSY),NNOCHA)
          CALL GETVTX('RESU','NOM_CHAM_MED',IOCCUR,IARG,
     &                NBCMDU,ZK80(JNCMED),IRET)
          IF ((NBCMDU.NE.0).AND.(NBCMDU.NE.NBNOSY)) THEN
            CALL U2MESS('F','PREPOST2_1')
          ENDIF
        ELSEIF(NNRMED.LT.0) THEN
          CALL GETVTX('RESU','NOM_CMP',IOCCUR,IARG,0,K8B,NNOCMP)
          IF(NNOCMP.LT.0) THEN
             VALK(1)='NOM_CMP'
             VALK(2)='NOM_RESU_MED'
             CALL U2MESK('F','MED2_6',2,VALK)
          ENDIF
          CALL JELIRA(LERESU//'           .DESC','NOMUTI',NBCMDU,K8B)
          CALL GETVTX('RESU','NOM_RESU_MED' ,IOCCUR,
     &                IARG,1,RESMED,NNRMED)
          CALL WKVECT(NCHSYM,'V V K16',NBCMDU,JNOSY)
          CALL WKVECT(NOVCMP,'V V K80',NBCMDU,JNCMED)
          NBNOSY=NBCMDU
          DO 13 ISY = 1,NBCMDU
            CALL JENUNO(JEXNUM(LERESU//'           .DESC',ISY),
     &                                      ZK16(JNOSY-1+ISY))
            ZK80(JNCMED+ISY-1)='________'
            NCHAR=LXLGUT(RESMED)
            ZK80(JNCMED+ISY-1)(1:NCHAR)=RESMED(1:NCHAR)
            NCHAR=LXLGUT(ZK16(JNOSY-1+ISY))
            ZK80(JNCMED+ISY-1)(9:8+NCHAR)=ZK16(JNOSY-1+ISY)(1:NCHAR)
   13     CONTINUE
        ENDIF

C       --- ON REGARDE QUELS SONT LES NOM_CMP A IMPRIMER:
        CALL GETVTX('RESU','NOM_CMP',IOCCUR,IARG,0,K8B,NNOCMP)
        IF(NNOCMP.LT.0) THEN
          NVCMP=-NNOCMP
          CALL WKVECT('&&IRCHOR.VERI_NOM_CMP','V V K8',NVCMP,JVCMP)
          CALL GETVTX('RESU','NOM_CMP',IOCCUR,IARG,NVCMP,
     &                ZK8(JVCMP),IBID)
          AFAIRE = .TRUE.
        ENDIF

C       *** NOMS DES CHAMPS DANS ZK16 A PARTIR DE ZK16(JNOSY)
C
C       --- NUMEROS D'ORDRE POUR IOCCUR DU MOT-CLE FACTEUR RESU
        KNUM = NNUORD
C       *** TEST DE PRESENCE DES MOTS CLES PRECISION ET CRITERE
        CALL GETVR8('RESU','PRECISION',IOCCUR,IARG,1,PREC,NPRECI)
        CALL GETVTX('RESU','CRITERE'  ,IOCCUR,IARG,1,CRIT,NCRIT)
C       *** RECUPERATION DES NUMEROS D'ORDRE DE LA STRUCTURE DE
C          DONNEES DE TYPE RESULTAT LERESU A PARTIR DES VARIABLES
C          D'ACCES UTILISATEUR 'NUME_ORDRE','FREQ','INST','NOEUD_CMP'
C           (VARIABLE D'ACCES 'TOUT_ORDRE' PAR DEFAUT)
        CALL RSUTNU(LERESU,'RESU',IOCCUR,KNUM,NBORDR,PREC,CRIT,IRET)
C       *** SI PB ON PASSE AU FACTEUR SUIVANT DE IMPR_RESU
        IF (IRET.NE.0) THEN
          CODRET = 1
          GOTO 9999
        ENDIF
        CALL JEVEUO(KNUM,'L',JORDR)
C
        IF( N22 .LT. 0 ) THEN
          DO 16 INNOSY = 0 , NBNOSY-1
            DO 14 JNORDR = 0 , NBORDR-1
              CALL RSEXCH(' ',LERESU,ZK16(JNOSY+INNOSY),
     &                    ZI(JORDR+JNORDR),NOCH19,IRET)
              IF ( IRET .NE. 0 ) THEN
                VALK (1) = ZK16(JNOSY+INNOSY)
                VALI = ZI(JORDR+JNORDR)
                CALL U2MESG('A', 'POSTRELE_41',1,VALK,1,VALI,0,0.D0)
              ENDIF
 14         CONTINUE
 16       CONTINUE
          IF ( AFAIRE ) THEN
            DO 15 ICMP = 0 , NVCMP-1
              NBCMPT = 0
              DO 17 INNOSY = 0 , NBNOSY-1
                CALL RSEXCH(' ',LERESU,ZK16(JNOSY+INNOSY),ZI(JORDR),
     &                      NOCH19,IRET)
                IF ( IRET .EQ. 0 ) THEN
                  CALL DISMOI('F','NUM_GD',NOCH19,'CHAMP',GD,K8B,IER)
                  CALL JENUNO(JEXNUM('&CATA.GD.NOMGD',GD),NOMGD)
                  IF ( NOMGD .EQ. 'VARI_R' ) THEN
C                   TRAITEMENT PARTICULIER POUR LA GRANDEUR VARI_R
                    NBCMPT = NBCMPT + 1
                    GOTO 17
                  ENDIF
                  CALL JELIRA(JEXNUM('&CATA.GD.NOMCMP',GD),'LONMAX',
     &                        NCMPMX,K1BID)
                  CALL JEVEUO(JEXNUM('&CATA.GD.NOMCMP',GD),'L',IAD)
                  CALL IRVCMP(NCMPMX,ZK8(IAD),ZK8(JVCMP+ICMP),NBCMPT)
                ENDIF
 17           CONTINUE
              IF ( NBCMPT .EQ. 0 ) THEN
                VALK (1) = ZK8(JVCMP+ICMP)
                VALK (2) = K1BID
                CALL U2MESG('A', 'PREPOST5_61',2,VALK,0,0,0,0.D0)
              ENDIF
 15         CONTINUE
          ENDIF
        ENDIF
C
C       --- ON RECHERCHE LES PARAMETRES A ECRIRE ---
C           (UNIQUEMENT SI FORMAT FICHIER = 'RESULTAT')
        TOUPAR = 'NON'
        CALL GETVTX('RESU','TOUT_PARA',IOCCUR,IARG,1,TOUPAR,NTPARA)
        CALL GETVTX('RESU','NOM_PARA' ,IOCCUR,IARG,0,K8B,NNPARA)
        IF(NNPARA.EQ.0 ) NTPARA = 1
        IF(NTPARA.NE.0.AND.TOUPAR.EQ.'NON') THEN
          NPARAM = 0
          JPA =  1
        ELSEIF(NTPARA.NE.0.AND.TOUPAR.EQ.'OUI') THEN
          NPARAM = -1
          JPA =  1
        ELSEIF(NNPARA.NE.0) THEN
          NPARAM = -NNPARA
          CALL WKVECT('&&IRCHOR.NOMUTI_PARA','V V K16',NPARAM,JPA)
          CALL GETVTX('RESU','NOM_PARA',IOCCUR,IARG,
     &                NPARAM,ZK16(JPA),NPARAM)
        ENDIF
      ENDIF
C
C     --- CHOIX DES COMPOSANTES AUX FORMATS ---
C         RESULTAT, CASTEM, MED ET GMSH
      CALL GETVTX('RESU','NOM_CMP',IOCCUR,IARG,0,K8B,NNOCMP)
      IF(NNOCMP.LT.0) THEN
        NBRCMP=-NNOCMP
        CALL WKVECT(NLICMP,'V V K8',NBRCMP,JCMP)
        CALL GETVTX('RESU','NOM_CMP',IOCCUR,IARG,NBRCMP,
     &              ZK8(JCMP),IBID)
      ENDIF
C
C     - VERIFICATION DES PARAMETRES (FORMAT 'RESULTAT')
      CALL IRPARB(LERESU,NPARAM,ZK16(JPA),NNOPAR,
     &            NBPARA)
C
 9999 CONTINUE
      CALL JEDETR ('&&IRCHOR.VERI_NOM_CMP')
      CALL JEDETR ('&&IRCHOR.NOMUTI_PARA')
C
      CALL JEDEMA()
C
      END
