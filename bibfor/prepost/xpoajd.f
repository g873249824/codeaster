      SUBROUTINE  XPOAJD(ELREFP,ENTITE,NNOP,JLSN,JLST,INO,IACOO1,IGEOM,
     &                   NDIME,NDIM,CMP,NBCMP,DDLH,NFE,IMA,JCONX1,
     &                   JCONX2,JCNSV1,JCNSV2,JCNSL2,NBNOC,INNTOT)
      IMPLICIT NONE

      INTEGER     NNOP,INO,JLSN,JLST,IGEOM,NDIM,IACOO1,NDIME
      INTEGER     NBCMP,CMP(NBCMP),NFE,DDLH,IMA,JCONX1,JCONX2,JCNSV1
      INTEGER     JCNSV2,JCNSL2,NBNOC,INNTOT
      CHARACTER*8 ENTITE,ELREFP

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 15/05/2007   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C TOLE CRP_21
C
C     BUT:  CALCUL DES DEPLACEMENTS AUX SOMMENTS DES SOUS-ELEMENTS 

C   IN
C     ELREFP : ÉLÉMENT DE RÉFÉRENCE PARENT
C     ENTITE : NOEUD OU POINT D'INTERSECTION
C     NNOP   : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C     JLSN   : ADRESSE DU CHAM_NO_S DE LA LEVEL NORMALE
C     JLST   : ADRESSE DU CHAM_NO_S DE LA LEVEL TANGENTE
C     INO    : NUMERO DU NOEUD DANS LE MAILLAGE SAIN
C     IACOO1 : SI ENTITE='NOEUD' :
C                  ADRESSE DES COORDONNES DES NOEUDS DU MAILLAGE SAIN 
C              SI ENTITE='POINT' :
C                  ADRESSE DES COORDONNES DES POINTS D'INTERSECTION 
C     IGEOM  : COORDONNÉES DES NOEUDS DE L'ÉLÉMENT PARENT
C     NDIME  : DIMENSION TOPOLOGIQUE DE LA MAILLE PARENT
C     NDIM   : DIMENSION DU MAILLAGE
C     CMP    : POSITION DES DDLS DE DEPL X-FEM DANS LE CHAMP_NO DE DEPL1
C     NBCMP  : NOMBRE DE COMPOSANTES DU CHAMP_NO DE DEPL1
C     DDLH   : NOMBRE DE DDL HEAVISIDE (PAR NOEUD)
C     NFE    : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT (1 A 4)
C     IMA    : NUMERO DE MAILLE COURANTE PARENT
C     JCONX1 : ADRESSE DE LA CONNECTIVITE DU MAILLAGE SAIN
C     JCONX2 : LONGUEUR CUMULEE DE LA CONNECTIVITE DU MAILLAGE SAIN
C     JCNSV1 : ADRESSE DU CNSV DU CHAM_NO DE DEPLACEMENT 1
C     NBNOC  : NOMBRE DE NOEUDS CLASSIQUES DU MAILLAGE FISSURE
C
C   OUT
C     JCNSV2 : ADRESSE DU CNSV DU CHAM_NO DE DEPLACEMENT 2
C     JCNSL2 : ADRESSE DU CNSL DU CHAM_NO DE DEPLACEMENT 2
C     INNTOT : COMPTEUR TOTAL DU NOMBRE DE NOUVEAU NOEUDS CREES

C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM
C     ----- FIN COMMUNS NORMALISES  JEVEUX  --------------------------
C
      REAL*8       HE(2),FF(NNOP),LSN,LST,FE(4)
      REAL*8       R,THETA,CO(3),DEPR(3),R8PREM
      INTEGER      NDOUBL,IBID,ID,I,J,INO1,IAD,IPOS,IG,INO2

      CALL JEMARQ()

C     ATTENTION :  CONVENTION POUR LES DOUBLES :
C     D'ABORD LE NOEUD "-", PUIS LE NOEUD "+"

      IF (ENTITE.EQ.'NOEUD'.AND.ZR(JLSN-1+INO).NE.0.D0) THEN
        NDOUBL=1
        HE(1)=SIGN(1.D0,ZR(JLSN-1+INO))
      ELSE
        NDOUBL=2
        HE(1)=-1.D0
        HE(2)= 1.D0
      ENDIF

      IF (ENTITE.EQ.'NOEUD') THEN 
        CO(1)=ZR(IACOO1-1+3*(INO-1)+1)
        CO(2)=ZR(IACOO1-1+3*(INO-1)+2)
        CO(3)=ZR(IACOO1-1+3*(INO-1)+3)
      ELSEIF (ENTITE.EQ.'POINT') THEN 
        DO 10 I=1,NDIM
          CO(I)=ZR(IACOO1-1+I)
 10     CONTINUE
      ENDIF

C     FF : FONCTIONS DE FORMES AU NOEUD SOMMET OU D'INTERSECTION
      CALL XPOFFO(NDIM,NDIME,ELREFP,NNOP,IGEOM,CO,FF)
      

C     DETERMINIATION DES LEVEL SETS AU NOEUD SOMMET OU D'INTERSECTION
      IF (ENTITE.EQ.'NOEUD') THEN 
        LSN = ZR(JLSN-1+INO)
        LST = ZR(JLST-1+INO)
      ELSEIF (ENTITE.EQ.'POINT') THEN 
        LSN = 0.D0
        LST = 0.D0
        DO 20 J=1,NNOP
          INO1=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+J-1)
          LSN = LSN + ZR(JLSN-1+INO1) * FF(J)
          LST = LST + ZR(JLST-1+INO1) * FF(J)
 20     CONTINUE
      ENDIF


      DO 100 ID=1,NDOUBL
C
        CALL LCINVN(NDIM,0.D0,DEPR)

        IF (NFE.NE.0) THEN
C         FE : FONCTIONS D'ENRICHISSEMENT
          R = SQRT(LSN**2+LST**2)
          IF (R.GT.R8PREM()) THEN
C           LE POINT N'EST PAS SUR LE FOND DE FISSURE
            THETA = HE(ID)*ABS(ATAN2(LSN,LST))
          ELSE
C           LE POINT EST SUR LE FOND DE FISSURE :
C           L'ANGLE N'EST PAS DÉFINI, ON LE MET À ZÉRO
            THETA=0.D0
          ENDIF

          FE(1)=SQRT(R)*SIN(THETA/2.D0)
          FE(2)=SQRT(R)*COS(THETA/2.D0)
          FE(3)=SQRT(R)*SIN(THETA/2.D0)*SIN(THETA)
          FE(4)=SQRT(R)*COS(THETA/2.D0)*SIN(THETA)
        ENDIF

C       CALCUL DE L'APPROXIMATION DU DEPLACEMENT
        DO 200 J=1,NNOP

C         NUMERO DU NOEUD DANS MALINI (MA1)
          INO1=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+J-1)

C         ADRESSE DE LA 1ERE CMP DU DEPLACEMENT DU NOEUD INO1
          IAD=JCNSV1-1+NBCMP*(INO1-1)

          IPOS=0

C         DDLS CLASSIQUES
          DO 201 I=1,NDIM
            IPOS=IPOS+1
            DEPR(I) = DEPR(I) +  FF(J) * ZR(IAD+CMP(IPOS))
 201      CONTINUE
                
C         DDLS HEAVISIDE
          DO 202 I=1,DDLH
            IPOS=IPOS+1
            DEPR(I) = DEPR(I) + HE(ID) * FF(J) *  ZR(IAD+CMP(IPOS))
 202      CONTINUE    
 
C         DDL ENRICHIS EN FOND DE FISSURE
          DO 203 IG=1,NFE
            DO 204 I=1,NDIM
              IPOS=IPOS+1         
              DEPR(I) = DEPR(I) + FE(IG) * FF(J) * ZR(IAD+CMP(IPOS))
 204        CONTINUE    
 203      CONTINUE

 200    CONTINUE

C       ECRITURE DANS LE .VALE2 POUR LE NOEUD INO2
        INNTOT = INNTOT + 1
        INO2 = NBNOC + INNTOT
        DO 210 I=1,NDIM
          ZR(JCNSV2-1+NDIM*(INO2-1)+I)=DEPR(I)      
          ZL(JCNSL2-1+NDIM*(INO2-1)+I)=.TRUE.   
 210    CONTINUE

 100  CONTINUE

      CALL JEDEMA()

      END
