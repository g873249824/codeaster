      SUBROUTINE LRCAME ( NROFIC, NOCHMD, NOMAMD,
     >                    NBVATO, TYPENT, TYPGEO,
     >                    NUMPT, NUMORD, NBCMPV, NCMPVA, NCMPVM,
     >                    NOMGD, NCMPRF, JNOCMP, ADSL, ADSV,
     >                    CODRET )
C_____________________________________________________________________
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 16/10/2002   AUTEUR GNICOLAS G.NICOLAS 
C RESPONSABLE GNICOLAS G.NICOLAS
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C     LECTURE D'UN CHAMP - FORMAT MED
C     -    -       - -            --
C  ATTENTION : CELA NE MARCHE QUE POUR UN CHAMNO ! 
C-----------------------------------------------------------------------
C      ENTREES:
C        NROFIC : UNITE LOGIQUE DU FICHIER MED
C        NOCHMD : NOM MED DU CHAMP A LIRE
C        NOMAMD : NOM MED DU MAILLAGE LIE AU CHAMP A LIRE
C                  SI ' ' : ON SUPPOSE QUE C'EST LE PREMIER MAILLAGE
C                          DU FICHIER 
C        NBVATO : NOMBRE DE VALEURS TOTALES
C        TYPENT : TYPE D'ENTITE AU SENS MED
C        TYPGEO : TYPE DE SUPPORT AU SENS MED
C        NUMPT  : NUMERO DE PAS DE TEMPS
C        NUMORD : NUMERO D'ORDRE DU CHAMP
C        NBCMPV : NOMBRE DE COMPOSANTES VOULUES
C                 SI NUL, ON LIT LES COMPOSANTES A NOM IDENTIQUE
C        NCMPVA : LISTE DES COMPOSANTES VOULUES POUR ASTER
C        NCMPVM : LISTE DES COMPOSANTES VOULUES DANS MED
C        NOMGD  : NOM DE LA GRANDEUR ASSOCIEE AU CHAMP
C        NCMPRF : NOMBRE DE COMPOSANTES DE REFERENCE DU CHAMNOS
C        JNOCMP : ADRESSE DU NOM DES COMPOSANTES DE REFERENCE DU CHAMNOS
C        ADSL, ADSV : ADRESSES DES TABLEAUX DES CHAMPS SIMPLIFIES
C      SORTIES:
C         CODRET : CODE DE RETOUR (0 : PAS DE PB, NON NUL SI PB)
C_____________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NROFIC
      INTEGER ADSL, ADSV
      INTEGER NBVATO, NCMPRF, JNOCMP
      INTEGER NUMPT, NUMORD, NBCMPV
      INTEGER TYPENT, TYPGEO
      INTEGER CODRET
C
      CHARACTER*8 NOMGD
      CHARACTER*32 NOCHMD, NOMAMD
      CHARACTER*(*) NCMPVA, NCMPVM
C
C 0.2. ==> COMMUNS
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX --------------------------
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'LRCAME' )
C
      INTEGER EDLECT
      PARAMETER (EDLECT=0)
      CHARACTER*32 EDNOPF
      PARAMETER ( EDNOPF='                                ' )
C
      INTEGER IAUX
      INTEGER IDFIMD
      INTEGER NBCMFI
      INTEGER NCMPUT
      INTEGER EXISTC
      INTEGER NDIM
      INTEGER LGPROA
      INTEGER IFM, NIVINF
C
      CHARACTER*8 SAUX08
      CHARACTER*24 NUMCMP, NTNCMP, NTUCMP, NTVALE, NMCMFI
      CHARACTER*24 NTPROA
      CHARACTER*32 NOMPRF
      CHARACTER*200 NOFIMD
C
      LOGICAL EXISTM
C
      CALL JEMARQ ( )
C
C====
C 1. PREALABLES
C====
C
C 1.1. ==> RECUPERATION DU NIVEAU D'IMPRESSION
C
      CALL INFMAJ
      CALL INFNIV ( IFM, NIVINF )
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,1001) 'DEBUT DE '//NOMPRO
      ENDIF
 1001 FORMAT(/,10('='),A,10('='),/)
C
C 1.2. ==> NOMS DES TABLEAUX DE TRAVAIL
C               12   345678   9012345678901234
      NUMCMP = '&&'//NOMPRO//'.NUMERO_CMP     '
      NTNCMP = '&&'//NOMPRO//'.N0MCMP         '
      NTUCMP = '&&'//NOMPRO//'.UNITECMP       '
      NTVALE = '&&'//NOMPRO//'.VALEUR         '
      NMCMFI = '&&'//NOMPRO//'.N0MCMP_FICHIER '
      NTPROA = '&&'//NOMPRO//'.PROFIL_ASTER   '
C
C 1.3. ==> NOM DU FICHIER MED
C
      CALL CODENT ( NROFIC, 'G', SAUX08 )
      NOFIMD = 'fort.'//SAUX08
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,*) NOMPRO, ' : NOM DU FICHIER MED : ', NOFIMD
      ENDIF
C
C 1.4. ==> VERIFICATION DE LA BONNE VERSION DU FICHIER MED
C
      CALL EFFOCO ( NOFIMD, CODRET )
      IF ( CODRET.NE.0 ) THEN
        CALL UTDEBM ( 'A', NOMPRO, 'FICHIER ' )
        CALL UTIMPK ( 'S', 'MED : ', 1, NOFIMD )
        CALL UTIMPK ( 'L', 'CHAMP : ', 1, NOCHMD )
        CALL UTIMPI ( 'L', 'ERREUR EFFOCO NUMERO ', 1, CODRET )
        CALL UTFINM ()
        CALL UTMESS ( 'F', NOMPRO, 'SOIT LE FICHIER N''EXISTE PAS,'//
     >             ' SOIT C''EST UNE MAUVAISE VERSION DE MED.' )
      ENDIF
C
C 1.5. ==> VERIFICATION DE L'EXISTENCE DU MAILLAGE CONCERNE
C
C 1.5.1. ==> C'EST LE PREMIER MAILLAGE DU FICHIER
C            ON RECUPERE SON NOM ET SA DIMENSION.
C
      IF ( NOMAMD.EQ.' ' ) THEN
C
        CALL MDEXPM ( NOFIMD, NOMAMD, EXISTM, NDIM, CODRET )
        IF ( .NOT.EXISTM ) THEN
          CALL UTMESS ( 'F', NOMPRO, 'PAS DE MAILLAGE DANS '//NOFIMD )
        ENDIF
C
C 1.5.2. ==> C'EST UN MAILLAGE DESIGNE PAR UN NOM
C            ON RECUPERE SA DIMENSION.
C
      ELSE
C
        CALL MDEXMA ( NOFIMD, NOMAMD, EXISTM, NDIM, CODRET )
        IF ( .NOT.EXISTM ) THEN
          CALL UTMESS ( 'F', NOMPRO,
     >   'LE MAILLAGE '//NOMAMD//' EST INCONNU DANS '//NOFIMD )
        ENDIF
C
      ENDIF
C
C 1.6. ==> LE CHAMP EXISTE-T-IL DANS LE FICHIER ?
C          AU BON NUMERO D'ORDRE ?
C          CONTIENT-IL A MINIMA LES COMPOSANTES VOULUES ?
C          LE NOMBRE DE VALEURS EST-IL CORRECT ?
C          SI OUI A TOUTES SES QUESTIONS, EXISTC VAUT 3.
C          ON RECUPERE ALORS :
C          . LE NOMBRE DE COMPOSANTES QU'IL Y A.
C          . LE NOM DE CES COMPOSANTES.
C
      CALL MDEXCH ( NOFIMD,
     >              NOCHMD, NUMPT, NUMORD, NBCMPV, NCMPVM,
     >              NBVATO, TYPENT, TYPGEO,
     >              EXISTC, NBCMFI, NMCMFI, CODRET )
C
      IF ( EXISTC.NE.3 ) THEN
        CALL UTDEBM ( 'A', NOMPRO, 'FICHIER ' )
        CALL UTIMPK ( 'S', 'MED : ', 1, NOFIMD )
        CALL UTIMPK ( 'L', 'CHAMP : ', 1, NOCHMD )
        CALL UTIMPI ( 'L', 'NUMERO D ORDRE : ', 1, NUMORD )
        CALL UTIMPI ( 'L', 'NUMERO DE PAS DE TEMPS : ', 1, NUMPT )
        CALL UTFINM ()
        IF ( EXISTC.EQ.0 ) THEN 
          CALL UTMESS ( 'A', NOMPRO, 'CHAMP INCONNU.' )
        ELSEIF ( EXISTC.EQ.1 ) THEN 
          CALL UTMESS ( 'A', NOMPRO, 'IL MANQUE DES COMPOSANTES.' )
        ELSEIF ( EXISTC.EQ.2 ) THEN 
          CALL UTMESS ( 'A', NOMPRO, 'AUCUNE VALEUR A CE NRO D ORDRE.' )
        ELSEIF ( EXISTC.EQ.4 ) THEN 
          CALL UTMESS ( 'A', NOMPRO, 'MAUVAIS NOMBRE DE VALEURS.' )
        ENDIF
        CALL UTMESS ( 'F', NOMPRO, 'LECTURE IMPOSSIBLE.' )
      ENDIF
C
C 1.7. ==> VERIFICATIONS DES COMPOSANTES ASTER DEMANDEES
C          EN SORTIE, ON A :
C       NCMPUT : NOMBRE DE COMPOSANTES VALIDES.
C       NUMCMP : TABLEAU DES NUMEROS DES COMPOSANTES VALIDES
C       NTNCMP : TABLEAU DES NOMS DES COMPOSANTES VALIDES (K8)
C       NTUCMP : TABLEAU DES UNITES DES COMPOSANTES VALIDES (K16)
C
      IF ( NBCMPV.NE.0 ) THEN
        CALL JEVEUO ( NCMPVA, 'L', IAUX )
      ELSE
        IAUX = 1
      ENDIF
C
      CALL UTLICM ( NBCMPV, ZK8(IAUX),
     >              NOMGD, NCMPRF, ZK8(JNOCMP),
     >              NCMPUT, NUMCMP, NTNCMP, NTUCMP )
C
C====
C 2. OUVERTURE DU FICHIER EN LECTURE
C====
C
      CALL CODENT (NROFIC, 'G', SAUX08 )
      NOFIMD = 'fort.'//SAUX08
C
      CALL EFOUVR ( IDFIMD, NOFIMD, EDLECT, CODRET)
      IF ( CODRET.NE.0 ) THEN
        CALL UTDEBM ( 'A', NOMPRO, 'FICHIER ' )
        CALL UTIMPK ( 'S', 'MED : ', 1, NOFIMD )
        CALL UTIMPK ( 'L', 'CHAMP : ', 1, NOCHMD )
        CALL UTIMPI ( 'L', 'ERREUR EFOUVR NUMERO ', 1, CODRET )
        CALL UTFINM ()
        CALL UTMESS ( 'F', NOMPRO, 'PROBLEME A L OUVERTURE DU FICHIER' )
      ENDIF
C
C ATTENTION :
C LES ETAPES SUIVANTES 3,4 ET 5 SONT VALABLKES UNIQUEMENT POUR DES
C CHAMPS AUX NOEUDS. QUAND ON IMPLANTERA LA LECTURE DE CHAMELEM, IL
C FAUDRA S'INSPIRER DU PROGRAMME D'ECRITURE, IRCAME, POUR BOUCLER
C SUR TOUS LE STYPES DE SUPPORT. IL FAUDRA ALORS PRNDRE EN COMPTE
C DES NOMBRES DE VALEURS, DES POINTS DE GAUSS ET DES PROFILS VARIABLES
C EN FOCNTION DU TYPE DE MAILLE.
C BON COURAGE ! 
C GERALD NICOLAS LE 16 OCTOBRE 2002
C====
C 3. LECTURE DES VALEURS
C====
C
      CALL LRCMLE ( IDFIMD, NOCHMD, NOMAMD,
     >              NBCMFI, NBVATO, NUMPT, NUMORD,
     >              TYPENT, TYPGEO,
     >              NTVALE, NOMPRF,
     >              CODRET )
C
C====
C 4. LECTURE DU PROFIL
C====
C
      IF ( NOMPRF.EQ.EDNOPF ) THEN
C
        LGPROA = 0
C
      ELSE
C
        CALL LRCMPR ( IDFIMD, NOMPRF,
     >                NTPROA, LGPROA,
     >                CODRET )
C
      ENDIF
C
C====
C 5. TRANFERT DES VALEURS
C====
C
      CALL LRCMVA ( NTVALE, NBVATO, NTPROA, LGPROA,
     >              NCMPRF, ZK8(JNOCMP),
     >              NBCMFI, NMCMFI, NBCMPV, NCMPVM, NUMCMP,
     >              ADSL, ADSV,
     >              CODRET )
C
C====
C 6. FIN
C====
C
C 6.1. ==> FERMETURE FICHIER
C
      CALL EFFERM ( IDFIMD, CODRET )
      IF ( CODRET.NE.0 ) THEN
        CALL UTDEBM ( 'A', NOMPRO, 'FICHIER ' )
        CALL UTIMPK ( 'S', 'MED : ', 1, NOFIMD )
        CALL UTIMPK ( 'L', 'CHAMP : ', 1, NOCHMD )
        CALL UTIMPI ( 'L', 'ERREUR EFFERM NUMERO ', 1, CODRET )
        CALL UTFINM ()
        CALL UTMESS ( 'F', NOMPRO, 'PROBLEME A LA FERMETURE DU FICHIER')
      ENDIF
C
C 6.2. ==> MENAGE
C
      CALL JEDETC ('V','&&'//NOMPRO,1)
C
      CALL JEDEMA ( )
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,1001) 'FIN DE '//NOMPRO
      ENDIF
C
      END
