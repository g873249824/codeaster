       SUBROUTINE ASCSEP(ALPHA, RM, RC, EP, GEOM, SYME, NBSEP, ITYPE,
     +                   ICIRC, ILONC, ISLC, IBETC, ISCC, IPHIC,
     +                   ICIRP, ILONP, ISCP, ISLP, SSEPV2)
       IMPLICIT NONE
       REAL*8       ALPHA, RM, RC, EP
       CHARACTER*8  GEOM, SYME
       INTEGER      NBSEP, ITYPE, ICIRC, ILONC, ISLC, IBETC, ISCC, 
     +              IPHIC, ICIRP, ILONP, ISCP, ISLP
       LOGICAL      SSEPV2
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 08/03/2004   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C     MACR_ASCOUF_MAIL
C      
C     TAILLE INITIALE SUR LA PLAQUE DES SOUS-EPAISSEURS
C
C-----------------DONNEES FOURNIES PAR L'UTILISATEUR--------------------
C
C     ALPHA = ANGLE DU COUDE
C     RM    = RAYON MOYEN DU COUDE
C     RC    = RAYON DE CINTRAGE DU COUDE
C     EP    = EPAISSEUR DU COUDE
C     GEOM  = TYPE DE GEOMETRIE MODELISEE (COUDE OU TUBE)  
C     SYME  = QUART DE STRUCTURE SI 'OUI'
C     NBSEP = NOMBRE DE SOUS-EPAISSEURS
C     ITYPE = TYPE DE SOUS-EPAISSEUR (AXISYMETRIQUE OU ELLIPTIQUE)
C     ISCC  = ABSC. CIRCONF. CENTRE SOUS-EPAISSEUR SUR LE COUDE
C     IPHIC = AZIMUT CIRCONF. CENTRE SOUS-EPAISSEUR SUR LE COUDE
C     IBETC = POSITION ANGULAIRE DU CENTRE DE LA SOUS-EPAISSEUR
C     ILONC = TAILLE LONGI SUR LE COUDE DE LA SOUS-EPAISSEUR
C     ISLC  = ABSC. LONGIT. CENTRE SOUS-EPAISSEUR SUR LE COUDE
C     ICIRC = TAILLE CIRCONF. SUR LE COUDE  DE LA SOUS-EPAISSEUR
C
C----------------------DONNEES FOURNIES PAR ASTER-----------------------
C
C     ISCP  = ABSC. CIRCONF. CENTRE SOUS-EPAISSEUR SUR LA PLAQUE
C     ISLP  = ABSC. LONGIT. CENTRE SOUS-EPAISSEUR SUR LA PLAQUE
C     ICIRP = TAILLE CIRCONF. SUR LA PLAQUE DE LA SOUS-EPAISSEUR
C     ILONP = TAILLE LONGIT. SUR LA PLAQUE DE LA SOUS-EPAISSEUR
C 
C ----------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER           ZI
      COMMON / IVARJE / ZI(1)
      REAL*8            ZR
      COMMON / RVARJE / ZR(1)
      COMPLEX*16        ZC
      COMMON / CVARJE / ZC(1)
      LOGICAL           ZL
      COMMON / LVARJE / ZL(1)
      CHARACTER*8       ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                       ZK24
      CHARACTER*32                                ZK32
      CHARACTER*80                                         ZK80
      COMMON / KVARJE / ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
C     ----- FIN COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      CHARACTER*16 MOTCL
      REAL*8       PI,R8PI,AZIMC,AZIML,BSUP,BINF,BCOUD1,BCOUD2,DIST,
     &             BEMB1,BEMB2,BEMB,BCOUD,BPLAQ,R8BID,CG
      INTEGER      IFM,I,N1L,N1C,IUNIFI
      LOGICAL       COUDE,SYM
C      
      IFM   = IUNIFI('MESSAGE')     
      COUDE = (GEOM.EQ.'COUDE')
      SYM   = (SYME(1:5).EQ.'QUART'.OR.SYME(1:4).EQ.'DEMI')
C
      IF (SSEPV2) THEN 
         MOTCL = 'SOUS_EPAIS_COUDE'
      ELSE
         MOTCL = 'SOUS_EPAIS_MULTI'
      END IF
      PI = R8PI()
      CG = PI/180.D0
C
C --- BOUCLE SUR L'ENSEMBLE DES SOUS-EPAISSEURS
C
      DO 100 I=1,NBSEP
C
        WRITE(IFM,10) 
        WRITE(IFM,*) 'SOUS-EPAISSEUR NUMERO ',I    
        WRITE(IFM,10)
C
C --- CAS DES SOUS-EPAISSEURS AXISYMETRIQUES 
C
       IF (ZK8(ITYPE+I-1).EQ.'AXIS') THEN
          WRITE(IFM,*) 'SOUS-EPAISSEUR AXISYMETRIQUE : '
          WRITE(IFM,*) ' CALCUL DE LA TAILLE LONGI ENVELOPPE EN '
     &                // 'INTRADOS (AZIMUT PI)'
          ZR(ICIRP+I-1) = 2.D0*PI*RM
          ZR(ISCP+I-1)  = PI*RM
          ZR(IPHIC+I-1) = 180.D0
          AZIMC = PI
          GOTO 110
       END IF         
C
C -- CALCUL DE L'ABSCISSE CURVILIGNE CIRCONF.SUR LA PLAQUE  
C    EN FONCTION DE L'AZIMUT OU DE L'ABSCISSE CURVIL.CIRCONF
C    SUR LE COUDE DU CENTRE DE LA SOUS-EPAISSEUR
C    NB : MESURE FAITE EN PEAU EXTERNE SUR LE COUDE
C
       CALL GETVR8(MOTCL,'POSI_CURV_CIRC',I,1,0,R8BID,N1C)
       IF (N1C.NE.0) THEN
         ZR(ISCP+I-1) = ZR(ISCC+I-1)*RM/(RM+EP/2.D0)
         AZIMC = ZR(ISCP+I-1)/RM
         ZR(IPHIC+I-1) = ZR(ISCC+I-1)/(RM+EP/2.D0)*180.D0/PI
         WRITE(IFM,20) 'AZIMUT CENTRE SOUS-EPAISSEUR (DEGRES) : ',
     &                ZR(IPHIC+I-1)
       ELSE
         ZR(ISCP+I-1) = ZR(IPHIC+I-1)*CG*RM 
         AZIMC = ZR(IPHIC+I-1)*CG
         WRITE(IFM,20) 'ABSC. CURV. CIRCONF. CENTRE SOUS-EPAISSEUR '
     &             //'SUR COUDE : ',AZIMC*(RM+EP/2.D0)
       END IF  
C
C    PASSAGE DANS LE REPERE PLAQUE (0,2PI) AVEC ORIGINE FLANC DROIT
C    CAR L'ORIGINE DES DONNEES CIRCONF. EST EN EXTRADOS 
C
       IF (ZR(ISCP+I-1).GT.(3.D0*PI*RM/2.D0)) THEN
           ZR(ISCP+I-1) = ZR(ISCP+I-1) - 3.D0*PI*RM/2.D0
       ELSE
           ZR(ISCP+I-1) = ZR(ISCP+I-1) + PI*RM/2.D0
       END IF
C
       WRITE(IFM,20) 'ABSC. CURV. CIRCONF. CENTRE SOUS-EPAISSEUR '
     &             //'SUR PLAQUE : ',ZR(ISCP+I-1) 
C
C -- CALCUL DE LA TAILLE CIRCONFERENTIELLE 
C    NB : MESURE FAITE EN PEAU EXTERNE SUR LE COUDE
C
       ZR(ICIRP+I-1) = ZR(ICIRC+I-1)*RM/(RM+EP/2.D0)
C
       IF (ZR(ICIRP+I-1).GT.(2.D0*PI*RM)) THEN
        CALL UTDEBM('F','ASCSEP','VALEUR HORS DOMAINE')
        CALL UTIMPI('L','SOUS-EPAISSEUR NUMERO : ',1,I)
        CALL UTIMPR('L','TAILLE AXE CIRCONFERENTIEL : ',1,ZR(ICIRP+I-1))
        CALL UTIMPR('L','BORD PLAQUE : ',1,2*PI*RM )
        CALL UTFINM()   
        GOTO 9999
       END IF    
C
       WRITE(IFM,20) 'TAILLE CIRCONFERENTIELLE SOUS-EPAISSEUR '
     &             //'SUR PLAQUE : ',ZR(ICIRP+I-1)  
       WRITE(IFM,20) '<=> TAILLE EQUIVALENTE SUR LA '
     &             //'CIRCONFERENCE (DEGRES) : ',
     &                ZR(ICIRP+I-1)*360.D0/(2.D0*PI*RM)            
C
C -- CALCUL COTE AXIALE DU CENTRE SOUS-EPAISSEUR SUR LA PLAQUE 
C    EN FONCTION DE L'ABSCISSE CURVILIGNE DONNEE SUR LE COUDE 
C    OU DE LA POSITION ANGULAIRE
C    NB : MESURE FAITE EN PEAU EXTERNE SUR LE COUDE
C
110    CONTINUE
       CALL GETVR8(MOTCL,'POSI_CURV_LONGI',I,1,0,R8BID,N1L)
       IF (N1L.NE.0) THEN
         IF (COUDE) THEN 
           ZR(ISLP+I-1) = ZR(ISLC+I-1)/(1.D0+(RM+EP/2.D0)/RC*COS(AZIMC))
           AZIML = ZR(ISLP+I-1)/RC
           WRITE(IFM,20) 'ANGLE COUDE CENTRE SOUS-EPAISSEUR (DEGRES) : '
     &                   ,AZIML*180.D0/PI
         ELSE
           ZR(ISLP+I-1) = ZR(ISLC+I-1)
         END IF
         IF (SYM.AND.(ZR(ISLP+I-1).NE.ALPHA*CG*RC/2.D0)) THEN
             CALL UTMESS('F','ASCSEP','CAS DE SYMETRIE : LA SOUS-'//
     &       'EPAISSEUR DOIT ETRE DANS LA SECTION MEDIANE DU COUDE !') 
         END IF
       ELSE 
         IF (COUDE) THEN  
           WRITE(IFM,20) 'ABSC. CURV. AXIALE CENTRE SOUS-EPAISSEUR '
     &                 //'SUR COUDE : ',
     &     ZR(IBETC+I-1)*CG*(RC+(RM+EP/2.D0)*COS(AZIMC))
           AZIML = ZR(IBETC+I-1)*CG
         ELSE
           WRITE(IFM,20) 'ABSC. CURV. AXIALE CENTRE SOUS-EPAISSEUR '
     &        //'SUR TUBE : ',ZR(IBETC+I-1)*CG*RC 
         END IF       
         ZR(ISLP+I-1) = ZR(IBETC+I-1)*CG*RC
         IF (SYM.AND.(ZR(IBETC+I-1).NE.ALPHA/2.D0)) THEN
             CALL UTMESS('F','ASCSEP','CAS DE SYMETRIE : LA SOUS-'//
     &       'EPAISSEUR DOIT ETRE DANS LA SECTION MEDIANE DU COUDE !') 
        END IF
       END IF
       WRITE(IFM,20) 'ABSC. CURV. AXIALE CENTRE SOUS-EPAISSEUR '
     &             //'SUR PLAQUE : ',ZR(ISLP+I-1)
C
C -- CALCUL DE LA TAILLE LONGITUDINALE 
C    NB : MESURE FAITE EN PEAU EXTERNE SUR LE COUDE
C
C   ON ENVISAGE LE CAS OU UNE PARTIE DE L'AXE EST DANS LES
C   DROITES DES EMBOUTS. LA TRANSFORMATION N'EST FAITE QUE SUR LA 
C   PARTIE RESTANT DANS LE COUDE.
C
       IF (COUDE) THEN
         DIST = ALPHA*CG*(RC+(RM+EP/2.D0)*COS(AZIMC))
       ELSE
         DIST = ALPHA*CG*RC
       END IF
       IF (N1L.NE.0) THEN
         BSUP = ZR(ISLC+I-1)+ZR(ILONC+I-1)/2.D0
         BINF = ZR(ISLC+I-1)-ZR(ILONC+I-1)/2.D0
       ELSE
         IF (COUDE) THEN
           BSUP = ZR(IBETC+I-1)*CG
     &             *(RC+(RM+EP/2.D0)*COS(AZIMC))+ZR(ILONC+I-1)/2.D0
           BINF = ZR(IBETC+I-1)*CG
     &             *(RC+(RM+EP/2.D0)*COS(AZIMC))-ZR(ILONC+I-1)/2.D0
         ELSE
           BSUP = ZR(IBETC+I-1)*CG*RC + ZR(ILONC+I-1)/2.D0
           BINF = ZR(IBETC+I-1)*CG*RC - ZR(ILONC+I-1)/2.D0
         END IF
       END IF
C
       BCOUD1 = 0.D0
       BCOUD2 = 0.D0
       BEMB1  = 0.D0
       BEMB2  = 0.D0
       IF (BINF.LT.0.D0.AND.BSUP.GT.DIST) THEN
         BCOUD1 = DIST
         BEMB1 =  ABS(BINF) + BSUP-DIST
       ELSE IF (BSUP.GT.DIST) THEN
         BCOUD1 = DIST - BINF
         BEMB1  = BSUP - DIST
       ELSE IF (BINF.LT.0.D0) THEN
         BCOUD2 = BSUP 
         BEMB2  = ABS(BINF)
       ELSE IF (BINF.GE.0.D0.AND.BSUP.LE.DIST) THEN
         BCOUD1 = ZR(ILONC+I-1)
       END IF
       BCOUD = BCOUD1+ BCOUD2
       BEMB  = BEMB1 + BEMB2
C       
       IF (COUDE) THEN
         BPLAQ = BCOUD/(1.D0+(RM+EP/2.d0)/RC*COS(AZIMC))
       ELSE
         BPLAQ = BCOUD
       END IF
       ZR(ILONP+I-1) = BPLAQ+BEMB
       
       IF (BEMB1 .GT. 0.D0) THEN
         ZR(ISLP+I-1) =  ALPHA*CG*RC - ZR(ILONP+I-1)/2.D0 + BEMB1
         WRITE(IFM,*) 'CORRECTION CENTRE : SOUS-EP. A CHEVAL SUR EMBOUT'
         WRITE(IFM,20) 'ABSC. CURV. AXIALE CENTRE SOUS-EPAISSEUR '
     &                 //'SUR PLAQUE : ',ZR(ISLP+I-1)
       END IF
       IF (BEMB2 .GT. 0.D0) THEN
         ZR(ISLP+I-1) = + ZR(ILONP+I-1)/2.D0 - BEMB2
         WRITE(IFM,*) 'CORRECTION CENTRE : SOUS-EP. A CHEVAL SUR EMBOUT'
         WRITE(IFM,20) 'ABSC. CURV. AXIALE CENTRE SOUS-EPAISSEUR '
     &                 //'SUR PLAQUE : ',ZR(ISLP+I-1)
       END IF
C
       IF (ZR(ISLP+I-1).LT.0.D0) ZR(ISLP+I-1) = 0.D0 
       IF (ZR(ISLP+I-1).GT.ALPHA*CG*RC) ZR(ISLP+I-1) = ALPHA*CG*RC 
C
C     SI LE CENTRE DE LA SOUS-EP CALCULE SUR LA PLAQUE EST DANS L'EMBOUT
C     ON CORRIGE SA POSITION EN LE METTANT A L'INTERFACE SINON CA PLANTE
C     DANS LA PROC DE MAILLAGE (A AMELIORER)
C
       WRITE(IFM,20) 'TAILLE LONGITUDINALE SOUS-EPAISSEUR '
     &             //'SUR PLAQUE : ',ZR(ILONP+I-1)
       WRITE(IFM,20) '<=> TAILLE EQUIVALENTE PAR RAPPORT A '
     &             //'L''ANGLE DU COUDE (DEGRES):',
     &                ZR(ILONP+I-1)*360/(2*PI*RC)   
C
C -- PROFONDEUR DE LA SOUS-EPAISSEUR : INCHANGEE 
C    
C       SEPPL(3,I) = SEPCO(3,I)
C
 100  CONTINUE
C
 10   FORMAT(37('-'))
 20   FORMAT(T1,A,T64,F8.2)
 30   FORMAT(T1,A,T64,F8.2)
 9999 CONTINUE
C
      END
