      SUBROUTINE IRADHE
      IMPLICIT REAL*8 (A-H,O-Z)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 20/12/2000   AUTEUR ADBHHPM P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C--------------------------------------------------------------------
C BUT: TRAITER LES "ADHERENCES ENSIGHT":
C  OBJETS JEVEUX CREES:
C    &&IRADHE.NOMAENS : TABLEAU DE K8 DIMENSIONNE AU NOMBRE DE TYPE
C             DE MAILLES ASTER DONNANT LE NOM DE CHAQUE TYPE DE MAILLE
C             POUR ENSIGHT. UN TYPE DE MAILLE ASTER INEXISTANT POUR
C             ENSIGHT PORTERA LE NOM 'NONEXIST'
C    &&IRADHE.PERMUTA : TABLEAU D'ENTIERS DIMENSIONNE
C             AU NOMBRE DE TYPE_MAILLES*NOMBRE MAXI DE NOEUDS/MAILLE
C             DONNANT LES PERMUTATIONS EVENTUELLES DANS L'ORDRE DES
C             NOEUDS DE CHAQUE TYPE_MAILLE, ENTRE ASTER ET ENSIGHT.
C             (NOMBRE MAXI DE NOEUDS/MAILLE STOCKE EN FIN DE VECTEUR)
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER           ZI
      COMMON / IVARJE / ZI(1)
      REAL*8            ZR
      COMMON / RVARJE / ZR(1)
      COMPLEX*16        ZC
      COMMON / CVARJE / ZC(1)
      LOGICAL           ZL
      COMMON / LVARJE / ZL(1)
      CHARACTER*8       ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                       ZK24
      CHARACTER*32                                ZK32
      CHARACTER*80                                         ZK80
      COMMON / KVARJE / ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
      CHARACTER*32      JEXNUM
C     ----- FIN COMMUNS NORMALISES  JEVEUX  ----------------------------
      PARAMETER (MAXNOD=32,NBTYMX=70)
C     --- AU 31/08/95, ENSIGHT VERSION 5.5, 11 TYPES DE MAILLES SONT
C         SUPPORTES, AVEC UN NOMBRE MAXIMUM DE NOEUDS PAR MAILLE DE 20
C         (POUR L'ELEMENT 'HEXA20'.) ON FIXE MAXNOD=32 ET NBTYMX=35
      CHARACTER*8 NOMAST(NBTYMX),NOMAEN(NBTYMX),NOMTM
      INTEGER INDIC(NBTYMX)
      CHARACTER*1 K1BID
C
C     --- VERIFIE L'EXISTENCE DES TABLEAUX DE TRAVAIL :
C         ON SORT SI TOUS LES TABLEAUX EXISTENT DEJA
      CALL JEMARQ()
      CALL JEEXIN('&&IRADHE.PERMENS',IRET1)
      CALL JEEXIN('&&IRADHE.NOMAENS',IRET2)
      CALL JEEXIN('&&IRADHE.PERMUTA',IRET3)
      IF (IRET1*IRET2*IRET3.NE.0) GOTO 9999
C
C     --- ALLOCATION D'UN TABLEAU MAXNOD*NBTYMX POUR DEFINIR
C         TABLEAU DE PERMUTATION
      IF (IRET1.EQ.0) THEN
         CALL WKVECT('&&IRADHE.PERMENS','V V I',MAXNOD*NBTYMX,JPEREN)
      END IF
      CALL JEVEUO('&&IRADHE.PERMENS','E',JPEREN)
      CALL INIENS(MAXNOD,NBTYMX,NOMAST,NOMAEN,INDIC,ZI(JPEREN))
C
C     --- CREATION DE L'OBJET CONTENANT LES NOMS DE MAILLES ENSIGHT
      CALL JELIRA('&CATA.TM.NOMTM','NOMMAX',NBTYMA,K1BID)
      IF(IRET2.EQ.0) THEN
        CALL WKVECT('&&IRADHE.NOMAENS','V V K8',NBTYMA,JNOMAE)
      ENDIF
      CALL JEVEUO('&&IRADHE.NOMAENS','E',JNOMAE)
      DO 1 IMA=1,NBTYMA
         CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',IMA),NOMTM)
         DO 2 IEN=1,NBTYMX
            IF(NOMTM.EQ.NOMAST(IEN)) THEN
              ZK8(JNOMAE-1+IMA)=NOMAEN(IEN)
              GO TO 1
            END IF
    2    CONTINUE
         ZK8(JNOMAE-1+IMA)='NONEXIST'
    1 CONTINUE
      IF (IRET3.EQ.0) THEN
         CALL WKVECT('&&IRADHE.PERMUTA','V V I',MAXNOD*NBTYMX+1,JPERMU)
         ZI(JPERMU-1+MAXNOD*NBTYMX+1)=MAXNOD
      END IF
      CALL JEVEUO('&&IRADHE.PERMUTA','E',JPERMU)
      CALL JEVEUO('&CATA.TM.NBNO','L',JNBNOE)
      DO 4 IMA=1,NBTYMA
         NBN=ZI(JNBNOE-1+IMA)
         CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',IMA),NOMTM)
         IEN=-1
         DO 41 IEXEN=1,NBTYMX
            IF(NOMTM.EQ.NOMAST(IEXEN)) THEN
              IEN=IEXEN
            END IF
   41    CONTINUE
         IF(IEN.EQ.-1) THEN
           DO 42 INO=1,NBN
             ZI(JPERMU-1+MAXNOD*(IMA-1)+INO)=0
   42      CONTINUE
         ELSE IF(INDIC(IEN).LT.0) THEN
           DO 43 INO=1,NBN
             ZI(JPERMU-1+MAXNOD*(IMA-1)+INO)=0
   43      CONTINUE
         ELSE IF (INDIC(IEN).EQ.0) THEN
           DO 44 INO=1,NBN
             ZI(JPERMU-1+MAXNOD*(IMA-1)+INO)=INO
   44      CONTINUE
         ELSE IF (INDIC(IEN).EQ.1) THEN
           DO 45 INO=1,NBN
             DO 46 INOE=1,NBN
               IMPER=ZI(JPEREN-1+MAXNOD*(IEN-1)+INOE)
               IF(INO.EQ.IMPER) THEN
                 ZI(JPERMU-1+MAXNOD*(IMA-1)+INO)=INOE
                 GO TO 45
               END IF
   46        CONTINUE
   45      CONTINUE
         END IF
    4 CONTINUE
      CALL JEDETR('&&IRADHE.PERMENS')
 9999 CONTINUE
      CALL JEDEMA()
      END
