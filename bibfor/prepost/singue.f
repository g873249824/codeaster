      SUBROUTINE SINGUE (CHERRS,CHENES,NOMAIL,NDIM,NNOEM,NELEM,XY,
     &                    PREC,LIGRMO,IORDR,CHELEM)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 11/07/2005   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

C****************************************************************
C          BUT DE CETTE ROUTINE :                               *
C 1) RECUPERATION DE L ERREUR ET DE L ENERGIE EN CHAQUE EF      *
C 2) CALCUL DU DEGRE DE LA SINGULARITE ET                       *
C    DU RAPPORT ENTRE L ANCIENNE ET LA NOUVELLE TAILLE DES EF   *
C    RQ : CES DEUX QUANTITES SONT CALCULEES DANS CHAQUE ELEMENT *
C    ET SONT CONSTANTES PAR ELEMENT                             *
C 3) STOCKAGE DE CES DEUX COMPOSANTES DANS CHELEM               *
C****************************************************************

C IN  CHERRS       : NOM SD_S OU EST STOCKE L ERREUR
C IN  CHENES       : NOM SD_S OU EST STOCKE L ENERGIE
C IN  NOMAIL       : NOM DU MAILLAGE
C IN  NDIM         : DIMENSION DU PROBLEME
C IN  NNOEM        : NOMBRE DE NOEUDS DU MAILLAGE
C IN  NELEM        : NOMBRE D ELEMENTS FINIS DU MAILLAGE
C IN  XY(3,NNOEM)  : COORDONNEES DES NOEUDS
C IN  PREC         : % DE L ERREUR TOTALE SOUHAITE
C                    POUR CALCULER LA NOUVELLE CARTE DE TAILLE
C                    DES EF H*
C                    ERREUR_TOTALE(H*)=PREC*ERREUR_TOTALE
C IN  LIGRMO       : NOM DU LIGREL DU MODELE
C IN  IORDR        : NUMERO D ORDRE
C OUT CHELEM       : CHAM_ELEM QUI VA CONTENIR LE DEGRE ET LA TAILLE

      IMPLICIT NONE

C     ----- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      CHARACTER*8 ZK8
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM,JEXATR
C     ----- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

C DECLARATION GLOBALE
      
      INTEGER NDIM,NNOEM,NELEM,IORDR
      REAL*8  XY(3,NNOEM),PREC
      CHARACTER*8 NOMAIL
      CHARACTER*19 CHERRS,CHENES
      CHARACTER*24 LIGRMO,CHELEM
      
C DECLARATION LOCALE

      INTEGER JDIME,JMESU,JCONN,JCINV
      INTEGER JCESC,JCESD,JCESL,JCESV,IAD
      INTEGER NSOMMX,NELCOM,DEGRE
      INTEGER NBCMP,NCMP
      INTEGER ICMP,INEL,NBR(NELEM)
      REAL*8  ERREUR(NELEM),ENERGI(NELEM)
      REAL*8  ALPHA(NELEM),RE(NELEM)
      CHARACTER*8  K8BID
            
      CALL JEMARQ()

C 1 - RECUPERATION DES ADRESSES DES OBJETS CREES DANS SINGUM
            
      CALL JEVEUO('&&SINGUM.DIME           ','L',JDIME)            
      CALL JEVEUO('&&SINGUM.MESU           ','L',JMESU)
      CALL JEVEUO('&&SINGUM.CONN           ','L',JCONN)
      CALL JEVEUO('&&SINGUM.CINV           ','L',JCINV)
      
C 2 - NSOMMX = NBRE MAX DE NOEUDS SOMMETS CONNECTES AUX EF 
C     NELCOM = NBRE MAX D EFS SURF EN 2D OU VOL EN 3D
C              CONNECTES AUX NOEUDS
C     DEGRE  = 1 EF LINEAIRE - 2 EF QUADRATIQUE
                  
      NSOMMX=ZI(JDIME+1-1)
      NELCOM=ZI(JDIME+2-1)
      DEGRE =ZI(JDIME+3-1)
      
C 3 - RECUPERATION DE L'ERREUR EN CHAQUE EF ERREUR(EF)
C     NOMBRE DE COMPOSANTES A STOCKER PAR EF NBR(NELEM)
C       2 SI EF SURFACIQUES EN 2D OU VOLUMIQUES EN 3D
C       0 SINON
      
      CALL JEVEUO(CHERRS//'.CESC','L',JCESC)
      CALL JELIRA(CHERRS//'.CESC','LONMAX',NBCMP,K8BID)   
      CALL JEVEUO(CHERRS//'.CESD','L',JCESD)
      CALL JEVEUO(CHERRS//'.CESL','L',JCESL)
      CALL JEVEUO(CHERRS//'.CESV','L',JCESV)
      
C LECTURE DE LA SD .CESC
      
      DO 10 ICMP=1,NBCMP
        IF (ZK8(JCESC+ICMP-1)(1:6).EQ.'ERREST') NCMP=ICMP
 10   CONTINUE    
      
      DO 20 INEL=1,NELEM
        CALL CESEXI('C',JCESD,JCESL,INEL,1,1,NCMP,IAD)
        IF (IAD.GT.0) THEN
          ERREUR(INEL)=ZR(JCESV+IAD-1)
          NBR(INEL)=2
        ELSE
          ERREUR(INEL)=0.D0
          NBR(INEL)=0
        ENDIF
 20   CONTINUE
      
C 4 - RECUPERATION DE L'ENERGIE EN CHAQUE EF ENERGI(EF)

      CALL JEVEUO(CHENES//'.CESC','L',JCESC)
      CALL JELIRA(CHENES//'.CESC','LONMAX',NBCMP,K8BID)   
      CALL JEVEUO(CHENES//'.CESD','L',JCESD)
      CALL JEVEUO(CHENES//'.CESL','L',JCESL)
      CALL JEVEUO(CHENES//'.CESV','L',JCESV)
      
C LECTURE DE LA SD .CESC
      DO 30 ICMP=1,NBCMP
        IF (ZK8(JCESC+ICMP-1)(1:6).EQ.'TOTALE') NCMP=ICMP
 30   CONTINUE    
 
      DO 40 INEL=1,NELEM
        CALL CESEXI('C',JCESD,JCESL,INEL,1,1,NCMP,IAD)
        IF (IAD.GT.0) THEN
          ENERGI(INEL)=ZR(JCESV+IAD-1)
        ELSE
          ENERGI(INEL)=0.D0
        ENDIF
 40   CONTINUE

C 5 - CALCUL DU DEGRE DE LA SINGULARITE ALPHA(NELEM) PAR EF      
C     ET DU RAPPORT ENTRE ANCIENNE ET NOUVELLE TAILLE RE(NELEM)      

      CALL DSINGU(NDIM,NELEM,NNOEM,NSOMMX,NELCOM,DEGRE,NBR,ZI(JCONN),
     &            ZI(JCINV),XY,ERREUR,ENERGI,PREC,ZR(JMESU),
     &            ALPHA,RE)
                  
C 6 - STOCKAGE DE ALPHA ET RE DANS CHELEM

      CALL SSINGU(NOMAIL,NELEM,NBR,LIGRMO,ALPHA,RE,CHELEM)
      
      CALL JEDEMA()        

      END
