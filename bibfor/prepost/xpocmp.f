      SUBROUTINE XPOCMP(ELREFP,CNS1,IMA,N,JCONX1,JCONX2,NDIM,NFH,NFE,
     &                  DDLC,NBCMP,CMP,LMECA)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 26/02/2013   AUTEUR CUVILLIE M.CUVILLIEZ 
C TOLE CRS_1404
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT

      IMPLICIT NONE

      INCLUDE 'jeveux.h'
      INTEGER       NDIM,NFH,NFE,IMA,N,JCONX1,JCONX2,NBCMP,CMP(NBCMP)
      INTEGER       DDLC
      LOGICAL       LMECA
      CHARACTER*8   ELREFP
      CHARACTER*19  CNS1
C
C   DETERMINER LES COMPOSANTES ACTIVES DU CHAMP DE DEPLACEMENT
C
C   IN
C     CNS1   : CHAMP_NO_S DU DEPLACEMENT EN ENTREE
C     IMA    : NUMERO DE MAILLE COURANTE PARENT
C     N      : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C     JCONX1 : ADRESSE DE LA CONNECTIVITE DU MAILLAGE SAIN
C     JCONX2 : LONGUEUR CUMULEE DE LA CONNECTIVITE DU MAILLAGE SAIN
C     NDIM   : DIMENSION DU MAILLAGE
C     NBCMP  : NOMBRE DE COMPOSANTES DU CHAMP_NO DE DEPL1
C     LMECA  : VRAI DANS LE CAS MECANIQUE (SINON CAS THERMIQUE)
C
C   OUT
C     NFH    : NOMBRE DE FONCTIONS HEAVISIDE (PAR NOEUD)
C     NFE    : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT (1 A 4)
C     CMP    : POSITION DES DDLS DE DEPL X-FEM DANS LE CHAMP_NO DE DEPL1

C
      INTEGER       JCNSC1,JCNSL1,I,J,K,INO,ICMP,NDC,IPOS,NNOS,IBID
      LOGICAL       EXIST(N,NBCMP),CONTAS
      CHARACTER*8   NOMCMP,K8BID
C
C     ------------------------------------------------------------------

      CALL JEMARQ()

C     COMPOSANTES DU CHAMP DE DEPLACEMENT 1
      CALL JEVEUO(CNS1//'.CNSC','L',JCNSC1)
      CALL JEVEUO(CNS1//'.CNSL','L',JCNSL1)

      DO 110 J=1,N
C       INO : NUMÉRO DU NOEUD DANS MALINI
        INO=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+J-1)
        DO 111 ICMP = 1,NBCMP
          EXIST(J,ICMP)= ZL(JCNSL1-1+(INO-1)*NBCMP + ICMP)
 111    CONTINUE
 110  CONTINUE

C     ON REGARDE LES COMPOSANTES ACTIVES EN CHAQUE NOEUD
      IPOS = 0
      NDC  = 0
      NFH  = 0
      NFE  = 0
      DDLC=0
      CONTAS=.TRUE.
      CALL ELELIN(1,ELREFP,K8BID,IBID,NNOS)

      DO 21 I = 1,NBCMP
        NOMCMP = ZK8(JCNSC1-1+I)

        IF (NOMCMP(1:4).EQ.'LAGS') THEN
            DO 22 K = 1,NNOS
              IF (.NOT. EXIST(K,I)) CONTAS=.FALSE.
 22       CONTINUE
            IF (CONTAS) GOTO 1
        ENDIF

        DO 24,J=1,N
           IF (.NOT.EXIST(J,I)) GOTO 21
 24     CONTINUE

 1     CONTINUE

        IF (NOMCMP(1:1).EQ.'D'.OR.NOMCMP(1:1).EQ.'T') THEN
          IPOS = IPOS +1
          NDC = NDC +1
          CMP(IPOS)=I
        ENDIF
        IF (NOMCMP(1:1).EQ.'H') THEN
          IPOS = IPOS +1
          NFH = NFH +1
          CMP(IPOS)=I
        ENDIF
        IF (NOMCMP(1:2).EQ.'E1'.OR.NOMCMP(1:2).EQ.'E2'.OR.
     &      NOMCMP(1:2).EQ.'E3'.OR.NOMCMP(1:2).EQ.'E4') THEN
          IPOS = IPOS +1
          NFE = NFE +1
          CMP(IPOS)=I
        ENDIF
        IF (NOMCMP(1:4).EQ.'LAGS') THEN
           IPOS=IPOS+1
           DDLC=DDLC+1
           CMP(IPOS)=I
        ENDIF

 21   CONTINUE

      IF (LMECA) THEN
C       CAS DE LA MECANIQUE
        NFE = NFE/NDIM
        NFH = NFH/NDIM
        CALL ASSERT(NDIM.EQ.NDC)
      ELSE
C       CAS DE LA THERMIQUE
        CALL ASSERT(NDC.EQ.1)
      END IF

      CALL JEDEMA()
      END
