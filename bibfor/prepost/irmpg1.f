      SUBROUTINE IRMPG1 ( NOFIMD,
     &                    NOMFPG, NBNOTO, NBREPG, NBSP, NDIM, TYPGEO,
     &                    REFCOO, GSCOO, WG,
     &                    RAUX1, RAUX2, RAUX3,
     &                    NOLOPG, CODRET )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 22/06/2010   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C_______________________________________________________________________
C     ECRITURE AU FORMAT MED - LOCALISATION POINTS DE GAUSS - PHASE 1
C        -  -            -                  -         -             -
C_______________________________________________________________________
C     ENTREES :
C       NOFIMD : NOM DU FICHIER MED
C       NOMFPG : NOM DE LA FAMILLE DES POINTS DE GAUSS
C       NBNOTO : NOMBRE DE NOEUDS TOTAL
C       NBREPG : NOMBRE DE POINTS DE GAUSS
C       NBSP   : NOMBRE DE SOUS-POINTS
C       NDIM   : DIMENSION DE L'ELEMENT
C       TYPGEO : TYPE GEOMETRIQUE DE LA MAILLE ASSOCIEE
C       REFCOO : COORDONNEES DES NBNOTO NOEUDS
C       GSCOO  : COORDONNEES DES POINTS DE GAUSS, SI CHAMP ELGA
C       WG     : POIDS DES POINTS DE GAUSS, SI CHAMP ELGA
C       RAUX1, RAUX2, RAUX3 : TABLEAUX REELS AUXILIAIRES
C     SORTIES :
C       NOLOPG : NON DE LA LOCALISATION ECRITE
C       CODRET : CODE DE RETOUR (0 : PAS DE PB, NON NUL SI PB)
C
C     REMARQUE :
C     ON DOIT FOURNIR LES COORDONNEES SOUS LA FORME :
C     . ELEMENT 1D : X1 X2 ... ... XN
C     . ELEMENT 2D : X1 Y1 X2 Y2 ... ... XN YN
C     . ELEMENT 3D : X1 Y1 Z1 X2 Y2 Z2 ... ... XN YN ZN
C     C'EST CE QUE MED APPELLE LE MODE ENTRELACE
C     ON DOIT FOURNIR LES POIDS SOUS LA FORME :
C     WG1 WG2 ... ... WGN
C_______________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NBNOTO, NBREPG, NBSP, NDIM, TYPGEO
C
      REAL*8 REFCOO(*), GSCOO(*), WG(*)
      REAL*8 RAUX1(*), RAUX2(*), RAUX3(*)
C
      CHARACTER*16 NOMFPG
      CHARACTER*32 NOLOPG
      CHARACTER*(*) NOFIMD
C
      INTEGER CODRET
C
C 0.2. ==> COMMUNS
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'IRMPG1' )
C
      INTEGER EDFUIN
      PARAMETER (EDFUIN=0)
      INTEGER EDLEAJ
      PARAMETER (EDLEAJ=2)
C
      INTEGER IFM, NIVINF
      INTEGER IAUX, JAUX, KAUX
      INTEGER NBLOPG
      INTEGER IDFIMD
      INTEGER TYPGEL, NBREPL
      INTEGER VALI
C
      CHARACTER*8 SAUX08
      CHARACTER*16 SAUX16
      CHARACTER*32 SAUX32
      CHARACTER*24 VALK
      LOGICAL FICEXI
C
C====
C 1. PREALABLES
C====
C
C 1.1. ==> RECUPERATION DU NIVEAU D'IMPRESSION
C
      CALL INFNIV ( IFM, NIVINF )
C
C 1.2. ==> INFORMATION
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,1001) 'DEBUT DE '//NOMPRO
        WRITE (IFM,12001) NOMFPG
12001 FORMAT(/,'ECRITURE D''UNE LOCALISATION DES POINTS DE GAUSS',
     &       /,'==> NOM DE LA FAMILLE D''ELEMENT FINI ASSOCIEE : ',A)
      ENDIF
 1001 FORMAT(/,4X,10('='),A,10('='),/)
C
C 1.3. ==> OUVERTURE FICHIER MED EN MODE 'LECTURE_AJOUT'
C      CELA SIGNIFIE QUE LE FICHIER EST ENRICHI MAIS ON NE PEUT PAS
C      Y ECRASER UNE DONNEE DEJA PRESENTE.
C      REMARQUE : LE FICHIER EXISTE DEJA CAR IL CONTIENT LE MAILLAGE.
C
      INQUIRE(FILE=NOFIMD,EXIST=FICEXI)
      IF ( .NOT. FICEXI ) THEN
        CALL U2MESS('F','MED2_3')
      ENDIF
      CALL EFOUVR (IDFIMD, NOFIMD, EDLEAJ, CODRET)
      IF ( CODRET.NE.0 ) THEN
        SAUX08='EFOUVR  '
        CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
      ENDIF
C
C 1.4. ==> CREATION DE LA PRMIERE MOITIE DU NOM DE LA LOCALISATION :
C          ON L'IDENTIFIE AU NOM DE LA FAMILLE, EN REMPLACANT LES BLANCS
C          PAR DES '_'
C
      SAUX16 = NOMFPG
      DO 14 , IAUX = 1 , 16
        IF ( SAUX16(IAUX:IAUX).EQ.' ' ) THEN
          SAUX16(IAUX:IAUX) = '_'
        ENDIF
   14 CONTINUE
C
C====
C 2. ON CHERCHE SI UNE DESCRIPTION IDENTIQUE EXISTE DEJA DANS LE
C    FICHIER. SI OUI, ON LA DECLARE COMME ETANT CELLE A UTILISER.
C====
C
C 2.1. ==> COMBIEN DE LOCALISATIONS SONT PRESENTES DANS LE FICHIER
C
      CALL EFNGAU ( IDFIMD, NBLOPG, CODRET )
C
      IF ( CODRET.NE.0 ) THEN
        SAUX08='EFNGAU  '
        CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
      ENDIF

C
      IF ( NIVINF.GT.1 ) THEN
        IF ( NBLOPG.EQ.0 ) THEN
          WRITE (IFM,21001)
        ELSE
          WRITE (IFM,21002) NBLOPG
        ENDIF
      ENDIF
21001 FORMAT(/,2X,'LE FICHIER NE CONTIENT PAS DE',
     &' LOCALISATION DE POINTS DE GAUSS.')
21002 FORMAT(/,2X,'LE FICHIER CONTIENT DEJA',I8,
     &' LOCALISATION(S) DE POINTS DE GAUSS : ')
C
C 2.2. ==> PARCOURS DES LOCALISATIONS DEJA ENREGISTREES
C
      DO 22 , IAUX = 1 , NBLOPG
C
C 2.2.1. ==> LECTURE DES CARACTERISTIQUES DE LA IAUX-EME LOCALISATION
C              PRESENTE DANS LE FICHIER :
C              SAUX32 = NOM
C              TYPGEL = TYPGEO
C              NBREPL = NOMBRE DE POINTS DE GAUSS
C
        CALL EFGAUI ( IDFIMD, IAUX,
     &                SAUX32, TYPGEL, NBREPL, CODRET )
C
        IF ( CODRET.NE.0 ) THEN
          CALL CODENT ( CODRET,'G',SAUX08 )
          CALL U2MESK('F','MED_50',1,SAUX08)
        ENDIF
C
        IF ( NIVINF.GT.1 ) THEN
          WRITE (IFM,22101) IAUX, SAUX32, TYPGEL, NBREPL
        ENDIF
22101 FORMAT(
     & /,2X,'. CARACTERISTIQUES DE LA LOCALISATION NUMERO',I4,' : ',
     & /,2X,'... NOM    : ',A,
     & /,2X,'... TYPGEO :',I4,
     & /,2X,'... NBREPG :',I4)
C
C 2.2.2. ==> ON REPERE SI LA LOCALISATION EST BATIE SUR LA MEME
C            FAMILLE D'ELEMENT FINI ASTER.
C            ON PEUT DEJA FILTRER SUR LE TYPE D'ELEMENT FINI
C            ENSUITE, SI LES CARACTERISTIQUES SONT LES MEMES, ON LIT LES
C            POIDS ET LES COORDONNEES ET LES ON COMPARE A CEUX DE
C            LA LOCALISATION VOULUE.
C            DES QUE L'ON TROUVE UN TERME DIFFERENT, ON PASSE A LA
C            LOCALISATION SUIVANTE.
C
        IF ( SAUX32(1:16).EQ.SAUX16 ) THEN
C
C
          IF ( TYPGEL.EQ.TYPGEO .AND. NBREPL.EQ.NBREPG ) THEN
C
            CALL EFGAUL ( IDFIMD, RAUX1, RAUX2, RAUX3,
     &                    EDFUIN, SAUX32, CODRET )
C
            IF ( CODRET.NE.0 ) THEN
              SAUX08='EFGAUL  '
              CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
            ENDIF
C
            KAUX = NBNOTO*NDIM
            DO 2221 , JAUX = 1 , KAUX
              IF ( REFCOO(JAUX).NE.RAUX1(JAUX) ) THEN
                GOTO 22
              ENDIF
 2221       CONTINUE
            KAUX = NBREPG*NDIM
            DO 2222 , JAUX = 1 , KAUX
              IF ( GSCOO(JAUX).NE.RAUX2(JAUX) ) THEN
                GOTO 22
              ENDIF
 2222       CONTINUE
            DO 2223 , JAUX = 1 , NBREPG
              IF ( WG(JAUX).NE.RAUX3(JAUX) ) THEN
                GOTO 22
              ENDIF
 2223       CONTINUE
C
C           SI ON ARRIVE ICI, C'EST QUE LES LOCALISATIONS SONT
C           IDENTIQUES ; ON LE NOTIFIE ET ON TERMINE LE PROGRAMME
C
            IF ( NIVINF.GT.1 ) THEN
              WRITE (IFM,22201) SAUX32
            ENDIF
22201 FORMAT(/,2X,'LA LOCALISATION ',A,' EST LA BONNE.')
C
            NOLOPG = SAUX32
C
            GOTO 40
C
          ENDIF
C
        ENDIF
C
   22 CONTINUE
C
C====
C 3. ON DOIT ECRIRE UNE NOUVELLE LOCALISATION
C    ON LUI CREE UN NOM AUTOMATIQUE.
C    ON S'ASSURE QUE LE NOM CREE N'EXISTE PAS DEJA DANS LE FICHIER
C====
C
C 3.0. ==> IMPRESSION EVENTUELLE DES CARACTERISTIQUES
C
      IF ( NIVINF.GT.1 ) THEN
C
        WRITE (IFM,*) ' '
        WRITE (IFM,61000) 'FAMILLE DE POINTS DE GAUSS', NOMFPG
        WRITE (IFM,61001) 'NOMBRE DE NOEUDS          ', NBNOTO
        WRITE (IFM,61001) 'NOMBRE DE POINTS DE GAUSS ', NBREPG
C
C     6.1. DIMENSION 1
C
        IF ( NDIM.EQ.1 ) THEN
C                            123456789012345
          WRITE (IFM,60001) 'NOEUDS         '
          DO 6011 , IAUX = 1 , NBNOTO
            WRITE (IFM,60011) IAUX,REFCOO(IAUX)
 6011     CONTINUE
          WRITE (IFM,60021)
          WRITE (IFM,60001) 'POINTS DE GAUSS'
          DO 6021 , IAUX = 1 , NBREPG
            WRITE (IFM,60011) IAUX,GSCOO(IAUX)
 6021     CONTINUE
          WRITE (IFM,60021)
C
C     6.2. DIMENSION 2
C
        ELSEIF ( NDIM.EQ.2 ) THEN
          WRITE (IFM,60002) 'NOEUDS         '
          DO 6012 , IAUX = 1 , NBNOTO
            WRITE (IFM,60012) IAUX, REFCOO(NDIM*(IAUX-1)+1),
     &                              REFCOO(NDIM*(IAUX-1)+2)
 6012     CONTINUE
          WRITE (IFM,60022)
          WRITE (IFM,60002) 'POINTS DE GAUSS'
          DO 6022 , IAUX = 1 , NBREPG
            WRITE (IFM,60012) IAUX, GSCOO(NDIM*(IAUX-1)+1),
     &                              GSCOO(NDIM*(IAUX-1)+2)
 6022     CONTINUE
          WRITE (IFM,60022)
C
C     6.3. DIMENSION 3
C
        ELSE
          WRITE (IFM,60003) 'NOEUDS         '
          DO 6013 , IAUX = 1 , NBNOTO
            WRITE (IFM,60013) IAUX, REFCOO(NDIM*(IAUX-1)+1),
     &                              REFCOO(NDIM*(IAUX-1)+2),
     &                              REFCOO(NDIM*(IAUX-1)+3)
 6013     CONTINUE
          WRITE (IFM,60023)
          WRITE (IFM,60003) 'POINTS DE GAUSS'
          DO 6023 , IAUX = 1 , NBREPG
            WRITE (IFM,60013) IAUX, GSCOO(NDIM*(IAUX-1)+1),
     &                              GSCOO(NDIM*(IAUX-1)+2),
     &                              GSCOO(NDIM*(IAUX-1)+3)
 6023     CONTINUE
          WRITE (IFM,60023)
        ENDIF
C
        WRITE (IFM,60004)
        DO 6024 , IAUX = 1 , NBREPG
          WRITE (IFM,60011) IAUX, WG(IAUX)
 6024   CONTINUE
        WRITE (IFM,60021)
C
      ENDIF
C
60001 FORMAT(
     &/,28('*'),
     &/,'*      COORDONNEES DES     *',
     &/,'*      ',A15        ,'     *',
     &/,28('*'),
     &/,'*  NUMERO  *       X       *',
     &/,28('*'))
60002 FORMAT(
     &/,44('*'),
     &/,'*       COORDONNEES DES ',A15        ,'    *',
     &/,44('*'),
     &/,'*  NUMERO  *       X       *       Y       *',
     &/,44('*'))
60003 FORMAT(
     &/,60('*'),
     &/,'*            COORDONNEES DES ',A15         ,
     &'               *',
     &/,60('*'),
     &/,'*  NUMERO  *       X       *       Y       *',
     &'       Z       *',
     &/,60('*'))
60004 FORMAT(
     &/,28('*'),
     &/,'*      POINTS DE GAUSS     *',
     &/,'*  NUMERO  *     POIDS     *',
     &/,28('*'))
60011 FORMAT('* ',I5,'    *',G11.5,'    *')
60012 FORMAT('* ',I5,2('    *',G11.5),'    *')
60013 FORMAT('* ',I5,3('    *',G11.5),'    *')
60021 FORMAT(28('*'))
60022 FORMAT(44('*'))
60023 FORMAT(60('*'))
61000 FORMAT(A,' : ',A)
61001 FORMAT(A,' : ',I4)
C
      KAUX = -1
C
   30 CONTINUE
C
C 3.1. ==> CREATION D'UN NOM POUR LA LOCALISATION
C      NOLOPG( 1:16) = NOM DE LA FAMILLE DES POINTS DE GAUSS
C      NOLOPG(17:25) = NOMBRE DE SOUS-POINTS SI > 1
C      NOLOPG(26:32) = COMPTEUR
C      LES MANQUES SONT COMPLETES PAR DES '_'
C
C     EXEMPLE : QU4_____FPG4 __________________3
C               12345678901234567890123456789012
      NOLOPG = NOMFPG
C
      IF ( NBSP.LE.1 ) THEN
        SAUX08 = '        '
C                 12345678
      ELSE
        CALL CODENT ( NBSP, 'D', SAUX08 )
      ENDIF
      NOLOPG (17:24) = SAUX08
C
      KAUX = KAUX + 1
      IF ( KAUX.EQ.0 ) THEN
        SAUX08 = '        '
C                 12345678
      ELSE
        CALL CODENT ( KAUX, 'D', SAUX08 )
      ENDIF
      NOLOPG (25:32) = SAUX08
C
      DO 31 , IAUX = 1, 32
        IF ( NOLOPG(IAUX:IAUX).EQ.' ' ) THEN
          NOLOPG(IAUX:IAUX) = '_'
        ENDIF
   31 CONTINUE
C
C 3.2. ==> LECTURE DES CARACTERISTIQUES DE LA IAUX-EME LOCALISATION
C            PRESENTE DANS LE FICHIER :
C              SAUX32 = NOM
C              TYPGEL = TYPGEO
C              NBREPL = NOMBRE DE POINTS DE GAUSS
C          SI ON EN TROUVE UNE QUI PORTE LE MEME NOM, ON RECOMMENCE
C
      DO 32 , IAUX = 1 , NBLOPG
C
        CALL EFGAUI ( IDFIMD, IAUX,
     &                SAUX32, TYPGEL, NBREPL, CODRET )
C
        IF ( CODRET.NE.0 ) THEN
          SAUX08='EFGAUI  '
          CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
        ENDIF
C
        IF ( SAUX32.EQ.NOLOPG ) THEN
          GOTO 30
        ENDIF
C
   32 CONTINUE
C
C 3.3. ==> ECRITURE DE LA NOUVELLE DESCRIPTION
C
      IF ( NIVINF.GT.1 ) THEN
C
        WRITE (IFM,33000) TYPGEO, NBNOTO, NBREPG, NOLOPG
33000 FORMAT(
     &/,'TYPE DE MAILLES MED :', I4,
     &/,'NOMBRE DE NOEUDS          :', I4
     &/,'NOMBRE DE POINTS DE GAUSS :', I4,
     &/,'ECRITURE D''UNE NOUVELLE LOCALISATION DES POINTS DE GAUSS, ',
     &  'NOMMEE : ',/,A,/)
C
      ENDIF
C
      CALL EFGAUE ( IDFIMD, TYPGEO, REFCOO, EDFUIN,
     &              NBREPG, GSCOO, WG, NOLOPG, CODRET )
C
      IF ( CODRET.NE.0 ) THEN
        SAUX08='EFGAUE  '
        CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
      ENDIF
C
C====
C 4. LA FIN
C====
C
   40 CONTINUE
C
C 4.1. ==> FERMETURE DU FICHIER MED
C
      CALL EFFERM ( IDFIMD, CODRET )
      IF ( CODRET.NE.0 ) THEN
        SAUX08='EFFERM  '
        CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
      ENDIF
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,1001) 'FIN DE '//NOMPRO
      ENDIF
C
      END
