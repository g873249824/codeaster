      SUBROUTINE IRTOPO(IOCCUR,FORMAF,IFICHI,LERESU,LRESUL,
     &                  NBMATO,NONUMA,NBNOTO,NONUNO,CODRET)
      IMPLICIT   NONE
C
      INTEGER      IOCCUR,NBNOTO,NBMATO,IFICHI,CODRET
      CHARACTER*8  FORMAF,LERESU
      CHARACTER*24 NONUMA,NONUNO
      LOGICAL      LRESUL
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 18/12/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE SELLENET N.SELLENET
C ----------------------------------------------------------------------
C  IMPR_RESU - TRAITEMENT DU MOT CLE FACTEUR IOCCUR
C  -    -                    -       ---
C ----------------------------------------------------------------------
C
C  CETTE ROUTINE SCRUTE LES MOTS-CLES NOEUD, GROUP_NO, ...
C   ET REALISE DES IMPRESSIONS POUR LE FORMAT RESULTAT
C
C IN  :
C   IOCCUR  I    NUMERO D'OCCURENCE DU MOT CLE FACTEUR
C   FORMAF  K8   FORMAT DU FICHIER A IMPRIMER
C   IFICHI  I    UNITE LOGIQUE DU FICHIER A IMPRIMER
C   LERESU  K9   NOM DU CHAMP OU DU RESULTAT
C   LRESUL  L    INDIQUE SI LERESU EST UN RESU OU UN CHAMP
C
C IN/OUT :
C   NONUMA  K24  NOM DE L'OBJET A CREER POUR LES MAILLES
C   NONUNO  K24  NOM DE L'OBJET A CREER POUR LES NOEUDS
C
C OUT :
C   NBMATO  I    NOMBRE TOTAL DE MAILLES TROUVEES (INDIQUE SI UN
C                 JEVEUO SUR NONUMA EST FAISABLE)
C   NBNOTO  I    NOMBRE TOTAL DE NOEUDS TROUVES (INDIQUE SI UN
C                 JEVEUO SUR NONUNO EST FAISABLE)
C   CODRET  I    CODE RETOUR (0 SI OK, 1 SINON)
C
      INCLUDE 'jeveux.h'
C
      INTEGER NBNO,NBGRN,NBMA,NBGRM,IARG,NBNOFA,NBGNFA,NBMAFA
      INTEGER NBGMFA,JTOPO,JLGRN,JNGRN,IBID,JLNO,JNNO,JLGRM,JNGRM
      INTEGER JLMA,JMMA,NBNOE,JINDNO,INO,JNUNOU,NBNOU,NBELE,JNUMA
      INTEGER JNUNOS,NBNOS,JNOFI,II,IGRM,IGRN,IMA,NBNOMX,NBGNMX
      INTEGER NBMAMX,NBGMMX,IMXNO,IMXGN,IMXMA,IMXGM,IDEBU,JNUNOT
      INTEGER IUTIL,LXLGUT
C
      CHARACTER*1  K1BID
      CHARACTER*8  K8B,NOMMA
      CHARACTER*24 TEXTE
C
      CALL JEMARQ()
C
C     --- TRAITEMENT DES NOEUDS,MAILLES,GPES DE NOEUDS ET MAILLES
C         (OPERANDE DE SELECTION SUR DES ENTITES TOPOLOGIQUES)

      JLNO   = 1
      NBNO   = 0
      NBGRN  = 0
      NBMA   = 0
      NBGRM  = 0
      NBMATO = 0
      NBNOS  = 0
      NBNOTO = 0
      NBNOU  = 0
C
      CODRET=0
      CALL GETVTX('RESU','NOEUD'   ,IOCCUR,IARG,0,K8B,NBNOFA)
      CALL GETVTX('RESU','GROUP_NO',IOCCUR,IARG,0,K8B,NBGNFA)
      CALL GETVTX('RESU','MAILLE'  ,IOCCUR,IARG,0,K8B,NBMAFA)
      CALL GETVTX('RESU','GROUP_MA',IOCCUR,IARG,0,K8B,NBGMFA)
      IF((NBNOFA.NE.0.OR.NBGNFA.NE.0.OR.NBMAFA.NE.0.OR.NBGMFA.NE.0)
     &     .AND. (FORMAF(1:6).EQ.'CASTEM')) THEN
        CALL U2MESS('A','PREPOST3_73')
      ENDIF
C
C     *** ON S'ALLOUE UN TABLEAU DE 8 ENTIERS POUR LA TOPOLOGIE
      CALL WKVECT('&&IRTOPO.LIST_TOPO','V V I',8,JTOPO)
C
      IF(FORMAF(1:6).NE.'CASTEM') THEN
C       *** CAS D'UNE LISTE DE GROUPES DE NOEUDS
        IF(NBGNFA.LT.0) THEN
          NBGRN = -NBGNFA
C         - ON S'ALLOUE :
C           UN TABLEAU DE K8, LISTE DES NOMS DE GPES DE NOEUDS
C           UN TABLEAU DE K80 (POUR FORMAT 'RESULTAT')
          CALL WKVECT('&&IRTOPO.LIST_GRNO','V V K24',
     &                NBGRN,JLGRN)
          CALL WKVECT('&&IRTOPO.NOM_GRNO','V V K80',
     &                NBGRN,JNGRN)
          CALL GETVTX('RESU','GROUP_NO',IOCCUR,IARG,NBGRN,
     &                ZK24(JLGRN),IBID)
          ZI(JTOPO-1+3) = NBGRN
        ELSE
      JLGRN=1
        ENDIF
C
C       *** CAS D'UNE LISTE DE NOEUDS
        IF(NBNOFA.LT.0) THEN
          NBNO = -NBNOFA
C         - ON S'ALLOUE :
C           UN TABLEAU DE K8, LISTE DES NOMS DE NOEUDS
C           UN TABLEAU DE K80 (POUR FORMAT 'RESULTAT')
          CALL WKVECT('&&IRTOPO.LIST_NOE','V V K8',NBNO,JLNO)
          CALL WKVECT('&&IRTOPO.NOM_NOE','V V K80',NBNO,JNNO)
          CALL GETVTX('RESU','NOEUD',IOCCUR,IARG,NBNO,ZK8(JLNO),IBID)
          ZI(JTOPO-1+1) = NBNO
        ENDIF
C
C       *** CAS D'UNE LISTE DE GROUPES DE MAILLES
        IF(NBGMFA.LT.0) THEN
          NBGRM = -NBGMFA
C         - ON S'ALLOUE :
C           UN TABLEAU DE K8, LISTE DES NOMS DE GPES DE MAILLES
C           UN TABLEAU DE K80 (POUR FORMAT 'RESULTAT')
          CALL WKVECT('&&IRTOPO.LIST_GRMA','V V K24',
     &                NBGRM,JLGRM)
          CALL WKVECT('&&IRTOPO.NOM_GRMA','V V K80',
     &                NBGRM,JNGRM)
          CALL GETVTX('RESU','GROUP_MA',IOCCUR,IARG,NBGRM,
     &                ZK24(JLGRM),IBID)
          ZI(JTOPO-1+7) = NBGRM
        ELSE
          JLGRM=1
          JNGRM=1
        ENDIF
C
C       ***  CAS D'UNE LISTE DE MAILLES
        IF(NBMAFA.LT.0) THEN
          NBMA = -NBMAFA
C         - ON S'ALLOUE :
C           UN TABLEAU DE K8, LISTE DES NOMS DE MAILLES
C           UN TABLEAU DE K80 (POUR FORMAT 'RESULTAT')
          CALL WKVECT('&&IRTOPO.LIST_MAI','V V K8',NBMA,JLMA)
          CALL WKVECT('&&IRTOPO.NOM_MAI','V V K80',NBMA,JMMA)
          CALL GETVTX('RESU','MAILLE',IOCCUR,IARG,NBMA,
     &                ZK8(JLMA),IBID)
          ZI(JTOPO-1+5) = NBMA
        ELSE
          JLMA=1
          JMMA=1
        ENDIF
C
C       ***  IL Y A SELECTION EN OPERANDE SUR NOEUDS OU MAILLES
C            OU DES GROUPES DE NOEUDS OU DES GROUPES DE MAILLES
        IF(NBNO.NE.0.OR.NBGRN.NE.0
     &     .OR.NBMA.NE.0.OR.NBGRM.NE.0) THEN
          IF(LRESUL) THEN
C           - C'EST UN RESULTAT COMPOSE: NOM DU MAILLAGE DANS NOMMA
            CALL DISMOI('F','NOM_MAILLA',LERESU,'RESULTAT',IBID,
     &                  NOMMA,IBID)
          ELSE
C           - C'EST UN CHAM_GD: NOM DU MAILLAGE DANS NOMMA
            CALL DISMOI('F','NOM_MAILLA',LERESU,'CHAMP',IBID,
     &                  NOMMA,IBID)
          ENDIF
C         - NOMBRE TOTAL DE NOEUDS DU MAILLAGE NOMMA = NBNOE
          CALL DISMOI('F','NB_NO_MAILLA',NOMMA,'MAILLAGE',
     &                NBNOE,K8B,IBID)
          CALL WKVECT('&&IRTOPO.IND_NOEU','V V I',NBNOE,JINDNO)
          DO 70 INO=1,NBNOE
            ZI(JINDNO+INO-1)=0
 70       CONTINUE
        ENDIF
C
C       ***  SELECTION SUR DES NOEUDS OU GROUPES DE NOEUDS
        IF( NBNO.NE.0 .OR. NBGRN.NE.0 ) THEN
C         - ON S'ALLOUE UN TABLEAU D'ENTIERS A PARTIR DE ZI(JNUNOU)
C           POUR LA LISTE DES NUMEROS DES NOEUDS A IMPRIMER
          CALL WKVECT('&&IRTOPO.NUMNOE','V V I',NBNOE,JNUNOU)
C         - ON RECUPERE A PARTIR DE ZI(JNUNOU) LES NUMEROS DES
C           NOEUDS DE LA LISTE DE NOEUDS OU DE GROUPES DE NOEUDS
C           (NBNOU EST LE NBRE TOTAL DE NOEUDS TROUVES A IMPRIMER)
          CALL IRNONO(NOMMA,NBNOE,NBNO,ZK8(JLNO),NBGRN,ZK24(JLGRN),
     &                '&&IRTOPO.NUMNOE',NBNOU,ZI(JINDNO),
     &                '&&IRTOPO.LIST_TOPO')
C         - ON RECUPERE DE NOUVEAU L'ADRESSE DE .NUMNOE CAR IRNONO
C           A PU AGRANDIR CET OBJET :
          CALL JEVEUO('&&IRTOPO.NUMNOE','L',JNUNOU)
          NBNOTO = NBNOTO + NBNOU
        ENDIF
C
C       ***  SELECTION SUR DES MAILLES OU GROUPES DE MAILLES
        IF(NBMA.NE.0.OR.NBGRM.NE.0) THEN
C         - ON S'ALLOUE UN TABLEAU POUR LES NUMEROS DES MAILLES ET
C           UN TABLEAU POUR LES NUMEROS DES NOEUDS DE CES MAILLES
          CALL JELIRA(NOMMA//'.NOMMAI','NOMMAX',NBELE,K1BID)
          CALL WKVECT(NONUMA,'V V I',NBELE,JNUMA)
          CALL WKVECT('&&IRTOPO.NUMNOS','V V I',NBNOE,JNUNOS)
C         - ON RECUPERE A PARTIR DE ZI(JNUMA) LES NUMEROS DES
C           MAILLES DE LA LISTE DE MAILLES OU DE GROUPES DE MAILLES
C           (NBMATO = NBRE TOTAL DE MAILLES TROUVEES A IMPRIMER)
          CALL IRMAMA(NOMMA,NBMA,ZK8(JLMA),NBGRM,ZK24(JLGRM),
     &                NONUMA,NBMATO,'&&IRTOPO.LIST_TOPO')
C         - ON RECUPERE DE NOUVEAU L'ADRESSE DE .NUMMAI CAR IRMAMA
C           A PU AGRANDIR CET OBJET :
          CALL JEVEUO(NONUMA,'L',JNUMA)

C         - ON RECUPERE A PARTIR DE ZI(JNUNOS) LA LISTE DES NUMEROS
C           DES NOEUDS SOMMETS DE CES MAILLES
C          (NBNOS = NOMBRE DE NOEUDS SOMMETS DE CETTE LISTE)
          CALL IRMANO(NOMMA,NBMATO,ZI(JNUMA),NBNOS,ZI(JNUNOS))
          IF ( NBNOS.EQ.0 ) CALL U2MESS('F','PREPOST5_4')
          CALL WKVECT('&&IRTOPO.FILTRE_NO','V V I',NBNOS,JNOFI)
          II=0
          DO 490 INO=1,NBNOS
            IF(ZI(JINDNO+ZI(JNUNOS+INO-1)-1).EQ.0)THEN
               II=II+1
               ZI(JNOFI+II-1)=ZI(JNUNOS+INO-1)
            ENDIF
 490      CONTINUE
          NBNOS=II
          NBNOTO = NBNOTO + NBNOS
        ENDIF
C
        IF ( NBNO.NE.0 .OR. NBGRN.NE.0 .OR.
     &       NBMA.NE.0 .OR. NBGRM.NE.0 ) THEN
          IF ( NBNOU.EQ.0 .AND. NBMATO.EQ.0 ) THEN
            CODRET=1
            GOTO 9999
          ENDIF
        ENDIF
C
C       ***  ON CREE UNE LISTE DE NUMEROS DE NOEUDS ISSUS
C            DE GROUP_NO ET GROUP_MA
        IF(NBNOTO.GT.0) THEN
C         - ON S'ALLOUE UN TABLEAU POUR LES NUMEROS DE CES NOEUDS
          CALL WKVECT(NONUNO,'V V I',NBNOTO,JNUNOT)
        ENDIF
        IF( NBNOU .GT. 0 ) THEN
C         - LISTE DES NUMEROS DE NOEUDS
          DO 500 INO=1,NBNOU
            ZI(JNUNOT-1+INO)=ZI(JNUNOU-1+INO)
  500     CONTINUE
        ENDIF
        IF( NBNOS .GT. 0 ) THEN
C         - SUIVIE DE LA LISTE DES NUMEROS DE NOEUDS SOMMETS
          DO 501 INO=1,NBNOS
            ZI(JNUNOT-1+NBNOU+INO)= ZI(JNOFI-1+INO)
  501     CONTINUE
        ENDIF
      ENDIF
C
C     ***************************************************************
C     - CHAM_GD OU RESULTAT COMPOSE AU FORMAT 'RESULTAT':
C       IMPRESSION LISTES DES NOMS DES NOEUDS ET MAILLES SELECTIONNES
C     ***************************************************************
      IF(FORMAF.EQ.'RESULTAT') THEN
        NBNOMX = ZI(JTOPO-1+2)
        NBGNMX = ZI(JTOPO-1+4)
        NBMAMX = ZI(JTOPO-1+6)
        NBGMMX = ZI(JTOPO-1+8)
        IMXNO = 0
        IMXGN = 0
        IMXMA = 0
        IMXGM = 0
        IF(NBNOMX.NE.0) THEN
          IDEBU = 12
          IMXNO = IMXNO+1
          DO 800 INO=1,NBNO
            TEXTE = ZK8(JLNO-1+INO)
            IUTIL = LXLGUT(TEXTE)
            IF(IUTIL.NE.0) THEN
              IF((IDEBU+IUTIL).GT.80) THEN
                 IMXNO = IMXNO + 1
                 IDEBU = 1
              ENDIF
              ZK80(JNNO-1+IMXNO)(IDEBU:IDEBU+IUTIL)=TEXTE(1:IUTIL)
              IDEBU=IDEBU+IUTIL+1
            ENDIF
 800      CONTINUE
        ENDIF
        IF(NBGNMX.NE.0) THEN
          IDEBU = 12
          IMXGN = IMXGN + 1
          DO 810 IGRN=1,NBGRN
            TEXTE = ZK24(JLGRN-1+IGRN)
            IUTIL = LXLGUT(TEXTE)
            IF(IUTIL.NE.0) THEN
              IF((IDEBU+IUTIL).GT.80) THEN
                 IMXGN = IMXGN + 1
                 IDEBU = 1
              ENDIF
              ZK80(JNGRN-1+IMXGN)(IDEBU:IDEBU+IUTIL)=TEXTE(1:IUTIL)
              IDEBU=IDEBU+IUTIL+1
            ENDIF
 810      CONTINUE
        ENDIF
        IF(NBGMMX.NE.0) THEN
          IDEBU = 12
          IMXGM = IMXGM + 1
          DO 820 IGRM=1,NBGRM
            TEXTE = ZK24(JLGRM-1+IGRM)
            IUTIL = LXLGUT(TEXTE)
            IF(IUTIL.NE.0) THEN
              IF((IDEBU+IUTIL).GT.80) THEN
                 IDEBU = 1
              ENDIF
              ZK80(JNGRM-1+IMXGM)(IDEBU:IDEBU+IUTIL)=TEXTE(1:IUTIL)
              IDEBU=IDEBU+IUTIL+1
            ENDIF
 820      CONTINUE
        ENDIF
        IF(NBMAMX.NE.0) THEN
          IDEBU = 12
          IMXMA = IMXMA + 1
          DO 830 IMA=1,NBMA
            TEXTE = ZK8(JLMA-1+IMA)
            IUTIL = LXLGUT(TEXTE)
            IF(IUTIL.NE.0) THEN
              IF((IDEBU+IUTIL).GT.80) THEN
                 IMXMA = IMXMA + 1
                 IDEBU = 1
              ENDIF
              ZK80(JMMA-1+IMXMA)(IDEBU:IDEBU+IUTIL)=TEXTE(1:IUTIL)
              IDEBU=IDEBU+IUTIL+1
            ENDIF
 830      CONTINUE
        ENDIF
        CALL JEVEUO('&&IRTOPO.LIST_TOPO','L',JTOPO)
        IF(IMXNO.NE.0.OR.IMXGN.NE.0.OR.IMXMA.NE.0.OR.
     &    IMXGM.NE.0)  WRITE(IFICHI,'(/,20X,A)') 'ENTITES '
     &            //'TOPOLOGIQUES SELECTIONNEES '
        IF(IMXNO.NE.0) THEN
          ZK80(JNNO-1+1)(1:11) = 'NOEUD    : '
          WRITE(IFICHI,'(1X,A80)') (ZK80(JNNO-1+INO),INO=1,IMXNO)
        ENDIF
        IF(IMXGN.NE.0) THEN
          ZK80(JNGRN-1+1)(1:11) = 'GROUP_NO : '
          WRITE(IFICHI,'(1X,A80)') (ZK80(JNGRN-1+IGRN),IGRN=1,IMXGN)
        ENDIF
        IF(IMXMA.NE.0) THEN
          ZK80(JMMA-1+1)(1:11) = 'MAILLE   : '
          WRITE(IFICHI,'(1X,A80)') (ZK80(JMMA-1+IMA),IMA=1,IMXMA)
        ENDIF
        IF(IMXGM.NE.0) THEN
          ZK80(JNGRM-1+1)(1:11) = 'GROUP_MA : '
          WRITE(IFICHI,'(1X,A80)') (ZK80(JNGRM-1+IGRM),IGRM=1,IMXGM)
        ENDIF
        WRITE(IFICHI,'(A)')
      ENDIF
 9999 CONTINUE
      CALL JEDETR ('&&IRTOPO.LIST_TOPO')
      CALL JEDETR ('&&IRTOPO.LIST_GRNO')
      CALL JEDETR ('&&IRTOPO.NOM_GRNO')
      CALL JEDETR ('&&IRTOPO.LIST_NOE')
      CALL JEDETR ('&&IRTOPO.NOM_NOE')
      CALL JEDETR ('&&IRTOPO.LIST_GRMA')
      CALL JEDETR ('&&IRTOPO.NOM_GRMA')
      CALL JEDETR ('&&IRTOPO.LIST_MAI')
      CALL JEDETR ('&&IRTOPO.NOM_MAI')
      CALL JEDETR ('&&IRTOPO.IND_NOEU')
      CALL JEDETR ('&&IRTOPO.NUMNOE')
      CALL JEDETR ('&&IRTOPO.NUMNOS')
      CALL JEDETR ('&&IRTOPO.FILTRE_NO')
C
      CALL JEDEMA()
C
      END
