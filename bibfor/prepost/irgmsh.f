      SUBROUTINE IRGMSH ( NOMCON, IFI, NBCHAM, CHAM, LRESU, NBORDR,
     +                    ORDR, NBCMP, NOMCMP, NBMAT, NUMMAI, VERSIO,
     +                    IOCC,LGMSH )
      IMPLICIT NONE
      INTEGER           IFI, NBCHAM, NBORDR, NBCMP, ORDR(*), NBMAT,
     +                  NUMMAI(*),IOCC,VERSIO
      LOGICAL           LRESU,LGMSH
      CHARACTER*(*)     NOMCON, CHAM(*),NOMCMP(*)
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 07/07/2003   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C     BUT: ECRITURE D'UN CHAMP OU D'UN CONCEPT RESULTAT AU FORMAT GMSH
C
C     ENTREE:
C     NOMCON : K8  : NOM DU CONCEPT A IMPRIMER
C     IFI    : I   : NUMERO D'UNITE LOGIQUE DU FICHIER GMSH 
C     NBCHAM : I   : NOMBRE DE CHAMP DANS LE TABLEAU CHAM
C     CHAM   : K16 : NOM DES CHAMPS SYMBOLIQUES A IMPRIMER (EX 'DEPL',
C     LRESU  : L   : INDIQUE SI NOMCON EST UN CHAMP OU UN RESULTAT
C     NBORDR : I   : NOMBRE DE NUMEROS D'ORDRE DANS LE TABLEAU ORDR
C     ORDR   : I   : LISTE DES NUMEROS D'ORDRE A IMPRIMER
C     NBCMP  : I   : NOMBRE DE COMPOSANTES A IMPRIMER
C     NOMCMP : K*  : NOMS DES COMPOSANTES A IMPRIMER
C     NBMAT  : I   : NOMBRE DE MAILLES A IMPRIMER
C     NUMMAI : I   : NUMEROS DES MAILLES A IMPRIMER
C     VERSIO : I   : =1 SI TOUTES LES MAILLES SONT DES TRIA3 OU DES TET4
C                    =2 SINON ( LES MAILLLES SONT LINEAIRES)
C     IOCC   : I   : NUM OCCURRENCE MOT CLE FACTERU RESU
C
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER           ZI
      COMMON / IVARJE / ZI(1)
      REAL*8            ZR
      COMMON / RVARJE / ZR(1)
      COMPLEX*16        ZC
      COMMON / CVARJE / ZC(1)
      LOGICAL           ZL
      COMMON / LVARJE / ZL(1)
      CHARACTER*8       ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                       ZK24
      CHARACTER*32                                ZK32
      CHARACTER*80                                         ZK80
      COMMON / KVARJE / ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
      CHARACTER*32      JEXNUM, JEXNOM, JEXATR
C     ----- FIN COMMUNS NORMALISES  JEVEUX  ----------------------------
      INTEGER       IOR, ICH, IRET, IBID, IERD, NBMA
      INTEGER       NBPOI, NBSEG, NBTRI, NBQUA, NBTET, NBPYR, NBPRI, 
     +              NBHEX
      INTEGER       JCOOR, JCONX, JPOIN, JPARA, IAD
      CHARACTER*8   TYCH, NOMA, K8B, NOMAOU
      CHARACTER*24  NJVPOI, NJVSEG, NJVTRI, NJVQUA, NJVTET, NJVPYR,
     +              NJVPRI, NJVHEX
      CHARACTER*19  NOCH19
C
C     ------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- RECUPERATION DU MAILLAGE, NB_MAILLE, ...
C
      IF ( LRESU ) THEN
         DO 30 IOR = 1 , NBORDR 
            DO 32 ICH = 1 , NBCHAM
               CALL RSEXCH(NOMCON,CHAM(ICH),ORDR(IOR),NOCH19,IRET)
               IF ( IRET .EQ. 0 ) GOTO 34
 32         CONTINUE
 30      CONTINUE
         CALL UTMESS('A','IRGMSH','AUCUN CHAMP TROUVE, PAS '//
     +                            'D''IMPRESSION AU FORMAT "GMSH"')
         GOTO 9999
 34      CONTINUE
      ELSE
         NOCH19 = NOMCON
      ENDIF
      CALL DISMOI ('F','NOM_MAILLA'  ,NOCH19,'CHAMP',IBID,NOMA,IERD)
      CALL DISMOI ('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8B ,IERD)
C
C --- ECRITURE DE L'ENTETE DU FICHIER AU FORMAT GMSH
C
      IF (.NOT.LGMSH) THEN
          CALL IRGMPF ( IFI, VERSIO )
          LGMSH = .TRUE.
      ENDIF
C
C --- RECUPERATION DES INSTANTS, FREQUENCES, ...
C
      CALL WKVECT ( '&&IRGMSH.PARA' , 'V V R', MAX(1,NBORDR), JPARA  )
      IF ( LRESU ) THEN
         CALL JEEXIN ( NOMCON(1:8)//'           .INST', IRET )
         IF ( IRET .NE. 0 ) THEN
            DO 20 IOR = 1 , NBORDR 
               CALL RSADPA(NOMCON,'L',1,'INST',ORDR(IOR),0,IAD,K8B)
               ZR(JPARA+IOR-1) = ZR(IAD)
 20         CONTINUE
         ENDIF
         CALL JEEXIN ( NOMCON(1:8)//'           .FREQ', IRET )
         IF ( IRET .NE. 0 ) THEN
            DO 22 IOR = 1 , NBORDR 
               CALL RSADPA(NOMCON,'L',1,'FREQ',ORDR(IOR),0,IAD,K8B)
               ZR(JPARA+IOR-1) = ZR(IAD)
 22         CONTINUE
         ENDIF
      ELSE
         ZR(JPARA) = 0.D0
      ENDIF
C
C --- TRANSFORMATION DU MAILLAGE EN MAILLAGE ELEMENTAIRE :
C                    POI1 , SEG2 , TRIA3  ET TETRA4
C
      NJVPOI = '&&OP0001_POI'
      NJVSEG = '&&OP0001_SEG'
      NJVTRI = '&&OP0001_TRI'
      NJVQUA = '&&OP0001_QUA'
      NJVTET = '&&OP0001_TET'
      NJVPYR = '&&OP0001_PYR'
      NJVPRI = '&&OP0001_PRI'
      NJVHEX = '&&OP0001_HEX'
      NOMAOU = '&&MAILLA'
C
      NBPOI = 0
      NBSEG = 0
      NBTRI = 0
      NBQUA = 0
      NBTET = 0
      NBPYR = 0
      NBPRI = 0
      NBHEX = 0
C
      IF (VERSIO.EQ.1) THEN
        CALL IRGMMA ( NOMA, NOMAOU, NBMAT, NUMMAI, 'V', NJVPOI, NJVSEG,
     +                NJVTRI, NJVTET, NBPOI, NBSEG, NBTRI, NBTET )
      ELSEIF (VERSIO.EQ.2) THEN
        CALL IRGMM2 ( NOMA, NOMAOU, NBMAT, NUMMAI, 'V', NJVPOI, NJVSEG,
     +                NJVTRI, NJVQUA, NJVTET, NJVPYR, NJVPRI, NJVHEX,
     +                NBPOI, NBSEG, NBTRI, NBQUA, NBTET, NBPYR, NBPRI,
     +                NBHEX )
      ENDIF
C
      CALL JEVEUO(NOMAOU//'.COORDO    .VALE'        ,'L',JCOOR)
      CALL JEVEUO(NOMAOU//'.CONNEX'                 ,'L',JCONX)
      CALL JEVEUO(JEXATR(NOMAOU//'.CONNEX','LONCUM'),'L',JPOIN)
C
C
C --- BOUCLE SUR LE NOMBRE DE CHAMPS A IMPRIMER
C 
      DO 10 ICH = 1 , NBCHAM
C
         IF( LRESU ) THEN
C        --VERIFICATION DE L'EXISTENCE DU CHAMP CHAM(ICH) DANS LA 
C          SD RESULTAT NOMCON POUR LE NO. D'ORDRE ORDR(1)
C          ET RECUPERATION DANS NOCH19 DU NOM SI LE CHAM_GD EXISTE
           CALL RSEXCH(NOMCON,CHAM(ICH),ORDR(1),NOCH19,IRET)
           IF ( IRET .NE. 0 ) GOTO 10
         ELSE
           NOCH19 = NOMCON
         ENDIF
C
C ------ RECHERCHE DU TYPE DU CHAMP (CHAM_NO OU CHAM_ELEM)
C
         CALL DISMOI('F','TYPE_CHAMP',NOCH19,'CHAMP',IBID,TYCH,IERD)
C
C ------ TRAITEMENT DU CAS CHAM_NO:
C
         IF (TYCH(1:4).EQ.'NOEU' ) THEN
            CALL IRGMCN (CHAM(ICH),IFI,NOMCON,ORDR,NBORDR,ZR(JCOOR),
     +                   ZI(JCONX),ZI(JPOIN),NJVPOI,NJVSEG,NJVTRI,
     +                   NJVQUA,NJVTET,NJVPYR,NJVPRI,NJVHEX,NBPOI,
     +                   NBSEG,NBTRI,NBQUA,NBTET,NBPYR,NBPRI,NBHEX,
     +                   NBCMP,NOMCMP,LRESU,ZR(JPARA),VERSIO)
C
C ------ TRAITEMENT DU CAS CHAM_ELEM AUX NOEUDS:
C
         ELSE IF (TYCH(1:4).EQ.'ELNO' ) THEN
            CALL IRGMCE (CHAM(ICH),IFI,NOMCON,ORDR,NBORDR,ZR(JCOOR),
     +                   ZI(JCONX),ZI(JPOIN),NJVPOI,NJVSEG,NJVTRI,
     +                   NJVQUA,NJVTET,NJVPYR,NJVPRI,NJVHEX,NBPOI,
     +                   NBSEG,NBTRI,NBQUA,NBTET,NBPYR,NBPRI,NBHEX,
     +                   NBCMP,NOMCMP,LRESU,ZR(JPARA),NOMAOU,NOMA,
     +                   VERSIO)

C
C ------ TRAITEMENT DU CAS CHAM_ELEM AUX GAUSS:
C
         ELSE IF (TYCH(1:4).EQ.'ELGA' .OR.
     +            TYCH(1:4).EQ.'ELEM' ) THEN
            CALL IRGMCG (CHAM(ICH),IFI,NOMCON,ORDR,NBORDR,ZR(JCOOR),
     +                   ZI(JCONX),ZI(JPOIN),NJVPOI,NJVSEG,NJVTRI,
     +                   NJVQUA,NJVTET,NJVPYR,NJVPRI,NJVHEX,NBPOI,
     +                   NBSEG,NBTRI,NBQUA,NBTET,NBPYR,NBPRI,NBHEX,
     +                   NBCMP,NOMCMP,LRESU,ZR(JPARA),NOMAOU,NOMA,
     +                   VERSIO)
C
C ------ AUTRE: PAS D'IMPRESSION
C
         ELSE 
            CALL UTMESS('A','IRGMSH','ON NE SAIT PAS IMPRIMER AU '
     +             // 'FORMAT "GMSH" CE TYPE DE CHAMP: '//TYCH)
         ENDIF
 10   CONTINUE      
C
      CALL JEDETR ( NJVPOI )
      CALL JEDETR ( NJVSEG )
      CALL JEDETR ( NJVTRI )
      CALL JEDETR ( NJVQUA )
      CALL JEDETR ( NJVTET )
      CALL JEDETR ( NJVPYR )
      CALL JEDETR ( NJVPRI )
      CALL JEDETR ( NJVHEX )
      CALL JEDETR ( '&&IRGMSH.PARA' )
      CALL JEDETC ( 'V', NOMAOU, 1 )
C
 9999 CONTINUE      
C
      CALL JEDEMA()
C
      END
