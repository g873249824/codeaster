      SUBROUTINE IRCHME ( FICH, CHANOM,
     >                    LRESU, NORESU, NOSIMP, NOPASE, NOMSYM,
     >                    TYPECH, NUMORD,
     >                    NBCMP,  NOMCMP, 
     >                    NBNOEC, LINOEC, NBMAEC, LIMAEC,
     >                    CODRET )
C_______________________________________________________________________
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 16/10/2002   AUTEUR GNICOLAS G.NICOLAS 
C RESPONSABLE GNICOLAS G.NICOLAS
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C        IMPRESSION DU CHAMP CHANOM NOEUD/ELEMENT ENTIER/REEL 
C        AU FORMAT MED
C     ENTREES:
C        FICH   : NOM DU FICHIER OU ON DOIT IMPRIMER LE CHAMP
C        CHANOM : NOM ASTER DU CHAM A ECRIRE
C        LRESU  : .TRUE. : INDIQUE IMPRESSION D'UN CONCEPT RESULTAT
C                 .FALSE. : IMPRESSION D'UN CHAMP GRANDEUR
C        NORESU : NOM DU RESULTAT D'OU PROVIENT LE CHAMP A IMPRIMER.
C        NOSIMP : NOM SIMPLE ASSOCIE AU CONCEPT NORESU SI SENSIBILITE
C        NOPASE : NOM DU PARAMETRE SENSIBLE
C        NOMSYM : NOM SYMBOLIQUE DU CHAMP
C        TYPECH : TYPE DU CHAMP
C        NUMORD : NUMERO D'ORDRE DU CHAMP DANS LE RESULTAT_COMPOSE.
C        NBCMP  : NOMBRE DE COMPOSANTES A ECRIRE
C        NOMCMP : NOMS DES COMPOSANTES A ECRIRE
C        NBNOEC : NOMBRE DE NOEUDS A ECRIRE (O, SI TOUS LES NOEUDS)
C        LINOEC : LISTE DES NOEUDS A ECRIRE SI EXTRAIT
C        NBMAEC : NOMBRE DE MAILLES A ECRIRE (0, SI TOUTES LES MAILLES)
C        LIMAEC : LISTE DES MAILLES A ECRIRE SI EXTRAIT
C     SORTIES:
C        CODRET : CODE DE RETOUR (0 : PAS DE PB, NON NUL SI PB)
C_______________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      CHARACTER*8 NORESU, NOSIMP, NOPASE, TYPECH
      CHARACTER*16 NOMSYM
      CHARACTER*19 CHANOM
      CHARACTER*(*) FICH, NOMCMP(*)
C
      LOGICAL LRESU
C
      INTEGER NUMORD, NBCMP
      INTEGER NBNOEC, NBMAEC
      INTEGER LINOEC(*), LIMAEC(*)
C
      INTEGER CODRET
C
C 0.2. ==> COMMUNS
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      REAL*8       ZR
      COMMON /RVARJE/ZR(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'IRCHME' )
C
      INTEGER EDNONO
      PARAMETER (EDNONO=-1)
      INTEGER EDNOPT
      PARAMETER (EDNOPT=-1)
C
      INTEGER IFM, NIVINF
      INTEGER NUMPT
      INTEGER LNOCHM
      INTEGER IAUX
C
      CHARACTER*8 SAUX08
      CHARACTER*8 UNIINS
      CHARACTER*32 NOCHMD
C
      REAL*8 INSTAN
C
      CALL INFNIV ( IFM, NIVINF )
C
C====
C 1. PREPARATIFS
C====
C
      CALL INFNIV ( IFM, NIVINF )
C
      IF ( NIVINF.GT.1 ) THEN
        CALL UTFLSH (CODRET)
        WRITE (IFM,*) '================================================'
        WRITE (IFM,*) NOMPRO, ' : ECRITURE MED DE ', CHANOM
      ENDIF
C
C 1.1. ==> NOM DU CHAMP DANS LE FICHIER MED
C
      IF ( NOPASE.EQ.'        ' ) THEN
        SAUX08 = NORESU
      ELSE
        SAUX08 = NOSIMP
      ENDIF
C
      CALL MDNOCH ( NOCHMD, LNOCHM,
     >              LRESU, SAUX08, NOMSYM, NOPASE, CODRET )
C
      IF ( CODRET.EQ.0 ) THEN
        WRITE (IFM,11000) SAUX08
        WRITE (IFM,11001) NOMSYM
        IF ( NOPASE.NE.'        ' ) THEN
          WRITE (IFM,11002) NOPASE
        ENDIF
        WRITE (IFM,11003) NOCHMD
      ELSE
         CALL UTMESS
     > ( 'A' , NOMPRO, 'IMPOSSIBLE DE DETERMINER UN NOM DE CHAMP MED.' )
         CALL UTMESS ( 'A' , NOMPRO, 'PAS D ECRITURE POUR '//CHANOM )
         CALL UTMESS ( 'A' , NOMPRO, 'ISSU DE '//NORESU )
      ENDIF
C
11000 FORMAT(/,2X,'RESULTAT   : ',A8)
11001 FORMAT(  2X,'CHAMP      : ',A16)
11002 FORMAT(  2X,'PARAMETRE  : ',A8)
11003 FORMAT(  5X,'==> NOM MED DU CHAMP : ',A32,/)
C
C 1.2. ==> INSTANT CORRESPONDANT AU NUMERO D'ORDRE
C          REMARQUE : COMME ON NE SAIT PAS FAIRE MIEUX, ON IDENTIFIE
C                     LE NUMERO DE PAS DE TEMPS ET LE NUMERO D'ORDRE.
C                     L'UNITE EST INCONNUE.
C          REMARQUE : CELA N'A DU SENS QUE POUR UN CHAMP ISSU D'UN
C                     RESULTAT. POUR UNE GRANDEUR, IL N'Y A NI NUMERO
C                     D'ORDRE, NI PAS DE TEMPS.
C
      IF ( CODRET.EQ.0 ) THEN
C
      IF ( LRESU ) THEN
C
        CALL RSADPA ( NORESU, 'L', 1, 'INST', NUMORD, 0, IAUX, SAUX08 )
        INSTAN = ZR(IAUX)
C
        NUMPT = NUMORD
C                 12345678
        UNIINS = 'INCONNUE'
C
      ELSE
C
        NUMORD = EDNONO
        NUMPT = EDNOPT
C
      ENDIF

      ENDIF
C
C====
C 2. ECRITURE DANS LE FICHIER MED
C====
C
      IF ( CODRET.EQ.0 ) THEN
C
      IF ( TYPECH(1:4).EQ.'NOEU' ) THEN
        CALL IRCNME ( FICH, NOCHMD, CHANOM,
     >                NBCMP, NOMCMP,
     >                NUMPT, INSTAN, UNIINS, NUMORD,
     >                NBNOEC, LINOEC,
     >                CODRET )
      ELSE IF ( TYPECH(1:2).EQ.'EL' ) THEN
        CALL IRCEME ( FICH, NOCHMD, CHANOM,
     >                NBCMP, NOMCMP,
     >                NUMPT, INSTAN, UNIINS, NUMORD,
     >                NBMAEC, LIMAEC,
     >                CODRET )
      ELSE
        CODRET = 1
        CALL UTMESS
     >   ( 'A' , NOMPRO, 'TYPE '//TYPECH(1:4)//' INCONNU POUR MED.' )
      ENDIF
C
      ENDIF
C
C====
C 3. BILAN
C====
C
      IF ( CODRET.NE.0 ) THEN
         CALL UTMESS
     > ( 'A' , NOMPRO, 'ECRITURE IMPOSSIBLE POUR '//CHANOM//
     >   ' AU FORMAT MED' )
      ENDIF
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,*) '================================================'
        CALL UTFLSH (CODRET)
      ENDIF
C
      END
