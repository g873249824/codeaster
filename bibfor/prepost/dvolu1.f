      FUNCTION DVOLU1(NUMELE,COORD,NORM,VOLT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 22/09/2008   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C**********************************************************
C              BUT DE CETTE ROUTINE :                     *
C CALCULER LE VOLUME DE L INTERSECTION CYLINDRE-TETRAEDRE *
C**********************************************************

C IN   NUMELE : NUMERO DE L ELEMENT
C IN   COORD  : COORDONNEES DES NOEUDS DU TETRA
C               ET DES INTERSECTIONS AVEC LE CYLINDRE
C IN   NORM   : POSITION DES NOEUDS PAR RAPPORT AU CYLINDRE
C IN   VOLT   : VOLUME DES ELEMENTS
C OUT  DVOLU1 : VOLUME DE L INTERSECTION

      IMPLICIT NONE

C DECLARATION GLOBALE

      INTEGER NUMELE,NORM(2,4)
      REAL*8 COORD(3,12),VOLT(*),DVOLU1

C DECLARATION LOCALE

      INTEGER I,J,K,L

C 1 - RECHERCHE DU POINT INTERNE (NUMELE<0) OU EXTERNE (NUMELE>0)

      DO 10 J = 1,4
        IF ( NORM(1,J).EQ.1  .AND. NUMELE.LT.0 ) I = J
        IF ( NORM(1,J).EQ.-1 .AND. NUMELE.GT.0 ) I = J
 10   CONTINUE

C 2 - DETERMINATION DANS LE TABLEAU DES POSITIONS DES INTERSECTIONS

      IF ( I.EQ.1 ) THEN
        J = 5
        K = 6
        L = 7
      ELSE IF ( I.EQ.2 ) THEN
        J = 8
        K = 5
        L = 9
      ELSE IF ( I.EQ.3 ) THEN
        J = 6
        K = 8
        L = 10
      ELSE
        J = 10
        K = 9
        L = 7
      ENDIF

C 3 - CALCUL DU VOLUME

      DVOLU1 =(COORD(1,L)-COORD(1,I))*
     &       ((COORD(2,J)-COORD(2,I))*(COORD(3,K)-COORD(3,I)) -
     &        (COORD(3,J)-COORD(3,I))*(COORD(2,K)-COORD(2,I)) )
      DVOLU1 = DVOLU1 +(COORD(2,L)-COORD(2,I))*
     &      ((COORD(1,K)-COORD(1,I))*(COORD(3,J)-COORD(3,I)) -
     &        (COORD(3,K)-COORD(3,I))*(COORD(1,J)-COORD(1,I)) )
      DVOLU1 = DVOLU1 +(COORD(3,L)-COORD(3,I))*
     &      ((COORD(1,J)-COORD(1,I))*(COORD(2,K)-COORD(2,I)) -
     &        (COORD(2,J)-COORD(2,I))*(COORD(1,K)-COORD(1,I)) )

      DVOLU1 = DVOLU1 / 6.D0
      IF ( DVOLU1.LT.0.0D0 ) DVOLU1 = -DVOLU1

      IF ( NUMELE.GT.0 ) THEN
        DVOLU1 = VOLT(NUMELE) - DVOLU1
      ENDIF
      IF ( ABS(DVOLU1).LT.1.0D-10 ) DVOLU1 = 0.0D0
      IF ( DVOLU1.LT.0.D0 ) THEN
        DVOLU1 = -DVOLU1
      ENDIF

      END
