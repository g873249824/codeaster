      SUBROUTINE ASCSYM(RM, RC, ALPHA, LT, LGV, NBSEP, ISCP, ILONC, 
     +                  ISLP, ICIRP, ILONP, DENC, DENL, NBEC, NBEL,
     +                  INDBG, INDBD, BG, BD, INDBI, INDBS, BI, BS, DNX,
     +                  DNY, INDSEX, INDSEY, IABSC1, IABSC2, IORDO1,
     +                  IORDO2, CORXG, CORXD, CORYI, CORYS, TAMPON,
     +                  NZONEX, NZONEY, NZMAX)
C
      IMPLICIT NONE
      REAL*8  RM,RC,ALPHA,LT,LGV,CORXG(*),CORXD(*),TAMPON(*),
     +        BG(*),BD(*),BI(*),BS(*),DNX(2,*),DNY(2,*),CORYI(*),
     +        CORYS(*),DENC(*),DENL(*)
      INTEGER ISCP,ICIRP,ILONP,ISLP,INDBD(*),INDBG(*),INDBI(*),INDBS(*),
     +        INDSEX(*),INDSEY(*),IABSC1(*),IABSC2(*),NBSEP, ILONC,
     +        IORDO1(*),IORDO2(*),NBEC(*),NBEL(*), NZONEX, NZONEY, NZMAX
C
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 03/05/2000   AUTEUR AUBHHMB M.BONNAMY 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C TOLE  CRP_21
C     MACR_ASCOUF_MAIL
C
C PREPARATION DES DONNEES POUR LE MAILLAGE DE PLAQUE AVEC 
C SOUS-EPAISSEURS :
C CAS D'UNE SOUS-EPAISSEUR DANS LE PLAN DE SYMETRIE 
C CONSTRUCTION D'UN QUART DU MAILLAGE
C  - CALCUL TABLEAU TRIE DES ABSCISSES ET ORDONNEES DES CENTRES 
C  - CALCUL TABLEAU DES ZONES COUVERTES PAR LES SOUS-EPAISSEURS    
C
C-----------------DONNEES FOURNIES PAR L'UTILISATEUR--------------------
C
C     RM    = RAYON MOYEN DU COUDE
C     RC    = RAYON DE CINTRAGE DU COUDE
C     ALPHA = ANGLE DU COUDE
C     LT    = LONGUEUR DE L'EMBOUT DU COTE CHARGEMENT
C     LGV   = LONGUEUR DE L'EMBOUT DU COTE CONDITIONS AUX LIMITES
C     NBSEP  = NOMBRE DE SOUS-EPAISSEURS
C     ISCP = ABSC. CIRCONF. CENTRE SOUS-EPAISSEUR SUR LA PLAQUE
C     ILONC = TAILLE LONGI SUR LE COUDE DE LA SOUS-EPAISSEUR
C     ISLP =  ABSC. LONGIT. CENTRE SOUS-EPAISSEUR SUR LA PLAQUE
C     ICIRP = TAILLE CIRCONF. SUR LA PLAQUE DE LA SOUS-EPAISSEUR
C     ILONP = TAILLE LONGIT. SUR LA PLAQUE DE LA SOUS-EPAISSEUR
C     DENC = DENSITE CIRCONF. DE LA SOUS-EPAISSEUR
C     DENL = DENSITE LONGIT. DE LA SOUS-EPAISSEUR
C     NBEC = NOMBRE D'ELEMENTS CIRCONF. DE LA SOUS-EPAISSEUR
C     NBEL = NOMBRE D'ELEMENTS LONGI DE LA SOUS-EPAISSEUR
C     COORXG = ABSCISSE DU BORD GAUCHE DE LA SOUS-EPAISSEUR I
C     COORXD = ABSCISSE DU BORD DROIT DE LA SOUS-EPAISSEUR I
C     COORYI = ORDONNEE DU BORD INFERIEUR DE LA SOUS-EPAISSEUR I
C     COORYS = ORDONNEE DU BORD SUPERIEUR DE LA SOUS-EPAISSEUR I
C     IABSC1 = CORRESPONDANCE ABSCISSE CURVILIGNE CIRCONF. SOUS-EP. I
C     IORDO1 = CORRESPONDANCE ABSCISSE CURVILIGNE LONGIT. SOUS-EP. I
C     IABSC2 = CORRESPONDANCE ABSC. GAUCHE ET DROITE CIRCONF. SOUS-EP. I
C     IORDO2 = CORRESPONDANCE ORDO. INF ET SUP. LONGIT. SOUS-EP. I
C     NZMAX = NOMBRE MAXIMAL DE ZONES
C     
C----------------------DONNEES FOURNIES PAR ASTER-----------------------
C
C     INDBG = INDICATEUR BORD GAUCHE DE LA ZONE CIRCONF J
C     INDBD = INDICATEUR BORD DROIT DE LA ZONE CIRCONF J
C     BG = ABSCISSE DU BORD GAUCHE DE LA ZONE CIRCONF J
C     BD = ABSCISSE DU BORD DROIT DE LA ZONE CIRCONF J
C     BI = ORDONNEE DU BORD INFERIEUR DE LA ZONE LONGI J
C     BS = ORDONNEE DU BORD SUPERIEUR DE LA ZONE LONGI J
C     INDBI = INDICATEUR BORD INFERIEUR DE LA ZONE LONGI J
C     INDBS = INDICATEUR BORD SUPERIEUR DE LA ZONE LONGI J
C     INDSEX = NUMERO DE SOUS-EPAISSEUR CONTENU DANS LA ZONE CIRCONF J
C     INDSEY = NUMERO DE SOUS-EPAISSEUR CONTENU DANS LA ZONE LONGI J
C     DNX = DENSITE ET NOMBRE D'ELEMENTS CIRCONF. DE LA ZONE J
C     DNY = DENSITE ET NOMBRE D'ELEMENTS LONGIT. DE LA ZONE J
C     NZONEX = NOMBRE DE ZONES CIRCONFERENTIELLES
C     NZONEY = NOMBRE DE ZONES LONGITUDINALES       
C ----------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER           ZI
      COMMON / IVARJE / ZI(1)
      REAL*8            ZR
      COMMON / RVARJE / ZR(1)
      COMPLEX*16        ZC
      COMMON / CVARJE / ZC(1)
      LOGICAL           ZL
      COMMON / LVARJE / ZL(1)
      CHARACTER*8       ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                       ZK24
      CHARACTER*32                                ZK32
      CHARACTER*80                                         ZK80
      COMMON / KVARJE / ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
C     ----- FIN COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      REAL*8  R8PI, PI, DERAFC, DERAFL
      INTEGER I, J, IFM, IUNIFI
C
      IFM = IUNIFI('MESSAGE')     
      PI = R8PI()
      DERAFC = 18.D0
      DERAFL = 5.D0                       
C
      DO 10 I=1,NZMAX
         INDSEX(I) = 0
         INDSEY(I) = 0
  10  CONTINUE
C
C --- TRI DES DONNEES SOUS-EPAISSEURS EN CIRCONFERENTIEL
C
      CALL ASCTCI(RM,NBSEP,ISCP,ICIRP,IABSC1,IABSC2,TAMPON,
     +            CORXG,CORXD) 
C
C --- CALCUL DES ZONES EN CIRCONFERENTIEL
C
      IF (ZR(ISCP).LT.(PI*RM)) THEN
C
C     LE CENTRE SOUS-EP EST DANS LA ZONE FLANC DROIT/EXTRADOS/FLANC  
C     GAUCHE, ON PRELEVE PI*RM A DROITE DE LA SOUS-EPAISSEUR
C
C            ZONE (CENTRE SOUS-EP , BORD DROIT)
C
             NZONEX = 1        
             BG(NZONEX) = ZR(ISCP)
             BD(NZONEX) =  ZR(ISCP) + ZR(ICIRP)/2.D0
             INDBG(NZONEX) = 0
             INDBD(NZONEX) = 0
             DNX(1,NZONEX) = DENC(1)
             DNX(2,NZONEX) = 0
             INDSEX(NZONEX) = 1
C
C            ZONE (BORD DROIT , CENTRE + PI*RM)
C
             NZONEX = 2
             BG(NZONEX) = BD(NZONEX-1)
             BD(NZONEX) = ZR(ISCP) + PI*RM
             INDBG(NZONEX) = 1
             INDBD(NZONEX) = 0
             DNX(1,NZONEX) = DERAFC
             DNX(2,NZONEX) = 0
C
      ELSE IF ((ZR(ISCP)+PI*RM).EQ.(2.D0*PI*RM)) THEN
C
C     SOUS-EP AXISYMETRIQUE : ON PRELEVE PI*RM A DROITE
C
C          ZONE (CENTRE SOUS-EP , BORD DROIT)
C
              NZONEX = 1
              BG(NZONEX) = ZR(ISCP)
              BD(NZONEX) = ZR(ISCP) + ZR(ICIRP)/2.D0
              INDBG(NZONEX) = 0
              INDBD(NZONEX) = 0
              DNX(1,NZONEX) = DENC(1)
              DNX(2,NZONEX) = 0
         INDSEX(NZONEX) = 1
C
      ELSE
C
C     LE CENTRE SOUS-EP EST DANS LA ZONE FLANC GAUCHE/INTRADOS/FLANC 
C     DROIT : ON PRELEVE PI*RM A GAUCHE DE LA SOUS-EPAISSEUR
C
C            ZONE (CENTRE -PI*RM, BORD GAUCHE)
C
             NZONEX = 1
             BG(NZONEX) = ZR(ISCP) - PI*RM
             BD(NZONEX) = ZR(ISCP) - ZR(ICIRP)/2.D0
             INDBG(NZONEX) = 0
             INDBD(NZONEX) = 1
             DNX(1,NZONEX) = DERAFC
             DNX(2,NZONEX) = 0
C
C            ZONE (BORD GAUCHE , CENTRE SOUS-EP)
C
             NZONEX = 2
             BG(NZONEX) = BD(NZONEX-1)
             BD(NZONEX) = ZR(ISCP)
             INDBG(NZONEX) = 0
             INDBD(NZONEX) = 0
             DNX(1,NZONEX) = DENC(1)
             DNX(2,NZONEX) = 0
         INDSEX(NZONEX) = 1
C
      END IF
C
      WRITE(IFM,*)   
      WRITE(IFM,*) 'ZONES APRES RECOUVREMENT ABSC. CURV. CIRCONF. :'
      WRITE(IFM,*) '---------------------------------------------'
      DO 20 J=1,NZONEX
        IF ((BG(J).EQ.0).AND.(BD(J).EQ.0)) THEN
          WRITE(IFM,101) 'ZONE NO',J,'<> BORNE GAUCHE = ',BG(J),
     &        ' / BORNE DROITE = ',BD(J),' * SOUS-EPAISSEUR'
        ELSE
          WRITE(IFM,101) 'ZONE NO',J,'<> BORNE GAUCHE = ',BG(J),
     &        ' / BORNE DROITE = ',BD(J)
        END IF       
 20   CONTINUE
C
C --- TRI DES DONNEES SOUS-EPAISSEURS EN AXIAL
C
      CALL ASCTLO(RC,ALPHA,LT,LGV,NBSEP,ILONC,ISLP,ILONP,IORDO1,
     &            IORDO2,TAMPON,CORYI,CORYS)      
C
C --- CALCUL DES ZONES EN AXIAL
C
C     ZONE (0 , BORD INFERIEUR)
C
      NZONEY = 1
      BI(NZONEY) = 0.D0
      BS(NZONEY) = ZR(ISLP) - ZR(ILONP)/2.D0
      INDBI(NZONEY) = 0
      INDBS(NZONEY) = 1
      DNY(1,NZONEY) = DERAFL
      DNY(2,NZONEY) = 0
C  
C     ZONE (BORD INFERIEUR, CENTRE SOUS-EP)
C
      NZONEY = 2
      BI(NZONEY) = BS(NZONEY-1)
      BS(NZONEY) = ZR(ISLP) 
      INDBI(NZONEY) = 0
      INDBS(NZONEY) = 0
      DNY(1,NZONEY) = DENL(1)
      DNY(2,NZONEY) = 0
      INDSEY(NZONEY) = 1
C 
      WRITE(IFM,*)   
      WRITE(IFM,*) 'ZONES APRES RECOUVREMENT ABSC. CURV. LONGIT. :'
      WRITE(IFM,*) '--------------------------------------------'
      DO 30 J=1,NZONEY
        IF ((BI(J).EQ.0).AND.(BS(J).EQ.0)) THEN
          WRITE(IFM,101) 'ZONE NO',J,'<> BORNE INF. = ',BI(J),
     &               ' / BORNE SUP. = ',BS(J),' * SOUS-EPAISSEUR'
        ELSE
          WRITE(IFM,101) 'ZONE NO',J,'<> BORNE INF. = ',BI(J),
     &               ' / BORNE SUP. = ',BS(J)
        END IF       
 30   CONTINUE
C
      CALL ASCNBE(RM,RC,NBSEP,CORXG,CORXD,BG,BD,CORYI,CORYS,
     +            BI,BS,INDSEX,INDSEY,IABSC1,IORDO1,DNX,DNY,
     +            NZONEX,NZONEY,NBEC,NBEL)
C     
 101  FORMAT(A,I3,A,F8.2,A,F8.2,A)
C
      END
