      SUBROUTINE IRCMPN ( NOFIMD,
     >                    NCMPRF, NCMPVE, NTLCMP,
     >                    NBVATO, NBNOEC, LINOEC, ADSL,
     >                    NTPROA, NOPFTY, NVAL )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 16/10/2002   AUTEUR GNICOLAS G.NICOLAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C_______________________________________________________________________
C  ECRITURE D'UN CHAMP - FORMAT MED - PROFIL POUR LES NOEUDS
C     -  -       -              -     -               -
C_______________________________________________________________________
C     ENTREES :
C       NOFIMD : NOM DU FICHIER MED
C       NCMPRF : NOMBRE DE COMPOSANTES DU CHAMP DE REFERENCE
C       NCMPVE : NOMBRE DE COMPOSANTES VALIDES EN ECRITURE
C       NTLCMP : SD DES NUMEROS DES COMPOSANTES VALIDES
C       NBVATO : NOMBRE DE VALEURS TOTALES
C       NBNOEC : NOMBRE D'ENTITES A ECRIRE (O, SI TOUTES)
C       LINOEC : LISTE DES ENTITES A ECRIRE SI EXTRAIT
C       ADSK, D, ... : ADRESSES DES TABLEAUX DES CHAMPS SIMPLIFIES
C     SORTIES :
C       NTPROA : SD DU PROFIL ASTER. C'EST LA LISTE DES NUMEROS ASTER
C                DES NOEUDS POUR LESQUELS LE CHAMP EST DEFINI
C       NOPFTY : NOM DES PROFILS AU SENS MED
C       NVAL   : NOMBRE DE VALEURS EFFECTIVES
C
C  COMMENTAIRE : C'EST AVEC L'USAGE DE ZL(ADSL) QU'IL FAUT FILTRER LES
C                COMPOSANTES. SEUL MOYEN FIABLE AVEC UN CHAMELEM
C                SIMPLIFIE.
C_______________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NVAL
      INTEGER NBVATO, NCMPRF, NCMPVE
      INTEGER NBNOEC
      INTEGER LINOEC(*)
      INTEGER ADSL
C
      CHARACTER*(*) NOFIMD
      CHARACTER*(*) NTLCMP, NTPROA
      CHARACTER*32 NOPFTY(0:*)
C
C 0.2. ==> COMMUNS
C
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER      ZI
      LOGICAL      ZL
      COMMON /IVARJE/ZI(1)
      COMMON /LVARJE/ZL(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'IRCMPN' )
C
      CHARACTER*8 SAUX08
      CHARACTER*24 EXICMP, NTPROM
C
      INTEGER ADNUCM, ADPROA, ADPROM
      INTEGER IFM, NIVINF
C
      INTEGER IAUX, JAUX
      INTEGER NRCMP
      INTEGER ADEXIC
C
C====
C 1. PREALABLES
C====
C 1.1. ==> RECUPERATION DU NIVEAU D'IMPRESSION
C
      CALL INFMAJ
      CALL INFNIV ( IFM, NIVINF )
C
C 1.2. ==> NOMS DES TABLEAUX
C               12   345678   9012345678901234
      NTPROM = '&&'//NOMPRO//'.PROFIL_MED     '
      EXICMP = '&&'//NOMPRO//'.EXICMP         '
C
C====
C 2. ON CREE UN PREMIER TABLEAU PAR NOEUD :
C    VRAI DES QU'UNE DES COMPOSANTES DU CHAMP EST PRESENTE SUR LE NOEUD
C    FAUX SINON
C    REMARQUE : ON EXAMINE LES NCMPVE COMPOSANTES QUI SONT DEMANDEES,
C    MAIS IL FAUT BIEN TENIR COMPTE DE NCMPRF, NOMBRE DE COMPOSANTES DE
C    REFERENCE, POUR L'ADRESSAGE DANS LE TABLEAU ADSL
C====
C
      CALL WKVECT ( EXICMP, 'V V L', NBVATO, ADEXIC )
C
      CALL JEVEUO ( NTLCMP, 'L', ADNUCM )
C
      DO 21 , IAUX = 0 , NBVATO-1
C
        JAUX = ADSL-1+IAUX*NCMPRF
        DO 211 , NRCMP = 1 , NCMPVE
          IF ( ZL(JAUX+ZI(ADNUCM+NRCMP-1)) ) THEN
            ZL(ADEXIC+IAUX) = .TRUE.
            GOTO 21
          ENDIF
  211   CONTINUE
C
   21 CONTINUE
C
C====
C 3. PROFIL : LISTE DES NOEUDS POUR LESQUELS ON AURA IMPRESSION
C    UN NOEUD EN FAIT PARTIE SI ET SEULEMENT SI AU MOINS UNE COMPOSANTE
C    Y EST DEFINIE ET S'IL FAIT PARTIE DU FILTRAGE DEMANDE
C====
C
      CALL WKVECT ( NTPROA, 'V V I', NBVATO, ADPROA )
      NVAL = 0
C
C 3.1. ==> SANS FILTRAGE : C'EST LA LISTE DES NOEUDS AVEC UNE COMPOSANTE
C          VALIDE
C
      IF ( NBNOEC.EQ.0 ) THEN
C
        DO 31 , IAUX = 1 , NBVATO
          IF ( ZL(ADEXIC+IAUX-1) ) THEN
            ZI(ADPROA+NVAL) = IAUX
            NVAL = NVAL + 1
          ENDIF
   31   CONTINUE
C
C 3.2. ==> AVEC FILTRAGE
C
      ELSE
C
        DO 32 , JAUX = 1 , NBNOEC
          IAUX = LINOEC(JAUX)
          IF ( ZL(ADEXIC+IAUX-1) ) THEN
            ZI(ADPROA+NVAL) = IAUX
            NVAL = NVAL + 1
          ENDIF
   32   CONTINUE
C
      ENDIF
C
C 3.3. ==> SAUVEGARDE DU NOMBRE DE VALEURS UTILES
C
      CALL JEECRA ( NTPROA, 'LONUTI',  NVAL, SAUX08 )
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,3301) NOMPRO, ' : NOMBRE TOTAL DE VALEURS    : ',
     >                   NBVATO
        WRITE (IFM,3301) NOMPRO, ' : NOMBRE DE VALEURS A ECRIRE : ',
     >                   NVAL
      ENDIF
 3301 FORMAT(A6,A,I8)
C
C 3.4. ==> CONVERSION DU PROFIL EN NUMEROTATION MED
C          EN FAIT, DANS LE CAS DES NOEUDS, IL Y A IDENTITE ENTRE LES
C          DEUX CAR ON NE RENUMEROTE PAS LES NOEUDS (CF IRMMNO)
C
      CALL WKVECT ( NTPROM, 'V V I', NVAL, ADPROM )
C
      DO 34 , IAUX = 0 , NVAL-1
        ZI(ADPROM+IAUX) = ZI(ADPROA+IAUX)
   34 CONTINUE
C
C====
C 4. STOCKAGE DU PROFIL DANS LE FICHIER MED
C====
C
      IF ( NVAL.NE.NBVATO ) THEN
        IAUX = 0
        CALL IRCMPF ( NOFIMD,
     >                NVAL, ZI(ADPROM), IAUX,
     >                NOPFTY )
      ENDIF
C
C====
C 5. LA FIN
C====
C
      CALL JEDETC('V','&&'//NOMPRO,1)
C
      END
