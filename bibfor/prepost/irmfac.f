      SUBROUTINE IRMFAC(IOCCUR,FORMAF,IFICHI,NIVEAU,VERSIO,
     &                  MODELE,NOMAIL,NOMARE,RESURE,LGMSH)
      IMPLICIT NONE
      INTEGER      IOCCUR,IFICHI,VERSIO,NIVEAU
      CHARACTER*8  FORMAF,RESURE,MODELE,NOMARE,NOMAIL
      LOGICAL      LGMSH
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 16/10/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE SELLENET N.SELLENET
C ----------------------------------------------------------------------
C  IMPR_RESU - TRAITEMENT DU MOT CLE FACTEUR IOCCUR
C  -    -                    -       ---
C ----------------------------------------------------------------------
C
C IN  :
C   IOCCUR  I    NUMERO D'OCCURENCE DU MOT CLE FACTEUR
C   FORMAF  K8   FORMAT DU FICHIER A IMPRIMER
C   IFICHI  I    UNITE LOGIQUE DU FICHIER A IMPRIMER
C   NIVEAU  I    NIVEAU D'IMPRESSION POUR GIBI
C   VERSIO  I    VERSION DU FICHIER IDEAS OU GMSH
C   MODELE  K8   NOM DU MODELE DONNE PAR L'UTILISATEUR
C   NOMAIL  K8   NOM DU MAILLAGE
C   NOMARE  K8   NOM DU MAILLAGE POUR LE MOT CLE RESTREINT
C   RESURE  K8   NOM DU CHAMP A IMPRIMER POUR LE MOT CLE RESTREINT
C
C IN/OUT :
C   LGMSH   L    LOGICAL SERVANT A ECRIRE L'ENTETE DU FICHIER GMSH
C
      INCLUDE 'jeveux.h'
C
      INTEGER NBNOT,NVAMIN,NBPARA,JPARA,NBMAT,NBCMP,NBNOSY,NBORDR
      INTEGER NBCMDU,JNUNOT,JNOSY,JORDR,JCMP,JNCMED,JNUMAT,NFOR,NRESU
      INTEGER NCHAM,N01,NMAIL,NCOOR,NINF,NSUP,NVAMAX,NPART,INFMAI
      INTEGER IER,IBID,IRET,NCARAE,NVARI,IARG
C
      REAL*8 BORSUP,BORINF
C
      CHARACTER*1  CECR
      CHARACTER*3  COOR,TMAX,TMIN,SAUX03,VARIEL
      CHARACTER*4  PARTIE
      CHARACTER*8  TABL,RESU,NOMAB,TYCHA,LERESU,NOMGD,CARAEL
      CHARACTER*16 FORMR,TYRES
      CHARACTER*19 RESU19
      CHARACTER*24 NOVCMP,NONUMA,NONUNO,NCHSYM,NNUORD,NNOPAR,NLICMP
      CHARACTER*80 TITRE
      PARAMETER   (NONUMA = '&&IRMFAC.NUMMAI')
      PARAMETER   (NONUNO = '&&IRMFAC.NUMNOT')
      PARAMETER   (NCHSYM = '&&IRMFAC.NOM_SYMB')
      PARAMETER   (NNUORD = '&&IRMFAC.NUME_ORDRE')
      PARAMETER   (NLICMP = '&&IRMFAC.NOM_CMP')
      PARAMETER   (NOVCMP = '&&IRMFAC.NOM_CH_MED')
      PARAMETER   (NNOPAR = '&&IRMFAC.NOM_PAR')
C
      LOGICAL LRESU,LCOR,LMAX,LMIN,LINF,LSUP,LVARIE,LRESTR,LMODEL
C
      CALL JEMARQ()
C
      NBCMDU = 0
      BORINF = 0.D0
      BORSUP = 0.D0
      LMAX = .FALSE.
      LMIN = .FALSE.
      LINF = .FALSE.
      LSUP = .FALSE.
      LCOR = .FALSE.
      CECR = 'L'
      LMODEL = .FALSE.
      IF ( MODELE.NE.' ' ) LMODEL = .TRUE.
      LRESTR = .FALSE.
      IF ( NOMARE.NE.' ' ) LRESTR = .TRUE.
C
C     --- FORMAF D'ECRITURE DES REELS ---
      FORMR=' '
      CALL GETVTX('RESU','FORMAT_R',IOCCUR,IARG,1,FORMR,NFOR)
C
C     --- MODE D'ECRITURE DES PARAMETRES------
C         (RMQUE: UNIQUEMENT INTERESSANT POUR FORMAT 'RESULTAT')
      CALL GETVTX('RESU','FORM_TABL',IOCCUR,IARG,1,TABL,NFOR)
      IF ( NFOR .NE. 0 ) THEN
         IF ( TABL(1:3) .EQ. 'OUI' ) THEN
            CECR = 'T'
         ELSEIF ( TABL(1:5) .EQ. 'EXCEL' ) THEN
            CECR = 'E'
         ENDIF
      ENDIF
C
C     --- RECUPERATION DU CARA_ELEM
      CARAEL=' '
      CALL GETVID('RESU','CARA_ELEM',IOCCUR,IARG,1,CARAEL,NCARAE)
C
C     --- IMPRESSION DES COORDONNEES------
C         (ECRITURE VARIABLES DE TYPE RESULTAT AU FORMAT 'RESULTAT')
      COOR = ' '
      CALL GETVTX('RESU','IMPR_COOR',IOCCUR,IARG,1,COOR,NCOOR)
      IF (NCOOR.NE.0 .AND. COOR.EQ.'OUI') LCOR = .TRUE.
C
C     --- SEPARATION DES DIFFERENTES OCCURENCES (FORMAT 'RESULTAT')
      IF (FORMAF.EQ.'RESULTAT') WRITE(IFICHI,'(/,1X,80(''-''))')
C
C     --- RECHERCHE TYPE DE DONNEES A TRAITER POUR L'OCCURENCE IOCCUR
C         VARIABLE DE TYPE RESULTAT (NRESU!=0)
C         OU CHAMP_GD (NCHAMP!=0)
      RESU = ' '
      PARTIE = ' '
      CALL GETVID('RESU','RESULTAT',IOCCUR,IARG,1,RESU,NRESU)
      IF (LRESTR) THEN
        NRESU=1
        RESU=RESURE
      ENDIF
      CALL GETVTX('RESU','PARTIE',IOCCUR,IARG,1,PARTIE,NPART)
      IF(NRESU.NE.0)THEN
        CALL GETTCO(RESU,TYRES)
        IF(TYRES(1:10).EQ.'DYNA_HARMO' .OR.
     &     TYRES(1:10).EQ.'ACOU_HARMO')THEN
          IF(FORMAF(1:4).EQ.'GMSH'  .OR.
     &       FORMAF(1:6).EQ.'CASTEM'.OR.
     &       FORMAF(1:3).EQ.'MED'   )THEN
            IF(NPART.EQ.0) CALL U2MESS('F','PREPOST3_69')
          ENDIF
        ENDIF
      ENDIF
C
      CALL GETVID('RESU','CHAM_GD' ,IOCCUR,IARG,1,RESU,NCHAM)
      IF(NCHAM.NE.0)THEN
        RESU19=RESU
        CALL DISMOI('C','NOM_GD',RESU19,'CHAMP',IBID,NOMGD,IER)
        IF(NOMGD(6:6).EQ.'C')THEN
          IF(FORMAF(1:4).EQ.'GMSH'.OR.FORMAF(1:6).EQ.'CASTEM') THEN
            IF(NPART.EQ.0) CALL U2MESS('F','PREPOST3_69')
          ENDIF
        ENDIF
      ENDIF
C
      LRESU = NRESU.NE.0
C     --- TEST PRESENCE DU MOT CLE INFO_MAILLAGE (FORMAT 'MED')
      INFMAI = 1
      CALL GETVTX('RESU','INFO_MAILLAGE',IOCCUR,IARG,1,SAUX03,N01)
      IF (N01.NE.0) THEN
        IF (SAUX03.EQ.'OUI'.AND.FORMAF.EQ.'MED') THEN
          INFMAI = 2
        ELSEIF (SAUX03.EQ.'OUI'.AND.FORMAF.NE.'MED') THEN
          CALL U2MESS('A','MED_63')
        ENDIF
      ENDIF
C
C     --- MAILLAGE AVEC OU SANS MODELE
C          SI LE MOT-CLE 'MODELE' EST PRESENT DANS LA COMMANDE
C          ET QUE L'ON DEMANDE L'IMPRESSION DU MAILLAGE, IL NE FAUDRA
C          IMPRIMER QUE LA PARTIE DU MAILLAGE AFFECTEE DANS LE MODELE
      NOMAIL = ' '
      NOMAB = ' '
      CALL GETVID('RESU','MAILLAGE',IOCCUR,IARG,1,NOMAIL,NMAIL)
      IF ( (FORMAF.EQ.'ASTER').AND.(NOMAIL.EQ.' ')) THEN
        CALL U2MESS('A','PREPOST3_70')
      ENDIF
      IF (LRESTR) THEN
        IF (IOCCUR.EQ.1) THEN
          NMAIL=1
          NOMAIL=NOMARE
        ELSE
          NMAIL=0
        ENDIF
      ENDIF
C
C     --- TEST DE LA COHERENCE DU MAILLAGE ET DU MODELE ---
C
      IF ( LMODEL .AND. NMAIL.NE.0 ) THEN
        CALL DISMOI('C','NOM_MAILLA',MODELE,'MODELE',IBID,NOMAB,IRET)
        IF (NOMAIL.NE.NOMAB) THEN
          CALL U2MESS('F','PREPOST3_66')
        ENDIF
      ENDIF
C
      NBCMP = 0
      NBMAT = 0
      NBNOT = 0
      LERESU = RESU
C
C     --- ECRITURE DU TITRE ---
C          SI NOMAIL = ' ' ON NE DEMANDE PAS L'IMPRESSION DU MAILLAGE
C          IRTITR SE RESUME ALORS A L'ECRITURE D'UN TITRE DANS UN K80
      CALL IRTITR(RESU,NOMAIL,FORMAF,IFICHI,TITRE)
C
C     ---  IMPRESSION DU MAILLAGE AU PREMIER PASSAGE -----
      IF( NMAIL.NE.0 .AND. FORMAF.NE.'CASTEM') THEN
        IF(FORMAF(1:4).NE.'GMSH'.OR.(NRESU.EQ.0.AND.NCHAM.EQ.0))THEN
          CALL IRMAIL(FORMAF,IFICHI,VERSIO,NOMAIL,LMODEL,MODELE,
     &                NIVEAU,INFMAI,FORMR )
        ENDIF
      ENDIF
C
      IF(NCHAM.NE.0.OR.NRESU.NE.0) THEN
        CALL IRCHOR(IOCCUR,LERESU,LRESU,NCHSYM,NNUORD,
     &              NLICMP,NOVCMP,NNOPAR,NBNOSY,NBORDR,
     &              NBCMP,NBCMDU,NBPARA,IRET)
        IF (IRET.NE.0) GOTO 99
        JNOSY = 0
        IF (NBNOSY.GT.0) CALL JEVEUO(NCHSYM,'L',JNOSY)
        JORDR = 0
        IF (NBORDR.GT.0) CALL JEVEUO(NNUORD,'L',JORDR)
        JCMP = 0
        IF (NBCMP.GT.0) CALL JEVEUO(NLICMP,'L',JCMP)
        JNCMED = 0
        IF (NBCMDU.GT.0) CALL JEVEUO(NOVCMP,'L',JNCMED)
        JPARA = 0
        IF (NBPARA.GT.0) CALL JEVEUO(NNOPAR,'L',JPARA)
      ENDIF
C
C     ON RENTRE DANS CE QUI SUIT SAUF SI ON IMPRIME LE MAILLAGE
C       NCHAM!=0 SI CHAM_GD, NRESU!=0 SI RESULTAT COMPOSE
C       DE TYPE RESULTAT
      IF( NCHAM.NE.0 .OR. NRESU.NE.0 ) THEN
        CALL IRTOPO(IOCCUR,FORMAF,IFICHI,LERESU,LRESU,
     &              NBMAT,NONUMA,NBNOT,NONUNO,IRET)
        IF (IRET.NE.0) GOTO 99
        IF (NBMAT.NE.0) THEN
          CALL JEVEUO(NONUMA,'L',JNUMAT)
        ELSE
          JNUMAT=0
        ENDIF
        IF (NBNOT.NE.0) THEN
          CALL JEVEUO(NONUNO,'L',JNUNOT)
        ELSE
          JNUNOT=0
        ENDIF
      ENDIF
C     --- IMPRESSION DANS UN INTERVALLE   BORINF,BORSUP
C      BORINF = 0.D
C      BORSUP = 0.D
      IF((NCHAM.NE.0.OR.NRESU.NE.0).AND.(FORMAF.EQ.'RESULTAT')) THEN
        CALL GETVR8('RESU','BORNE_INF',IOCCUR,IARG,1,BORINF,NINF)
        CALL GETVR8('RESU','BORNE_SUP',IOCCUR,IARG,1,BORSUP,NSUP)
        IF(NINF.NE.0) LINF=.TRUE.
        IF(NSUP.NE.0) LSUP=.TRUE.
      ENDIF
C
C     ---- IMPRESSION VALEUR MAX, VALEUR MIN----
      IF((NCHAM.NE.0.OR.NRESU.NE.0).AND.(FORMAF.EQ.'RESULTAT')) THEN
        TMAX=' '
        TMIN=' '
        CALL GETVTX('RESU','VALE_MAX',IOCCUR,IARG,1,TMAX,NVAMAX)
        CALL GETVTX('RESU','VALE_MIN',IOCCUR,IARG,1,TMIN,NVAMIN)
        IF(NVAMAX.NE.0.AND.TMAX.EQ.'OUI') LMAX=.TRUE.
        IF(NVAMIN.NE.0.AND.TMIN.EQ.'OUI') LMIN=.TRUE.
      ENDIF
C
C     TYPE DE CHAMP A IMPRIMER POUR LE FORMAT GMSH (VERSION >= 1.2)
      TYCHA=' '
      IF( (NCHAM.NE.0.OR.NRESU.NE.0) .AND.
     &     FORMAF(1:4).EQ.'GMSH' .AND. VERSIO.GE.2) THEN
        CALL GETVTX('RESU','TYPE_CHAM',IOCCUR,IARG,1,TYCHA,IBID)
      ENDIF
C
      VARIEL=' '
      CALL GETVTX('RESU','IMPR_NOM_VARI',IOCCUR,IARG,1,
     &            VARIEL,NVARI)
      IF ( VARIEL.EQ.'OUI' ) THEN
        LVARIE=.TRUE.
      ELSE
        LVARIE=.FALSE.
      ENDIF
C
C     --- APPEL A LA ROUTINE D'IMPRESSION ---
      IF(NCHAM.NE.0.OR.NRESU.NE.0) THEN
C
C       - ECRITURE DU CONCEPT LERESU SUR FICHIER FICH AU FORMAT FORM
        IF ( FORMAF(1:4).EQ.'MED' ) THEN
          CALL IREMED(LERESU,IFICHI,NCHSYM,NOVCMP,PARTIE,
     &                NNUORD,LRESU,NBNOT,ZI(JNUNOT),NBMAT,
     &                ZI(JNUMAT),NLICMP,LVARIE,CARAEL)
        ELSE
          CALL IRECRI(LERESU,FORMAF,IFICHI,TITRE,LGMSH,NBNOSY,
     &     ZK16(JNOSY),PARTIE,NBPARA,ZK16(JPARA),NBORDR,
     &     ZI(JORDR),LRESU,'RESU',IOCCUR,CECR,TYCHA,LCOR,NBNOT,
     &     ZI(JNUNOT),NBMAT,ZI(JNUMAT),NBCMP,ZK8(JCMP),LSUP,BORSUP,
     &     LINF,BORINF,LMAX,LMIN,FORMR,NIVEAU,VERSIO)
        ENDIF
      ENDIF
C     **********************
C     --- FIN IMPRESSION ---
C     **********************
 99   CONTINUE
C
C     --- DESTRUCTION TABLEAUX DE TRAVAIL
      CALL JEDETR (NCHSYM)
      CALL JEDETR (NNUORD)
      CALL JEDETR (NNOPAR)
      CALL JEDETR (NLICMP)
      CALL JEDETR (NNOPAR)
      CALL JEDETR (NOVCMP)
      CALL JEDETR (NONUMA)
      CALL JEDETR (NONUNO)
C
      CALL JEDEMA()
      END
