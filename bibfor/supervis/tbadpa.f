      SUBROUTINE TBADPA(NOMSD,LPARA,IORDR,ITYPE,RVAL,IVAL,KVAL)
      IMPLICIT   NONE
      INTEGER IORDR,ITYPE
      CHARACTER*(*) NOMSD,LPARA,KVAL
      INTEGER IVAL
      REAL*8 RVAL(2)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF SUPERVIS  DATE 09/05/2001   AUTEUR YESSAYAN A.YESSAYAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C ----------------------------------------------------------------------
C      RECUPERATION DES ADRESSES JEVEUX DES PARAMETRES DE CALCUL
C      (OU DES VARIABLES D'ACCES)
C      D'UN RESULTAT-COMPOSE POUR LE NUMERO D'ORDRE : IORDR
C      ET POUR LA LISTE DE VARIABLES DE NOMS SYMBOLIQUES LPARA.
C ----------------------------------------------------------------------
C IN  : NOMSD  : NOM DE LA STRUCTURE 'RESULTAT'.
C IN  : LPARA  : NOM DU PARAMETRE (COLONNE DE LA TABLE).
C IN  : IORDR  : NUMERO D'ORDRE SOUHAITE.
C OUT : ITYPE : I    : TYPE 0=INEXISTANT,1=REEL,2=ENTIER,3=COMPLEXE,
C                          4=K8,5=K16,6=K24,7=K32,8=K80
C                          -1=DEMANDE IMPOSSIBLE A SATISFAIRE
C OUT : RVAL  : R(2) : VALEUR R(1) SI TYPE REEL (1),
C                             R(1),R(2) SI COMPLEXE (3)
C OUT : IVAL  : I    : VALEUR SI TYPE ENTIER (2)
C OUT : KVAL  : K    : VALEUR SI TYPE CHARACTER (4,5,6,7,8)
C REMARQUE : SI ITYPE = 0, LA VALEUR N'EXISTE PAS. CE CAS DE FIGURE SE
C       RENCONTRE DANS LES TABLES OU IL PEUT EXISTER DES TROUS
C-----------------------------------------------------------------------
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR,JEXR8
C ---------------- FIN COMMUNS NORMALISES  JEVEUX  -------------------
      CHARACTER*19 NOMTAB
      INTEGER IRET,JTBNP,NBPARA,NBLIGN,ITROUV
      INTEGER JVALE,JVALL,JTBLP,J
      CHARACTER*24 INPAR,JNPAR,NOMJV,NOMJVL
      CHARACTER*4 TYPE
      COMPLEX*16 VALC

      CALL JEMARQ()

      NOMTAB=NOMSD
      CALL JEEXIN ( NOMTAB//'.TBBA', IRET )
      IF ( IRET .EQ. 0 ) THEN
        CALL UTMESS('F','TBLIVA','LA TABLE N''EXISTE PAS')
      ENDIF
C
      CALL JEVEUO ( NOMTAB//'.TBNP' , 'E', JTBNP )
      NBPARA = ZI(JTBNP  )
      NBLIGN = ZI(JTBNP+1)
      IF ( NBPARA .EQ. 0 ) THEN
        CALL UTMESS('F','TBLIVA','PAS DE PARAMETRES DEFINIS')
      ENDIF
      IF ( NBLIGN .EQ. 0 ) THEN
        CALL UTMESS('F','TBLIVA','PAS DE LIGNES DEFINIS')
      ENDIF
C
      CALL JEVEUO ( NOMTAB//'.TBLP' , 'L', JTBLP )

      INPAR = LPARA
      ITYPE=-1
      DO 40 J = 1 , NBPARA
        JNPAR = ZK24(JTBLP+4*(J-1))
        IF ( INPAR .EQ. JNPAR ) THEN
          TYPE   = ZK24(JTBLP+4*(J-1)+1)
          NOMJV  = ZK24(JTBLP+4*(J-1)+2)
          NOMJVL = ZK24(JTBLP+4*(J-1)+3)
          CALL JEVEUO ( NOMJV, 'L', JVALE )
          CALL JEVEUO ( NOMJVL, 'L', JVALL )
          IF(IORDR.GT.NBLIGN)THEN
            ITYPE=-1
          ELSEIF(IORDR.LE.0)THEN
            ITYPE=-1
          ELSEIF(.NOT. ZL(JVALL+IORDR-1))THEN
            ITYPE=0
          ELSE
            IF( TYPE(1:1) .EQ. 'R' )THEN
              RVAL(1) = ZR(JVALE+IORDR-1)
              ITYPE=1
            ELSEIF( TYPE(1:1) .EQ. 'I' )THEN
              IVAL = ZI(JVALE+IORDR-1)
              ITYPE=2
            ELSEIF( TYPE(1:1) .EQ. 'C' )THEN
              VALC=ZC(JVALE+IORDR-1)
              RVAL(1) = DBLE(VALC)
              RVAL(2) = DIMAG(VALC)
              ITYPE=3
            ELSEIF( TYPE(1:2) .EQ. 'K8' )THEN
              KVAL=ZK8(JVALE+IORDR-1)
              ITYPE=4
            ELSEIF( TYPE(1:3) .EQ. 'K16' )THEN
              KVAL=ZK16(JVALE+IORDR-1)
              ITYPE=5
            ELSEIF( TYPE(1:3) .EQ. 'K24' )THEN
              KVAL=ZK24(JVALE+IORDR-1)
              ITYPE=6
            ELSEIF( TYPE(1:3) .EQ. 'K32' )THEN
              KVAL=ZK32(JVALE+IORDR-1)
              ITYPE=7
            ELSEIF( TYPE(1:3) .EQ. 'K80' )THEN
              KVAL=ZK80(JVALE+IORDR-1)
              ITYPE=8
            ENDIF
          ENDIF
          GO TO 9999
        ENDIF
40    CONTINUE

9999  CONTINUE
      CALL JEDEMA()
      END
