      SUBROUTINE OPS024 ( ICMD , ICOND , IER )
C
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF SUPERVIS  DATE 25/01/2002   AUTEUR GNICOLAS G.NICOLAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C                   MACR_ADAP_MAIL / MACR_INFO_MAIL
C
C     ------------------------------------------------------------------
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER             ICMD , ICOND , IER
C
C     ICMD : NUMERO DE LA COMMANDE COURANTE
C     ICOND : NUMERO DE LA PHASE SUPERVISEUR
C             (OBLIGATOIRMENT -1 POUR UNE MACRO-COMMANDE)
C     IER : COMPTEUR D'ERREUR A INCREMENTER
C
C 0.2. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'OPS024' )
C
      INTEGER NBOPT
      PARAMETER ( NBOPT = 40 )
C
      INTEGER IAUX
      INTEGER TABENT(NBOPT), LGCAR(NBOPT)
C
      REAL*8 TABREE(NBOPT)
C
      CHARACTER*128 TABCAR(NBOPT)
C
      INTEGER IERUSR
C
      CHARACTER*8 NOMRES
      CHARACTER*16 TYPRES, NOMCMD
      CHARACTER*19 NOMSTI, NOMSTK
      CHARACTER*128 NOMREP
C
C     ------------------------------------------------------------------
C
C     CONTENU DU TABLEAU DES ARGUMENTS ENTIERS TABENT :
C
C   1 : NUMERO D'UNITE LOGIQUE POUR LE FICHIER DE CONFIGURATION HOMARD
C   2 : MODE DE FONCTIONNEMENT DE HOMARD
C       1 : ADAPTATION COMPLETE     2 : INFORMATION
C       3 : INTERPOLATION DE SOLUTION
C   3 : NUMERO D'ITERATION DU MAILLAGE AVANT ADAPTATION
C   6 : TYPE DE RAFFINEMENT
C       0 : PAS DE RAFFINEMENT      1 : RAFFINEMENT LIBRE
C       2 : RAFFINEMENT UNIFORME
C   7 : TYPE DE DERAFFINEMENT
C       0 : PAS DE DERAFFINEMENT    1 : DERAFFINEMENT LIBRE
C       2 : DERAFFINEMENT UNIFORME
C   8 : NOMBRE DE SOLUTIONS A INTERPOLER
C   9 : TYPE DE CRITERE DE RAFFINEMENT
C       0 : PAS DE DONNEES          1 : EN ABSOLU
C       2 : EN RELATIF              3 : EN PROPORTION D'ELEMENTS
C  10 : TYPE DE CRITERE DE DERAFFINEMENT
C       0 : PAS DE DONNEES          1 : EN ABSOLU
C       2 : EN RELATIF              3 : EN PROPORTION D'ELEMENTS
C  11 : NIVEAU MAXIMUM DE RAFFINEMENT
C  12 : NIVEAU MINIMUM DE DERAFFINEMENT
C  15 : NUMERO DE PAS DE TEMPS DE L'INDICATEUR
C  16 : NUMERO D'ORDRE DE L'INDICATEUR
C  17 : PAS DES INSTANTS D'ADAPTATION
C  18 : MENAGE DU CONCEPT MAILLAGE DE L'ETAPE INITIALE
C       0 : PAS DE DESTRUCTION      1 : DESTRUCTION
C  19 : MENAGE DU CONCEPT SOLUTION DE L'ETAPE INITIALE
C       0 : PAS DE DESTRUCTION      1 : DESTRUCTION
C  21 : UNITE LOGIQUE MED DU MAILLAGE A L'ITERATION N
C  22 : UNITE LOGIQUE MED DU MAILLAGE A L'ITERATION N+1
C  23 : UNITE LOGIQUE MED DES SOLUTIONS A L'ITERATION N
C  24 : UNITE LOGIQUE MED DES SOLUTIONS A L'ITERATION N+1
C  25 : UNITE LOGIQUE MED DE L'INDICATEUR A L'ITERATION N
C  28 : TYPE D'EXECUTABLE POUR HOMARD
C       0 : PUBLIC                  1 : PERSONNEL
C  29 : NUMERO D'UNITE LOGIQUE DU FICHIER DE MESSAGE
C  30 : NIVEAU D'INFO
C  31 : TYPE DE BILAN SUR LES NOMBRES DE NOEUDS ET ELEMENTS
C       0 : PAS DE BILAN            1 : BILAN
C  32 : TYPE DE BILAN SUR LA QUALITE DES ELEMENTS
C       0 : PAS DE BILAN            1 : BILAN
C  33 : TYPE DE BILAN SUR LA CONNEXITE DU DOMAINE
C       0 : PAS DE BILAN            1 : BILAN
C  34 : TYPE DE BILAN SUR LES TAILLES DES SOUS-DOMAINES
C       0 : PAS DE BILAN            1 : BILAN
C  35 : TYPE DE BILAN SUR L'INTERPENETRATION DES ELEMENTS
C       0 : PAS DE BILAN            1 : BILAN
C
C     ------------------------------------------------------------------
C
C     CONTENU DU TABLEAU DES ARGUMENTS REELS TABREE :
C   1 : CRITERE DE RAFFINEMENT ABSOLU, RELATIF, PROPORTION D'ELEMENTS
C   2 : CRITERE DE DERAFFINEMENT ABSOLU, RELATIF, PROPORTION D'ELEMENTS
C
C     ------------------------------------------------------------------
C
C     CONTENU DU TABLEAU DES ARGUMENTS CARACTERES TABCAR :
C     ( LGCAR(I) EST EGAL A LA LONGUEUR DE LA CHAINE TABCAR(I) )
C     ATTENTION : IL FAUT RESPECTER LES POSITIONS DE 11 A 15 POUR LES
C                 FICHIERS MED, CAR CET ORDRE EST UTILISE DANS LE
C                 LANCEMENT DE LA PROCEDURE PAR ADDM04
C
C   1 : NOM DU FICHIER DE LISTE POUR HOMARD
C   2 : NOM DU CONCEPT MAILLAGE A L'ITERATION N
C   3 : NOM DU CONCEPT MAILLAGE A L'ITERATION N+1
C   4 : NOM DU CONCEPT RESULTAT A L'ITERATION N
C   5 : NOM DU CHAMP D'INDICATEUR D'ERREUR
C   6 : NOM DE LA COMPOSANTE DE L'INDICATEUR
C  11 : NOM DU FICHIER MED DU MAILLAGE A L'ITERATION N
C  12 : NOM DU FICHIER MED DU MAILLAGE A L'ITERATION N+1
C  13 : NOM DU FICHIER MED DES SOLUTIONS A L'ITERATION N
C  14 : NOM DU FICHIER MED DES SOLUTIONS A L'ITERATION N+1
C  15 : NOM DU FICHIER MED DE L'INDICATEUR A L'ITERATION N
C  19 : NOM DU FICHIER HOMARD DU MAILLAGE A L'ITERATION N
C  20 : NOM DU FICHIER HOMARD DU MAILLAGE A L'ITERATION N+1
C  21 : NOM DE L'OBJET MAILLAGE HOMARD A L'ITERATION N
C  22 : NOM DE L'OBJET MAILLAGE HOMARD A L'ITERATION N+1
C  23 : NOM DE L'OBJET RENUMEROTATION HOMARD A L'ITERATION N
C  24 : NOM DE L'OBJET RENUMEROTATION HOMARD A L'ITERATION N+1
C  26 : NOM DU TABLEAU K8 UTILE A LA CONVERSION
C  27 : NOM DU TABLEAU ENTIER UTILE A LA CONVERSION
C  28 : NOM DE LA VERSION DE HOMARD
C  29 : NOM DU REPERTOIRE DE CALCUL DE HOMARD
C  30 : NOM DE LA PROCEDURE DE LANCEMENT DE HOMARD
C  31 : NOM DE L'OBJET MED DU MAILLAGE A L'ITERATION N
C  32 : NOM DE L'OBJET MED DU MAILLAGE A L'ITERATION N+1
C  35 : NOM DE L'OBJET MED DE L'INDICATEUR A L'ITERATION N
C  38 : LANGUE DES MESSAGES HOMARD
C  39 : NOM DE L'OBJET JEVEUX ENTIER POUR SOUS-REPERTOIRE
C  40 : NOM DE L'OBJET JEVEUX K16 POUR SOUS-REPERTOIRE
C
C     ------------------------------------------------------------------
C
      INTEGER NUMPAS
      SAVE NUMPAS
C
      DATA NUMPAS / 0 /
C
C====
C 1. PREALABLE
C====
C
C 1.1. ==> CONTROLE DE LA PHASE DU TRAITEMENT
C
      CALL JEMARQ ( )
C
C 1.1. ==> RECUPERATION DU NIVEAU D'IMPRESSION
C
      CALL INFMAJ
C
C 1.2. ==> SORTIE SI CE N'EST PAS LA BONNE PHASE
C
      IF ( ICOND .NE. -1 ) THEN
        GOTO 9999
      ENDIF
C     ------------------------------------------------------------------
C
C 1.3. ==> COMMANDE EN COURS
C
      IERUSR = 0
C
      CALL GETRES ( NOMRES, TYPRES, NOMCMD )
C
C 1.4. ==> CONSTANTES
C
      NUMPAS = NUMPAS + 1
C
      TABENT(1) = 71
      TABENT(21) = 1789 + 2*NUMPAS - 2
      TABENT(22) = 1789 + 2*NUMPAS - 1
      TABENT(23) = 1789 + 2*NUMPAS - 2
      TABENT(24) = 1789 + 2*NUMPAS - 1
      TABENT(25) = 1789 + 2*NUMPAS - 2
      TABENT(27) = 1789 + 2*NUMPAS - 2
      TABENT(28) = 1789 + 2*NUMPAS - 1
C
      CALL REPOUT ( 1, IAUX, NOMREP )
C
      TABCAR(30)(1:IAUX) = NOMREP(1:IAUX)
      TABCAR(30)(IAUX+1:IAUX+6) = 'homard'
      LGCAR(30) = IAUX + 6
C
C              123456   7890123456789
      NOMSTI = NOMPRO//'_CPT_3_I_____'
      NOMSTK = NOMPRO//'_CPT_K16_____'
      IF ( NUMPAS.EQ.1 ) THEN
        CALL JEEXIN ( NOMSTI, IAUX )
        IF ( IAUX.NE.0 ) THEN
          CALL JEDETR ( NOMSTI )
        ENDIF
        CALL JEEXIN ( NOMSTK, IAUX )
        IF ( IAUX.NE.0 ) THEN
          CALL JEDETR ( NOMSTK )
        ENDIF
      ENDIF
      TABCAR(39)(1:19) = NOMSTI
      LGCAR(39) = 19
      TABCAR(40)(1:19) = NOMSTK
      LGCAR(40) = 19
C
C====
C 2. DECODAGE DE LA MACRO-COMMANDE
C====
C
      CALL ADDM00 ( IERUSR, NOMCMD,
     >              NBOPT, TABENT, TABREE, TABCAR, LGCAR )
C
C====
C 3. RECUPERATION DES NOMS DES FICHIERS ET DES OBJETS
C====
C
      IAUX = 1
      CALL ADNOFI ( IAUX,
     >              NBOPT, TABENT, TABREE, TABCAR, LGCAR )
C
C====
C 4. DESTRUCTION DE LA COMMANDE COURANTE
C====
C
      CALL SMCDEL (ICMD, 0, IERUSR )
      ICMD   = ICMD - 1
C
C====
C 5. CREATION DE LA COMMANDE D'ECRITURE DES DONNEES AU FORMAT MED
C====
C
      CALL ADDM01 ( IERUSR, ICMD,
     >              NBOPT, TABENT, TABREE, TABCAR, LGCAR )
C
C====
C 6. CREATION DE LA COMMANDE DE CREATION DU REPERTOIRE DE CALCUL
C    POUR HOMARD
C====
C
      CALL ADDM02 ( IERUSR, ICMD,
     >              NBOPT, TABENT, TABREE, TABCAR, LGCAR )
C
C====
C 7. CREATION DE LA COMMANDE D'ECRITURE DU FICHIER DE
C    CONFIGURATION POUR HOMARD
C====
C
      CALL ADDM03 ( IERUSR, ICMD,
     >              NBOPT, TABENT, TABREE, TABCAR, LGCAR )
C
C====
C 8. CREATION DE LA COMMANDE D'EXECUTION DE HOMARD
C====
C
      CALL ADDM04 ( IERUSR, ICMD,
     >              NBOPT, TABENT, TABREE, TABCAR, LGCAR )
C
C====
C 9. CREATION DE LA COMMANDE DE LECTURE DES RESULTATS AU FORMAT MED
C====
C
      CALL ADDM05 ( IERUSR, ICMD,
     >              NBOPT, TABENT, TABREE, TABCAR, LGCAR )
C
C====
C 10. MENAGE
C====
C
      IAUX = LGCAR(26)
      IF ( IAUX.NE.0 ) THEN
        CALL JEDETR ( TABCAR(26)(1:IAUX) )
      ENDIF
C
      IAUX = LGCAR(27)
      IF ( IAUX.NE.0 ) THEN
        CALL JEDETR ( TABCAR(27)(1:IAUX) )
      ENDIF
C
C====
C 11. BILAN
C====
C
      IF ( IERUSR .GT. 0 ) THEN
        CALL UTMESS('E',NOMCMD,'ERREURS DANS LE DECODAGE DE LA MACRO')
        IER = IER + IERUSR
      ENDIF
C
 9999 CONTINUE
C
      CALL JEDEMA ( )
C
      END
