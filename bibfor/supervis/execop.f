      SUBROUTINE EXECOP()
      IMPLICIT NONE
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF SUPERVIS  DATE 26/02/2013   AUTEUR BOITEAU O.BOITEAU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_4
C     EXECUTION DE LA COMMANDE
C     ------------------------------------------------------------------
C     COMMON POUR LE NIVEAU D'"INFO"
      INTEGER         NIVUTI,NIVPGM,UNITE
      COMMON /INF001/ NIVUTI,NIVPGM,UNITE

      INTEGER    NUOPER,IUNIFI,NUOP2,IBID,IMAAV,IMAAP,MPICOW,MPICOU
      INTEGER*4  COMCOU
      REAL*8     TPRES,PCENT,RVAL(12)
      CHARACTER*6  NOMMAR
      CHARACTER*8  K8BID,K8TAB(7)
      INTEGER    ETAUSR,IRET,IRET2
C     ------------------------------------------------------------------

      CALL GCECDU(NUOPER)

      NOMMAR = 'OP'
      CALL CODENT(NUOPER,'D0',NOMMAR )

      IF ( NUOPER.EQ.9999 ) THEN
        CALL OP9999()
      ENDIF

C     -- ON NOTE LA MARQUE AVANT D'APPELER LA PROCHAINE COMMANDE :
      CALL JEVEMA(IMAAV)

C     -- ON REMET A "ZERO" LES COMMONS UTILISES PAR FOINTE :
      CALL FOINT0()

C     -- ON INITIALISATION DES COMPTEURS DE TEMPS :
      CALL UTTCPG ('INIT',' ')

C     -- ON MET A JOUR LE COMMON INF001 :
      NIVUTI = 1
      NIVPGM = 1
      UNITE  = IUNIFI('MESSAGE')

C     -- ON INITIALISE LA VARIABLE IACTIF D'UN COMMON DE CALCUL:
      CALL MECOEL(0)

      K8TAB(1) = 'LIMIT_JV'
      K8TAB(2) = 'MEM_TOTA'
      K8TAB(3) = 'VMSIZE'
      K8TAB(4) = 'CMAX_JV'
      K8TAB(5) = 'RLQ_MEM'
      K8TAB(6) = 'COUR_JV'
      K8TAB(7) = 'VMPEAK'
      CALL UTGTME(7,K8TAB,RVAL,IRET)
      IF ( RVAL(5)+RVAL(6) .GT. RVAL(3) ) THEN
C
C --- ON AJUSTE LE RELIQUAT CAR IL A DIMINUE
C
         CALL UTPTME(1,'RLQ_MEM ',(RVAL(3)-RVAL(6)),IRET)
      ENDIF
      IF (RVAL(2)-RVAL(5) .GE. 0 ) THEN
        IF ( (RVAL(2)-RVAL(5)) .GT. RVAL(1) ) THEN
          CALL JERMXD((RVAL(2)-RVAL(5))*1024*1024,IRET)
          IF ( IRET.EQ.0 ) THEN
            K8TAB(1) = 'RLQ_MEM'
            K8TAB(2) = 'LIMIT_JV'
            CALL UTGTME(2,K8TAB,RVAL,IRET2)
            CALL U2MESR('I' ,'JEVEUX1_74',2,RVAL)
          ENDIF
        ENDIF
      ENDIF

C --- COMMUNICATEUR MPI_COMM_WORLD ET COM MUNICATEUR COURANT
C --- ON VERIFIE QU'ILS SONT IDENTIQUES (SINON ERREUR PROGRAMMEUR)
C --- AVANT ET APRES L'APPEL A L'OPERATEUR
      MPICOW=COMCOU(0)
      MPICOU=COMCOU(1)
      IF (MPICOW.NE.MPICOU) CALL ASSERT(.FALSE.)

      IF ( NUOPER .LT. 0 ) THEN
        NUOP2 = ABS(NUOPER)
        CALL OPSEXE(NUOP2)
      ELSEIF ( NUOPER.LT. 100 ) THEN
        CALL EX0000(NUOPER)
      ELSEIF ( NUOPER.LT. 200 ) THEN
        CALL EX0100(NUOPER)
      ELSEIF ( NUOPER.NE.9999 )  THEN
        CALL U2MESG('E','SUPERVIS_61',0,' ',1,NUOPER,0,0.D0)
      ENDIF

      MPICOW=COMCOU(0)
      MPICOU=COMCOU(1)
      IF (MPICOW.NE.MPICOU) CALL ASSERT(.FALSE.)
C
C --- VERIFICATION SI INTERRUPTION DEMANDEE PAR SIGNAL USR1
C
      IF ( ETAUSR().EQ.1 ) THEN
         CALL SIGUSR()
      ENDIF

      CALL UTTRST(TPRES)
      IF (TPRES .LT. 0.D0) THEN
        CALL UTEXCM(28,'SUPERVIS_63',0,K8BID,0,IBID,1,-TPRES)
      ENDIF

C     -- CONTROLE DE L'APPARIEMMENT DES JEMARQ/JEDEMA
      CALL JEVEMA(IMAAP)
      IF (IMAAV.NE.IMAAP) THEN
        CALL U2MESK('F','SUPERVIS_3',1,'JEMARQ/JEDEMA')
      ENDIF

C     -- DESTRUCTION DES MATRICES DE LA BASE VOLATILE
C        ET SURTOUT DE LEURS INSTANCES MUMPS ET/OU PETSC :
      CALL DETMAT()

C     --LIBERATION DES OBJETS RAMENES EN MEMOIRE PAR JEVEUT :
      CALL JELIBZ ( 'G' )

C     -- ON IMPRIME LES COMPTEURS DE TEMPS :
C        (IL FAUT LE FAIRE AVANT LA DESTRUCTION DES OBJETS VOLATILES)
      CALL UTTCPG ('IMPR','CUMU')

C     -- DESTRUCTION DES OBJETS DE LA BASE VOLATILE :
      CALL JEDETV()
C
      PCENT = 0.01D0
      CALL JEREOU('V' , PCENT )
      CALL JERECU('G')

      END
