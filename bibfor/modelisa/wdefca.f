      FUNCTION WDEFCA(INO,S,ALPHA,F0,FRCO,FRLI)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 26/05/98   AUTEUR H1BAXBG M.LAINET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C  DESCRIPTION : CALCUL D'UNE ENERGIE DE DEFORMATION POUR DETERMINATION
C  -----------   ITERATIVE DE LA LONGUEUR SUR LAQUELLE ON DOIT PRENDRE
C                EN COMPTE LES PERTES DE TENSION PAR RECUL DE L'ANCRAGE
C                APPELANT : ANCRCA
C
C  IN     : INO    : INTEGER , SCALAIRE
C                    INDICE DU NOEUD DONT L'ABSCISSE CURVILIGNE
C                    CORRESPOND A L'ESTIMATION A L'ITERATION COURANTE
C                    DE LA LONGUEUR SUR LAQUELLE ON PREND EN COMPTE
C                    LES PERTES DE TENSION PAR RECUL DE L'ANCRAGE
C  IN     : S      : REAL*8 , VECTEUR DE DIMENSION NBNO
C                    CONTIENT LES VALEURS DE L'ABSCISSE CURVILIGNE
C                    LE LONG DU CABLE
C  IN     : ALPHA  : REAL*8 , VECTEUR DE DIMENSION NBNO
C                    CONTIENT LES VALEURS DE LA DEVIATION ANGULAIRE
C                    CUMULEE LE LONG DU CABLE
C  IN     : F0     : REAL*8 , SCALAIRE
C                    VALEUR DE LA TENSION APPLIQUEE A L'UN OU AUX DEUX
C                    ANCRAGES ACTIFS DU CABLE
C  IN     : FRCO   : REAL*8 , SCALAIRE
C                    VALEUR DU COEFFICIENT DE FROTTEMENT EN COURBE
C                    (CONTACT ENTRE LE CABLE ACIER ET LE MASSIF BETON)
C  IN     : FRLI   : REAL*8 , SCALAIRE
C                    VALEUR DU COEFFICIENT DE FROTTEMENT EN LIGNE
C                    (CONTACT ENTRE LE CABLE ACIER ET LE MASSIF BETON)
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
      REAL*8        WDEFCA
C
C ARGUMENTS
C ---------
      INTEGER       INO
      REAL*8        S(*), ALPHA(*), F0, FRCO, FRLI
C
C VARIABLES LOCALES
C -----------------
      INTEGER       I
      REAL*8        XI, XIP1, XREF
C
C-------------------   DEBUT DU CODE EXECUTABLE    ---------------------
C
      IF ( INO.EQ.1 ) THEN
         WDEFCA = 0.0D0
         GO TO 9999
      ENDIF
C
      XREF = - FRCO * ALPHA(INO) - FRLI * S(INO)
      XREF = 2.0D0 * XREF
      XREF = DBLE(EXP(XREF))
C
      WDEFCA = 0.0D0
      DO 10 I = 1, INO-1
         XI = - FRCO * ALPHA(I) - FRLI * S(I)
         XI = DBLE(EXP(XI))
         XIP1 = - FRCO * ALPHA(I+1) - FRLI * S(I+1)
         XIP1 = DBLE(EXP(XIP1))
         WDEFCA = WDEFCA + ( XI - XREF/XI + XIP1 - XREF/XIP1 ) / 2.0D0
     &                   * ( S(I+1) - S(I) )
  10  CONTINUE
      WDEFCA = WDEFCA * F0
C
9999  CONTINUE
C
C --- FIN DE WDEFCA.
      END
