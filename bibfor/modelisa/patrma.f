      SUBROUTINE PATRMA(LLIST1,LLIST2,O,THETA,T,NBTYMX,NOMMA,
     &                  LLISTT,NTYPM)
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*(*) LLIST1,LLIST2,LLISTT
      CHARACTER*8   NOMMA
      REAL*8        O(3),THETA(3),T(3)
      INTEGER       NBTYMX,NTYPM
C---------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     BUT: TRIER DE 2 LISTES DE MAILLES GENEREES PAR LA ROUTINE PALIMA
C     A FIN DE GENERER UN OJB METTANT EN VIS A VIS LES 2 LISTES VIA
C     LA TRANSFORMATION  OM.THETA+T
C     LLISTT : OJB XD V I NUM()
C     LLISTT(I) : VECTEUR D'ENTIER DE DIM=3+NBMA*(2+2*NBNTOT)
C          V(1) = NUMTMC NUMERO DU TYPE_MAIL DES MAILLES DE COUPLAGE
C          V(2) = NBMA : NBRE DE COUPLAGE
C          V(3) = NBNTOT : NOMBRE DE NOEUDS DES MAILLES EN VIS A VIS
C          POUR IC = 1,NBMA
C    V(3+(IC-1)*(2+2*NBNTOT)+1)= NUMA1 NUM. DE LA 1ERE MAILLE DU COUPLE
C    V(3+(IC-1)*(2+2*NBNTOT)+2)= NUMA2 NUM. DE LA 2EME MAILLE DU COUPLE
C                        EN VIS A VIS AVEC NUMA1 VIA LA TRANSFORMATION
C           POUR INO = 1,NBNTOT
C V(3+(IC-1)*(2+2*NBNTOT)+2+2*(INO-1)+1)=N1(INO)NUM.DU NO. INO DE NUMA1
C V(3+(IC-1)*(2+2*NBNTOT)+2+2*(INO-1)+1)= N2(N1(INO)) NUM.DU NOEUD DE
C                   NUMA2 EN VIS A VIS AVEC LE NOEUD N1(INO)
C
C ARGUMENTS D'ENTREE:
C IN   LLIST1 K24 : NOM DE LA 1ERE LISTE
C IN   LLIST2 K24 : NOM DE LA 2EME LISTE
C IN   NOMMA  K8  : NOM DU MAILLAGE
C IN   O      R(3): COORDONNEES DU CENTRE DE ROTATION
C IN   THETA  R(3): ANGLES DE ROTATION
C IN   T      R(3): COORDONNEES DE LA TRANSLATION
C IN   NBTYMX I   : NBRE MAX DE TYPE_MAILLE DIFFERENT (< OU = NBGREL)
C OUT  LLISTT K24 : NOM DE LA LISTE TRIEE RESULTAT
C OUT  NTYPM  I   : NBRE DE TYPE_MAIL DIFFERENT
C
C ROUTINES APPELEES:
C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER           ZI
      COMMON / IVARJE / ZI(1)
      REAL*8            ZR
      COMMON / RVARJE / ZR(1)
      COMPLEX*16        ZC
      COMMON / CVARJE / ZC(1)
      LOGICAL           ZL
      COMMON / LVARJE / ZL(1)
      CHARACTER*8       ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                       ZK24
      CHARACTER*32                                ZK32
      CHARACTER*80                                         ZK80
      COMMON / KVARJE / ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
      CHARACTER*32      JEXNOM, JEXNUM
C     ------- FIN COMMUNS NORMALISES  JEVEUX  --------------------------
      LOGICAL FINTYP
      INTEGER NBNOTT(3)
      REAL*8 MROT(3,3)
      CHARACTER*16 NOMTEM,NOMTE
      CHARACTER*8  NOMMA1,NOMMA2,NOMMA3
      CHARACTER*24 L1,L2,LT,TW,COOR1,COOR2,COUPLE,WCPL,CONNEX,BIJECT
      CHARACTER*8 K8BID

C --- DEBUT
      CALL JEMARQ()
      L1 = LLIST1
      L2 = LLIST2
      LT = LLISTT
      CALL JEEXIN(LT,IRET)
      IF (IRET.NE.0) CALL JEDETR(LT)
      CONNEX = NOMMA//'.CONNEX'
      COOR1 = '&&PATRMA.COOR1'
      COOR2 = '&&PATRMA.COOR2'
      WCPL = '&&PATRMA.WCOUPLE'
      COUPLE = '&&PATRMA.COUPLE'
      BIJECT = '&&PATRMA.BIJECT'
      CALL JEVEUO(NOMMA//'.COORDO    .VALE','L',IAGEOM)


C
      CALL JEVEUO(L1,'L',IDL1)
      NBMA1 = ZI(IDL1)
      CALL JEVEUO(L2,'L',IDL2)
      NBMA2 = ZI(IDL2)
      IF (NBMA1.NE.NBMA2) CALL UTMESS('F','PATRMA_1','LES 2 LISTES'//
     &   L1//' ET '//L2//' NE SONT PAS DE MEME LONGUEUR')
C        CALL U2MESK('F','MODELISA6_22', 2 ,VALK)
      NBMA = NBMA1
      CALL WKVECT(BIJECT,'V V I',NBMA,IDBIJ)
C
C     CREATION DE LLISTT
C
      CALL JECREC(LT,'V V I','NU','DISPERSE','VARIABLE',NBTYMX)
      FINTYP = .FALSE.
      NTYPM = 0
      JDEB = 1
      JFIN = NBMA
      IDTYP = 1
      IFTYP = NBMA
      ITYP1 = -1
      CALL MATROT ( THETA , MROT )
      DO 1 I1 = 1, NBMA
        NUMA1 = ZI(IDL1+2*I1-1)
        CALL JENUNO(JEXNUM(NOMMA//'.NOMMAI',NUMA1),NOMMA1)
        ITYP = ZI(IDL1+2*I1)
        IF (ITYP.NE.ITYP1) THEN
          IF (ITYP1.NE.-1) THEN
            CALL JEDETR(COOR1)
            CALL JEDETR(COOR2)
            CALL JEDETR(WCPL)
            CALL JEDETR(COUPLE)
          ENDIF
          IMA= 0
          NTYPM = NTYPM+1
          ITYP1 = ITYP
          CALL PANBNO(ITYP1,NBNOTT)
          NBNTOT= NBNOTT(1)+NBNOTT(2)+NBNOTT(3)
C --- NUM DU TYPE_MAILLE DE COUPLAGE : NUMTMC
C
          JDEB = 1
          JFIN = NBMA
          FINTYP = .FALSE.
          IDTYP = 1
          IFTYP = NBMA
          CALL WKVECT(COOR1,'V V R',3*NBNOTT(1),IDCOO1)
          CALL WKVECT(COOR2,'V V R',3*NBNOTT(1),IDCOO2)
          CALL WKVECT(WCPL,'V V I',NBNTOT,IDWCPL)
          CALL WKVECT(COUPLE,'V V I',NBNTOT,IDCOPL)
        ENDIF
        CALL PAROTR(NOMMA,IAGEOM,NUMA1,NBNOTT,O,MROT,T,ZR(IDCOO1))
        DMIN = 999999999999.D0
        DO 2 J = JDEB,JFIN
          JTYP = ZI(IDL2+2*J)
          IF (JTYP.NE.ITYP1) THEN
            IF (FINTYP) THEN
              IFTYP = J-1
              GOTO 21
            ELSE
              IDTYP = J+1
              GOTO 2
            ENDIF
          ELSE
            FINTYP = .TRUE.
            IMA2 = ZI(IDL2+2*J-1)
            CALL JENUNO(JEXNUM(NOMMA//'.NOMMAI',IMA2),NOMMA2)
            CALL PACOOR(NOMMA,IMA2,NBNOTT(1),ZR(IDCOO2))
            CALL PADTMA(ZR(IDCOO1),ZR(IDCOO2),NBNOTT,ZI(IDWCPL),D)
            IF (D.LT.DMIN) THEN
              DMIN = D
              NUMA2 = IMA2
              J2 = J
              DO 3 INO = 1, NBNTOT
                ZI(IDCOPL-1+INO) = ZI(IDWCPL-1+INO)
3             CONTINUE
            ENDIF
          ENDIF
2       CONTINUE
        IF (IDTYP.EQ.NBMA+1) THEN
          CALL UTDEBM('F','PATRMA_2','ON NE TROUVE PAS DANS LA PAROI 2')
          CALL UTIMPI('S',' DE MAILLE DE TYPE : ',1,ITYP)
          CALL UTFINM()
        ENDIF
21      CONTINUE
        IMA = IMA+1
        JDEB = IDTYP
        JFIN = IFTYP
        NBMATY = JFIN-JDEB+1
        FINTYP = .FALSE.
        IF (IMA.EQ.1) THEN
          IF (NTYPM.GT.1) CALL JELIBE(JEXNUM(LT,NTYPM-1))
          CALL JECROC(JEXNUM(LT,NTYPM))
          LONMX =3+NBMATY*(2+2*NBNTOT)
          CALL JEECRA(JEXNUM(LT,NTYPM),'LONMAX',LONMX,K8BID)
          CALL JEVEUO(JEXNUM(LT,NTYPM),'E',IDLT)
          ZI(IDLT) = ITYP1
          ZI(IDLT+1) = NBMATY
          ZI(IDLT+2) = NBNTOT
          IDLT =IDLT+2
        ENDIF
        ZI(IDLT+1) = NUMA1
        ZI(IDLT+2) = NUMA2
        IF (ZI(IDBIJ-1+J2).EQ.0) THEN
          ZI(IDBIJ-1+J2) = NUMA1
        ELSE
          CALL JENUNO(JEXNUM(NOMMA//'.NOMMAI',NUMA1),NOMMA1)
          CALL JENUNO(JEXNUM(NOMMA//'.NOMMAI',NUMA2),NOMMA2)
          NUMA3 = ZI(IDBIJ+J2-1)
          CALL JENUNO(JEXNUM(NOMMA//'.NOMMAI' ,NUMA3),NOMMA3)
          CALL UTDEBM('F','PATRMA_3','CONFLIT DANS LES VIS_A_VIS')
          CALL UTIMPK('L','LES MAILLES ',1,NOMMA1)
          CALL UTIMPK('S',' ET ',1,NOMMA3)
          CALL UTIMPK('L',' ONT TOUTES LES 2 COMME VIS_A_VIS LA MAILLE',
     &                 1,NOMMA2)
          CALL UTFINM()
        ENDIF
        CALL JEVEUO(JEXNUM(CONNEX,NUMA1),'L',IDNO1)
        CALL JEVEUO(JEXNUM(CONNEX,NUMA2),'L',IDNO2)
        DO 4 K =1,NBNTOT
          ZI(IDLT+2*K+1) = ZI(IDNO1-1+K)
          ZI(IDLT+2*K+2) = ZI(IDNO2-1+ZI(IDCOPL-1+K))
4       CONTINUE
        IDLT = IDLT+2+2*NBNTOT
1     CONTINUE
      CALL JELIBE(JEXNUM(LT,NTYPM))
      CALL JEDETR(BIJECT)
      CALL JEDETR(COOR1)
      CALL JEDETR(COOR2)
      CALL JEDETR(WCPL)
      CALL JEDETR(COUPLE)
      CALL JEDEMA()
      END
