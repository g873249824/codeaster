      SUBROUTINE POINCO(CHAR,MOTFAC,NOMA,NZOCO,ORDSTC,INDQUA,
     &                  NSUCO,NMACO,NNOCO,NNOQUA,NTRAV)
     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 07/10/2004   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
      IMPLICIT     NONE
      CHARACTER*8  CHAR
      CHARACTER*16 MOTFAC
      CHARACTER*8  NOMA
      INTEGER      NZOCO
      INTEGER      ORDSTC
      INTEGER      INDQUA     
      INTEGER      NSUCO
      INTEGER      NMACO
      INTEGER      NNOCO
      INTEGER      NNOQUA
      INTEGER      NTRAV    
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : CALICO
C ----------------------------------------------------------------------
C
C DETERMINATION DU NOMBRE DE ZONES DE CONTACT ET DU NOMBRE TOTAL DE
C MAILLES ET DE NOEUDS DE CONTACT. REMPLISSAGE DES POINTEURS ASSOCIES.
C
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  MOTFAC : MOT-CLE FACTEUR
C IN  NOMA   : NOM DU MAILLAGE
C IN  NZOCO  : NOMBRE DE ZONES DE CONTACT
C IN  ORDSTC : ORDRE DE STOCKAGE DES ZONES MAITRES ET ESCLAVES
C              0: MAITRES PUIS ESCLAVES
C              1: ESCLAVES PUIS MAITRES
C IN  INDQUA : VAUT 1 LORSQUE L'ON DOIT IGNORER LES NOEUDS MILIEUX DANS
C              NBNOEL (PENALISATION SUR LE CONTACT OU METHODE CONTINUE)
C OUT NSUCO  : NOMBRE TOTAL DE SURFACES DE CONTACT
C OUT NMACO  : NOMBRE TOTAL DE MAILLES DES SURFACES DE CONTACT
C OUT NNOCO  : NOMBRE TOTAL DE NOEUDS DES SURFACES DE CONTACT
C OUT NNOQUA : NOMBRE TOTAL DE NOEUDS QUADRATIQUES DES SURFACES DE
C              CONTACT
C OUT NTRAV  : NOMBRE MAXIMUM DE GROUPES ET DE MAILLES IMPLIQUES DANS 
C              LE CONTACT (TOUTES ZONES CONFONDUES)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      IOC,ISY,IWRITE,IREAD,ISYME
      INTEGER      NZOCP,NGTOT,NSYME
      CHARACTER*24 PZONE,PSURMA,PSURNO,PNOQUA
      INTEGER      JZONE,JSUMA,JSUNO,JNOQUA
      CHARACTER*24 SYMECO
      INTEGER      JSYME
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()

C --- NOMS JEVEUX
      PZONE  = CHAR(1:8)//'.CONTACT.PZONECO'
      PSURMA = CHAR(1:8)//'.CONTACT.PSUMACO'
      PSURNO = CHAR(1:8)//'.CONTACT.PSUNOCO'
      PNOQUA = CHAR(1:8)//'.CONTACT.PNOEUQU'
      SYMECO = CHAR(1:8)//'.CONTACT.SYMECO'

C --- NOMBRE DE ZONES SYMETRIQUES
      NSYME = 0
      CALL JEVEUO(SYMECO,'L',JSYME)    
      NSYME = ZI(JSYME)

C  --- DETERMINATION DU NOMBRE DE GROUPES DE MAILLES
C  --- ET DE MAILLES PAR ZONE POUR TOUTES LES ZONES DE CONTACT
C  --- REMPLISSAGE DU POINTEUR ASSOCIE JZONE. 

      CALL WKVECT (PZONE,'G V I',NZOCO+1,JZONE)     

      ZI(JZONE) = 0  
      NSUCO = 0
      
C  --- NOMBRE MAXIMUM DE GROUPES ET DE MAILLES IMPLIQUES DANS LE CONTACT
C  --- (TOUTES ZONES CONFONDUES).
C  --- SERT POUR LA CONSTRUCTION D'UN VECTEUR DE TRAVAIL DANS NBSUCO
      NTRAV = 0  

C ----------------------------------------------------------------------
C --- GESTION DES ZONES SYMETRIQUES ET DES OCCURRENCES DE CONTACT
C --- UNE ZONE DE CONTACT (QUELCONQUE) EST REFERENCEE A DEUX NIVEAUX:
C ---  1/ DANS AFFE_CHAR_MECA (NIVEAU UTILISATEUR)
C ---  2/ DANS LA SD DE CONTACT (NIVEAU PROGRAMMEUR)
C --- POUR LE CAS DE L'APPARIEMMENT CLASSIQUE (NON SYMETRIQUE), LES 
C ---  REFERENCES SONT IDENTIQUES. 
C --- POUR LE CAS DES ZONES SYMETRIQUES, ON CREE DEUX REFERENCES DANS
C ---  LA SD ALORS QU'IL N'Y EN A QU'UNE DANS AFFE_CHAR_MECA.
C ----------------------------------------------------------------------

C --- IREAD: ON LIT LA IREAD-EME OCCURRENCE DANS LE MOT-CLEF 
C            AFFE_CHAR_MECA
C --- IWRITE: ON ECRIT LA IWRITE-EME OCCURRENCE DANS LA SD DE CONTACT

C --- ON NE BOUCLE QUE SUR LES ZONES PRINCIPALES:
      NZOCP = NZOCO - NSYME

      DO 2 IOC = 1,NZOCP
C         POUR LES ZONES DE CONTACT PRINCIPALES: IREAD = IWRITE
         IREAD  = IOC
         IWRITE = IOC            
C         DECOMPTE DU NOMBRE DE SURFACES, DE MAILLES ET DE GROUPES DE
C           MAILLES
         CALL NBZOCO(CHAR,MOTFAC,NOMA,IREAD,IWRITE,JZONE,
     +               NSUCO,NGTOT)
         NTRAV = MAX(NTRAV,NGTOT)
  2   CONTINUE  
  
C --- ON BOUCLE SUR LES ZONES PRINCIPALES MAIS ON AGIT SUR LES ZONES
C     SYMETRIQUES
C --- POUR ZONES DE CONTACT SYMETRIQUES
C ---   IREAD: OCCURRENCE DU MOT-CLEF CONTACT SYMETRIQUE
C ---   IWRITE: OCCURRENCE DANS LA SD DE LA ZONE DE CONTACT SYMETRIQUE
 
      IF (NSYME.GT.0) THEN  
         ISYME = 0
         DO 3 IOC = 1,NZOCP
C           POUR LES ZONES DE CONTACT SYMETRIQUES
C           IREAD = ON LIT LA IOC OCCURENCE DANS AFFE_CHAR_MECA
            IREAD = IOC
            DO 4 ISY = 1,NSYME      
               IF (ZI(JSYME+ISY).EQ.IOC) THEN
C                    CETTE ZONE EST DE TYPE SYMETRIQUE
                  ISYME  = ISYME +1
C                    IWRITE: ON ECRIT A LA FIN DE LA SD CONTACT
                  IWRITE = NZOCP+ISYME
C                    DECOMPTE DU NOMBRE DE SURFACES, DE MAILLES
C                     ET DE GROUPES DE MAILLES
                  CALL NBZOCO(CHAR,MOTFAC,NOMA,IREAD,IWRITE,JZONE,
     +                  NSUCO,NGTOT)
               ENDIF
  4         CONTINUE             
  3      CONTINUE   
         IF (ISYME.NE.NSYME) CALL UTMESS ('F','POINCO',
     +                                 'ERREUR NBZOCO/SYMETRIQUE')
         IF (IWRITE.NE.NZOCO) CALL UTMESS ('F','POINCO',
     +                                 'ERREUR NBZOCO/SYMETRIQUE')     
      ENDIF
      
C --- NSUCO EST LE NOMBRE TOTAL DE SURFACES DE CONTACT Y COMPRIS 
C     LES ZONES SYMETRIQUES 

C --- DETERMINATION DU NOMBRE DE MAILLES, DE NOEUDS DE CONTACT ET
C --- DE NOEUDS DE CONTACT QUADRATIQUES POUR TOUTES LES SURFACES 
C     DE CONTACT
C --- REMPLISSAGE DES POINTEURS ASSOCIES JSUMA,JSUNO,JNOQUA
     
      CALL WKVECT (PSURMA,'G V I',NSUCO+1,JSUMA)
      CALL WKVECT (PSURNO,'G V I',NSUCO+1,JSUNO)
      CALL WKVECT (PNOQUA,'G V I',NSUCO+1,JNOQUA)      
      ZI(JSUMA)  = 0
      ZI(JSUNO)  = 0
      ZI(JNOQUA) = 0     

C --- ON NE BOUCLE QUE SUR LES ZONES PRINCIPALES:

      IWRITE = 0

      DO 5 IOC = 1,NZOCP
         IREAD  = IOC   
C --- /!\ IWRITE EST INCREMENTE DANS NBSUCO->NBNOCO                    
         CALL NBSUCO (CHAR,MOTFAC,NOMA,IREAD,IWRITE,JSUMA,
     +                  JSUNO,JNOQUA,NTRAV,ORDSTC,INDQUA)          
  5   CONTINUE   
  
C --- ON BOUCLE SUR LES ZONES PRINCIPALES MAIS ON AGIT SUR LES 
C      ZONES SYMETRIQUES

      IF (NSYME.GT.0) THEN  
         ISYME = 0 
         IF (ORDSTC.EQ.1) THEN
          ORDSTC = 0 
         ELSE 
          ORDSTC = 1
         ENDIF

         DO 6 IOC = 1,NZOCP
            IREAD = IOC
            DO 7 ISY = 1,NSYME      
               IF (ZI(JSYME+ISY) .EQ.IOC) THEN
                  ISYME = ISYME +1
C                   /!\ IWRITE EST INCREMENTE DANS NBSUCO->NBNOCO 
                  CALL NBSUCO (CHAR,MOTFAC,NOMA,IREAD,IWRITE,JSUMA,
     +                  JSUNO,JNOQUA,NTRAV,ORDSTC,INDQUA)
               ENDIF
  7         CONTINUE             
  6      CONTINUE      
         IF (ISYME.NE.NSYME) CALL UTMESS ('F','POINCO',
     +                                 'ERREUR NBSUCO/SYMETRIQUE') 
         IF (ORDSTC.EQ.1) THEN
          ORDSTC = 0 
         ELSE 
          ORDSTC = 1
         ENDIF
      ENDIF

C --- NMACO EST LE NOMBRE TOTAL DE MAILLES DE CONTACT      
C --- NNOCO EST LE NOMBRE TOTAL DE NOEUDS DE CONTACT       
C --- NNOQUA EST LE NOMBRE TOTAL DE NOEUDS QUADRATIQUES DE CONTACT      

      NMACO  = ZI(JSUMA  + NSUCO)
      NNOCO  = ZI(JSUNO  + NSUCO)
      NNOQUA = ZI(JNOQUA + NSUCO)
C
C ----------------------------------------------------------------------
C
      CALL JEDEMA()
      END
