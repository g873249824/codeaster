      SUBROUTINE ARLAS1(NDIM  ,LCARA ,NDML1   ,NDML2   ,PTMT    ,
     &                  MTEL, MTMO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 13/10/2009   AUTEUR CAO B.CAO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C
C
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
      INTEGER  NDIM,NDML1,NDML2
      INTEGER  PTMT(NDML2,NDML1)
      REAL*8   LCARA
      REAL*8   MTEL(*)
      REAL*8   MTMO(*)
C
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C GESTION DES RELATIONS LINEAIRES
C ASSEMBLAGE MATRICE ELEMENTAIRE ARLEQUIN
C   MAILLE SOLIDE / MAILLE SOLIDE
C
C ----------------------------------------------------------------------
C
C
C IN  NDIM   : NDIMNSION DE L'ESPACE
C IN  LCARA  : LONGUEUR CARACTERISTIQUE POUR TERME DE COUPLAGE
C IN  NDML1  : NOMBRE DE NOEUDS MAILLE 1
C IN  NDML2  : NOMBRE DE NOEUDS MAILLE 2
C IN  PTMT   : POINTEURS DANS C (CF ARLAS0)
C IN  MTEL   : MATRICES ELEMENTAIRES (CF ARLTE)
C I/O MTMO   : MATRICE MORSE (CF ARLFC*)
C
C MATRICE PONCTUELLE DANS MTMO : (X1.X2, X1.Y2, [X1.Z2], Y1.X2, ...)
C
C ----------------------------------------------------------------------
C
      INTEGER  INO1,INO2,KAUX,PMAT1,LGNM1,PMAT,LGNM
      REAL*8   TRMT,VLR,MTMO0(9)
C
C ----------------------------------------------------------------------
C

C
C --- ASSEMBLAGE DE LA MATRICE ELEMENTAIRE MTEL

      PMAT1 = 1
      LGNM1 = NDML1*NDML2
      DO 10 INO1 = 1, NDML1
        DO 11 INO2 = 1, NDML2

          LGNM = 1 + NDIM*NDIM*(PTMT(INO2,INO1)-1)

C ------- CALCUL DE LA TRACE DE MTEL

          TRMT = 0.D0

          PMAT = 1
          DO 20 KAUX = 1, NDIM
            TRMT = TRMT + MTEL(LGNM1+PMAT)
            PMAT  = PMAT + NDIM + 1
 20       CONTINUE

C ------- CALCUL DE LA MATRICE PONCTUELLE C0

          DO 30 KAUX = 1, NDIM*NDIM
            LGNM1 = LGNM1 + 1
            MTMO0(KAUX) = LCARA*MTEL(LGNM1)
 30       CONTINUE

          VLR = MTEL(PMAT1) + LCARA*TRMT
          PMAT1 = PMAT1 + 1

          PMAT = 1
          DO 40 KAUX = 1, NDIM
            MTMO0(PMAT) = MTMO0(PMAT) + VLR
            PMAT = PMAT + NDIM + 1
 40       CONTINUE

C ------- ASSEMBLAGE DE LA MATRICE PONCTUELLE MTMO0
          DO 50 KAUX = 1, NDIM*NDIM
            MTMO(LGNM) = MTMO(LGNM) + MTMO0(KAUX)
            LGNM = LGNM + 1
 50       CONTINUE

 11     CONTINUE
 10   CONTINUE


      END
