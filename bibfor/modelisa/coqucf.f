      SUBROUTINE COQUCF(NOMU)
      IMPLICIT NONE
      CHARACTER*8    NOMU
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 14/01/2013   AUTEUR FLEJOU J-L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C ----------------------------------------------------------------------
C                          AFFE_CARA_ELEM
C
C     POUR LES COQUES ET LES GRILLES IL PEUT EXISTER UNE CARTE DE
C     FONCTIONS DE (X,Y,Z) DONNANT :
C        EPAISSEUR
C        SECTION
C        EXCENTREMENT
C
C     IL FAUT EVALUER LES FONCTIONS AU CENTRE DE GRAVITE DE L'ELEMENT
C     ET REMPLIR LA CARTE DE REELS
C ----------------------------------------------------------------------
C
      INCLUDE 'jeveux.h'
      CHARACTER*32   JEXATR
C
      INTEGER        IFM,NIV,IRET,IBID,II,JJ,KK,INDIK8
      INTEGER        JCESDF,JCESDO,JCESCF,JCESCO,NBMAIL,ADRM,IAD
      INTEGER        NBCMPF,NBCMPO,ICOMPO,INOEU,NBNO,NUNOE,IGEOM
      INTEGER        JCESLF,JCESLO,JCESVF,JCESVO
      INTEGER        JCONNE,IADR1,IADR2,JTABCO
      REAL*8         VALR(3),FRESU
      CHARACTER*8    NOMMA,NMCMPF,KBID,NOMVAL(3),NOMFCT
      CHARACTER*19   CARTCO,CARTCF,CELSCO,CELSCF,CONNEX
      CHARACTER*24   K24BID
      LOGICAL        LCOOR
C
      DATA   NOMVAL  /'X','Y','Z'/
C ----------------------------------------------------------------------
      CALL JEMARQ()
C
C --- CARTE POUR LES FONCTIONS
      CARTCF = NOMU//'.CARCOQUF'
      CALL EXISD('CARTE',CARTCF,IRET)
C     SI LA CARTE DE FONCTIONS N'EXISTE PAS : RIEN A FAIRE
      IF ( IRET.EQ.0 ) GOTO 9999
C
C --- CARTE POUR LES VALEURS REELLES
      CARTCO = NOMU//'.CARCOQUE'
      CALL EXISD('CARTE',CARTCO,IRET)
C     SI LA CARTE DE REELS N'EXISTE PAS : BIZARRE
      CALL ASSERT( IRET.NE.0 )
C     POUR TRACE DES INFORMATIONS DANS LE FICHIER DE MESSAGES
      CALL INFNIV(IFM,NIV)
C
      CELSCO = '&&COQUCF.CHSCO'
      CELSCF = '&&COQUCF.CHSCF'
C --- TRANSFORMATION DE LA CARTE DE REELS EN ELEM_S
      CALL CARCES(CARTCO,'ELEM',' ','V',CELSCO,'A',IBID)
C --- TRANSFORMATION DE LA CARTE DE FONCTIONS EN ELEM_S
      CALL CARCES(CARTCF,'ELEM',' ','V',CELSCF,'A',IBID)
      IF ( NIV.GE.2 ) THEN
         WRITE(IFM,'(A)') ' '
         WRITE(IFM,'(A)') 'TRANSFORMATION DES CARTES EN CHELEM_S'
         WRITE(IFM,'(A)') '  <'//CARTCO//'>  <'//CARTCF//'>'
      ENDIF
C
C --- VERIFICATION QUE LES NOMS DANS LES DEUX ELEM_S EXISTENT
C        RECUPERATION DES NOMS DES COMPOSANTES DANS CELSCF
C        VERIFICATION DE L'EXISTANCE DANS CELSCO
      CALL JEVEUO(CELSCF//'.CESD','L',JCESDF)
      CALL JEVEUO(CELSCO//'.CESD','L',JCESDO)
      NBCMPF = ZI(JCESDF+1)
      NBCMPO = ZI(JCESDO+1)
C     ADRESSE DES NOMS DES COMPOSANTES
      CALL JEVEUO(CELSCF//'.CESC','L',JCESCF)
      CALL JEVEUO(CELSCO//'.CESC','L',JCESCO)
85    FORMAT('(',I3,'A9)')
      IF ( NIV.GE.2 ) THEN
         WRITE(IFM,'(A,I3)') 'COMPOSANTES DE <'//CARTCO//'> ',NBCMPO
         WRITE(K24BID,85) NBCMPO
         WRITE(IFM,K24BID) (ZK8(JCESCO+JJ),JJ=0, NBCMPO-1)
         WRITE(IFM,'(A,I3)') 'COMPOSANTES DE <'//CARTCF//'> ',NBCMPF
         WRITE(K24BID,85) NBCMPF
         WRITE(IFM,K24BID) (ZK8(JCESCF+JJ),JJ=0, NBCMPF-1)
      ENDIF
C
      IRET = 0
      DO 100 II = 1 ,  NBCMPF
         NMCMPF = ZK8(JCESCF+II-1)
         JJ = INDIK8( ZK8(JCESCO) , NMCMPF, 1 , NBCMPO )
         IF ( JJ.NE.0 ) THEN
            IF ( NIV.GE.2 ) THEN
               WRITE(IFM,'(A)')    'COMPOSANTE <'//NMCMPF//'>'
               WRITE(IFM,'(A,I5)') '   INDEX DANS CARCOQUF ',II
               WRITE(IFM,'(A,I5)') '   INDEX DANS CARCOQUE ',JJ
            ENDIF
         ELSE
            IRET = IRET + 1
         ENDIF
100   CONTINUE
      CALL ASSERT( IRET.EQ.0 )
C
C --- INFORMATIONS SUR LE MAILLAGE
      CALL DISMOI('F','NOM_MAILLA',CARTCO,'CARTE',IBID,NOMMA,IBID)
      CALL DISMOI('F','NB_MA_MAILLA',NOMMA,'MAILLAGE',NBMAIL,KBID,IBID)
C
      K24BID = NOMMA//'.COORDO    .VALE'
      CALL JEVEUO (K24BID,'L',IGEOM)
      CONNEX = NOMMA//'.CONNEX'
      CALL JEVEUO(CONNEX,'L',JCONNE)
      CALL JEVEUO(JEXATR(CONNEX,'LONCUM'),'L',JTABCO)
C
      CALL JEVEUO(CELSCF//'.CESL','L',JCESLF)
      CALL JEVEUO(CELSCO//'.CESL','L',JCESLO)
      CALL JEVEUO(CELSCF//'.CESV','L',JCESVF)
      CALL JEVEUO(CELSCO//'.CESV','E',JCESVO)
C --- TRAITEMENT
C        BOUCLE SUR TOUTES LES MAILLES
C           BOUCLE SUR LES COMPOSANTES AVEC FONCTIONS DE CELSCF
C              SI LA FONCTION EXISTE
C                 CALCUL DE LA POSITION DU CDG DE LA MAILLE
C                 CALCUL DE LA FONCTION
C                 AFFECTE LE RESULTAT A LA COMPOSANTE DE CELSCO
      IF ( NIV.GE.2 ) THEN
         WRITE(IFM,'(A)') 'VALEURS DES FONCTIONS'
         WRITE(IFM,90)
      ENDIF
      DO 200 II = 1 , NBMAIL
         LCOOR = .FALSE.
         DO 210 JJ = 1 , NBCMPF
            NMCMPF = ZK8(JCESCF+JJ-1)
            CALL CESEXI('C',JCESDF,JCESLF,II,1,1,JJ,IAD)
            IF ( IAD.GT.0 ) THEN
               NOMFCT = ZK8(JCESVF-1+IAD)
               IF ( NOMFCT(1:2).NE.'&&' ) THEN
                  IF ( .NOT. LCOOR ) THEN
                     LCOOR = .TRUE.
                     IADR1 = ZI(JTABCO-1+II)
                     IADR2 = ZI(JTABCO-1+II+1)
                     NBNO  = IADR2 - IADR1
                     ADRM  = JCONNE-1+IADR1
C                    CENTRE DE GRAVITE DE LA MAILLE
                     DO 250 ICOMPO = 1 , 3
                        VALR(ICOMPO) = 0.0D0
                        DO 240 INOEU = 1 , NBNO
                           NUNOE = ZI(ADRM-1+INOEU)
                           VALR(ICOMPO) = VALR(ICOMPO) +
     &                                    ZR(IGEOM+3*(NUNOE-1)+ICOMPO-1)
240                     CONTINUE
                        VALR(ICOMPO) = VALR(ICOMPO)/NBNO
250                  CONTINUE
                  ENDIF
                  CALL FOINTE('F',NOMFCT,3,NOMVAL,VALR,FRESU,IRET)
                  IF ( NIV.GE.2 ) THEN
                     WRITE(IFM,91) II,JJ,(VALR(ICOMPO),ICOMPO=1,3),
     &                             NOMFCT,FRESU
                  ENDIF
C                 ON RECHERCHE DANS CELSCO LA COMPOSANTE CORRESPONDANTE
                  KK = INDIK8( ZK8(JCESCO) , NMCMPF, 1 , NBCMPO )
                  CALL CESEXI('S',JCESDO,JCESLO,II,1,1,KK,IAD)
                  ZR(JCESVO-1+IAD) = FRESU
               ENDIF
            ENDIF
210      CONTINUE
200   CONTINUE
C
C     DESTRUCTION DE LA CARTE DES REELS, DES FONCTIONS
      CALL DETRSD('CARTE',CARTCO)
      CALL DETRSD('CARTE',CARTCF)
C     CONSTRUCTION DE LA CARTE DES REELS A PARTIR DE CELSCO
      CALL CESCAR(CELSCO,CARTCO,'G')
C     DESTRUCTION DES CHELEM_S
      CALL DETRSD('CHAM_ELEM_S',CELSCO)
      CALL DETRSD('CHAM_ELEM_S',CELSCF)
C
90    FORMAT(' MAILLE_NB  : [ [  CENTRE DE GRAVITE ],',
     &       '  FONCTION  ,  VALEUR       ]')
91    FORMAT('''',I7,'_',I1,''' : [ [',3(E18.10,','),'], ''',
     &       A,''' ,',E18.10,'],')
C
9999  CONTINUE
      CALL JEDEMA()
      END
