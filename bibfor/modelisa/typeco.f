      SUBROUTINE TYPECO(CHAR  ,NOMA  )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8  NOMA ,CHAR
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES MAILLEES - LECTURE DONNEES)
C
C CONSTRUCTION DU TABLEAU POUR TYPE DE NOEUD/MAILLES
C
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C
C
C
C
      CHARACTER*24 TYPENO,TYPEMA,MAESCL
      INTEGER      JTYPNO,JTYPMA,JMAESC
      INTEGER      CFMMVD,ZTYPM,ZTYPN,ZMAES
      INTEGER      CFDISI,NZOCO,NNOCO,NMACO,NTMAE,IFORM
      INTEGER      MMINFI,IZONE
      INTEGER      JDECNM,JDECNE,INOE,INOM,NBNOE,NBNOM
      INTEGER      JDECMM,JDECME,IMAE,IMAM,NBMAE,NBMAM
      INTEGER      POSNOM,POSNOE
      INTEGER      POSMAM,POSMAE,NUMMAE
      INTEGER      INDMAE,INDMAM
      INTEGER      INO,IMA,POSNO,POSMA
      INTEGER      NDEXFR,TYPINT,NPTM
      CHARACTER*24 DEFICO
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      DEFICO = CHAR(1:8)//'.CONTACT'
      NZOCO  = CFDISI(DEFICO,'NZOCO' )
      NNOCO  = CFDISI(DEFICO,'NNOCO' )
      NMACO  = CFDISI(DEFICO,'NMACO' )
      NTMAE  = CFDISI(DEFICO,'NTMAE' )
      IFORM  = CFDISI(DEFICO,'FORMULATION')
C
C --- ACCES OBJETS JEVEUX
C
      TYPENO = DEFICO(1:16)//'.TYPENO'
      TYPEMA = DEFICO(1:16)//'.TYPEMA'
      MAESCL = DEFICO(1:16)//'.MAESCL'
      ZTYPM  = CFMMVD('ZTYPM')
      ZTYPN  = CFMMVD('ZTYPN')
      ZMAES  = CFMMVD('ZMAES')
C
C --- CREATION DES TABLEAUX
C
      CALL WKVECT(TYPENO,'G V I',ZTYPN*NNOCO,JTYPNO)
      CALL WKVECT(TYPEMA,'G V I',ZTYPM*NMACO,JTYPMA)
      CALL WKVECT(MAESCL,'G V I',ZMAES*NTMAE,JMAESC)
C
C --- REMPLISSAGE DU TABLEAU TYPE_NOEUD
C
      DO 50 IZONE = 1,NZOCO
        NBNOE  = MMINFI(DEFICO,'NBNOE' ,IZONE )
        NBNOM  = MMINFI(DEFICO,'NBNOM' ,IZONE )
        JDECNE = MMINFI(DEFICO,'JDECNE',IZONE )
        JDECNM = MMINFI(DEFICO,'JDECNM',IZONE )
C
        DO 60 INOM = 1,NBNOM
          POSNOM = JDECNM + INOM
          ZI(JTYPNO+ZTYPN*(POSNOM-1)+1-1) = 1
          ZI(JTYPNO+ZTYPN*(POSNOM-1)+2-1) = IZONE
 60     CONTINUE
C
        DO 61 INOE = 1,NBNOE
          POSNOE = JDECNE + INOE
          ZI(JTYPNO+ZTYPN*(POSNOE-1)+1-1) = -1
          ZI(JTYPNO+ZTYPN*(POSNOE-1)+2-1) = IZONE
 61     CONTINUE

 50   CONTINUE
C
C --- REMPLISSAGE DU TABLEAU TYPE_MAILLE - TYPE MAITRE OU ESCLAVE
C
      INDMAE = 0
      INDMAM = 0
      DO 150 IZONE = 1,NZOCO
        NBMAE  = MMINFI(DEFICO,'NBMAE' ,IZONE )
        NBMAM  = MMINFI(DEFICO,'NBMAM' ,IZONE )
        JDECME = MMINFI(DEFICO,'JDECME',IZONE )
        JDECMM = MMINFI(DEFICO,'JDECMM',IZONE )
C
        DO 160 IMAM = 1,NBMAM
          POSMAM = JDECMM + IMAM
          INDMAM = INDMAM + 1
          ZI(JTYPMA+ZTYPM*(POSMAM-1)+1-1) = 1
          ZI(JTYPMA+ZTYPM*(POSMAM-1)+2-1) = INDMAM
 160    CONTINUE
C
        DO 161 IMAE = 1,NBMAE
          POSMAE = JDECME + IMAE
          INDMAE = INDMAE + 1
          ZI(JTYPMA+ZTYPM*(POSMAE-1)+1-1) = -1
          ZI(JTYPMA+ZTYPM*(POSMAE-1)+2-1) = INDMAE
 161     CONTINUE
 150  CONTINUE
C
C --- REMPLISSAGE DU TABLEAU DES MAILLES ESCLAVES MAESC
C
      INDMAE = 0
      DO 30 IZONE = 1,NZOCO
        NBMAE  = MMINFI(DEFICO,'NBMAE' ,IZONE )
        JDECME = MMINFI(DEFICO,'JDECME',IZONE )

        DO 20 IMAE = 1,NBMAE
          POSMAE = JDECME + IMAE
          INDMAE = INDMAE + 1
          CALL CFNUMM(DEFICO,1     ,POSMAE,NUMMAE)
          IF (IFORM.EQ.2) THEN
            TYPINT =  MMINFI(DEFICO,'INTEGRATION',IZONE )
            CALL MMELIN(NOMA  ,NUMMAE,TYPINT,NPTM )
            CALL MMSSFR(DEFICO,IZONE ,POSMAE,NDEXFR)
          ELSE
            NPTM   = 0
            NDEXFR = 0
          ENDIF
          ZI(JMAESC+ZMAES*(INDMAE-1)+1-1) = POSMAE
          ZI(JMAESC+ZMAES*(INDMAE-1)+2-1) = IZONE
          ZI(JMAESC+ZMAES*(INDMAE-1)+3-1) = NPTM
          ZI(JMAESC+ZMAES*(INDMAE-1)+4-1) = NDEXFR
 20     CONTINUE
 30   CONTINUE
C
C --- VERIFS: TYPENO ET TYPEMA SANS TROUS !
C
      DO 1 INO = 1,NNOCO
        POSNO  = INO
        IF (ZI(JTYPNO+ZTYPN*(POSNO -1)+1-1).EQ.0) THEN
          CALL ASSERT(.FALSE.)
        ENDIF
 1    CONTINUE
C
      DO 2 IMA = 1,NMACO
        POSMA  = IMA
        IF (ZI(JTYPMA+ZTYPM*(POSMA -1)+1-1).EQ.0) THEN
          CALL ASSERT(.FALSE.)
        ENDIF
 2    CONTINUE
C
      CALL JEDEMA()
      END
