      SUBROUTINE W18IMP(LIGREL,NOMA,NOMO)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXNOM
      CHARACTER*19 LIGREL
      CHARACTER*8 NOMA,NOMO
C RESPONSABLE PELLET J.PELLET
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     ------------------------------------------------------------------
C     AFFE_MODELE IMPRESSION SUR 'MESSAGE' DES ELEMENTS FINIS AFFECTES
C     ------------------------------------------------------------------

      INTEGER IFM,NUMVEC,NBGREL,I,NMGREL,NUMAIL,JDLI,NUTYPM
      INTEGER NTYPOI,JDTM,NUTYPE,IBID,JC,J,NUMNOE
      INTEGER JDNW,K,NIV,IEXI,NBTE,JNBELE,JMODLI,JTYPMA
      INTEGER JTYPEL,NBELE
      CHARACTER*8 K8B
      CHARACTER*8 TYPEMA,NOMAIL,TABMAI(8)
      CHARACTER*16 TYPELE,TYPEMO
      CHARACTER*24 NOMMAI,NOMNOE
      CHARACTER*32 PHEMOD

      NOMMAI=NOMA//'.NOMMAI'
      NOMNOE=NOMA//'.NOMNOE'

      CALL INFNIV(IFM,NIV)

      WRITE (IFM,9000)

      CALL JEEXIN(LIGREL//'.LIEL',IEXI)
      IF (IEXI.EQ.0)GOTO 50


      CALL JELIRA(LIGREL//'.LIEL','NMAXOC',NBGREL,K8B)
      CALL JEVEUO(LIGREL//'.LIEL','L',JDLI)
      CALL JEVEUO(NOMA//'.TYPMAIL','L',JDTM)
      CALL JEEXIN(NOMO//'.MODELE    .NEMA',IEXI)
      JDNW=0
      IF (IEXI.GT.0) CALL JEVEUO(NOMO//'.MODELE    .NEMA','L',JDNW)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','POI1'),NTYPOI)


C     -- 1. ON COMPTE LES ELEMENTS DE CHAQUE TYPE :
C     ------------------------------------------------
      CALL JELIRA('&CATA.TE.NOMTE','NOMMAX',NBTE,K8B)
      CALL WKVECT('&&W18IMP.NBELE','V V I',NBTE,JNBELE)
      CALL WKVECT('&&W18IMP.MODELI','V V K16',NBTE,JMODLI)
      CALL WKVECT('&&W18IMP.TYPMA','V V K8',NBTE,JTYPMA)
      CALL WKVECT('&&W18IMP.TYPEL','V V K16',NBTE,JTYPEL)

      NUMVEC=1
      TYPEMO=' '
      DO 10 I=1,NBGREL
        CALL JELIRA(JEXNUM(LIGREL//'.LIEL',I),'LONMAX',NMGREL,K8B)
        IF (NMGREL.EQ.1) GOTO 9
        NUTYPE=ZI(JDLI+NUMVEC+NMGREL-2)
        IF (NUTYPE.EQ.0) GOTO 10

        CALL ASSERT(NUTYPE.GT.0 .AND. NUTYPE.LE.NBTE)
        ZI(JNBELE-1+NUTYPE)=ZI(JNBELE-1+NUTYPE)+NMGREL-1
        IF (ZK16(JMODLI-1+NUTYPE).EQ.' ') THEN
          CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',NUTYPE),TYPELE)
          NUMAIL=ZI(JDLI+NUMVEC-1)
          IF (NUMAIL.LT.0) THEN
            NUTYPM=NTYPOI
          ELSE
            NUTYPM=ZI(JDTM+NUMAIL-1)
          ENDIF
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',NUTYPM),TYPEMA)

          IF (TYPELE.EQ.'MECA_HEXS8') CALL U2MESS('A','ELEMENTS4_74')
          CALL DISMOI('F','PHEN_MODE',TYPELE,'TYPE_ELEM',IBID,PHEMOD,
     &                IBID)
          TYPEMO=PHEMOD(17:32)
          IF (PHEMOD(1:10).EQ.'#PLUSIEURS')TYPEMO=' '
          ZK16(JTYPEL-1+NUTYPE)=TYPELE
          ZK16(JMODLI-1+NUTYPE)=TYPEMO
          ZK8(JTYPMA-1+NUTYPE)=TYPEMA
        ENDIF

   9    CONTINUE
        NUMVEC=NUMVEC+NMGREL
   10 CONTINUE


C     -- 2. IMPRESSION DU NOMBRE D'ELEMENTS DE CHAQUE TYPE :
C     -------------------------------------------------------
      DO 20 I=1,NBTE
        NBELE=ZI(JNBELE-1+I)
        IF (NBELE.EQ.0)GOTO 20

        TYPEMO=ZK16(JMODLI-1+I)
        TYPEMA=ZK8(JTYPMA-1+I)
        TYPELE=ZK16(JTYPEL-1+I)
        WRITE (IFM,9010)TYPEMO,TYPELE,TYPEMA,NBELE
   20 CONTINUE
      WRITE(IFM,9050)


C     -- 3. IMPRESSIONS DE NIVEAU 2 (TOUS LES ELEMENTS) :
C     ---------------------------------------------------
      IF (NIV.EQ.2) THEN
        NUMVEC=1
        TYPEMO=' '
        DO 40 I=1,NBGREL
          CALL JELIRA(JEXNUM(LIGREL//'.LIEL',I),'LONMAX',NMGREL,K8B)
          IF (NMGREL.EQ.1) GOTO 39
          NUMAIL=ZI(JDLI+NUMVEC-1)
          IF (NUMAIL.LT.0) THEN
            NUTYPM=NTYPOI
          ELSE
            NUTYPM=ZI(JDTM+NUMAIL-1)
          ENDIF
          NUTYPE=ZI(JDLI+NUMVEC+NMGREL-2)
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',NUTYPM),TYPEMA)
          CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',NUTYPE),TYPELE)
          IF (TYPELE.EQ.'MECA_HEXS8') CALL U2MESS('A','ELEMENTS4_74')

          CALL DISMOI('F','PHEN_MODE',TYPELE,'TYPE_ELEM',IBID,PHEMOD,
     &                IBID)
          TYPEMO=PHEMOD(17:32)
          IF (PHEMOD(1:10).EQ.'#PLUSIEURS')TYPEMO=' '
          WRITE (IFM,9010)TYPEMO,TYPELE,TYPEMA,NMGREL-1
          IF (NUMAIL.LT.0) THEN
            WRITE (IFM,9020)
          ELSE
            WRITE (IFM,9030)
          ENDIF
          JC=0
          DO 30 J=JDLI+NUMVEC-1,JDLI+NUMVEC+NMGREL-3
            JC=JC+1
            NUMAIL=ZI(J)
            IF (NUMAIL.LT.0) THEN
              CALL ASSERT(JDNW.NE.0)
              NUMNOE=ZI(JDNW+(-NUMAIL*2-1)-1)
              CALL JENUNO(JEXNUM(NOMNOE,NUMNOE),NOMAIL)
            ELSE
              CALL JENUNO(JEXNUM(NOMMAI,NUMAIL),NOMAIL)
            ENDIF
            IF (JC.EQ.8 .OR. J.EQ.JDLI+NUMVEC+NMGREL-3) THEN
              TABMAI(JC)=NOMAIL
              WRITE (IFM,9040)(TABMAI(K),K=1,JC)
              JC=0
            ELSE
              TABMAI(JC)=NOMAIL
            ENDIF
   30     CONTINUE
   39     CONTINUE
          NUMVEC=NUMVEC+NMGREL
   40   CONTINUE
        WRITE(IFM,9050)
      ENDIF


   50 CONTINUE


 9000 FORMAT (/,'    MODELISATION      ELEMENT FINI      TYPE MAILLE',
     &       '          NOMBRE')
 9010 FORMAT (4X,A16,2X,A16,2X,A8,7X,I12)
 9020 FORMAT (6X,'LISTE DES NOEUDS AFFECTES:')
 9030 FORMAT (6X,'LISTE DES MAILLES AFFECTEES:')
 9040 FORMAT (4X,8(2X,A8))
 9050 FORMAT (1X)
      END
