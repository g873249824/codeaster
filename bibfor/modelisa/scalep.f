      SUBROUTINE SCALEP(SPECTR,NOMA,BASE,NUOR,NBM,IMODI,NBMR,NBEXCP,
     &                  LTABLE,IAXE,SCAL)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C     PROJECTION D'UN SPECTRE D'EXCITATION TURBULENTE LOCALISEE SUR UNE
C     BASE MODALE PERTURBEE PAR COUPLAGE FLUIDE-STRUCTURE
C     CALCUL DES PRODUITS SCALAIRES PHII(XK).NK ET PHII'(XM).XM
C     APPELANT : SPECEP
C-----------------------------------------------------------------------
C IN  : SPECTR : NOM DU CONCEPT SPECTRE
C IN  : NOMA   : NOM DU CONCEPT MAILLAGE
C IN  : BASE   : NOM DU CONCEPT MELASFLU
C IN  : NUOR   : NUMEROS D'ORDRE DES MODES DU CONCEPT MELASFLU
C IN  : NBM    : NOMBRE DE MODES DU CONCEPT MELASFLU
C IN  : IMODI  : INDICE DU PREMIER MODE PRIS EN COMPTE
C IN  : NBMR   : NOMBRE DE MODES PRIS EN COMPTE
C IN  : NBEXCP : NOMBRE D'EXCITATIONS PONCTUELLES
C IN  : LTABLE : BOOLEEN, DONNE LE CAS DE CALCUL
C       LTABLE = .TRUE.  => SPECTRE DEFINI PAR L'UTILISATEUR
C       LTABLE = .FALSE. => RECOURS A UN SPECTRE GRAPPE2 PREDEFINI
C IN  : IAXE   : ENTIER DONNANT L'AXE DIRECTEUR DE LA POUTRE
C OUT : SCAL   : TABLEAU DES PRODUITS SCALAIRES (NBEXCP,NBMR)
C
C
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNOM
      INTEGER      NBM,NUOR(NBM),IMODI,NBMR,NBEXCP,IAXE
      LOGICAL      LTABLE
      CHARACTER*8  NOMA
      CHARACTER*19 SPECTR,BASE
      REAL*8       SCAL(NBEXCP,NBMR)
C
      REAL*8       R8DGRD,DGRD
      CHARACTER*24 SPNNOE,SPVARE,SPVATE,NNOEMA,CHVALE
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
      INTEGER IDEC ,IDEFM ,IDIR1 ,IDIR2 ,IEX ,IMOD ,IMODF
      INTEGER IMR ,INAT ,INATU ,INUNO ,IROT1 ,IROT2 ,ISPNO
      INTEGER ISPRE ,ISPTE ,ITETA ,IV ,IVALE ,NUNO
      REAL*8 THETA
C-----------------------------------------------------------------------
      CALL JEMARQ()
C
C --- 1.INITIALISATIONS
C
      DGRD = R8DGRD()
      IMODF = IMODI + NBMR - 1
      IV = 1
      IF (IAXE.EQ.1) THEN
        IDIR1 = 2
        IDIR2 = 3
        IROT1 = 6
        IROT2 = 5
      ELSE IF (IAXE.EQ.2) THEN
        IDIR1 = 3
        IDIR2 = 1
        IROT1 = 4
        IROT2 = 6
      ELSE
        IDIR1 = 1
        IDIR2 = 2
        IROT1 = 5
        IROT2 = 4
      ENDIF
C
C --- 2.RECUPERATIONS SIMULTANEES
C       - DES NUMEROS DES NOEUDS D'APPLICATION DES EXCITATIONS
C       - DES DIRECTIONS D'APPLICATION DE L'EXCITATION EN CHAQUE NOEUD
C       - DE LA NATURE DE L'EXCITATION EN CHAQUE NOEUD
C
      NNOEMA = NOMA//'.NOMNOE'
      SPNNOE = SPECTR//'.NNOE'
      CALL JEVEUO(SPNNOE,'L',ISPNO)
C
      CALL WKVECT('&&SCALEP.TEMP.NUNO','V V I',NBEXCP,INUNO)
      CALL WKVECT('&&SCALEP.TEMP.TETA','V V R',2*NBEXCP,ITETA)
      CALL WKVECT('&&SCALEP.TEMP.NATU','V V K8',NBEXCP,INATU)
C
      IF ( LTABLE ) THEN
C
        SPVARE = SPECTR//'.VARE'
        SPVATE = SPECTR//'.VATE'
        CALL JEVEUO(SPVARE,'L',ISPRE)
        CALL JEVEUO(SPVATE,'L',ISPTE)
C
        DO 10 IEX = 1,NBEXCP
          CALL JENONU(JEXNOM(NNOEMA,ZK8(ISPNO+IEX-1)),ZI(INUNO+IEX-1))
          THETA = ZR(ISPRE+IEX-1) * DGRD
          ZR(ITETA+2*(IEX-1))   = DBLE(COS(THETA))
          ZR(ITETA+2*(IEX-1)+1) = DBLE(SIN(THETA))
          ZK8(INATU+IEX-1) = ZK16(ISPTE+4+IEX-1)(1:8)
  10    CONTINUE
C
      ELSE
C
        CALL JENONU(JEXNOM(NNOEMA,ZK8(ISPNO)),NUNO)
        DO 11 IEX = 1,NBEXCP
          ZI(INUNO+IEX-1) = NUNO
          ZR(ITETA+2*(IEX-1))   = 1.D0
          ZR(ITETA+2*(IEX-1)+1) = 1.D0
          INAT = IEX - INT(IEX/2) * 2
          IF (INAT.EQ.0) THEN
            ZK8(INATU+IEX-1) = 'MOMENT'
          ELSE
            ZK8(INATU+IEX-1) = 'FORCE'
          ENDIF
  11    CONTINUE
C
      ENDIF
C
C --- 3.RECUPERATION DES DEFORMEES MODALES OU/ET DES DERIVEES DES
C ---   DEFORMEES MODALES AUX NOEUDS D'APPLICATION
C
      CALL WKVECT('&&SCALEP.TEMP.DEFM','V V R',2*NBEXCP*NBMR,IDEFM)
C
      DO 20 IMOD = IMODI,IMODF
C
        WRITE(CHVALE,'(A8,A5,2I3.3,A5)') BASE(1:8),'.C01.',NUOR(IMOD),
     &                                   IV,'.VALE'
        CALL JEVEUO(CHVALE,'L',IVALE)
        IMR = IMOD - IMODI + 1
C
        DO 21 IEX = 1,NBEXCP
          IDEC = 2*NBEXCP*(IMR-1)+2*(IEX-1)
          IF (ZK8(INATU+IEX-1)(1:1).EQ.'F') THEN
            ZR(IDEFM+IDEC)   = ZR(IVALE+6*(ZI(INUNO+IEX-1)-1)+IDIR1-1)
            ZR(IDEFM+IDEC+1) = ZR(IVALE+6*(ZI(INUNO+IEX-1)-1)+IDIR2-1)
          ELSE
            ZR(IDEFM+IDEC)   = ZR(IVALE+6*(ZI(INUNO+IEX-1)-1)+IROT1-1)
            ZR(IDEFM+IDEC+1) = ZR(IVALE+6*(ZI(INUNO+IEX-1)-1)+IROT2-1)
          ENDIF
  21    CONTINUE
C
        CALL JELIBE(CHVALE)
C
  20  CONTINUE
C
C --- 4.CALCUL DES PRODUITS SCALAIRES
C
      DO 30 IMR = 1,NBMR
        DO 31 IEX = 1,NBEXCP
          IDEC = 2*NBEXCP*(IMR-1)+2*(IEX-1)
          SCAL(IEX,IMR) = ZR(IDEFM+IDEC)*ZR(ITETA+2*(IEX-1))
     &                  + ZR(IDEFM+IDEC+1)*ZR(ITETA+2*(IEX-1)+1)
  31    CONTINUE
  30  CONTINUE
C
      CALL JEDETR('&&SCALEP.TEMP.NUNO')
      CALL JEDETR('&&SCALEP.TEMP.TETA')
      CALL JEDETR('&&SCALEP.TEMP.NATU')
      CALL JEDETR('&&SCALEP.TEMP.DEFM')
      CALL JEDEMA()
      END
