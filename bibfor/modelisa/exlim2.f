      SUBROUTINE EXLIM2(SDFETI,NOMSD,LLIGRS,LIGRSD,NBCHAT,NUMSD,NBSD,
     &                  INFOFE,NBPROC,LIGRCF)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C IN  : SDFETI : NOM DE LA SD_FETI
C IN  : NOMSD  : NOM DU SOUS-DOMAINE
C IN/OUT: LLIGRS : NOM DU VECTEUR CONTENANT LA LISTE DES LIGRELS
C                ASSOCIES A NOMSD POUR CONSTITUER SON NUME_DDL
C IN  : LIGRSD : NOM DU LIGREL TEMPORAIRE EGALE A LA RESTRICTION DU SD
C                AU MODELE
C IN  : NUMSD  : NUMERO DE SOUS-DOMAINE
C OUT : NBCHAT : NOMBRE DE LIGRELS ASSOCIEES A NOMSD
C IN  :  NBSD  : NOMBRE DE SOUS-DOMAINES
C IN  : NBPROC : NOMBRE DE PROCESSEURS
C IN  : LIGRCF : NOM DU LIGREL DE CHARGE DE CONTACT CONTINUE
C-----------------------------------------------------------------------
C TOLE CRP_20
C RESPONSABLE BOITEAU O.BOITEAU
C CORPS DU PROGRAMME
      IMPLICIT NONE

C DECLARATION PARAMETRES D'APPELS
      INCLUDE 'jeveux.h'
      INTEGER      NBCHA,NUMSD,NBSD,NBPROC
      CHARACTER*8  NOMSD
      CHARACTER*19 LIGRSD,LIGRCF
      CHARACTER*24 SDFETI,LLIGRS,INFOFE


C DECLARATION VARIABLES LOCALES
      INTEGER      IRET,K,IFLII,NBMATA,NBMAT1,K1,IFLIM,NBGREL,I,IFEL1,
     &             NMGREL,IGREL,J,NUMGRE,NMGRE1,L,NUMGR1,LADR,IFM,NIV,
     &             IFLIN,LONT,NBNO,NBNEMA,IBID,INBNO,M,NBMAT2,
     &             MADR,NADR,NBNOET,IFEL2,IFEL3,NBMAS,IDECA,
     &             IVLIGR,IDD,IRET1,NBCHA2,NBMAS2,NBNO2,IFETB,
     &             LOFETB,LOFEL4,IFEL4,IFEL5,IAUX1,IAUX4,LOFEL5,IDIME,
     &             IFETI,NBDDLI,NBNOI,IAUXZI,IMULT,NBNOE2,LOFEL6,LOFEL7,
     &             IFCFL,IFCFM,IFCFN,NBCHAC,NBCHAT,IFCFI,NBCHA1,ISIG,
     &             INEM2,NOEUT,N,NBOEUT,NBCC2,NBPB
      CHARACTER*8  K8BID,NOMSD2
      CHARACTER*19 LIGRCH
      CHARACTER*24 K24B,K24DUL,K24CHL,K24DUN,K24CHN,K24CF1,K24CF2,
     &             K24CF3,NOMSDA,K24CF4,K24CF5,K24B1
      LOGICAL      LFCFL1,LFCFL2,LCC,LCC2,LOK
      CALL JEMARQ()

C RECUPERATION ET MAJ DU NIVEAU D'IMPRESSION
      CALL INFNIV(IFM,NIV)
C NOMBRE DE LIGREL DANS LA LISTE POUR NUMERO. ON COMPTE DEJA LE
C LIGREL DE MODELE
      NBCHAT=1
C SOUS-DOMAINE CONCERNE PAR DES LIGRELS TARDIFS DE CHARGE
C C'EST A DIRE COMPORTANT DES MAILLES TARDIVES (A NOEUDS TARDIFS OU PAS)
C A NOEUDS TARDIFS:    DDL_IMPO/FACE_IMPO/LIAISON_DDL/OBLIQUE....
C SANS NOEUD TARDIF:   FORCE_NODALE
      K24B=SDFETI(1:19)//'.FLIN'
      CALL JEEXIN(JEXNOM(K24B,NOMSD),IRET)
      IF (IRET.NE.0) THEN
        CALL JEVEUO(JEXNOM(K24B,NOMSD),'L',IFLIN)
        CALL JELIRA(JEXNOM(K24B,NOMSD),'LONMAX',NBCHA,K8BID)
        CALL JEVEUO(JEXNOM(K24B(1:23)//'I',NOMSD),'L',IFLII)
        CALL JEVEUO(JEXNOM(K24B(1:23)//'M',NOMSD),'L',IFLIM)
      ELSE
        NBCHA=0
      ENDIF
      NBCHAT=NBCHAT+NBCHA
C ON RAJOUTE EVENTUELLEMENT LE(S) LIGREL(S) DE CONTACT CONTINUE
      LFCFL1=.FALSE.
      LFCFL2=.FALSE.
      NBCHAC=0
C 1ERE PASSE ?
      K24B=SDFETI(1:19)//'.FCFL'
      CALL JEEXIN(JEXNOM(K24B,NOMSD),IRET)
      IF (IRET.NE.0) THEN
        CALL JEVEUO(JEXNOM(K24B,NOMSD),'L',IFCFL)
        CALL JELIRA(JEXNOM(K24B,NOMSD),'LONMAX',NBCHAC,K8BID)
        CALL JEVEUO(JEXNOM(K24B(1:23)//'I',NOMSD),'L',IFCFI)
        CALL JEVEUO(JEXNOM(K24B(1:23)//'M',NOMSD),'L',IFCFM)
        CALL JELIRA(JEXNOM(K24B(1:23)//'N',NOMSD),'LONMAX',NBOEUT,K8BID)
        CALL JEVEUO(JEXNOM(K24B(1:23)//'N',NOMSD),'L',IFCFN)
        LFCFL1=.TRUE.
      ENDIF
      NBCHAT=NBCHAT+NBCHAC
C 2ND PASSE ET + ?
C ON NE FAIT PAS COMME EN MONO-DOMAINE ON NE CUMUL PAS LES LIGREL DE
C  CONTACT REDONDANTS.
      CALL JEEXIN(LIGRCF//'.LIEL',IRET)
      IF (IRET.NE.0) THEN
        LFCFL2=.TRUE.
C ON DETRUIT (LA PREMIERE FOIS, SOIT CONTACT 2 PASSE), LES LIGRELS DE
C CONTACT CONTINUE PROJETES EVENTUELS ET LES POINTEURS ASSOCIES
C ON LAISSE BIEN SUR INTACT LE LIGREL GLOBAL DE CHARGE DE LA BASE 'G'
        DO 9 I=1,NBCHAC
          K24B=ZK24(IFCFL+I-1)
          CALL JEEXIN(K24B(1:19)//'.FEL1',IRET)
          IF (IRET.NE.0) THEN
            CALL JEVEUO(K24B(1:19)//'.FEL1','L',IFEL1)
            DO 8 IDD=1,NBSD
              K24B1=ZK24(IFEL1+IDD-1)
              IF ((K24B1.NE.' ').AND.(K24B1(1:19).NE.K24B(1:19)))
     &          CALL DETRSD('LIGREL',K24B1)
    8       CONTINUE
            CALL JEDETR(K24B(1:19)//'.FEL1')
          ENDIF
    9   CONTINUE
C GROSSE GLUTE POUR DETERMINER PAR AVANCE LES MAILLES TARDIVES DU
C LIGREL DE CHARGE CONTACT N+1 EME PASSE QUI APPARTIENNENT AU SD.
C ON UTILISE POUR CE FAIRE LE POINTEUR '&&EXLIM2.NEMA' DIMENSIONNE
C AU NBRE DE MAILLES TARDIVES DU LIGREL. LEUR NBRE TOTAL EST NBMAT1
        IF (LFCFL1) THEN
          K24CHN=LIGRCF//'.NEMA'
          CALL JELIRA(K24CHN,'NUTIOC',NBMATA,K8BID)
          NBCC2=0
          CALL WKVECT('&&EXLIM2.NEMA','V V I',NBMATA,INEM2)
          CALL JERAZO('&&EXLIM2.NEMA',NBMATA,1)
          DO 40 L=1,NBMATA
           CALL JELIRA(JEXNUM(K24CHN,L),'LONMAX',NBNEMA,K8BID)
            NBNEMA=NBNEMA-1
            CALL JEVEUO(JEXNUM(K24CHN,L),'L',MADR)
C COMPTEUR POUR LES NOEUDS DE LA MAILLE TARDIVE APPARTENANT BIEN
C A .FCFN
            NBPB=0
C BOUCLE SUR LES NOEUDS DE LA MAILLE TARDIVE
            DO 39 M=1,NBNEMA
C NUMERO DU NOEUD DE LA MAILLE TARDIVE
              NOEUT=ZI(MADR+M-1)
C IL FAUT QUE TOUTES LES NOEUDS DE LA MAILLE TARDIVE APPARTIENNENT
C AU SD POUR QU'ELLE SOIT PRISE EN COMPTE
C BOUCLE SUR LES NOEUDS DE CONTACT DU SD
              DO 37 N=1,NBOEUT
                IF (ZI(IFCFN+N-1).EQ.NOEUT) THEN
                  NBPB=NBPB+1
                  GOTO 38
                ENDIF
   37         CONTINUE
   38         CONTINUE
   39       CONTINUE
            IF (NBPB.EQ.NBNEMA) THEN
C ET UNE MAILLE TARDIVE DE PLUS ATTRIBUEE AU SD
              ZI(INEM2+NBCC2)=-L
              NBCC2=NBCC2+1
            ENDIF
   40     CONTINUE
          LFCFL1=.FALSE.
C SI LE SD EST CONCERNE PAR LE CONTACT N+1 PASSE
          IF (NBCC2.NE.0) THEN
            NBCHAT=NBCHAT+1-NBCHAC
          ELSE
            NBCHAT=NBCHAT-NBCHAC
          ENDIF
C MONITORING
C        WRITE(6,*)'NBCC2 ',NBCC2
C        CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,'&&EXLIM2.NEMA',1,' ')
C        CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,LIGRCF(1:19),1,' ')

        ENDIF
      ENDIF

C ALLOCATION DE LA LISTE DE LIGREL POUR NUME_DDL DU SD
      CALL WKVECT(LLIGRS,'V V K24',NBCHAT,IVLIGR)
      ZK24(IVLIGR)='                        '
C LIGREL DE MODELE PROJETE
      ZK24(IVLIGR)=LIGRSD

C-------------
C PRETRAITEMENT POUR DIMENSIONNER D'EVENTUELS VECTEURS AXILIAIRES .FEL4
C ET .FEL5
      IF (NBCHA.NE.0) THEN
        CALL JEVEUO(JEXNOM(SDFETI(1:19)//'.FETB',NOMSD),'L',IFETB)
        CALL JELIRA(JEXNOM(SDFETI(1:19)//'.FETB',NOMSD),'LONMAX',LOFETB,
     &            K8BID)
        LOFETB=LOFETB-1
        IMULT=2
C RECHERCHE DE LA MULTIPLICITE MAXIMALE (IMULT) DES NOEUDS D'INTERFACE
        CALL JEVEUO(SDFETI(1:19)//'.FETI','L',IFETI)
        CALL JEVEUO(SDFETI(1:19)//'.FDIM','L',IDIME)
        NBNOI=ZI(IDIME+1)-1
        NBDDLI=ZI(IDIME+3)
        DO 305 I=0,LOFETB
          IAUXZI=-ZI(IFETB+I)
          IF (IAUXZI.GT.0) THEN
            DO 300 K=0,NBNOI
              IF (ZI(IFETI+4*K).EQ.IAUXZI) THEN
                IF (ZI(IFETI+4*K+1).GT.IMULT) IMULT=ZI(IFETI+4*K+1)
              ENDIF
  300       CONTINUE
          ENDIF
  305   CONTINUE

        LOFEL6 = 3*IMULT*NBDDLI/(NBNOI+1)
C ON DIMENSIONNE .FEL5 EN TENANT COMPTE DE DEUX LAGRANGES PAR DDLS BLO
C CABLES
        LOFEL7=LOFEL6*2
      ELSE
        LOFEL6=1
        LOFEL7=1
      ENDIF
C-------------

C-----------------------------------------------------------------------
C BOUCLE SUR LES CHARGES
C-----------------------------------------------------------------------
      NBCHA1=NBCHAT-1
      IDECA=0
      DO 100 K=1,NBCHA1
        K1=K-1
        ISIG=-1
C LIGREL DE CHARGE AVEC DU CONTACT CONTINU 1ERE PASSE, ON CHANGE DE
C POINTEURS
        LCC=.FALSE.
        LCC2=.FALSE.
        IF ((K.GT.NBCHA).AND.LFCFL1) THEN
C LCC INDIQUE SI LA CHARGE EST DE TYPE CONTACT CONTINUE 1ERE PASSE
          LCC=.TRUE.
          IFLIN=IFCFL
          IFLII=IFCFI
          IFLIM=IFCFM
          K1=K1-NBCHA
          ISIG=1
          IDECA=0
        ELSE
C CHARGE TARDIVE DEJA PRISE EN COMPTE AU COUP D'AVANT (LIGREL DE CHARGES
C TARDIVES AVEC CONTACT CONTINUE)
          IF ((LFCFL2).AND.(K.LE.NBCHA)) THEN
            K24CF1=ZK24(IFLIN+K1)(1:19)//'.FEL1'
            CALL JEVEUO(K24CF1,'L',IFEL1)
            ZK24(IVLIGR+K)=ZK24(IFEL1+NUMSD-1)
            IF (INFOFE(1:1).EQ.'T') THEN
              WRITE(IFM,*)'<FETI/EXLIM2> LIGREL DEJA PROJETE ',
     &        ZK24(IFLIN+K1)(1:19),' POUR SOUS-DOMAINE ',NOMSD
              WRITE(IFM,*)'NOM DU PROJETE :',ZK24(IFEL1+NUMSD-1)
            ENDIF
C ON PASSE EN FIN DE BOUCLE SUR LA CHARGE SUIVANTE
            GOTO 100
          ENDIF
C LCC INDIQUE SI LA CHARGE EST DE TYPE CONTACT CONTINUE N+1ERE PASSE
          IF ((LFCFL2).AND.(K.GT.NBCHA)) THEN
            LCC2=.TRUE.
          ENDIF
        ENDIF
        IF (LCC2) THEN
C SI CONTACT 2 + N PASSE ON DETRUIT LES LIGRELS PROJETES PASSES ET LES
C POINTEURS
          LIGRCH=LIGRCF(1:19)
          K24CHL=LIGRCH//'.LIEL'
          K24CHN=LIGRCH//'.NEMA'
          CALL JELIRA(K24CHN,'NUTIOC',NBMATA,K8BID)
          NBMAT1=NBCC2
        ELSE
          LIGRCH=ZK24(IFLIN+K1)(1:19)
          K24CHL=LIGRCH//'.LIEL'
          K24CHN=LIGRCH//'.NEMA'
          NBMATA=ZI(IFLII+2*K1)
          NBMAT1=ZI(IFLII+2*K1+1)
        ENDIF

        IF ((NBMATA.NE.NBMAT1).AND.(NBMAT1.GT.0)) THEN
C **** ON PROJETE LA CHARGE

C LIGREL DE CHARGE PARTAGE ENTRE PLUSIEURS SOUS-DOMAINES, ON VA LE
C DUPLIQUER. SON NOUVEAU NOM TEMPORAIRE DU LIGREL DUPLIQUE EST
C 'F'//GNCON(2:8)//LIGRCH(9:19)
          CALL GCNCON('.',K8BID)
          K8BID(1:1)='F'
          K24DUL=K8BID//K24CHL(9:24)

C ON VA STOCKER LE NOM DU NOUVEAU LIGREL DANS LE .FEL1 SI IL EXISTE
C SINON ON LE CREER
          K24CF1=K24CHL(1:19)//'.FEL1'
          CALL JEEXIN(K24CF1,IRET)
          IF (IRET.EQ.0) THEN
C CONSTITUTION OBJET STOCKAGE.FEL1
            CALL WKVECT(K24CF1,'V V K24',NBSD,IFEL1)
            CALL JERAZO(K24CF1,NBSD,1)
          ELSE
            CALL JEVEUO(K24CF1,'E',IFEL1)
          ENDIF
          ZK24(IFEL1+NUMSD-1)=K24DUL(1:19)

C LIGREL.LIEL INITIAL: K24CHL
C             PROJETE: K24DUL
C LIGREL.NEMA INITIAL: K24CHN
C             PROJETE: K24DUN
C ON PREPARE LA PROJECTION DU .LIEL
          CALL JECREC(K24DUL,'V V I','NU','CONTIG','VARIABLE',NBMAT1)
          LONT=2*NBMAT1
          CALL JEECRA(K24DUL,'LONT',LONT,' ')

          K24DUN=K24DUL(1:19)//'.NEMA'
C ON PREPARE LA PROJECTION DU .NEMA
          IF (.NOT.LCC) THEN
            CALL JELIRA(K24CHN,'NUTIOC',NBMAS,K8BID)
            CALL JECREC(K24DUN,'V V I','NU','CONTIG','VARIABLE',NBMAT1)
            CALL JELIRA(K24CHN,'LONT',LONT,K8BID)
            CALL JEECRA(K24DUN,'LONT',LONT,' ')
            CALL JEVEUO(K24CHL(1:19)//'.NBNO','L',INBNO)
            NBNO=ZI(INBNO)
C POUR DIMENSIONNER LES .FEL4 ET .FEL5
            NBNOE2=MAX(NBNO,NBMAS)
            LOFEL4=NBNOE2*LOFEL6
            LOFEL5=NBNOE2*LOFEL7
          ELSE
            NBNO=0
C INIT POUR CREER INFORMATIQUEMENT LES OBJETS .FEL4 ET .FEL5 AVANT DE
C DETRUIRE !
            LOFEL4=1
            LOFEL5=1
          ENDIF
C SI LIGREL A MAILLE ET/OU A NOEUD TARDIF (TOUS SAUF CONTACT CONTINUE
C 1ERE PASSE DONC)
C ON VA STOCKER LES ANCIENS NUMEROS DE MAILLES TARDIVES DANS LE .FEL2
C ET LES ANCIENS NUMEROS DE NOEUDS TARDIFS DANS LE .FEL3
C SI IL EXISTE, SINON ON LE CREE.

C --- ON TRAITE DES MAILLES ET/OU NOEUDS TARDIFS, ON A DONC BESOIN
C --- DES OBJETS .FEL2, .FEL3, .FEL4 ET .FEL5
          IF (.NOT.LCC) THEN
            K24CF2=K24CHL(1:19)//'.FEL2'
            CALL JEEXIN(K24CF2,IRET)
            IF (IRET.EQ.0) THEN
C CONSTITUTION OBJET STOCKAGE.FEL2
              NBMAS2=2*NBMAS
              CALL WKVECT(K24CF2,'V V I',NBMAS2,IFEL2)
              CALL JERAZO(K24CF2,NBMAS2,1)
            ELSE
              CALL JEVEUO(K24CF2,'E',IFEL2)
            ENDIF
C SI SEULEMENT MAILLE TARDIVE SANS NOEUD TARDIF, PAS BESOIN DE .FEL3
            IF (NBNO.GT.0) THEN
              K24CF3=K24CHL(1:19)//'.FEL3'
              CALL JEEXIN(K24CF3,IRET)
              IF (IRET.EQ.0) THEN
C CONSTITUTION OBJET STOCKAGE.FEL3
                NBNO2=2*NBNO
                CALL WKVECT(K24CF3,'V V I',NBNO2,IFEL3)
                CALL JERAZO(K24CF3,NBNO2,1)
              ELSE
                CALL JEVEUO(K24CF3,'E',IFEL3)
              ENDIF
            ENDIF
            K24CF4=K24CHL(1:19)//'.FEL4'
            CALL JEEXIN(K24CF4,IRET)
            IF (IRET.EQ.0) THEN
C CONSTITUTION OBJET STOCKAGE.FEL4
              CALL WKVECT(K24CF4,'V V I',LOFEL4,IFEL4)
              ZI(IFEL4)=0
            ELSE
              CALL JEVEUO(K24CF4,'E',IFEL4)
            ENDIF
C SI SEULEMENT MAILLE TARDIVE SANS NOEUD TARDIF, PAS BESOIN DE .FEL5
            IF (NBNO.GT.0) THEN
              K24CF5=K24CHL(1:19)//'.FEL5'
              CALL JEEXIN(K24CF5,IRET)
              IF (IRET.EQ.0) THEN
C CONSTITUTION OBJET STOCKAGE.FEL5
                CALL WKVECT(K24CF5,'V V I',LOFEL5,IFEL5)
                ZI(IFEL5)=0
              ELSE
                CALL JEVEUO(K24CF5,'E',IFEL5)
              ENDIF
            ENDIF
          ENDIF
C ENDIF DU .NOT.LCFC1

C-----------------------------------------------------------------------
C --- BOUCLE SUR LES GRELS DU .LIEL A PROJETER
C-----------------------------------------------------------------------
          NBMAT2=0
          NBNOET=0
          CALL JELIRA(K24CHL,'NUTIOC',NBGREL,K8BID)
          DO 30 I=1,NBGREL
            CALL JELIRA(JEXNUM(K24CHL,I),'LONMAX',NMGREL,K8BID)
            CALL JEVEUO(JEXNUM(K24CHL,I),'L',IGREL)

C-----------------------------------------------------------------------
C --- BOUCLE SUR LES MAILLES DES GRELS
C-----------------------------------------------------------------------
            NMGRE1=NMGREL-2
            DO 20 J=0,NMGRE1

C NUMERO DE LA MAILLE DANS LE LIGREL GLOBAL
              NUMGRE=ZI(IGREL+J)
              LOK=.FALSE.
C ON DECIDE DE FAIRE LA BOUCLE OU NON
              IF (LCC2) THEN
C DANS LE CAS CONTACT 2EME PASSE OU PLUS
                DO 50 L=1,NBCC2
                  IF (ZI(INEM2+L-1).EQ.NUMGRE) LOK=.TRUE.
   50           CONTINUE
              ELSE IF (LCC) THEN
C DANS LE CAS CONTACT 1ERE PASSE
                 IF (NUMGRE.GT.0) LOK=.TRUE.
              ELSE
C DANS LES AUTRES CAS
                 IF (NUMGRE.LT.0) LOK=.TRUE.
              ENDIF

              IF (LOK) THEN
C MAILLE A PROJETER EVENTUELLEMENT
C MAILLE TARDIVE SI LIGREL AVEC DIRICHLET OU FORCE NODALE OU CONTACT
C N+1 PASSE
C MAILLE PHYSIQUE SI CONTACT CONTINUE 1ERE PASSE

C-----------------------------------------------------------------------
C --- BOUCLE SUR LES MAILLES DES .FLIM OU .FCFM
C-----------------------------------------------------------------------
                DO 10 L=1,NBMAT1

                  IF (LCC2) THEN
                    NUMGR1=ISIG*NUMGRE
                  ELSE
                    NUMGR1=ZI(IFLIM+IDECA+L-1)
                  ENDIF

                  IF (NUMGR1.EQ.ISIG*NUMGRE) THEN
C ON LA DUPLIQUE DANS .LIEL EN CREEANT UN GREL LIMITE A CETTE MAILLE
                    NBMAT2=NBMAT2+1
                    CALL JECROC(JEXNUM(K24DUL,NBMAT2))
                    CALL JEECRA(JEXNUM(K24DUL,NBMAT2),'LONMAX',2,K8BID)
                    CALL JEVEUO(JEXNUM(K24DUL,NBMAT2),'E',LADR)
C ON RECOPIE LE NUMERO NEGATIF DE MAILLE ET SON TYPE
                    IF (LCC) THEN
                      ZI(LADR)=NUMGRE
                    ELSE
                      ZI(LADR)=-NBMAT2
                    ENDIF
                    ZI(LADR+1)=ZI(IGREL+NMGRE1+1)

C---------------
C TRAITEMENTS LIES AUX .FEL2/.FEL4 POUR MAILLE TARDIVE D'INTERFACE
                    IF (.NOT.LCC) THEN
                      IAUX1=IFEL2+2*(NUMGR1-1)+1
                      IF (ZI(IAUX1).LT.0) THEN
                        IAUX4=ZI(IFEL4)
                        ZI(IFEL4+IAUX4+1)=-NBMAT2
                        ZI(IFEL4+IAUX4+2)=NUMSD
                        ZI(IFEL4+IAUX4+3)=NUMGR1
                        ZI(IAUX1)=ZI(IAUX1)-1
                        ZI(IFEL4)=IAUX4+3
                      ELSE
                        IF ((ZI(IAUX1-1).EQ.0).AND.
     &                      (ZI(IAUX1).EQ.0)) THEN
C C'EST LA PREMIERE FOIS QUE CETTE MAILLE TARDIVE EST ENUMEREE: ON
C L'ENREGISTRE SIMPLEMENT
                          ZI(IAUX1-1)=-NBMAT2
                          ZI(IAUX1)=NUMSD
                        ELSE
C C'EST LA DEUXIEME FOIS: ON MET EN PLACE LE PROCESSUS VIA .FEL4 CAR
C C'EST UNE MAILLE TARDIVE CONCERNANT UN POINT D'INTERFACE
C DERNIERE ADRESSE PRISE PAR L'OBJET .FEL4
                          IAUX4=ZI(IFEL4)
                          ZI(IFEL4+IAUX4+1)=ZI(IAUX1-1)
                          ZI(IFEL4+IAUX4+2)=ZI(IAUX1)
                          ZI(IFEL4+IAUX4+3)=NUMGR1
                          ZI(IFEL4+IAUX4+4)=-NBMAT2
                          ZI(IFEL4+IAUX4+5)=NUMSD
                          ZI(IFEL4+IAUX4+6)=NUMGR1
                          ZI(IAUX1)=-2
                          ZI(IFEL4)=IAUX4+6
                        ENDIF
                      ENDIF
C------------------

C ON DUPLIQUE LES INFOS DU .NEMA (DESCRIPTION DES NOEUDS TARDIFS
C ASSOCIES A LA MAILLE TARDIVE
                      CALL JELIRA(JEXNUM(K24CHN,NUMGR1),'LONMAX',
     &                             NBNEMA,K8BID)
                      CALL JEVEUO(JEXNUM(K24CHN,NUMGR1),'L',MADR)
                      CALL JECROC(JEXNUM(K24DUN,NBMAT2))
                      CALL JEECRA(JEXNUM(K24DUN,NBMAT2),'LONMAX',
     &                            NBNEMA,K8BID)
                      CALL JEVEUO(JEXNUM(K24DUN,NBMAT2),'E',NADR)
                      NBNEMA=NBNEMA-1
C-----------------------------------------------------------------------
C --- BOUCLE SUR LES NOEUDS DU .NEMA
C-----------------------------------------------------------------------
                      DO 3 M=0,NBNEMA
                        IF (ZI(MADR+M).LT.0) THEN
C NOEUD TARDIF
                          IBID=ABS(ZI(MADR+M))
C ON VA TESTER LA PRESENCE EVENTUELLE D'UNE VALEUR NON NULLE POUR
C TRAITER LE CAS DES DDL_IMPO QUI UTILISENT, POUR DES MAILLES TARDIVES
C DISTINCTES, LES MEMES LAGRANGES (IL NE FAUT DONC PAS INCREMENTER LEURS
C NOUVELLES VALEURS).
C---------------
C TRAITEMENTS LIES AUX .FEL3/.FEL5 POUR NOEUD TARDIF D'INTERFACE
                          IAUX1=IFEL3+2*(IBID-1)+1
                          IF (ZI(IAUX1).LT.0) THEN
                            IAUX4=ZI(IFEL5)
                            NBNOET=NBNOET+1
                            ZI(IFEL5+IAUX4+1)=-NBNOET
                            ZI(IFEL5+IAUX4+2)=NUMSD
                            ZI(IFEL5+IAUX4+3)=IBID
                            ZI(IAUX1)=ZI(IAUX1)-1
                            ZI(IFEL5)=IAUX4+3
                            ZI(NADR+M)=-NBNOET
                          ELSE
                            IF ((ZI(IAUX1-1).EQ.0).AND.(ZI(IAUX1).EQ.0))
     &                      THEN
C C'EST LA PREMIERE FOIS QUE CE NOEUD TARDIF EST ENUMERE: ON
C L'ENREGISTRE SIMPLEMENT
                              NBNOET=NBNOET+1
                              ZI(IAUX1-1)=-NBNOET
                              ZI(IAUX1)=NUMSD
                              ZI(NADR+M)=-NBNOET
                            ELSE IF ((ZI(IAUX1-1).NE.0).AND.
     &                           (NUMSD.EQ.ZI(IAUX1))) THEN
C C'EST UN NOEUD TARDIF DE LIAISON_DDL POUR CE SD, IL NE FAUT PAS LE
C DUPLIQUER, IL EXISTE DEJA
                              ZI(NADR+M)=ZI(IAUX1-1)
                            ELSE
C C'EST LA DEUXIEME FOIS: ON MET EN PLACE LE PROCESSUS VIA .FEL5 CAR
C C'EST UN NOEUD TARDIF CONCERNANT UN POINT D'INTERFACE
C DERNIERE ADRESSE PRISE PAR L'OBJET .FEL5
                              IAUX4=ZI(IFEL5)
                              ZI(IFEL5+IAUX4+1)=ZI(IAUX1-1)
                              ZI(IFEL5+IAUX4+2)=ZI(IAUX1)
                              ZI(IFEL5+IAUX4+3)=IBID
                              NBNOET=NBNOET+1
                              ZI(IFEL5+IAUX4+4)=-NBNOET
                              ZI(IFEL5+IAUX4+5)=NUMSD
                              ZI(IFEL5+IAUX4+6)=IBID
                              ZI(IAUX1)=-2
                              ZI(IFEL5)=IAUX4+6
                              ZI(NADR+M)=-NBNOET
                            ENDIF
                          ENDIF
C------------------

                        ELSE
C NOEUD PHYSIQUE
                          ZI(NADR+M)=ZI(MADR+M)
                        ENDIF
    3                 CONTINUE
C FIN DU IF SUR LFCFL1
                    ENDIF
C ON A TROUVE LA MAILLE CORRESPONDANTE DU .FLIM A DUPLIQUER ON PEUT
C PASSER A LA MAILLE SUIVANTE DU GREL
                    GOTO 15
C FIN DU IF SUR NUMGR1
                  ENDIF
C FIN BOUCLE SUR LES MAILLES DE .FLIM
   10           CONTINUE
C FIN DU IF SUR NUMGRE ET LFCFL1
              ENDIF

C LABEL DE SORTIE DE BOUCLE SUR LE MAILLE DE '.FLIM'
   15         CONTINUE
C FIN BOUCLE MAILLE DE GREL
   20       CONTINUE
C FIN BOUCLE SUR LES GREL DU LIGREL DE CHARGE
   30     CONTINUE

C ON RENTRE NUMERO DE NOEUD TARDIF MAX (EN VALEUR ABSOLUE) DANS .NBNO
          CALL WKVECT(K24DUL(1:19)//'.NBNO','V V I',1,INBNO)
          ZI(INBNO)=NBNOET

C ADAPTATION DE L'OBJET JEVEUX K24DUL (ON REGROUPE LES MAILLES PAR TYPE
C DANS UN SEUL GREL)
          CALL ADALIG(K24DUL(1:19))

C ON DUPLIQUE LES AUTRES OBJETS DU LIGREL DE CHARGE
          CALL JEDUPO(LIGRCH//'.PRNS','V',K24DUL(1:19)//'.PRNS',.FALSE.)
          CALL JEDUPO(LIGRCH//'.LGNS','V',K24DUL(1:19)//'.LGNS',.FALSE.)
          CALL JEDUPO(LIGRCH//'.LGRF','V',K24DUL(1:19)//'.LGRF',.FALSE.)
          CALL JEDUPO(LIGRCH//'.PRNM','V',K24DUL(1:19)//'.PRNM',.FALSE.)

C CREATION DE LA CORRESPONDANCE MAILLE --> (IGREL,IM) POUR LE .REPE
          CALL CORMGI('V',K24DUL(1:19))

C ON STOCKE LE NOM DU LIGREL DE CHARGE TEMPORAIRE DANS LA LISTE
          ZK24(IVLIGR+K)='                        '
          ZK24(IVLIGR+K)=K24DUL(1:19)
          IF (INFOFE(1:1).EQ.'T') THEN
            WRITE(IFM,*)'<FETI/EXLIM2> PROJECTION DU LIGREL ',
     &        LIGRCH,' POUR SOUS-DOMAINE ',NOMSD
            WRITE(IFM,*)'NOM DU PROJETE :',ZK24(IVLIGR+K)
            IF (LCC) THEN
              WRITE(IFM,*)'LIGREL DE CHARGE DE CONTACT 1ER PASSAGE'
            ELSE
              WRITE(IFM,*)'LIGREL DE CHARGE AVEC DU TARDIF'
            ENDIF
          ENDIF

C ON DETRUIT LES .FEL4 TE .FEL5 INUTILES
          IF (.NOT.LCC) THEN
            IF (ZI(IFEL4).EQ.0) CALL JEDETR(K24CF4)
            IF (NBNO.GT.0) THEN
              IF (ZI(IFEL5).EQ.0) CALL JEDETR(K24CF5)
            ENDIF
          ENDIF
C POUR MONITORING
C          CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,K24CF1(1:19),1,'V')
C          CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,K24CF2(1:19),1,'V')
C          CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,K24CF3(1:19),1,'V')
C          CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,K24CF4(1:19),1,'V')
C          CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,K24CF5(1:19),1,'V')
C          CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,K24DUL(1:19),1,'V')

        ELSE IF (NBMAT1.GT.0) THEN

C **** ON CONSERVE LA CHARGE

C ON VA STOCKER LE NOM DU NOUVEAU LIGREL DANS LE .FEL1 SI IL EXISTE
C SINON ON LE CREER
          IF (LCC2) THEN
            K24CF1=LIGRCF(1:19)//'.FEL1'
          ELSE
            K24CF1=ZK24(IFLIN+K1)(1:19)//'.FEL1'
          ENDIF
          CALL JEEXIN(K24CF1,IRET)
          IF (IRET.EQ.0) THEN
C CONSTITUTION OBJET STOCKAGE.FEL1
            CALL WKVECT(K24CF1,'V V K24',NBSD,IFEL1)
            CALL JERAZO(K24CF1,NBSD,1)
          ELSE
C INCOHERENCE QUE POUR LE SEQUENTIEL, CF BOUCLE 150 CI-DESSOUS
            CALL ASSERT (NBPROC.NE.1)
            CALL JEVEUO(K24CF1,'E',IFEL1)
          ENDIF
          ZK24(IFEL1+NUMSD-1)=K24CF1(1:19)

C ON STOCKE LE NOM DU LIGREL DE CHARGE INITIAL DANS LA LISTE
          ZK24(IVLIGR+K)='                        '
          ZK24(IVLIGR+K)=K24CF1(1:19)
          IF (INFOFE(1:1).EQ.'T') THEN
            WRITE(IFM,*)'<FETI/EXLIM2> ASSOCIATION DU LIGREL DE '//
     &      'CHARGE ',ZK24(IVLIGR+K)(1:19),'POUR SOUS-DOMAINE ',NOMSD
            IF (LCC) THEN
              WRITE(IFM,*)'LIGREL DE CHARGE A MAILLE/NOEUDS PHYSIQUES'
            ELSE
              WRITE(IFM,*)'LIGREL DE CHARGE AVEC DU TARDIF'
            ENDIF
          ENDIF
C FIN DU IF NBMATA.NE.NBMAT1
        ENDIF
        IDECA=IDECA+NBMAT1


        IF (INFOFE(2:2).EQ.'T') THEN
          CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,ZK24(IVLIGR+K)(1:19),1,'V')
          IF (LCC2) THEN
            CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,LIGRCF(1:19),1,'V')
          ELSE
            CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,ZK24(IFLIN+K1)(1:19),1,'V')
          ENDIF
        ENDIF
C FIN BOUCLE SUR LA LISTE DES CHARGES
  100 CONTINUE


C-----------------------------------------------------------------------
C MONITORING
C-----------------------------------------------------------------------
      IF (INFOFE(1:1).EQ.'T')
     &  WRITE(IFM,*)'<FETI/EXLIM2> REMPLISSAGE OBJET JEVEUX ',
     &        LLIGRS(1:19)
      IF (INFOFE(2:2).EQ.'T')
     &  CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,LLIGRS(1:19),1,'V')

C-----------------------------------------------------------------------
C SPECIAL PARALLELISME POUR .FEL1
C-----------------------------------------------------------------------
      K24B=SDFETI(1:19)//'.FLIN'
      IF (NBPROC.GT.1) THEN
C INIT POUR JENUNO
        NOMSDA=SDFETI(1:19)//'.FETA'
C SI ON EST EN PARALLELE, COMPTE-TENU DE LA GESTION PARTIELLE DES
C DONNEES, POUR UN PROCESSEUR DONNE, IL FAUT GENERER TOUT LES .FEL1
C POTENTIELS, SINON LES BOUCLES DE ASSMAM, QUI ELLES FONCTIONNENT SUR
C TOUS LES CHARGEMENTS, SONT PERDUES !
        DO 150 IDD=1,NBSD
C POUR NE PAS REFAIRE LE TRAVAIL FAIT JUSTE AU DESSUS
          IF (IDD.NE.NUMSD) THEN
            CALL JENUNO(JEXNUM(NOMSDA,IDD),NOMSD2)
            CALL JEEXIN(JEXNOM(K24B,NOMSD2),IRET)
            IF (IRET.NE.0) THEN
C SOUS-DOMAINE CONCERNE PAR DES LIGRELS TARDIFS DE CHARGE
              CALL JEVEUO(JEXNOM(K24B,NOMSD2),'L',IFLIN)
              CALL JELIRA(JEXNOM(K24B,NOMSD2),'LONMAX',NBCHA2,K8BID)
              DO 140 K=1,NBCHA2
                K1=K-1
                K24CF1=ZK24(IFLIN+K1)(1:19)//'.FEL1'
                CALL JEEXIN(K24CF1,IRET1)
                IF (IRET1.EQ.0) THEN
C CONSTITUTION OBJET STOCKAGE.FEL1
                  CALL WKVECT(K24CF1,'V V K24',NBSD,IFEL1)
                  CALL JERAZO(K24CF1,NBSD,1)
                ENDIF
  140         CONTINUE
            ENDIF
            IF (LFCFL2) THEN
              K24CF1=LIGRCF(1:19)//'.FEL1'
              CALL JEEXIN(K24CF1,IRET1)
              IF (IRET1.EQ.0) THEN
C CONSTITUTION OBJET STOCKAGE.FEL1
                CALL WKVECT(K24CF1,'V V K24',NBSD,IFEL1)
                CALL JERAZO(K24CF1,NBSD,1)
              ENDIF
            ENDIF
          ENDIF
  150   CONTINUE
      ENDIF

      CALL JEDETR('&&EXLIM2.NEMA')
      CALL JEDEMA()

      END
