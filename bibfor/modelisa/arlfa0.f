      SUBROUTINE ARLFA0(MAIL  ,NOMARL,NOM1  ,NOM2  ,NAPP  ,
     &                  TYPMAI,NOMC  ,ISMED ,QUADRA)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 20/07/2009   AUTEUR MEUNIER S.MEUNIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
      CHARACTER*10  NOM1,NOM2,NOMC
      CHARACTER*10  QUADRA
      CHARACTER*16  TYPMAI
      CHARACTER*8   MAIL,NOMARL
      INTEGER       NAPP
      LOGICAL       ISMED
C
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C REGROUPEMENT DES INTEGRALES A CALCULER POUR LA JONCTION
C
C ----------------------------------------------------------------------
C
C
C IN  MAIL   : NOM DU MAILLAGE
C IN  NOMARL : NOM DE LA SD PRINCIPALE ARLEQUIN
C IN  NOM1   : NOM DE LA SD DE STOCKAGE MAILLES GROUP_MA_1
C IN  NOM2   : NOM DE LA SD DE STOCKAGE MAILLES GROUP_MA_2
C I/O NAPP   : NOMBRE DE COUPLES D'APPARIEMENT
C               IN  : D'APRES LE GRAPHE TOTAL
C               OUT : APRES EXCLUSION DES MAILLES HORS ZONE DE COLLAGE
C IN  TYPMAI : SD CONTENANT NOM DES TYPES ELEMENTS (&&CATA.NOMTM)
C I/O NOMC   : NOM DE LA SD POUR LE COLLAGE
C IN  ISMED  : VAUT .TRUE. SI ZONE DE COLLAGE EST LA ZONE MEDIATRICE
C OUT QUADRA : SD DES QUADRATURES A CALCULER
C
C
C SD PRODUITE
C   LES INTEGRALES A CALCULER SONT REGROUPEES SUIVANT DES FAMILLES
C   CHACUNE DES FAMILLES CORRESPOND A UN TYPE DE MAILLE SUR LEQUEL
C   SE FAIT L'INTEGRATION ET AU NUMERO DE LA FORMULE D'INTEGRATION
C   A UTILISER.
C
C QUAD.DIME     : INFORMATIONS SUR QUELQUES DIMENSIONS
C                  DIME(1) = NFAM    - NOMBRE DE FAMILLES
C                  DIME(2) = NAPP    - NOMBRE TOTAL DE COUPLES
C                  DIME(3) = NMAIN1  - NBRE. MA. INTEG. SUR MAILLE 1
C                  DIME(4) = NMAIN2  - NBRE. MA. INTEG. SUR MAILLE 2
C                  DIME(5) = NMASS   - NBRE. MA .INTEG. PAR SOUS-MAILLES
C                  DIME(6) = NNOIN1  - NBRE. ND. INTEG. SUR MAILLE 1
C                  DIME(7) = NNOIN2  - NBRE. ND. INTEG. SUR MAILLE 2
C QUAD.TYPEMA   : VECTEUR (K8) DES TYPES DE MAILLE ASSOCIES AUX FAMILLES
C                 (TMA FAM.1, TMA FAM.2, ...)
C QUAD.NUMERO   : VECTEUR DES NUMERO DE FORMULE D'INTEGRATION (NFI)
C                 (NFI FAM.1, NFI FAM.2, ...)
C QUAD.MAMA     : LISTE DES COUPLES DE MAILLES A INTEGRER
C                 (MA1.1, MA1.2, MA2.1, MA2.2, MA3.1, MA3.2, ... )
C                    SI MA*.1 > 0, INTEGRATION SUR MA*.1
C                    SI MA*.1 < 0, INTEGRATION SUR MA*.2
C                    SI MA*.2 > 0, INTEGRATION PAR INCLUSION
C                    SI MA*.2 < 0, INTEGRATION PAR SOUS-MAILLES
C                       MA*.1 INDEX DANS .GROUPEMA ASSOCIE AUX MULTIPLIC
C                       MA*.2 INDEX DANS L'AUTRE .GROUPEMA
C QUAD.MAFA     : LISTE DES COUPLES: NUMERO DE LA FAMILLE ASSOCIEE
C QUAD.LIMAMA   : LISTES COUPLES DE MAILLE ASSOCIEES AUX FAMILLES
C                 (XC V I NUMERO VARIABLE)
C                 [FAM. 1] (COUPLE1.1, COUPLE1.2, ...)
C                 [FAM. 2] (COUPLE2.1, COUPLE2.2, ...)
C                    ...
C                           COUPLE*.* INDEX DANS QUAD.MAMA
C
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      CHARACTER*32       JEXNUM , JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      CHARACTER*16  NOMBO1,NOMBO2
      CHARACTER*8   K8BID
      CHARACTER*24  NOMAPP
      INTEGER       DIME,NMA,NFAM
      INTEGER       ICPL,IFAM
      INTEGER       JGRAPH,JCUMU
      INTEGER       JTRTY ,JTRNU ,JTRNM
      INTEGER       JDIME1,JDIME2,JMNMX1,JMNMX2,JBPAN1,JBPAN2
      INTEGER       JBSOM1,JBSOM2
      INTEGER       JMCUMU,JCOLM
      INTEGER       JQDIME,JQNUME,JQTYPM,JQLIMA,JQMAMA,JQCUMU,JQMAFA
      INTEGER       NMAIN1,NMAIN2,NMASS
      INTEGER       NNOIN1,NNOIN2

      CHARACTER*6  NOMPRO
      PARAMETER   (NOMPRO='ARLFA0')
C
C ----------------------------------------------------------------------
C
C====
C 1. PREALABLES
C====
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      NMAIN1 = 0
      NMAIN2 = 0
      NMASS  = 0
      NNOIN1 = 0
      NNOIN2 = 0
C
C --- LECTURE ZONE DE COLLAGE
C
      CALL JEVEUO(NOMC(1:10)//'.MAILLE','L',JCOLM)
C
C --- LECTURE DONNEES MAILLAGE
C
      CALL JEVEUO(JEXATR(MAIL(1:8)//'.CONNEX','LONCUM'),'L',JMCUMU)
C
C --- LECTURE GRAPHE APPARIEMENT
C
      NOMAPP = NOMARL(1:8)//'.GRAPH'
      CALL JEVEUO(NOMAPP,'L',JGRAPH)
      CALL JELIRA(NOMAPP,'NMAXOC',NMA,K8BID)
      CALL JEVEUO(JEXATR(NOMAPP,'LONCUM'),'L',JCUMU)
C
C --- LECTURE DONNEES BOITES APPARIEMENT
C
      NOMBO1 = NOM1(1:10)//'.BOITE'
      CALL JEVEUO(NOMBO1(1:16)//'.DIME'  ,'L',JDIME1)
      CALL JEVEUO(NOMBO1(1:16)//'.MINMAX','L',JMNMX1)
      CALL JEVEUO(NOMBO1(1:16)//'.PAN'   ,'L',JBPAN1)
      CALL JEVEUO(NOMBO1(1:16)//'.SOMMET','L',JBSOM1)
C
      NOMBO2 = NOM2(1:10)//'.BOITE'
      CALL JEVEUO(NOMBO2(1:16)//'.DIME'  ,'L',JDIME2)
      CALL JEVEUO(NOMBO2(1:16)//'.MINMAX','L',JMNMX2)
      CALL JEVEUO(NOMBO2(1:16)//'.PAN'   ,'L',JBPAN2)
      CALL JEVEUO(NOMBO2(1:16)//'.SOMMET','L',JBSOM2)
C
C --- DIMENSION DE L'ESPACE
C
      CALL ASSERT(ZI(JDIME1).EQ.ZI(JDIME2))
      DIME = ZI(JDIME2)
C
C --- ALLOCATIONS OBJETS TEMPORAIRES
C
      CALL WKVECT('&&'//NOMPRO//'.TYPEMA' ,'V V K8',NAPP,JTRTY)
      CALL WKVECT('&&'//NOMPRO//'.NUMERO' ,'V V I' ,NAPP,JTRNU)
      CALL WKVECT('&&'//NOMPRO//'.NMAMA'  ,'V V I' ,NAPP,JTRNM)
C
C --- CREATION OBJET .MAMA
C
      CALL WKVECT(QUADRA(1:10)//'.MAMA','V V I',2*NAPP,JQMAMA)
C
C --- CREATION OBJET .MAFA
C
      CALL WKVECT(QUADRA(1:10)//'.MAFA','V V I',NAPP,JQMAFA)
C
      NFAM = 0
C
C====
C 2. ECRITURE QUADRATURE.MAMA ET FAMILLES TEMPORAIRES
C====
C
      CALL ARLFA1(DIME      ,ISMED     ,
     &            NOMARL    ,ZI(JGRAPH),ZI(JQMAFA),ZI(JQMAMA),
     &            MAIL      ,ZI(JMCUMU),NOM1,NOM2 ,
     &            ZL(JCOLM) ,
     &            TYPMAI    ,NMA       ,ZI(JCUMU) ,
     &            ZR(JBPAN1),ZR(JBPAN2),
     &            ZR(JBSOM1),ZR(JBSOM2),ZI(JDIME1),ZI(JDIME2),
     &            ZR(JMNMX1),ZR(JMNMX2),
     &            NMAIN1    ,NMAIN2    ,NMASS     ,NNOIN1    ,NNOIN2,
     &            ZK8(JTRTY),ZI(JTRNU) ,ZI(JTRNM) ,NFAM      ,ICPL   )
C
C====
C 3. LA FIN
C====
C
      NAPP = ICPL - 1
C
C --- CREATION OBJET .DIME
C
      CALL WKVECT(QUADRA(1:10)//'.DIME','V V I',7,JQDIME)
      ZI(JQDIME     ) = NFAM
      ZI(JQDIME + 1 ) = NAPP
      ZI(JQDIME + 2 ) = NMAIN1
      ZI(JQDIME + 3 ) = NMAIN2
      ZI(JQDIME + 4 ) = NMASS
      ZI(JQDIME + 5 ) = NNOIN1
      ZI(JQDIME + 6 ) = NNOIN2
C
C --- RECOPIE INFOS TRAVAIL DANS SD ECRITURE
C
      CALL WKVECT(QUADRA(1:10)//'.TYPEMA','V V K8',NFAM,JQTYPM)
      CALL WKVECT(QUADRA(1:10)//'.NUMERO','V V I' ,NFAM,JQNUME)
C
C --- CREATION DE L'OBJET LIMAMA
C
      CALL JECREC(QUADRA(1:10)//'.LIMAMA','V V I' ,'NU',
     &            'CONTIG','VARIABLE',NFAM)
      CALL JEECRA(QUADRA(1:10)//'.LIMAMA','LONT'  ,NAPP,' ')
C
      DO 31 IFAM = 1, NFAM
        CALL JECROC(JEXNUM(QUADRA(1:10)//'.LIMAMA',IFAM))
        CALL JEECRA(JEXNUM(QUADRA(1:10)//'.LIMAMA',IFAM),'LONMAX',
     &               ZI(JTRNM-1+IFAM),' ')
        ZK8(JQTYPM-1+IFAM) = ZK8(JTRTY-1+IFAM)
        ZI(JQNUME-1+IFAM)  = ZI(JTRNU-1+IFAM)
 31   CONTINUE
C
      CALL JEVEUO(QUADRA(1:10)//'.LIMAMA','E',JQLIMA)
      CALL JEVEUO(JEXATR(QUADRA(1:10)//'.LIMAMA','LONCUM'),'L',JQCUMU)
C
C --- NOMBRE DE COUPLES DE MAILLES ASSOCIEES A CHAQUE FAMILLE
C
      DO 32 IFAM = 1, NFAM
        ZI(JTRNM+IFAM-1) = ZI(JQCUMU+IFAM-1) - 1
 32   CONTINUE
C
C --- LISTES COUPLES DE MAILLE ASSOCIEES AUX FAMILLES
C
      DO 33 ICPL = 1, NAPP
        IFAM = ZI(JQMAFA + ICPL - 1)
        ZI(JQLIMA + ZI(JTRNM + IFAM - 1)) = ICPL
        ZI(JTRNM + IFAM - 1) = ZI(JTRNM + IFAM - 1) + 1
 33   CONTINUE
C
C --- DESALLOCATIONS
C
      CALL JEDETR('&&'//NOMPRO//'.TYPEMA')
      CALL JEDETR('&&'//NOMPRO//'.NUMERO')
      CALL JEDETR('&&'//NOMPRO//'.NMAMA')

      CALL JEDEMA()

      END
