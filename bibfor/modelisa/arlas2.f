      SUBROUTINE ARLAS2(DIME  ,LCARA ,NDML1   ,NDML2   ,PTMT    ,
     &                  CNML1   ,TANG  ,MTEL    ,MTMO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 13/10/2009   AUTEUR CAO B.CAO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C
C
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
      INTEGER  DIME,NDML1,NDML2
      INTEGER  PTMT(NDML2,*)
      INTEGER  CNML1(*)
      REAL*8   LCARA
      REAL*8   TANG(DIME*(DIME-1),*)
      REAL*8   MTEL(*),MTMO(*)
C
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C GESTION DES RELATIONS LINEAIRES
C ASSEMBLAGE MATRICE ELEMENTAIRE ARLEQUIN
C   MAILLE COQUE / MAILLE SOLIDE
C
C ----------------------------------------------------------------------
C
C
C IN  DIME   : DIMENSION DE L'ESPACE
C IN  LCARA  : LONGUEUR CARACTERISTIQUE POUR TERME DE COUPLAGE
C IN  NDML1  : NOMBRE DE NOEUDS MAILLE 1
C IN  NDML2  : NOMBRE DE NOEUDS MAILLE 2
C IN  PTMT   : POINTEURS DANS MTMO (CF ARLAS0)
C IN  CNML1  : CONNECTIVITE MAILLE 1
C IN  TANG   : TANGENTES LISSEES POUR COQUE (CF LISNOR)
C IN  MTEL   : MATRICES ELEMENTAIRES (CF ARLTE)
C I/O MTMO   : MATRICE MORSE (CF ARLFC*)
C
C MATRICE PONCTUELLE DANS MTMO : (X1.X2, X1.Y2, [X1.Z2], Y1.X2, ...,
C                              T1.X2, T1.Y2, [T1.Z2, T2.X2, ...] )
C
C ----------------------------------------------------------------------
C
      INTEGER NOECOQ(2,9),NBNO,NDML12,IER
      INTEGER VLTER,VLTER2,VLTERT,D2N2,IN1,IAUX,JAUX,KAUX,IND1,IND2,
     &        PMAT,PMAT1,PMAT2,LGNM,LGNM1,LGNM2
      REAL*8  TRMT1,TRMT2,VLR1,VLR2,MTMO1(9),VLMT2(9),MTMO2(6)
C
C ----------------------------------------------------------------------
C
      IF (DIME.EQ.2) THEN
        VLTER2 = 4
        VLTERT = 2
        VLTER = 6
      ELSE IF (DIME.EQ.3) THEN
        VLTER2 = 9
        VLTERT = 6
        VLTER = 15
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF

      NBNO   = NDML1/2
      NDML12 = NDML1*NDML2
      D2N2   = VLTER2*NDML2

C --- ASSEMBLAGE DE LA MATRICE ELEMENTAIRE MTEL

      CALL NOCOQU(DIME,NBNO,NOECOQ)

      DO 10 IAUX = 1, NBNO

        IN1 = CNML1(IAUX)

        IND1  = NOECOQ(2,IAUX)
        IND2  = NOECOQ(1,IAUX)

        PMAT1 = 1 + NDML2*(IND1-1)
        PMAT2 = 1 + NDML2*(IND2-1)
        LGNM1 = NDML12 + D2N2*(IND1-1)
        LGNM2 = NDML12 + D2N2*(IND2-1)

        DO 10 JAUX = 1, NDML2

          LGNM = 1 + VLTER*(PTMT(JAUX,IAUX)-1)

C ------- CALCUL DE LA TRACE DE MTEL+ ET MTEL-

          TRMT1 = 0.D0
          TRMT2 = 0.D0

          PMAT = 1
          DO 20 KAUX = 1, DIME
            TRMT1 = TRMT1 + MTEL(LGNM1+PMAT)
            TRMT2 = TRMT2 + MTEL(LGNM2+PMAT)
            PMAT = PMAT + DIME + 1
 20       CONTINUE

C ------- CALCUL DES MATRICES PONCTUELLES MTMO1 ET MTMO2

          DO 30 KAUX = 1, VLTER2
            LGNM1 = LGNM1 + 1
            LGNM2 = LGNM2 + 1
            MTMO1(KAUX) = LCARA*(MTEL(LGNM1)+MTEL(LGNM2))
            VLMT2(KAUX) = LCARA*(MTEL(LGNM1)-MTEL(LGNM2))
 30       CONTINUE

          VLR1 = MTEL(PMAT1) + MTEL(PMAT2) + LCARA*(TRMT1+TRMT2)
          VLR2 = MTEL(PMAT1) - MTEL(PMAT2) + LCARA*(TRMT1-TRMT2)
          PMAT1 = PMAT1 + 1
          PMAT2 = PMAT2 + 1

          PMAT = 1
          DO 40 KAUX = 1, DIME
            MTMO1(PMAT) = MTMO1(PMAT) + VLR1
            VLMT2(PMAT) = VLMT2(PMAT) + VLR2
            PMAT = PMAT + DIME + 1
 40       CONTINUE

          CALL PRMAMA(1,VLMT2,DIME,DIME,DIME,TANG(1,IN1),DIME,DIME,
     &                DIME-1,MTMO2,DIME,DIME,DIME-1,IER)

C ------- ASSEMBLAGE DES MATRICES PONCTUELLES MTMO1 ET MTMO2

          DO 50 KAUX = 1, VLTER2
            MTMO(LGNM) = MTMO(LGNM) + MTMO1(KAUX)
            LGNM = LGNM + 1
 50       CONTINUE

          DO 60 KAUX = 1, VLTERT
            MTMO(LGNM) = MTMO(LGNM) + MTMO2(KAUX)
            LGNM = LGNM + 1
 60       CONTINUE

 10   CONTINUE

      END
