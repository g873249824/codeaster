      SUBROUTINE XMACON(CHAR  ,NOMA  ,NOMO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/10/2007   AUTEUR NISTOR I.NISTOR 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8  CHAR
      CHARACTER*8  NOMA
      CHARACTER*8  NOMO
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE XFEM - LECTURE DONNEES)
C CREATION DES SDS SPECIFIQUES FORMULATION XFEM
C
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NOMO   : NOM DU MODELE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)      
      CHARACTER*32 JEXNOM,JEXNUM,JEXATR
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER NDIM,JMA,ITYPMA
      INTEGER CFMMVD,ZMESX,ZTABF,NINTER,PINT,NZONE
      INTEGER JMAESC,JTABF,JMOFIS,JNFIS,NTMAE,NBMA,NBMZ
      INTEGER JCESD1,JCESV1,JCESL1,JCESD2,JCESV2,JCESL2
      INTEGER JLSN,JCESD3,JCESV3,JCESL3,JCONX1,JCONX2
      INTEGER JZOFI,JMAFI,JDIMC,JCESD,JCESL,JCESV,JCESC
      INTEGER IZONE,IMAE,IMA,NBPC,NNO,NTPC,IRET,IAD
      INTEGER IAD1,IAD2,IAD3,IFIS,NBFAC,NNGL,INO
      REAL*8  TLSN 
      CHARACTER*8  NOMFIS,NOMZON,K8BID,TYPMA
      CHARACTER*24 MAESCL,TABFIN
      CHARACTER*19 CHS1,CHS2,CHS3,FACLON,AINTER,LSN
      CHARACTER*19 CARSD,CARTE,MAI
      LOGICAL  PINTNO
          
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C 
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      MAESCL = CHAR(1:8)//'.CONTACT.MAESCL'
      TABFIN = CHAR(1:8)//'.CONTACT.TABFIN'
C
      ZMESX  = CFMMVD('ZMESX')
      ZTABF  = CFMMVD('ZTABF')      
C
      CHS1 = '&&XMACON.CHS1'
      CHS2 = '&&XMACON.CHS2'     
      CHS3 = '&&XMACON.CHS3'
C
C-----ON RECUPERE LE NOMBRE TOTAL DE MAILLES DU MAILLAGE ET SA DIMENSION
C
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8BID,IRET)
      CALL DISMOI('F','DIM_GEOM',NOMA,'MAILLAGE',NDIM,K8BID,IRET)

C-----SI ON N'EST PAS EN 2D, ON SORT
      IF (NDIM.EQ.3) CALL U2MESS('F','XFEM_26')

C
C-----ON RECUPERE LE NOMBRE DE ZONES DE CONTACT
C
      CALL JEVEUO(CHAR(1:8)//'.CONTACT.NDIMCO','L',JDIMC)
      NZONE=ZI(JDIMC+1)
C
C-----ON RECUPERE LE MAPPING ZONE CONTACT - NOM FISSURE
C
      CALL JEVEUO(CHAR(1:8)//'.CONTACT.XFIMAI','L',JZOFI)
C
C-----ON RECUPERE LE MAPPING MAILLE XFEM - NOM FISSURE 
C-----DONT ELLE APPARTIENT
C
      CARTE = NOMO(1:8)//'.XMAFIS'
      CARSD = '&&XMACON.MAFI'
      CALL CARCES ( CARTE, 'ELEM', ' ', 'V', CARSD, IRET )
      CALL JEVEUO(CARSD//'.CESC','L',JCESC)
      CALL JEVEUO(CARSD//'.CESD','L',JCESD)
      CALL JEVEUO(CARSD//'.CESL','L',JCESL)
      CALL JEVEUO(CARSD//'.CESV','L',JCESV)
C
C-----ON RECUPERE LA CONNECTIVITE DU MAILLAGE
C
      CALL JEVEUO(NOMA//'.CONNEX','L',JCONX1)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',JCONX2)

      MAI=NOMA//'.TYPMAIL'
      CALL JEVEUO(MAI,'L',JMA)
C     
C --- ON TRANSFORME LE CHAMP LSN EN CHAMP SIMPLE
C
      CALL CNOCNS(NOMO//'.LNNO','V',CHS1)
      CALL JEVEUO(CHS1//'.CNSV','L',JLSN)
C 
C --- NOMBRE TOTAL DES MAILLES ESCLAVES DE CONTACT.
C
      NTMAE = 0 
      FACLON = NOMO//'.TOPOFAC.LO'       
      CALL CELCES(FACLON,'V',CHS2)
      CALL JEVEUO(CHS2//'.CESD','L',JCESD2)
      CALL JEVEUO(CHS2//'.CESV','L',JCESV2)
      CALL JEVEUO(CHS2//'.CESL','L',JCESL2)
C
      DO 110 IMA = 1,NBMA
C -----ON VERIFIE QUE LE MAILLAGE NE CONTIENT PAS DES 'TRIA3'      
        ITYPMA=ZI(JMA-1+IMA)
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYPMA),TYPMA)
        IF (TYPMA.EQ.'TRIA3') CALL U2MESS('F','XFEM_27')
   
        CALL CESEXI('C',JCESD2,JCESL2,IMA,1,1,1,IAD)
        IF (IAD.GT.0) THEN
          IF (ZI(JCESV2-1+IAD).GT.0) THEN
            NTMAE = NTMAE + 1
          ENDIF
        ENDIF
 110  CONTINUE
C
C --- CREATION DU TABLEAU DES MAILLES ESCLAVES MAESC
C
      CALL WKVECT(MAESCL,'G V I',ZMESX*NTMAE+1,JMAESC)
C
C --- REMPLISSAGE DU TABLEAU DES MAILLES ESCLAVES MAESC
C
      AINTER = NOMO//'.TOPOFAC.AI' 
      CALL CELCES(AINTER,'V',CHS3)
      CALL JEVEUO(CHS3//'.CESD','L',JCESD3)
      CALL JEVEUO(CHS3//'.CESV','L',JCESV3)
      CALL JEVEUO(CHS3//'.CESL','L',JCESL3)
C
      ZI(JMAESC) = 0
      NTMAE = 0
C
      DO 200 IZONE = 1,NZONE
        NBMZ   = 0
        NOMZON = ZK8(JZOFI-1+IZONE)
        DO 210 IMA = 1,NBMA
          CALL CESEXI('C',JCESD2,JCESL2,IMA,1,1,1,IAD)
          IF (IAD.GT.0)  THEN
             NINTER = ZI(JCESV2-1+IAD)
             IF (NINTER.GT.1) THEN
               CALL CESEXI('C',JCESD,JCESL,IMA,1,1,1,IAD1)
               IF (IAD1.GT.0) NOMFIS  = ZK8(JCESV-1+IAD1)
               IF (NOMZON.NE.NOMFIS) GO TO 210
               NBMZ = NBMZ+1
               ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+1)= IMA
               ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+2)= IZONE
C --- ON RECUPERE LE NOMBRE DE FACETTES DE CONTACT
               CALL CESEXI('C',JCESD2,JCESL2,IMA,1,1,2,IAD2)
C               NBFAC = ZI(JCESV2-1+IAD2)     
C --- ON VERIFIE SI LA FISSURE COINCIDE AVEC UNE FACETTE	     
               PINTNO=.TRUE.
               DO 211 PINT = 1,NINTER
                 CALL CESEXI('S',JCESD3,JCESL3,IMA,
     &                      1,1,4*(PINT-1)+1,IAD3)
                 CALL ASSERT(IAD3.GT.0)
                 IF (ZR(JCESV3-1+IAD3).GT.0) THEN
                   PINTNO=.FALSE.
                 ENDIF
211            CONTINUE
C-----ON RECUPERE LE NOMBRE DE NOEUDS DE LA MAILLE
               CALL JELIRA(JEXNUM(NOMA//'.CONNEX',IMA),
     &                   'LONMAX',NNO,K8BID)
C --- SI LA FISSURE COINCIDE AVEC UNE FACETTE, ON VERIFIE LE 
C     STATUT DE LA MAILLE (ESCLAVE OU MAÎTRE)          
               IF (PINTNO) THEN
                 TLSN=0.D0
                 DO 212 INO=1,NNO
                   NNGL=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+INO-1)
                   TLSN=TLSN+ZR(JLSN-1+NNGL)
 212             CONTINUE
                 IF (TLSN.GT.0) THEN
                   ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+4)=1
                   ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+3)=0
                 ELSEIF (TLSN.LT.0) THEN
                   ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+4)=-1
                   ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+3)=2
                 ENDIF
               ELSEIF (.NOT.PINTNO) THEN
                 ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+4)=0
                 ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+3)=2
               ENDIF
             ENDIF
          ENDIF
 210    CONTINUE

        NTMAE = NTMAE + NBMZ
 200  CONTINUE      

      ZI(JMAESC) = NTMAE
C
C --- ON COMPTE LE NOMBRE DES POINTS ESCLAVES 
C
      NTPC = 0
      DO 300 IMAE = 1,NTMAE
        NBPC  = ZI(JMAESC+4*(IMAE-1)+3)
        NTPC = NTPC + NBPC
 300  CONTINUE
C
C --- CREATION DE TABLEAU TABFIN 
C
      CALL WKVECT(TABFIN,'G V R',ZTABF*NTPC+1,JTABF)
C    
      CALL JEDEMA()
      END
