      SUBROUTINE XMACON(CHAR  ,NOMA  ,NOMO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/12/2009   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8  CHAR
      CHARACTER*8  NOMA
      CHARACTER*8  NOMO
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE XFEM - LECTURE DONNEES)
C CREATION DES SDS SPECIFIQUES FORMULATION XFEM
C
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NOMO   : NOM DU MODELE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)      
      CHARACTER*32 JEXNUM,JEXATR
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER NDIM,JTYMAI,ITYPMA,JMAIL,ITYELE
      INTEGER CFMMVD,ZMESX,ZTABF,NINTER,PINT,NZONE
      INTEGER JMAESC,JTABF,NTMAE,NBMA,NBMZ
      INTEGER JCESD2,JCESV2,JCESL2
      INTEGER JLSN,JCESD3,JCESV3,JCESL3,JCONX1,JCONX2
      INTEGER JZOFI,JDIMC,JCESD,JCESL,JCESV,JCESC
      INTEGER IZONE,IMAE,IMA,NBPC,NNO,NTPC,IRET,IAD
      INTEGER IAD1,IAD2,IAD3,NNGL,INO,NFACE,STATUT
      REAL*8  LSN 
      CHARACTER*8  NOMFIS,NOMZON,K8BID,TYPMA,ELREF
      CHARACTER*16 TYELE
      CHARACTER*24 MAESCL,TABFIN
      CHARACTER*19 CHS1,CHS2,CHS3,FACLON,AINTER
      CHARACTER*19 CARSD,CARTE
      LOGICAL  PINTNO,ESCLAV,MAITRE,MALIN,ISMALI
      INTEGER  JCMCF,ZCMCF,TYPINT,NNINT
      INTEGER  ZXAIN,XXMMVD
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C 
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      MAESCL = CHAR(1:8)//'.CONTACT.MAESCL'
      TABFIN = CHAR(1:8)//'.CONTACT.TABFIN'
C     
      CALL JEVEUO(CHAR(1:8)//'.CONTACT.CARACF','L',JCMCF)
C
      ZMESX  = CFMMVD('ZMESX')
      ZTABF  = CFMMVD('ZTABF')
      ZCMCF  = CFMMVD('ZCMCF')
      ZXAIN  = XXMMVD('ZXAIN')
C
      CHS1 = '&&XMACON.CHS1'
      CHS2 = '&&XMACON.CHS2'     
      CHS3 = '&&XMACON.CHS3'         
C
C --- ON RECUPERE LE NOMBRE TOTAL DE MAILLES DU MAILLAGE ET SA DIMENSION
C
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8BID,IRET)
      CALL DISMOI('F','DIM_GEOM',NOMA,'MAILLAGE',NDIM,K8BID,IRET)

C --- SI ON N'EST PAS EN 2D, ON SORT
C      IF (NDIM.EQ.3) CALL U2MESS('F','XFEM_26')
C
C --- ON RECUPERE LE NOMBRE DE ZONES DE CONTACT
C
      CALL JEVEUO(CHAR(1:8)//'.CONTACT.NDIMCO','L',JDIMC)
      NZONE=ZI(JDIMC+1)
C
C --- ON RECUPERE LE MAPPING ZONE CONTACT - NOM FISSURE
C
      CALL JEVEUO(CHAR(1:8)//'.CONTACT.XFIMAI','L',JZOFI)
C
C --- ON RECUPERE LE MAPPING MAILLE XFEM - NOM FISSURE 
C --- DONT ELLE APPARTIENT
C
      CARTE = NOMO(1:8)//'.XMAFIS'
      CARSD = '&&XMACON.MAFI'
      CALL CARCES ( CARTE, 'ELEM', ' ', 'V', CARSD, IRET )
      CALL JEVEUO(CARSD//'.CESC','L',JCESC)
      CALL JEVEUO(CARSD//'.CESD','L',JCESD)
      CALL JEVEUO(CARSD//'.CESL','L',JCESL)
      CALL JEVEUO(CARSD//'.CESV','L',JCESV)
C
C --- ON RECUPERE LA CONNECTIVITE DU MAILLAGE
C
      CALL JEVEUO(NOMA//'.CONNEX','L',JCONX1)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',JCONX2)

      CALL JEVEUO(NOMA//'.TYPMAIL','L',JTYMAI)
      CALL JEVEUO(NOMO//'.MAILLE','L',JMAIL)
C     
C --- ON TRANSFORME LE CHAMP LSN EN CHAMP SIMPLE
C
      CALL CNOCNS(NOMO//'.LNNO','V',CHS1)
      CALL JEVEUO(CHS1//'.CNSV','L',JLSN)
C 
C --- NOMBRE TOTAL DES MAILLES ESCLAVES DE CONTACT.
C
      NTMAE = 0 
      FACLON = NOMO//'.TOPOFAC.LO'       
      CALL CELCES(FACLON,'V',CHS2)
      CALL JEVEUO(CHS2//'.CESD','L',JCESD2)
      CALL JEVEUO(CHS2//'.CESV','L',JCESV2)
      CALL JEVEUO(CHS2//'.CESL','L',JCESL2)
C
      DO 110 IMA = 1,NBMA
        CALL CESEXI('C',JCESD2,JCESL2,IMA,1,1,1,IAD)
        IF (IAD.GT.0) THEN
          IF (ZI(JCESV2-1+IAD).GT.0) THEN
            NTMAE = NTMAE + 1
          ENDIF
        ENDIF
 110  CONTINUE
C
C --- CREATION DU TABLEAU DES MAILLES ESCLAVES MAESC
C
      CALL WKVECT(MAESCL,'G V I',ZMESX*NTMAE+1,JMAESC)
C
C --- REMPLISSAGE DU TABLEAU DES MAILLES ESCLAVES MAESC
C
      AINTER = NOMO//'.TOPOFAC.AI' 
      CALL CELCES(AINTER,'V',CHS3)
      CALL JEVEUO(CHS3//'.CESD','L',JCESD3)
      CALL JEVEUO(CHS3//'.CESV','L',JCESV3)
      CALL JEVEUO(CHS3//'.CESL','L',JCESL3)
C
      ZI(JMAESC) = 0
      NTMAE = 0
      NTPC = 0
C
      DO 200 IZONE = 1,NZONE
        NBMZ   = 0
        NOMZON = ZK8(JZOFI-1+IZONE)
        DO 210 IMA = 1,NBMA
          CALL CESEXI('C',JCESD2,JCESL2,IMA,1,1,1,IAD)
          IF (IAD.GT.0)  THEN
            NINTER = ZI(JCESV2-1+IAD)
            IF (NINTER.GE.1) THEN
              CALL CESEXI('C',JCESD,JCESL,IMA,1,1,1,IAD1)
              IF (IAD1.GT.0) NOMFIS  = ZK8(JCESV-1+IAD1)
              IF (NOMZON.NE.NOMFIS) GO TO 210
C --- ON RECUPERE LE NOMBRE DE FACETTES DE CONTACT
              CALL CESEXI('C',JCESD2,JCESL2,IMA,1,1,2,IAD2)
              NFACE = ZI(JCESV2-1+IAD2)
              IF (NFACE.EQ.0.D0) GO TO 210
C
C --- PINTNO EST VRAI SI TOUT LES POINTS D'INTERSECTIONS DE LA MAILLE
C --- SONT SUR DES NOEUDS, IL EST ALORS POSSIBLE QUE LA FISSURE COUPE
C --- CONFORMEMENT LES MAILLES.
C
              PINTNO=.TRUE.
              DO 211 PINT = 1,NINTER
                CALL CESEXI('S',JCESD3,JCESL3,IMA,
     &                     1,1,ZXAIN*(PINT-1)+1,IAD3)
                CALL ASSERT(IAD3.GT.0)
                IF (ZR(JCESV3-1+IAD3).GT.0) THEN
                  PINTNO=.FALSE.
                ENDIF
211           CONTINUE
C --- ESCLAV ET MAITRE SONT RESPECTIVEMENT VRAI SI LA MAILLE POSSEDE
C --- DES NOEUDS EXCLUSIVEMENT ESCLAVE OU EXCLUSIVEMENT MAITRE
              IF (PINTNO) THEN
                ESCLAV = .FALSE.
                MAITRE = .FALSE.
C --- ON RECUPERE LE NOMBRE DE NOEUDS DE LA MAILLE
                CALL JELIRA(JEXNUM(NOMA//'.CONNEX',IMA),
     &                  'LONMAX',NNO,K8BID)
                IF ((TYPMA.EQ.'TRIA6').OR.(TYPMA.EQ.'QUAD8')) NNO=NNO/2
                DO 212 INO=1,NNO
C --- ON VERIFIE QUE LE NOEUD N'EST PAS INTERSECTÉ
                  DO 213 PINT = 1,NINTER
                    CALL CESEXI('S',JCESD3,JCESL3,IMA,
     &                     1,1,ZXAIN*(PINT-1)+2,IAD3)
                    CALL ASSERT(IAD3.GT.0)
                    IF (ZR(JCESV3-1+IAD3).EQ.INO) GOTO 212
213               CONTINUE
                  NNGL=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+INO-1)
                  LSN = ZR(JLSN-1+NNGL)
                  IF (LSN.LT.0) THEN
C --- AU MOINS UN NOEUD NON INTERS EST DU COTE ESCLAVE (LSN <0)
                    ESCLAV = .TRUE.
                  ELSEIF (LSN.GT.0) THEN
C --- AU MOINS UN NOEUD NON INTERS EST DU COTE MAITRE (LSN >0)
                    MAITRE = .TRUE.
                  ENDIF
 212            CONTINUE
              ELSE
                ESCLAV = .TRUE.
                MAITRE = .TRUE.
              ENDIF
C
C --- ON RECUPERE LE TYPE DE LA MAILLE                  
C --- ON VERIFIE QUE LE MAILLAGE NE CONTIENT QUE DES ELEMENTS LIN EN 3D
C
              ITYPMA=ZI(JTYMAI-1+IMA)
              CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYPMA),TYPMA)
              MALIN = ISMALI(TYPMA)
              IF (NDIM.EQ.3) THEN
                ELREF = 'TR3'
                IF (.NOT.MALIN) CALL U2MESS('F','XFEM_27')
              ELSEIF (NDIM.EQ.2) THEN
                ELREF = 'SE2'
              ENDIF
C --- ON RECUPERE LE TYPE DE SCHEMAS D'INTEGRATION               
              TYPINT = NINT(ZR(JCMCF+ZCMCF*(IZONE-1)+1))
C --- ON RECUPERE LE NOMBRE DE POINTS D'INTEGRATION
              CALL XMELIN(ELREF,TYPINT,NNINT)
C
C --- CALCUL DU STATUT DE LA MAILLE UTILE POUR LA PROJECTION :
C
C ---  1 SI HEAVISIDE
C --- -2 SI CRACK-TIP
C ---  3 SI HEAVISIDE CRACK-TIP
C --- NEGATIF SI ON NE PROJETE PAS DESSUS
C
              ITYELE=ZI(JMAIL-1+IMA)
              CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',ITYELE),TYELE)
              IF ((TYELE(9:12).EQ.'XHTC'.AND.NDIM.EQ.2).OR.
     &            (TYELE(6:9).EQ.'XHTC'.AND.NDIM.EQ.3)) THEN
                STATUT = 3
              ELSEIF ((TYELE(9:11).EQ.'XTC'.AND.NDIM.EQ.2).OR.
     &            (TYELE(6:8).EQ.'XTC'.AND.NDIM.EQ.3)) THEN
                STATUT = -2
              ELSE
                STATUT = 1
              ENDIF
              IF (NDIM.EQ.2.AND.(.NOT.MALIN)) THEN
                IF (ABS(STATUT).GT.1) CALL U2MESS('F','XFEM_38')
              ENDIF
              IF (ESCLAV.AND.(.NOT.MAITRE)) THEN
C --- C'EST UNE MAILLE EXCLUSIVEMENT ESCLAVE, LE STATUT EST NEGATIF
C --- CAR ON PREVOIT DE NE PAS PROJETER SUR SES FACETTES
                STATUT = -1*INT(ABS(STATUT))
              ELSEIF (.NOT.ESCLAV) THEN
C --- DU COTE EXCLUSIVEMENT MAITRE, PAS DE POINTS D'INTEGRATION
                NNINT=0
              ENDIF
              NBMZ = NBMZ+1
              ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+1) = IMA
              ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+2) = IZONE
              ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+3) = NNINT
              ZI(JMAESC+ZMESX*(NTMAE+NBMZ-1)+4) = STATUT
              NTPC = NTPC + NNINT*NFACE
            ENDIF
          ENDIF
 210    CONTINUE
        NTMAE = NTMAE + NBMZ
 200  CONTINUE      

      ZI(JMAESC) = NTMAE
C
C --- CREATION DE TABLEAU TABFIN 
C
      CALL WKVECT(TABFIN,'G V R',ZTABF*NTPC+1,JTABF)
C
C --- MENAGE
C
      CALL DETRSD('CHAM_ELEM_S',CHS1)
      CALL DETRSD('CHAM_ELEM_S',CHS2)
      CALL DETRSD('CHAM_ELEM_S',CHS3)
C
      CALL JEDEMA()
      END
