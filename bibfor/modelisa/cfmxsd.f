      SUBROUTINE CFMXSD(NOMA  ,NUMEDD,DEFICO,RESOCO,LIGRCF,
     &                  LIGRXF)
C      
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 22/12/2009   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C 
      IMPLICIT NONE
      CHARACTER*24 DEFICO,RESOCO
      CHARACTER*8  NOMA
      CHARACTER*24 NUMEDD
      CHARACTER*19 LIGRCF,LIGRXF      
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (TOUTES METHODES)
C
C CREATION DES SDS DE RESOLUTION DU CONTACT (RESOCO)
C      
C ----------------------------------------------------------------------
C
C
C IN  NUMEDD : NUME_DDL DE LA MATRICE TANGENTE GLOBALE
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  LIGRCF : NOM DU LIGREL TARDIF POUR ELEMENTS DE CONTACT CONTINUE
C IN  LIGRXF : NOM DU LIGREL TARDIF POUR ELEMENTS DE CONTACT XFEM GG
C OUT RESOCO : SD POUR LA RESOLUTION DE CONTACT
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32  JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      CFMMVD,ZDIAG,ZBOUC
      INTEGER      IFM,NIV
      LOGICAL      CFDISL,LCTCD,LCTCC,LXFCM,LMAIL
      INTEGER      IBID,POSMAI,IMA,NBELNO,LONGC,LONGT
      INTEGER      CFDISI,NNOCO,NTELNO,NMACO
      CHARACTER*24 DIAGI,DIAGT,MBOUCL
      INTEGER      JDIAGI,JDIAGT,JMBOUC
      CHARACTER*24 TGNOEU,TGELNO
      INTEGER      JTGNOE
      CHARACTER*24 NOSDCO
      INTEGER      JNOSDC
      CHARACTER*8  K8BID 
      CHARACTER*14 NUMEDF
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)    
C
C --- FONCTIONNALITES ACTIVEES
C
      LMAIL  = CFDISL(DEFICO,'FORMUL_MAILLEE')
      LXFCM  = CFDISL(DEFICO,'FORMUL_XFEM')
      LCTCC  = CFDISL(DEFICO,'FORMUL_CONTINUE')
      LCTCD  = CFDISL(DEFICO,'FORMUL_DISCRET')
C --- NUME_DDL MATRICE FROTTEMENT      
      NUMEDF = '&&OP0070.NUMDF'
C
C --- INITIALISATIONS
C
      ZDIAG  = CFMMVD('ZDIAG')
      ZBOUC  = CFMMVD('ZBOUC')  
      NNOCO  = CFDISI(DEFICO,'NNOCO') 
      NTELNO = CFDISI(DEFICO,'NTELNO') 
      NMACO  = CFDISI(DEFICO,'NMACO')
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> CREATION SD DE RESOLUTION' 
      ENDIF 
C
C --- VECTEURS DE DIAGNOSTIC
C
      DIAGI  = RESOCO(1:14)//'.DIAG.ITER'
      CALL WKVECT(DIAGI,'V V I',ZDIAG,JDIAGI)
      DIAGT  = RESOCO(1:14)//'.DIAG.TIME'
      CALL WKVECT(DIAGT,'V V R',ZDIAG,JDIAGT)
C
C --- CREATION DES COMPTEURS DE BOUCLE
C
      MBOUCL = RESOCO(1:14)//'.MBOUCL'
      CALL WKVECT(MBOUCL,'V V I'  ,ZBOUC,JMBOUC)
C
C --- STOCKAGE NOM DES SD DE DONNES TYPE LIGREL ET CARTE
C
      NOSDCO = RESOCO(1:14)//'.NOSDCO'
      CALL WKVECT(NOSDCO,'V V K24',5,JNOSDC)
      ZK24(JNOSDC+1-1) = NUMEDF
      ZK24(JNOSDC+2-1) = LIGRCF
      ZK24(JNOSDC+3-1) = LIGRXF
C
C --- CREATION DES OBJETS POUR STCCKAGE DES TANGENTES
C
      IF (LMAIL) THEN
        TGNOEU = RESOCO(1:14)//'.TGNOEU'  
        CALL WKVECT(TGNOEU,'V V R',6*NNOCO,JTGNOE)
        
        TGELNO = RESOCO(1:14)//'.TGELNO'
        CALL JECREC(TGELNO,'V V R','NU','CONTIG','VARIABLE',NMACO)

        LONGT  = 0
        CALL JEECRA(TGELNO,'LONT',6*NTELNO,K8BID)
        DO 20 IMA=1,NMACO
          POSMAI = IMA
          CALL CFNBEN(DEFICO,POSMAI,'MAIL',NBELNO,IBID  )
          LONGC  = 6*NBELNO
          CALL JEECRA(JEXNUM(TGELNO,IMA),'LONMAX',LONGC ,K8BID)
          CALL JECROC(JEXNUM(TGELNO,IMA))

          LONGT = LONGT + LONGC
 20     CONTINUE
        CALL ASSERT(LONGT.EQ.6*NTELNO)
      ENDIF     
C      
C --- CONTACT DISCRET
C    
      IF (LCTCD) THEN
        CALL CFCRSD(NOMA  ,NUMEDD,DEFICO,RESOCO)
      ENDIF
C
C --- CONTACT CONTINU
C 
      IF (LCTCC) THEN
        CALL CFMXME(NOMA  ,NUMEDD,DEFICO,RESOCO)
      ENDIF
C
C --- CONTACT XFEM
C 
      IF (LXFCM) THEN
        CALL XXMXME(DEFICO,RESOCO)
      ENDIF
C      
      CALL JEDEMA()     
C
      END
