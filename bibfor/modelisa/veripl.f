      SUBROUTINE VERIPL(MA,NBMA,LINUMA,ANG,TYPERR)
      IMPLICIT NONE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 31/01/2005   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      CHARACTER*(*) MA
      INTEGER NBMA,JCOOR,JCONX,IMA,K,IPREM,NUMAI1,NUMAIL,LINUMA(NBMA)
      REAL*8       N1(3),N(3),AB(3),AC(3),NN,N1N,N1N1
      REAL*8       R8DGRD,COS2B,COS2A,A(3),B(3),C(3),ANG
      CHARACTER*1  KBID,TYPERR
      CHARACTER*8  MA2,NOMAIL,NOMAI1
C -------------------------------------------------------
C BUT : VERIFIER LA PLANEITE D'UNE LISTE DE MAILLES
C -------------------------------------------------------
C  MA    IN   K8 : NOM DU MAILLAGE
C  NBMA  IN   I  : NOMBRE DE MAILLES DANS LINUMA
C  LINUMA IN  V(I): LISTE DES NUMEROS DES MAILLES DONT ON VEUT
C                    VERIFIER LA PLANEITE.
C  ANG   IN   R  : ANGLE (EN DEGRE) QUE NE DOIT PAS DEPASSER
C                  LA NORMALE DES FACETTES 2,3,... AVEC LA FACETTE 1
C  TYPERR IN  K1 : /'A' -> <A>LARME SI NECESSAIRE
C                  /'F' -> ERREUR <F>ATALE SI NECESSAIRE

C REMARQUES :
C   ON CALCULE TOUS LES ANGLES QUE FONT LES MAILLES 2,3,...,N
C   AVEC LA 1ERE. L'ANGLE MAX ENTRE 2 MAILLES QUELCONQUES
C   EST DONC INFERIEUR A 2*ANG

C   LA NORMALE A UNE FACETTE EST CALCULEE AVEC SES 3 1ER SOMMETS

C   LES MAILLES DE LINUMA DOIVENT ETRE "SURFACIQUES" 3D
C   (LE PROGRAMME NE VERIFIE PAS CETTE CONDITION)
C -------------------------------------------------------
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ------
      CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ------


      CALL JEMARQ()
      MA2 = MA

      CALL JEVEUO(MA2//'.COORDO    .VALE','L',JCOOR)

      COS2B = COS(ANG*R8DGRD())**2

      IPREM = 0
      DO 20,IMA = 1,NBMA
        NUMAIL = LINUMA(IMA)
        CALL JEVEUO(JEXNUM(MA2//'.CONNEX',NUMAIL),'L',JCONX)

C       -- VERIFIER NOMAIL : TRIA OU QUAD ???

C       CALCUL DE LA NORMALE (N) A LA FACETTE IMA :
        DO 10,K = 1,3
          A(K) = ZR(JCOOR-1+3* (ZI(JCONX-1+1)-1)+K)
          B(K) = ZR(JCOOR-1+3* (ZI(JCONX-1+2)-1)+K)
          C(K) = ZR(JCOOR-1+3* (ZI(JCONX-1+3)-1)+K)
          AB(K) = B(K) - A(K)
          AC(K) = C(K) - A(K)
   10   CONTINUE
        N(1) = AB(2)*AC(3) - AB(3)*AC(2)
        N(2) = AB(3)*AC(1) - AB(1)*AC(3)
        N(3) = AB(1)*AC(2) - AB(2)*AC(1)


        IPREM = IPREM + 1
        IF (IPREM.EQ.1) THEN
          N1(1) = N(1)
          N1(2) = N(2)
          N1(3) = N(3)
          NUMAI1 = NUMAIL
          GO TO 20
        END IF

        NN = N(1)*N(1) + N(2)*N(2) + N(3)*N(3)
        N1N = N1(1)*N(1) + N1(2)*N(2) + N1(3)*N(3)
        N1N1 = N1(1)*N1(1) + N1(2)*N1(2) + N1(3)*N1(3)

        COS2A = (N1N*N1N)/ (NN*N1N1)

        IF (COS2A.LT.COS2B) THEN
          CALL JENUNO(JEXNUM(MA2//'.NOMMAI',NUMAIL),NOMAIL)
          CALL JENUNO(JEXNUM(MA2//'.NOMMAI',NUMAI1),NOMAI1)
          CALL UTMESS(TYPERR,'RACCORD 3D/POUTRE:',
     +                'DEFAUT DE PLANEITE.'//
     +                'L ANGLE ENTRE LES NORMALES AUX MAILLES:'//
     +                NOMAI1//' ET '//NOMAIL//
     +                ' EST SUPERIEUR A ANGL_MAX.')
        END IF

   20 CONTINUE
   30 CONTINUE

      CALL JEDEMA()

      END
