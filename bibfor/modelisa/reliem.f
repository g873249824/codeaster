      SUBROUTINE RELIEM(MO,MA,TYPEM,MOTFAZ,IOCC,NBMOCL,LIMOCL,TYMOCL,
     &                  LITROZ,NBTROU)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXNOM
      INTEGER IOCC,NBMOCL,NBTROU
      CHARACTER*8 MA,MODELE
      CHARACTER*(*) LIMOCL(NBMOCL),TYMOCL(NBMOCL),MO
      CHARACTER*(*) LITROZ,TYPEM,MOTFAZ
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 09/04/2013   AUTEUR PELLET J.PELLET 
C RESPONSABLE PELLET J.PELLET
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C     CE MODULE PERMET DE CREER UN OBJET JEVEUX CONTENANT UNE LISTE
C     DE NOMS OU NUMEROS DE MAILLES OU DE NOEUDS CORRESPONDANT AUX
C     MOTS-CLES TRANSMIS EN ARGUMENTS.

C IN  : MO     : NOM DU MODELE (FACULTATIF SINON : ' ')
C           SI LE NOM DU MODELE EST DONNE, ON VERIFIERA QUE LES MAILLES
C           (OU LES NOEUDS) RECUPERES FONT PARTIE DU MODELE.
C           S'ILS NE FONT PAS PARTIE DU MODELE => ALARME
C IN  : MA     : NOM DU MAILLAGE
C IN  : TYPEM  : PRECISE LE TYPE DE LISTE QUE L'ON VEUT RECUPERER
C              : 'NU_MAILLE'  : NUMEROS DE MAILLES
C              : 'NO_MAILLE'  : NOMS    DE MAILLES
C              : 'NU_NOEUD'   : NUMEROS DE NOEUDS
C              : 'NO_NOEUD'   : NOMS    DE NOEUDS
C IN  : MOTFAZ : NOM DU MOT CLE FACTEUR (OU ' ')
C IN  : IOCC   : NUMERO DE L'OCCURENCE DU MOT CLE FACTEUR
C IN  : NBMOCL : NOMBRE DE MOTS CLES A SCRUTER
C                (DIMENSION DE LIMOCL)
C IN  : LIMOCL : LISTE DES MOTS CLE A SCRUTER
C IN  : TYMOCL : LISTE DES TYPES DE MOTS CLE A SCRUTER :
C                / 'GROUP_MA'
C                / 'GROUP_NO'
C                / 'MAILLE'
C                / 'NOEUD'
C                / 'TOUT'   % TOUT:'OUI'
C IN/JXOUT : LITROZ : NOM DE L'OBJET JEVEUX QUI CONTIENDRA LA LISTE DES
C                     ENTITES (MAILLE OU NOEUD) TROUVEES
C OUT : NBTROU : NOMBRE D'ENTITES TROUVEES
C ----------------------------------------------------------------------
      CHARACTER*24 LITROU
      INTEGER JNO,JMA,KNO,KMA,IACNEX,IEM,NEM,NUMNO,NNO,NMA,NBENC
      INTEGER IBID,IENT,JPRNM
      INTEGER ITRNO,ITRMA,IMA,INO,NBMA,NBNO,NBNOMA,IMO,IER,JMODEL
      INTEGER LMA,LNO,ITBMA,ITBNO,IRET,INOEM,NTOU,K,IFM,NIV
      REAL*8  R8BID
      CHARACTER*8 K8B,TYPE2,OUI,NOENT,NOMGD
      CHARACTER*16 MOTFAC,MOTCLE,TYPMCL,PHENOM
      CHARACTER*19 LIGREL
      CHARACTER*24 KARG
      INTEGER      IARG
C     ------------------------------------------------------------------

      CALL JEMARQ()
      LITROU = LITROZ
      MOTFAC = MOTFAZ
      MODELE = MO
      CALL INFNIV(IFM,NIV)

C     --- VERIFICATIONS PRELIMINAIRES ---

      IF (TYPEM.NE.'NO_MAILLE' .AND. TYPEM.NE.'NO_NOEUD' .AND.
     &    TYPEM.NE.'NU_MAILLE' .AND. TYPEM.NE.'NU_NOEUD') THEN
        CALL ASSERT(.FALSE.)
      END IF

      TYPE2 = TYPEM(4:)
      DO 10 IMO = 1,NBMOCL
        MOTCLE = LIMOCL(IMO)
        TYPMCL = TYMOCL(IMO)
        IF (TYPMCL.EQ.'NOEUD' .OR. TYPMCL.EQ.'GROUP_NO') THEN
          IF (TYPE2.EQ.'MAILLE') THEN
            CALL ASSERT(.FALSE.)
          END IF
        ELSE IF (TYPMCL.NE.'MAILLE' .AND. TYPMCL.NE.'GROUP_MA' .AND.
     &           TYPMCL.NE.'TOUT') THEN
          CALL ASSERT(.FALSE.)
        END IF
   10 CONTINUE

C     --- EN CAS D'EXISTENCE DE L'OBJET, ON LE DETRUIT ---

      CALL JEEXIN(LITROU,IRET)
      IF (IRET.NE.0) CALL JEDETR(LITROU)

C     --- CREATION DES TABLEAUX DE TRAVAIL ---

      CALL DISMOI('F','NB_MA_MAILLA',MA,'MAILLAGE',NBMA,K8B,IRET)
      IF (NBMA.GT.0) THEN
        CALL WKVECT('&&RELIEM.INDIC_MAILLE','V V S',MAX(NBMA,1),ITRMA)
        IF (MODELE.NE.' ') CALL JEVEUO(MODELE//'.MAILLE','L',JMODEL)
      END IF
      CALL DISMOI('F','NB_NO_MAILLA',MA,'MAILLAGE',NBNO,K8B,IRET)
      CALL WKVECT('&&RELIEM.INDIC_NOEUD','V V S',NBNO,ITRNO)

      DO 20 K = 1,NBMA
        ZI4(ITRMA-1+K) = 0
   20 CONTINUE
      DO 30 K = 1,NBNO
        ZI4(ITRNO-1+K) = 0
   30 CONTINUE



C     --- CONSTITUTION DES LISTES DES MAILLES ET DES NOEUDS
C         PAR MARQUAGE DANS LES TABLEAUX DE TRAVAIL         ---


      DO 90 IMO = 1,NBMOCL
        MOTCLE = LIMOCL(IMO)
        TYPMCL = TYMOCL(IMO)

C        -- CAS TOUT:'OUI'
C        -----------------
        IF (TYPMCL.EQ.'TOUT') THEN
          CALL GETVTX(MOTFAC,MOTCLE,IOCC,IARG,1,OUI,NTOU)
          IF (NTOU.GT.0) THEN
            IF (TYPE2.EQ.'MAILLE') THEN
              DO 40,K = 1,NBMA
                IF (MODELE.NE.' ') THEN
                  IF (ZI(JMODEL-1+K).NE.0) THEN
                    ZI4(ITRMA-1+K) = 1
                  END IF
                ELSE
                  ZI4(ITRMA-1+K) = 1
                END IF
   40         CONTINUE
            END IF
            IF (TYPE2.EQ.'NOEUD') THEN
              DO 50,K = 1,NBNO
                ZI4(ITRNO-1+K) = 1
   50         CONTINUE
            END IF
          END IF
          GO TO 90
        END IF


        CALL GETVEM(MA,TYPMCL,MOTFAC,MOTCLE,IOCC,IARG,0,KARG,NEM)
        NEM = -NEM
        IF (NEM.EQ.0) GO TO 90
        IF ( TYPMCL(1:6).NE.'GROUP_' ) THEN
          CALL WKVECT('&&RELIEM.NOM_EM','V V K8',NEM,INOEM)
          CALL GETVEM(MA,TYPMCL,MOTFAC,MOTCLE,IOCC,IARG,NEM,
     &                ZK8(INOEM),NEM)
        ELSE
          CALL WKVECT('&&RELIEM.NOM_EM','V V K24',NEM,INOEM)
          CALL GETVEM(MA,TYPMCL,MOTFAC,MOTCLE,IOCC,IARG,NEM,
     &                ZK24(INOEM),NEM)
        ENDIF

        DO 80 IEM = 1,NEM
          IF ( TYPMCL(1:6).NE.'GROUP_' ) THEN
            KARG = ZK8(INOEM-1+IEM)
          ELSE
            KARG = ZK24(INOEM-1+IEM)
          ENDIF

          IF (TYPMCL.EQ.'MAILLE') THEN
            CALL JENONU(JEXNOM(MA//'.NOMMAI',KARG),IMA)
            ZI4(ITRMA-1+IMA) = 1

          ELSE IF (TYPMCL.EQ.'GROUP_MA') THEN
            CALL JELIRA(JEXNOM(MA//'.GROUPEMA',KARG),'LONUTI',NMA,K8B)
            CALL JEVEUO(JEXNOM(MA//'.GROUPEMA',KARG),'L',KMA)

C           -- UNE VERIFICATION PENDANT LE CHANTIER "GROUPES VIDES" :
            CALL JELIRA(JEXNOM(MA//'.GROUPEMA',KARG),'LONMAX',IBID,K8B)
            IF (IBID.EQ.1) THEN
              CALL ASSERT(NMA.LE.1)
            ELSE
              CALL ASSERT(NMA.EQ.IBID)
            ENDIF

            DO 60 JMA = 1,NMA
              IMA = ZI(KMA-1+JMA)
              ZI4(ITRMA-1+IMA) = 1
   60       CONTINUE

          ELSE IF (TYPMCL.EQ.'NOEUD') THEN
            CALL JENONU(JEXNOM(MA//'.NOMNOE',KARG),INO)
            ZI4(ITRNO-1+INO) = 1

          ELSE IF (TYPMCL.EQ.'GROUP_NO') THEN
            CALL JELIRA(JEXNOM(MA//'.GROUPENO',KARG),'LONUTI',NNO,K8B)
            CALL JEVEUO(JEXNOM(MA//'.GROUPENO',KARG),'L',KNO)

C           -- UNE VERIFICATION PENDANT LE CHANTIER "GROUPES VIDES" :
            CALL JELIRA(JEXNOM(MA//'.GROUPENO',KARG),'LONMAX',IBID,K8B)
            IF (IBID.EQ.1) THEN
              CALL ASSERT(NNO.LE.1)
            ELSE
              CALL ASSERT(NNO.EQ.IBID)
            ENDIF

            DO 70 JNO = 1,NNO
              INO = ZI(KNO-1+JNO)
              ZI4(ITRNO-1+INO) = 1
   70       CONTINUE
          END IF
   80   CONTINUE
        CALL JEDETR('&&RELIEM.NOM_EM')
   90 CONTINUE

C     --- AJOUT DES NOEUDS DE LA LISTE DES MAILLES A CELLE DES NOEUDS

      IF (TYPE2.EQ.'NOEUD') THEN
        DO 110 IMA = 1,NBMA
          IF (ZI4(ITRMA-1+IMA).NE.0) THEN
            CALL JEVEUO(JEXNUM(MA//'.CONNEX',IMA),'L',IACNEX)
            CALL JELIRA(JEXNUM(MA//'.CONNEX',IMA),'LONMAX',NBNOMA,K8B)
            DO 100 INO = 1,NBNOMA
              NUMNO = ZI(IACNEX-1+INO)
              ZI4(ITRNO-1+NUMNO) = 1
  100       CONTINUE
          END IF
  110   CONTINUE
      END IF


C     --- CREATION DE L'OBJET JEVEUX LITROU ---
      IF (TYPE2.EQ.'MAILLE') THEN

C        --- COMPTAGE DES MAILLES ---

        NBTROU = 0
        DO 120 IMA = 1,NBMA
          IF (ZI4(ITRMA-1+IMA).NE.0) NBTROU = NBTROU + 1
  120   CONTINUE
        IF (NBTROU.EQ.0) GO TO 200



        IF (TYPEM(1:2).EQ.'NU') THEN
          CALL WKVECT(LITROU,'V V I',NBTROU,ITBMA)


C           --- RANGEMENT DES NUMEROS DE MAILLES ---
          LMA = 0
          DO 130 IMA = 1,NBMA
            IF (ZI4(ITRMA-1+IMA).NE.0) THEN
              LMA = LMA + 1
              ZI(ITBMA-1+LMA) = IMA
            END IF
  130     CONTINUE

        ELSE
          CALL WKVECT(LITROU,'V V K8',NBTROU,ITBMA)


C           --- RANGEMENT DES NOMS DE MAILLES ---
          LMA = 0
          DO 140 IMA = 1,NBMA
            IF (ZI4(ITRMA-1+IMA).NE.0) THEN
              LMA = LMA + 1
              CALL JENUNO(JEXNUM(MA//'.NOMMAI',IMA),ZK8(ITBMA-1+LMA))
            END IF
  140     CONTINUE
        END IF



C       -- ON VERIFIE QUE LES MAILLES FONT PARTIE DU MODELE :
C       ----------------------------------------------------
        IF (MODELE.NE.' ') THEN
          IER = 0
          DO 150 IMA = 1,NBMA
            IF (ZI4(ITRMA-1+IMA).NE.0) THEN
              IF (ZI(JMODEL-1+IMA).EQ.0) THEN
                IER = IER + 1
                CALL JENUNO(JEXNUM(MA//'.NOMMAI',IMA),NOENT)
                WRITE (IFM,*) ' MAILLE : ',NOENT
              END IF
            END IF
  150     CONTINUE
          IF (IER.NE.0) CALL U2MESG('F','MODELISA6_96',1,MOTFAC,1,IER,
     &                                                          0,R8BID)
        END IF



      ELSE

C        --- COMPTAGE DES NOEUDS ---

        NBTROU = 0
        DO 160 INO = 1,NBNO
          IF (ZI4(ITRNO-1+INO).NE.0) NBTROU = NBTROU + 1
  160   CONTINUE
        IF (NBTROU.EQ.0) GO TO 200



        IF (TYPEM(1:2).EQ.'NU') THEN
          CALL WKVECT(LITROU,'V V I',NBTROU,ITBNO)


C           --- RANGEMENT DES NUMEROS DE NOEUDS ---
          LNO = 0
          DO 170 INO = 1,NBNO
            IF (ZI4(ITRNO-1+INO).NE.0) THEN
              LNO = LNO + 1
              ZI(ITBNO-1+LNO) = INO
            END IF
  170     CONTINUE

        ELSE
          CALL WKVECT(LITROU,'V V K8',NBTROU,ITBNO)


C           --- RANGEMENT DES NOMS DE NOEUDS ---
          LNO = 0
          DO 180 INO = 1,NBNO
            IF (ZI4(ITRNO-1+INO).NE.0) THEN
              LNO = LNO + 1
              CALL JENUNO(JEXNUM(MA//'.NOMNOE',INO),ZK8(ITBNO-1+LNO))
            END IF
  180     CONTINUE
        END IF

C       -- ON VERIFIE QUE LES NOEUDS FONT PARTIE DU MODELE :
C       ----------------------------------------------------
        IF (MODELE.NE.' ') THEN
          CALL DISMOI('F','PHENOMENE',MODELE,'MODELE',IBID,PHENOM,IRET)
          CALL DISMOI('F','NOM_GD',PHENOM,'PHENOMENE',IBID,NOMGD ,IRET)
          CALL DISMOI('F','NB_EC',NOMGD,'GRANDEUR',NBENC,K8B,IRET)
          CALL DISMOI('F','NOM_LIGREL',MODELE,'MODELE',IBID,LIGREL,IRET)
          CALL JEVEUO(LIGREL//'.PRNM','L',JPRNM)
          IER = 0
          DO 191 INO = 1,NBNO
            IF (ZI4(ITRNO-1+INO).NE.0) THEN
              DO 190 IENT  = 1,NBENC
                IF (ZI(JPRNM-1+NBENC*(INO-1)+IENT).NE.0) GOTO 191
  190         CONTINUE
C             LE NOEUD NE PORTE AUCUNE COMPOSANTE DE LA GRANDEUR
C             ASSOCIEE AU PHENOMENE
              IER = IER + 1
              CALL JENUNO(JEXNUM(MA//'.NOMNOE',INO),NOENT)
              WRITE (IFM,*) ' NOEUD : ',NOENT
            END IF
  191     CONTINUE
          IF (IER.NE.0) CALL U2MESG('F','MODELISA6_13',1,MOTFAC,1,IER,
     &                                                          0,R8BID)
        END IF

      END IF


C     --- DESTRUCTION DES TABLEAUX DE TRAVAIL ---
  200 CONTINUE
      CALL JEDETR('&&RELIEM.INDIC_MAILLE')
      CALL JEDETR('&&RELIEM.INDIC_NOEUD')

      CALL JEDEMA()
      END
