      SUBROUTINE DISCFF(NBFONC,NOMFON,NBP1,NBP2,DISC,VALE)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 05/11/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C     DISCRETISATION DES FONCTIONS DE FORME
C     APPELANT : SPECFF
C-----------------------------------------------------------------------
C IN  : NBFONC : NOMBRE DE TABLE_FONCTIONS
C IN  : NOMFON : LISTE DES NOMS DES CONCEPTS DE TYPE TABLE_FONCTION
C IN  : NBP1   : NOMBRE DE POINTS DE DISCRETISATION DES FONCTIONS DE
C                FORME SUR LA PREMIERE DIRECTION D'ESPACE DU REPERE
C IN  : NBP2   : NOMBRE DE POINTS DE DISCRETISATION DES FONCTIONS DE
C                FORME SUR LA DEUXIEME DIRECTION D'ESPACE DU REPERE
C OUT : DISC   : VALEURS DU PARAMETRE SUR 0,2L   - DIM : NBP1+NBP2
C OUT : VALE   : TABLEAU DES VALEURS DES FONCTIONS SUR 0,2L
C                - DIM : (NBP1+NBP2,NBFONC)
C
C REMARQUE : 
C ----------
C LES DEUX FONCTIONS DE FORME SONT CONCATENEES ET LES VALEURS DES 
C DISCRETISATIONS PASSENT DE (0,L) POUR LA PREMIERE FONCTION ET (0,L)
C POUR LA DEUXIEME A UNE SEULE FONCTION DEFINIE SUR (0,2L): (0,L),(L,2L)
C
      INCLUDE 'jeveux.h'
      INTEGER      NBFONC,NBP1,NBP2
      CHARACTER*8  NOMFON(NBFONC)
      CHARACTER*19 TBFONC, FONC1, FONC2
      REAL*8       DISC(NBP1+NBP2),VALE(NBP1+NBP2,NBFONC)
C
      CHARACTER*24 FVALE1,FPROL1,FVALE2,FPROL2,LSTFON,TBLP
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
      INTEGER IER1,IER2,IFO,IP, ITBLP, ILFON 
      INTEGER IFABS1,IFORD1,IFPRO1, IFABS2,IFORD2,IFPRO2
      REAL*8  LONG
C-----------------------------------------------------------------------
      CALL JEMARQ()
C
C
C-----1.VALEURS DU PARAMETRE : ON RETIENT LA DISCRETISATION DE LA
C-----  PREMIERE FONCTION DE FORME
C
      TBFONC = NOMFON(1)
      TBLP = TBFONC//'.TBLP'
C
      CALL JEVEUO(TBLP,'L',ITBLP)
      LSTFON = ZK24(ITBLP+2)
      CALL JEVEUO(LSTFON,'L',ILFON)
      FONC1 = ZK8(ILFON)  //'           '
      FONC2 = ZK8(ILFON+1)//'           '
C
      CALL JELIBE(LSTFON)
      CALL JELIBE(TBLP)
C
      FVALE1 = FONC1//'.VALE'
      FVALE2 = FONC2//'.VALE'

      CALL JEVEUO(FVALE1,'L',IFABS1)
      CALL JEVEUO(FVALE2,'L',IFABS2)
      DO 10 IP = 1,NBP1
        DISC(IP) = ZR(IFABS1+IP-1)
  10  CONTINUE
      LONG = DISC(NBP1)
      DO 15 IP = 1,NBP2
        DISC(NBP1+IP) = ZR(IFABS2+IP-1)
  15  CONTINUE
C
C-----2.VALEURS DES FONCTIONS
C
C-----2.1.ON RECOPIE LES VALEURS DE LA PREMIERE FONCTION
C
      IFORD1 = IFABS1 + NBP1
      IFORD2 = IFABS2 + NBP2
      DO 20 IP = 1,NBP1
        VALE(IP,1) = ZR(IFORD1+IP-1)
  20  CONTINUE
      DO 25 IP = 1,NBP2
        VALE(NBP1+IP,1) = ZR(IFORD2+IP-1)
  25  CONTINUE
C
C-----2.2.ON INTERPOLE LES AUTRES FONCTIONS LE CAS ECHEANT
C
      IF (NBFONC.GT.1) THEN
C
        DO 30 IFO = 2,NBFONC
C
          TBFONC = NOMFON(IFO)
          TBLP = TBFONC//'.TBLP'
C    
          CALL JEVEUO(TBLP,'L',ITBLP)
          LSTFON = ZK24(ITBLP+2)
          CALL JEVEUO(LSTFON,'L',ILFON)
          FONC1 = ZK8(ILFON)  //'           '
          FONC2 = ZK8(ILFON+1)//'           '
C    
          CALL JELIBE(LSTFON)
          CALL JELIBE(TBLP)
          CALL JELIBE(FVALE1)
          CALL JELIBE(FVALE2)
C    
          FVALE1 = FONC1//'.VALE'
          FPROL1 = FONC1//'.PROL'
C
          FVALE2 = FONC2//'.VALE'
          FPROL2 = FONC2//'.PROL'
C
          CALL JEVEUO(FVALE1,'L',IFABS1)
          CALL JEVEUO(FPROL1,'L',IFPRO1)
C
          CALL JEVEUO(FVALE2,'L',IFABS2)
          CALL JEVEUO(FPROL2,'L',IFPRO2)
C
          IFORD1 = IFABS1 + NBP1
          IFORD2 = IFABS2 + NBP2
C
          CALL FOINTR(FONC1,ZK24(IFPRO1),NBP1,ZR(IFABS1),ZR(IFORD1),
     &                NBP1,DISC(1),     VALE(1,IFO),     IER1)
          CALL FOINTR(FONC2,ZK24(IFPRO2),NBP2,ZR(IFABS2),ZR(IFORD2),
     &                NBP2,DISC(NBP1+1),VALE(NBP1+1,IFO),IER2)
C
C
          IF (IER1.NE.0 .OR. IER2.NE.0) THEN
            CALL U2MESS('F','MODELISA4_40')
          ENDIF
C
          CALL JELIBE(FPROL1)
          CALL JELIBE(FPROL2)
C
  30    CONTINUE
C
C     --- CONCATENATION DES DISCRETISATIONS DES FONCTIONS DE FORME 
C       - SUR (0,2L)
        LONG = DISC(NBP1)
        DO 26 IP = 1,NBP2
          DISC(NBP1+IP) = ZR(IFABS2+IP-1)+LONG
  26    CONTINUE
C
      ENDIF
C
      CALL JELIBE(FVALE1)
      CALL JELIBE(FVALE2)
C
      CALL JEDEMA()
      END
