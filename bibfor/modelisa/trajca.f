      SUBROUTINE TRAJCA(TABLCA,MAILLA,ICABL,NBNOCA,XNOCA,YNOCA,ZNOCA,
     &                                                 COMIMA,GROMAI)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 30/04/2013   AUTEUR CHEIGNON E.CHEIGNON 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C  DESCRIPTION : INTERPOLATION DE LA TRAJECTOIRE D'UN CABLE
C  -----------   APPELANT : OP0180 , OPERATEUR DEFI_CABLE_BP
C
C                EN SORTIE ON COMPLETE LA TABLE RESULTAT
C                LES LIGNES COMPLETEES CORRESPONDENT AU DERNIER CABLE
C                LES CASES RENSEIGNEES CORRESPONDENT AUX PARAMETRES
C                <ABSC_CURV> ET <ALPHA>
C
C  IN     : TABLCA : CHARACTER*19
C                    NOM DE LA TABLE DECRIVANT LES CABLES
C  IN     : MAILLA : CHARACTER*8 , SCALAIRE
C                    NOM DU CONCEPT MAILLAGE ASSOCIE A L'ETUDE
C  IN     : ICABL  : INTEGER , SCALAIRE
C                    NUMERO DU CABLE
C  IN     : NBNOCA : INTEGER , VECTEUR DE DIMENSION NBCABL
C                    CONTIENT LES NOMBRES DE NOEUDS DE CHAQUE CABLE
C  IN     : XNOCA  : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR DE REELS POUR STOCKAGE DES
C                    ABSCISSES X DES NOEUDS APPARTENANT AUX CABLES
C  IN     : YNOCA  : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR DE REELS POUR STOCKAGE DES
C                    ORDONNEES Y DES NOEUDS APPARTENANT AUX CABLES
C  IN     : ZNOCA  : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR DE REELS POUR STOCKAGE DES
C                    COTES Z DES NOEUDS APPARTENANT AUX CABLES
C  IN     : GROMAI   CHARACTER*24
C                    NOM DU VECTEUR CONTENANT LES PLUS GRANDS
C                    DIAMETRES DES MAILLES DE BETON SELON LES
C                    DIRECTIONS X, Y ET Z
C  OUT    : COMIMA : CHARACTER*24
C                    NOM DU VECTEUR CONTENANT LES 6 COORDONNEES
C                    EXTREME QUI CONSTITUENT LE PAVE DANS LEQUEL
C                    EST CONTENU LE CALBLE
C
C  N.B. LES VECTEURS XNOCA, YNOCA ET ZNOCA SONT COMPLETES A CHAQUE
C       PASSAGE DANS LA ROUTINE TRAJCA : REAJUSTEMENT DE LEUR DIMENSION
C       PUIS REMPLISSAGE DES DERNIERS SOUS-BLOCS ALLOUES
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
C
C ARGUMENTS
C ---------
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNOM
      CHARACTER*8   MAILLA
      CHARACTER*19  XNOCA, YNOCA, ZNOCA, TABLCA
      CHARACTER*24  COMIMA,GROMAI
      INTEGER       ICABL, NBNOCA(*)
C
C VARIABLES LOCALES
C -----------------
      INTEGER       IBID, IDECAL, INO, IPARA, IRET, ISUB, JABSC, JALPH,
     &              JCOOR, JCORD, JNOCA, JTBLP, JTBNP, JD2X, JD2Y, JD2Z,
     &              JX, JY, JZ, NBLIGN, NBNO, NBPARA, NUMNOE,ICMIMA,
     &              JALPHD,NBVAR,NBVAR2,SVAR,VALI(3)
      REAL*8        ABSC, ALPHA, CORDE,ALPHCU,D1M,D1P,EPS,DCP,
     &              D1X, D1X1, D1XN, D1Y, D1Y1, D1YN, D1Z, D1Z1, D1ZN,
     &              D2X, D2Y, D2Z, DC, DC1, DCN, DET1, DET2, DET3, DU,
     &              DX, DY, DZ, NORMV2, VALPAR(2),DX1, DY1, DZ1,PSC,DU1
      REAL*8        XMIN,XMAX,YMIN,YMAX,ZMIN,ZMAX,R8MAEM,RR,TRIGOM
      COMPLEX*16    CBID
      CHARACTER*3   K3B
      CHARACTER*24  COORNO, NONOCA, NONOMA
      LOGICAL       LSPLIN
C
      INTEGER       NBSUB,JGMAI
      PARAMETER    (NBSUB=5)
      CHARACTER*24  PARAM(2), PARCR
      DATA          PARAM /'ABSC_CURV               ',
     &                     'ALPHA                   '/
      DATA          PARCR /'NOEUD_CABLE             '/
C
C-------------------   DEBUT DU CODE EXECUTABLE    ---------------------
C
      CALL JEMARQ()
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 1   CREATION DES OBJETS DE TRAVAIL
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
      NBNO = NBNOCA(ICABL)
      CALL WKVECT('&&TRAJCA.CORDE_CUMU','V V R',NBNO,JCORD)
      CALL WKVECT('&&TRAJCA.D2X'       ,'V V R',NBNO,JD2X )
      CALL WKVECT('&&TRAJCA.D2Y'       ,'V V R',NBNO,JD2Y )
      CALL WKVECT('&&TRAJCA.D2Z'       ,'V V R',NBNO,JD2Z )
      CALL WKVECT('&&TRAJCA.ABSC_CURV' ,'V V R',NBNO,JABSC)
      CALL WKVECT('&&TRAJCA.ALPHA'     ,'V V R',NBNO,JALPH)
      CALL WKVECT('&&TRAJCA.ALPHA_DISC','V V R',NBNO,JALPHD)
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 2   RECUPERATION DES COORDONNEES DES NOEUDS DU CABLE
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
      CALL JEVEUO(TABLCA//'.TBNP','L',JTBNP)
      NBPARA = ZI(JTBNP)
      NBLIGN = ZI(JTBNP+1)
      CALL JEVEUO(TABLCA//'.TBLP','L',JTBLP)
      DO 10 IPARA = 1, NBPARA
         IF ( ZK24(JTBLP+4*(IPARA-1)).EQ.PARCR ) THEN
            NONOCA = ZK24(JTBLP+4*(IPARA-1)+2)
            CALL JEVEUO(NONOCA,'L',JNOCA)
            GO TO 20
         ENDIF
  10  CONTINUE
C
  20  CONTINUE
      IDECAL = NBLIGN - NBNO
C
      CALL JEECRA(XNOCA,'LONUTI',NBLIGN,' ')
      CALL JEVEUO(XNOCA,'E',JX)
      CALL JEECRA(YNOCA,'LONUTI',NBLIGN,' ')
      CALL JEVEUO(YNOCA,'E',JY)
      CALL JEECRA(ZNOCA,'LONUTI',NBLIGN,' ')
      CALL JEVEUO(ZNOCA,'E',JZ)
C
      CALL JEVEUO(COMIMA,'E',ICMIMA)
      XMIN=R8MAEM()
      YMIN=R8MAEM()
      ZMIN=R8MAEM()
      XMAX=-R8MAEM()
      YMAX=-R8MAEM()
      ZMAX=-R8MAEM()
      NONOMA = MAILLA//'.NOMNOE'
      COORNO = MAILLA//'.COORDO    .VALE'
      CALL JEVEUO(COORNO,'L',JCOOR)
      DO 30 INO = 1, NBNO
         CALL JENONU(JEXNOM(NONOMA,ZK8(JNOCA+IDECAL+INO-1)),NUMNOE)
         ZR(JX+IDECAL+INO-1) = ZR(JCOOR+3*(NUMNOE-1)  )
         ZR(JY+IDECAL+INO-1) = ZR(JCOOR+3*(NUMNOE-1)+1)
         ZR(JZ+IDECAL+INO-1) = ZR(JCOOR+3*(NUMNOE-1)+2)

C        RECHERCHE DES COORDONNEES EXTREMES
         IF (ZR(JX+IDECAL+INO-1).LT.XMIN) XMIN=ZR(JX+IDECAL+INO-1)
         IF (ZR(JY+IDECAL+INO-1).LT.YMIN) YMIN=ZR(JY+IDECAL+INO-1)
         IF (ZR(JZ+IDECAL+INO-1).LT.ZMIN) ZMIN=ZR(JZ+IDECAL+INO-1)
         IF (ZR(JX+IDECAL+INO-1).GT.XMAX) XMAX=ZR(JX+IDECAL+INO-1)
         IF (ZR(JY+IDECAL+INO-1).GT.YMAX) YMAX=ZR(JY+IDECAL+INO-1)
         IF (ZR(JZ+IDECAL+INO-1).GT.ZMAX) ZMAX=ZR(JZ+IDECAL+INO-1)
  30  CONTINUE
C     CONSTRUCTION DES 6 PLANS DEFINISSANT DE PAVE CONTENANT LE CABLE
      CALL JEVEUO(GROMAI,'L',JGMAI)
      RR = ZR(JGMAI)
      ZR(ICMIMA-1+1)=XMIN-RR/2.D0
      ZR(ICMIMA-1+2)=XMAX+RR/2.D0
      RR = ZR(JGMAI+1)
      ZR(ICMIMA-1+3)=YMIN-RR/2.D0
      ZR(ICMIMA-1+4)=YMAX+RR/2.D0
      RR = ZR(JGMAI+2)
      ZR(ICMIMA-1+5)=ZMIN-RR/2.D0
      ZR(ICMIMA-1+6)=ZMAX+RR/2.D0


C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 3   CALCUL DU PARAMETRE CORDE CUMULEE
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
      ZR(JCORD)  = 0.0D0
      ZR(JALPHD) = 0.0D0
      ALPHCU     = 0.0D0
C
C.... N.B. LE PASSAGE PREALABLE DANS LA ROUTINE TOPOCA GARANTIT NBNO > 2
C
      DO 40 INO = 2, NBNO
         DX = ZR(JX+IDECAL+INO-1) - ZR(JX+IDECAL+INO-2)
         DY = ZR(JY+IDECAL+INO-1) - ZR(JY+IDECAL+INO-2)
         DZ = ZR(JZ+IDECAL+INO-1) - ZR(JZ+IDECAL+INO-2)
         DU = DBLE ( SQRT ( DX * DX + DY * DY + DZ * DZ ) )
         IF ( DU.EQ.0.0D0 ) THEN
            WRITE(K3B,'(I3)') ICABL
            CALL U2MESK('F','MODELISA7_59',1,K3B)
         ENDIF
         ZR(JCORD+INO-1) = ZR(JCORD+INO-2) + DU

C        CALCUL DISCRET DES DEVIATIONS ANGULAIRES CUMULEES
C        UTILISE EN CAS D ECHEC DE L INTERPOLATION PAR SPLINE
         IF (INO .LT.NBNO)THEN
           DX1 = ZR(JX+IDECAL+INO) - ZR(JX+IDECAL+INO-1)
           DY1 = ZR(JY+IDECAL+INO) - ZR(JY+IDECAL+INO-1)
           DZ1 = ZR(JZ+IDECAL+INO) - ZR(JZ+IDECAL+INO-1)
           DU1 = DBLE ( SQRT ( DX1 * DX1 + DY1 * DY1 + DZ1 * DZ1 ) )
           PSC = DX * DX1 + DY *DY1 + DZ * DZ1
           PSC = ABS(PSC /(DU * DU1))
           ALPHA = TRIGOM('ACOS',PSC)
           ZR(JALPHD+INO-1) = ALPHCU + ALPHA/2.D0
           ALPHCU = ALPHCU + ALPHA
         ELSE
           ZR(JALPHD+INO-1) = ALPHCU
         ENDIF

  40  CONTINUE
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 4   INTERPOLATION SPLINE CUBIQUE DE LA TRAJECTOIRE DU CABLE
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
      DC1 = ZR(JCORD+1) - ZR(JCORD)
      DCN = ZR(JCORD+NBNO-1) - ZR(JCORD+NBNO-2)
C
C 4.1 INTERPOLATION DE LA COORDONNEE X
C ---
      D1X1 = ( ZR(JX+IDECAL+1) - ZR(JX+IDECAL) ) / DC1
      D1XN = ( ZR(JX+IDECAL+NBNO-1) - ZR(JX+IDECAL+NBNO-2) ) / DCN

      CALL SPLINE(ZR(JCORD),ZR(JX+IDECAL),NBNO,D1X1,D1XN,ZR(JD2X),IRET)
C
C 4.2 INTERPOLATION DE LA COORDONNEE Y
C ---
      D1Y1 = ( ZR(JY+IDECAL+1) - ZR(JY+IDECAL) ) / DC1
      D1YN = ( ZR(JY+IDECAL+NBNO-1) - ZR(JY+IDECAL+NBNO-2) ) / DCN
      CALL SPLINE(ZR(JCORD),ZR(JY+IDECAL),NBNO,D1Y1,D1YN,ZR(JD2Y),IRET)
C
C 4.3 INTERPOLATION DE LA COORDONNEE Z
C ---
      D1Z1 = ( ZR(JZ+IDECAL+1) - ZR(JZ+IDECAL) ) / DC1
      D1ZN = ( ZR(JZ+IDECAL+NBNO-1) - ZR(JZ+IDECAL+NBNO-2) ) / DCN
      CALL SPLINE(ZR(JCORD),ZR(JZ+IDECAL),NBNO,D1Z1,D1ZN,ZR(JD2Z),IRET)

C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 4-BIS  CONTROLE DE L'INTERPOLATION SPLINE CUBIQUE
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
      LSPLIN = .TRUE.
C
C 4-BIS .1 INTERPOLATION DE LA COORDONNEE X
C ---

C     CALCUL DU NOMBRE DE VARIATION DE LA DERIVEE PREMIERE
      NBVAR = 0
      D1M = D1X1
      SVAR = 0
      DO 45 INO = 2,NBNO-1
        DCP = ZR(JCORD+INO) - ZR(JCORD+INO-1)
        D1P = ( ZR(JX+IDECAL+INO) - ZR(JX+IDECAL+INO-1) ) / DCP
        IF (D1P .GT. D1M) THEN
            IF (SVAR .EQ. -1) NBVAR = NBVAR + 1
            SVAR = 1
        ELSEIF (D1P .LT. D1M) THEN
            IF (SVAR .EQ. 1) NBVAR = NBVAR + 1
            SVAR = -1
        ENDIF
45    CONTINUE

C     CONTROLE DE LA REGULARITE DE LA DERIVEE SECONDE
      NBVAR2 = 0
      DO 46 INO=2,NBNO
        IF (ZR(JD2X-1+INO)*ZR(JD2X-1+INO-1).LT.0.D0) NBVAR2 = NBVAR2+1
46    CONTINUE

      IF (NBVAR2 .GE. NBVAR+10) THEN
        VALI(1) = ICABL
        VALI(2) = NBVAR2
        VALI(3) = NBVAR
        CALL U2MESI('I','MODELISA7_13',3,VALI)
        LSPLIN = .FALSE.
        GOTO 888
      ENDIF
C
C 4-BIS .2 INTERPOLATION DE LA COORDONNEE Y
C ---

C     CALCUL DU NOMBRE DE VARIATION DE LA DERIVEE PREMIERE
      NBVAR = 0
      D1M = D1Y1
      SVAR = 0
      DO 55 INO = 2,NBNO-1
        DCP = ZR(JCORD+INO) - ZR(JCORD+INO-1)
        D1P = ( ZR(JY+IDECAL+INO) - ZR(JY+IDECAL+INO-1) ) / DCP
        IF (D1P .GT. D1M) THEN
            IF (SVAR .EQ. -1) NBVAR = NBVAR + 1
            SVAR = 1
        ELSEIF (D1P .LT. D1M) THEN
            IF (SVAR .EQ. 1) NBVAR = NBVAR + 1
            SVAR = -1
        ENDIF
55    CONTINUE

C     CONTROLE DE LA REGULARITE DE LA DERIVEE SECONDE
      NBVAR2 = 0
      DO 56 INO=2,NBNO
        IF (ZR(JD2Y-1+INO)*ZR(JD2Y-1+INO-1).LT.0.D0) NBVAR2 = NBVAR2+1
56    CONTINUE

      IF (NBVAR2 .GE. NBVAR+10) THEN
        VALI(1) = ICABL
        VALI(2) = NBVAR2
        VALI(3) = NBVAR
        CALL U2MESI('I','MODELISA7_13',3,VALI)
        LSPLIN = .FALSE.
        GOTO 888
      ENDIF
C
C 4-BIS .3 INTERPOLATION DE LA COORDONNEE Z
C ---

C     CALCUL DU NOMBRE DE VARIATION DE LA DERIVEE PREMIERE
      NBVAR = 0
      D1M = D1Z1
      SVAR = 0
      DO 65 INO = 2,NBNO-1
        DCP = ZR(JCORD+INO) - ZR(JCORD+INO-1)
        D1P = ( ZR(JZ+IDECAL+INO) - ZR(JZ+IDECAL+INO-1) ) / DCP
        IF (D1P .GT. D1M) THEN
            IF (SVAR .EQ. -1) NBVAR = NBVAR + 1
            SVAR = 1
        ELSEIF (D1P .LT. D1M) THEN
            IF (SVAR .EQ. 1) NBVAR = NBVAR + 1
            SVAR = -1
        ENDIF
65    CONTINUE

C     CONTROLE DE LA REGULARITE DE LA DERIVEE SECONDE
      NBVAR2 = 0
      DO 66 INO=2,NBNO
        IF (ZR(JD2Z-1+INO)*ZR(JD2Z-1+INO-1).LT.0.D0) NBVAR2 = NBVAR2+1
66    CONTINUE

      IF (NBVAR2 .GE. NBVAR+10) THEN
        VALI(1) = ICABL
        VALI(2) = NBVAR2
        VALI(3) = NBVAR
        CALL U2MESI('I','MODELISA7_13',3,VALI)
        LSPLIN = .FALSE.
        GOTO 888
      ENDIF
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 5   CALCULS DE L'ABSCISSE CURVILIGNE ET DE LA DEVIATION ANGULAIRE
C     CUMULEE LE LONG DU CABLE, PAR INTEGRATION NUMERIQUE
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
      ZR(JABSC) = 0.0D0
      ZR(JALPH) = 0.0D0
C
      DO 50 INO = 2, NBNO
C
         CORDE = ZR(JCORD+INO-2)
         DC = ( ZR(JCORD+INO-1) - CORDE ) / DBLE ( NBSUB )
C
C....... CONTRIBUTION DU PREMIER POINT
C
         CALL SPLIN1(ZR(JCORD),ZR(JX+IDECAL),ZR(JD2X),NBNO,CORDE,D1X,
     &               IRET)
         CALL SPLIN1(ZR(JCORD),ZR(JY+IDECAL),ZR(JD2Y),NBNO,CORDE,D1Y,
     &               IRET)
         CALL SPLIN1(ZR(JCORD),ZR(JZ+IDECAL),ZR(JD2Z),NBNO,CORDE,D1Z,
     &               IRET)
         CALL SPLIN2(ZR(JCORD),ZR(JD2X),NBNO,CORDE,D2X,IRET)
         CALL SPLIN2(ZR(JCORD),ZR(JD2Y),NBNO,CORDE,D2Y,IRET)
         CALL SPLIN2(ZR(JCORD),ZR(JD2Z),NBNO,CORDE,D2Z,IRET)
         NORMV2 = D1X * D1X + D1Y * D1Y + D1Z * D1Z
         IF ( NORMV2.EQ.0.0D0 ) THEN
            WRITE(K3B,'(I3)') ICABL
            CALL U2MESK('F','MODELISA7_60',1,K3B)
         ENDIF
         ABSC = DBLE ( SQRT ( NORMV2 ) ) / 2.0D0
         DET1 = D1Y * D2Z - D1Z * D2Y
         DET2 = D1Z * D2X - D1X * D2Z
         DET3 = D1X * D2Y - D1Y * D2X
         ALPHA = DBLE ( SQRT ( DET1*DET1 + DET2*DET2 + DET3*DET3 ) )
     &         / ( NORMV2 * 2.0D0 )
C
C....... CONTRIBUTION DES POINTS INTERMEDIAIRES
C
         DO 60 ISUB = 1, NBSUB-1
            CORDE = CORDE + DC
            CALL SPLIN1(ZR(JCORD),ZR(JX+IDECAL),ZR(JD2X),NBNO,CORDE,D1X,
     &                  IRET)
            CALL SPLIN1(ZR(JCORD),ZR(JY+IDECAL),ZR(JD2Y),NBNO,CORDE,D1Y,
     &                  IRET)
            CALL SPLIN1(ZR(JCORD),ZR(JZ+IDECAL),ZR(JD2Z),NBNO,CORDE,D1Z,
     &                  IRET)
            CALL SPLIN2(ZR(JCORD),ZR(JD2X),NBNO,CORDE,D2X,IRET)
            CALL SPLIN2(ZR(JCORD),ZR(JD2Y),NBNO,CORDE,D2Y,IRET)
            CALL SPLIN2(ZR(JCORD),ZR(JD2Z),NBNO,CORDE,D2Z,IRET)
            NORMV2 = D1X * D1X + D1Y * D1Y + D1Z * D1Z
            IF ( NORMV2.EQ.0.0D0 ) THEN
               WRITE(K3B,'(I3)') ICABL
               CALL U2MESK('F','MODELISA7_60',1,K3B)
            ENDIF
            ABSC = ABSC + DBLE ( SQRT ( NORMV2 ) )
            DET1 = D1Y * D2Z - D1Z * D2Y
            DET2 = D1Z * D2X - D1X * D2Z
            DET3 = D1X * D2Y - D1Y * D2X
            ALPHA = ALPHA + DBLE(SQRT(DET1*DET1+DET2*DET2+DET3*DET3))
     &                      / NORMV2
  60     CONTINUE
C
C....... CONTRIBUTION DU DERNIER POINT
C
         CORDE = CORDE + DC
         CALL SPLIN1(ZR(JCORD),ZR(JX+IDECAL),ZR(JD2X),NBNO,CORDE,D1X,
     &               IRET)
         CALL SPLIN1(ZR(JCORD),ZR(JY+IDECAL),ZR(JD2Y),NBNO,CORDE,D1Y,
     &               IRET)
         CALL SPLIN1(ZR(JCORD),ZR(JZ+IDECAL),ZR(JD2Z),NBNO,CORDE,D1Z,
     &               IRET)
         CALL SPLIN2(ZR(JCORD),ZR(JD2X),NBNO,CORDE,D2X,IRET)
         CALL SPLIN2(ZR(JCORD),ZR(JD2Y),NBNO,CORDE,D2Y,IRET)
         CALL SPLIN2(ZR(JCORD),ZR(JD2Z),NBNO,CORDE,D2Z,IRET)
         NORMV2 = D1X * D1X + D1Y * D1Y + D1Z * D1Z
         IF ( NORMV2.EQ.0.0D0 ) THEN
            WRITE(K3B,'(I3)') ICABL
            CALL U2MESK('F','MODELISA7_60',1,K3B)
         ENDIF
         ABSC = ABSC + DBLE ( SQRT ( NORMV2 ) ) / 2.0D0
         DET1 = D1Y * D2Z - D1Z * D2Y
         DET2 = D1Z * D2X - D1X * D2Z
         DET3 = D1X * D2Y - D1Y * D2X
         ALPHA = ALPHA + DBLE(SQRT(DET1*DET1+DET2*DET2+DET3*DET3))
     &                   / ( NORMV2 * 2.0D0 )
C
C....... ABSCISSE CURVILIGNE ET DEVIATION ANGULAIRE CUMULEE
C
         ABSC = ABSC * DC
         ZR(JABSC+INO-1) = ZR(JABSC+INO-2) + ABSC
         ALPHA = ALPHA * DC
         ZR(JALPH+INO-1) = ZR(JALPH+INO-2) + ALPHA
C
  50  CONTINUE
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 6   MISE A JOUR DES OBJETS DE SORTIE
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
888   CONTINUE
      IF(LSPLIN)THEN
        DO 70 INO = 1, NBNO
          VALPAR(1) = ZR(JABSC+INO-1)
          VALPAR(2) = ZR(JALPH+INO-1)
          CALL TBAJLI(TABLCA,2,PARAM,IBID,VALPAR,CBID,K3B,IDECAL+INO)
70      CONTINUE
      ELSE
        DO 71 INO = 1, NBNO
          VALPAR(1) = ZR(JCORD+INO-1)
          VALPAR(2) = ZR(JALPHD+INO-1)
          CALL TBAJLI(TABLCA,2,PARAM,IBID,VALPAR,CBID,K3B,IDECAL+INO)
71      CONTINUE
      ENDIF
C
C --- MENAGE
C
      CALL JEDETR('&&TRAJCA.CORDE_CUMU')
      CALL JEDETR('&&TRAJCA.D2X')
      CALL JEDETR('&&TRAJCA.D2Y')
      CALL JEDETR('&&TRAJCA.D2Z')
      CALL JEDETR('&&TRAJCA.ABSC_CURV')
      CALL JEDETR('&&TRAJCA.ALPHA')
      CALL JEDETR('&&TRAJCA.ALPHA_DISC')
C
      CALL JEDEMA()
C
C --- FIN DE TRAJCA.
      END
