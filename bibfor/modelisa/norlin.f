      SUBROUTINE NORLIN (TYPMA,L,KNUMAI,COOR,DFONC,IN,PREC,A,B,C)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      CHARACTER*3 TYPMA
      CHARACTER*8 KNUMAI
      REAL*8 COOR(3,*),DFONC(*),A,B,C,PREC
      INTEGER IN,L

C     OBJECTIF : CALCULER LA NORMALE OU LA TANGENTE A UNE MAILLE
C                QUADRATIQUE EN LINEAIRE ( SEULS LES NOEUDS SOMMETS
C                IMPORTENT DONC ) SUR UN NOEUD

C     ARGUMENT D ENTREE
C     TYPMA  : TYPE DE LA MAILLE
C     L      : VAUT 2 SI ON CHERCHE LA TANGENTE ET 1 POUR LA NORMALE
C     KNUMAI : NUMERO DE LA MAILLE ACTUELLE ( UTILE SEULEMENT POUR LES
C              MESSAGES D ALARME/D ERREUR )
C     COOR   : COORDONNEES DES NOEUDS DE LA MAILLE
C     DFONC  : VALEURS DES DERIVES DES FONCTIONS DE FORMES AUX NOEUDS
C     IN     : NUMERO DU NOEUD DANS LA CONNECTIVITE DE LA MAILLE
C     PREC   : PRECISION SUR LA NORME DE LA NORMALE AU DESSOUS DE
C              LAQUELLE ON CONSIDERE QUE LA MAILLE EST DEGENEREE
C     A      : VALEUR SELON X DU VECTEUR NORMAL
C     B      : VALEUR SELON Y DU VECTEUR NORMAL
C     C      : VALEUR SELON Z DU VECTEUR NORMAL

      INTEGER IFONC,NN
      REAL*8 EKS1X,EKS1Y,EKS1Z,EET1X,EET1Y,EET1Z,NORME
      REAL*8 EKS2X,EKS2Y,EKS2Z,EET2X,EET2Y,EET2Z


C     ON CALCULE LE NOMBRE DE NOEUDS SOMMETS
      IF (TYPMA.EQ.'QU8') THEN
        NN=4
      ELSE IF ( TYPMA.EQ.'TR6') THEN
        NN=3
      ELSE IF ( TYPMA.EQ.'SE3') THEN
        NN=2
      ELSE
        CALL U2MESS('F','MODELISA5_43')
      ENDIF
C     CAS 3D
      IF ( TYPMA.NE.'SE3' ) THEN
C     ON EST SUR UN NOEUD SOMMET : ON CALCULE SA NORMALE
        IF (IN.LE.NN) THEN
          EKS1X=0.D0
          EKS1Y=0.D0
          EKS1Z=0.D0
          EET1X=0.D0
          EET1Y=0.D0
          EET1Z=0.D0
          DO 10 IFONC=1,NN
            EKS1X=EKS1X+COOR(1,IFONC)*DFONC((IN-1)*NN*2+IFONC)
            EKS1Y=EKS1Y+COOR(2,IFONC)*DFONC((IN-1)*NN*2+IFONC)
            EKS1Z=EKS1Z+COOR(3,IFONC)*DFONC((IN-1)*NN*2+IFONC)

            EET1X=EET1X+COOR(1,IFONC)*DFONC((IN-1)*NN*2+NN+IFONC)
            EET1Y=EET1Y+COOR(2,IFONC)*DFONC((IN-1)*NN*2+NN+IFONC)
            EET1Z=EET1Z+COOR(3,IFONC)*DFONC((IN-1)*NN*2+NN+IFONC)
 10       CONTINUE
C         CALCUL DU VECTEUR NORMAL ET NORMALISATION
          A=EKS1Y*EET1Z-EKS1Z*EET1Y
          B=EKS1Z*EET1X-EKS1X*EET1Z
          C=EKS1X*EET1Y-EKS1Y*EET1X
          NORME=SQRT(A*A+B*B+C*C)
          IF (NORME.GT.PREC) THEN
             A=A/NORME
             B=B/NORME
             C=C/NORME
          ELSE
             CALL U2MESK('F','MODELISA3_26',1,KNUMAI)
          ENDIF
C     ON EST SUR UN NOEUD MILIEUX : ON CALCULE SA NORMALE EN FAISANT
C     LA MOYENNE DES DEUX NOEUDS DE SON ARETE
       ELSE
          EKS1X=0.D0
          EKS1Y=0.D0
          EKS1Z=0.D0
          EET1X=0.D0
          EET1Y=0.D0
          EET1Z=0.D0
          EKS2X=0.D0
          EKS2Y=0.D0
          EKS2Z=0.D0
          EET2X=0.D0
          EET2Y=0.D0
          EET2Z=0.D0
          DO 20 IFONC=1,NN
            EKS1X=EKS1X+COOR(1,IFONC)*DFONC((IN-1-NN)*NN*2+IFONC)
            EKS1Y=EKS1Y+COOR(2,IFONC)*DFONC((IN-1-NN)*NN*2+IFONC)
            EKS1Z=EKS1Z+COOR(3,IFONC)*DFONC((IN-1-NN)*NN*2+IFONC)
            EKS2X=EKS2X+COOR(1,IFONC)*DFONC((IN-1-NN)*NN*2+IFONC)
            EKS2Y=EKS2Y+COOR(2,IFONC)*DFONC((IN-1-NN)*NN*2+IFONC)
            EKS2Z=EKS2Z+COOR(3,IFONC)*DFONC((IN-1-NN)*NN*2+IFONC)

            EET1X=EET1X+COOR(1,IFONC)*DFONC((IN-NN)*NN*2+NN+IFONC)
            EET1Y=EET1Y+COOR(2,IFONC)*DFONC((IN-NN)*NN*2+NN+IFONC)
            EET1Z=EET1Z+COOR(3,IFONC)*DFONC((IN-NN)*NN*2+NN+IFONC)
            EET2X=EET2X+COOR(1,IFONC)*DFONC((IN-NN)*NN*2+NN+IFONC)
            EET2Y=EET2Y+COOR(2,IFONC)*DFONC((IN-NN)*NN*2+NN+IFONC)
            EET2Z=EET2Z+COOR(3,IFONC)*DFONC((IN-NN)*NN*2+NN+IFONC)
 20       CONTINUE
          A=(EKS1Y*EET1Z-EKS1Z*EET1Y+EKS2Y*EET2Z-EKS2Z*EET2Y)/2.D0
          B=(EKS1Z*EET1X-EKS1X*EET1Z+EKS2Z*EET2X-EKS2X*EET2Z)/2.D0
          C=(EKS1X*EET1Y-EKS1Y*EET1X+EKS2X*EET2Y-EKS2Y*EET2X)/2.D0
          NORME=SQRT(A*A+B*B+C*C)
         IF (NORME.GT.PREC) THEN
             A=A/NORME
             B=B/NORME
             C=C/NORME
          ELSE
             CALL U2MESK('F','MODELISA3_26',1,KNUMAI)
          ENDIF
        ENDIF
C     ON EST EN 2D
      ELSE
C     ON EST SUR UN NOEUD SOMMET : ON CALCULE SA NORMALE
        IF (IN.LE.NN) THEN
          EKS1X=0.D0
          EKS1Y=0.D0
          DO 30 IFONC=1,NN
            EKS1X=EKS1X+COOR(1,IFONC)*DFONC((IN-1)*NN+IFONC)
            EKS1Y=EKS1Y+COOR(2,IFONC)*DFONC((IN-1)*NN+IFONC)
 30       CONTINUE
C         ON S INTERESSE AU VECTEUR TANGENT
          IF (L.EQ.2) THEN
            NORME=SQRT(EKS1X**2+EKS1Y**2)
            IF (NORME.GT.PREC) THEN
              A=EKS1X/NORME
              B=EKS1Y/NORME
            ELSE
              CALL U2MESK('F','MODELISA3_23',1,KNUMAI)
            ENDIF
C         ON S INTERESSE AU VECTEUR NORMAL
          ELSE IF (L.EQ.1) THEN
             NORME=SQRT(EKS1X**2+EKS1Y**2)
             IF (NORME.GT.PREC) THEN
               A=EKS1Y/NORME
               B=-EKS1X/NORME
             ELSE
               CALL U2MESK('F','MODELISA3_24',1,KNUMAI)
             ENDIF
          ENDIF
C     ON EST SUR UN NOEUD MILIEUX : ON CALCULE SA NORMALE EN FAISANT
C     LA MOYENNE DES DEUX NOEUDS DE SON ARETE
        ELSE
          EKS1X=0.D0
          EKS1Y=0.D0
          EKS2X=0.D0
          EKS2Y=0.D0
          DO 40 IFONC=1,NN
            EKS1X=EKS1X+COOR(1,IFONC)*DFONC((IN-1-NN)*NN+IFONC)
            EKS1Y=EKS1Y+COOR(2,IFONC)*DFONC((IN-1-NN)*NN+IFONC)
            EKS2X=EKS2X+COOR(1,IFONC)*DFONC((IN-NN)*NN+IFONC)
            EKS2Y=EKS2Y+COOR(2,IFONC)*DFONC((IN-NN)*NN+IFONC)
 40       CONTINUE
          EKS1X=(EKS1X+EKS2X)/2
          EKS1Y=(EKS1Y+EKS2Y)/2
C         ON S INTERESSE AU VECTEUR TANGENT
          IF (L.EQ.2) THEN
            NORME=SQRT(EKS1X**2+EKS1Y**2)
            IF (NORME.GT.PREC) THEN
               A=EKS1X/NORME
               B=EKS1Y/NORME
            ELSE
              CALL U2MESK('F','MODELISA3_23',1,KNUMAI)
            ENDIF
C         ON S INTERESSE AU VECTEUR NORMAL
          ELSE IF (L.EQ.1) THEN
            NORME=SQRT(EKS1X**2+EKS1Y**2)
            IF (NORME.GT.PREC) THEN
               A=EKS1Y/NORME
               B=-EKS1X/NORME
            ELSE
               CALL U2MESK('F','MODELISA3_24',1,KNUMAI)
            ENDIF
          ENDIF
        ENDIF
      ENDIF

      END
