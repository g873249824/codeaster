      SUBROUTINE ARLCHI(MAIL  ,NOM1  ,NOM2  ,MAILAR,QUADRA,
     &                  TYPMAI,LCARA ,TABCPL,NBCART,CTARLQ)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/04/2008   AUTEUR MEUNIER S.MEUNIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
      CHARACTER*8  MAILAR,MAIL
      CHARACTER*10 QUADRA
      CHARACTER*24 TABCPL
      CHARACTER*10 NOM1,NOM2
      CHARACTER*16 TYPMAI
      REAL*8       LCARA
      INTEGER      NBCART
      CHARACTER*19 CTARLQ(NBCART)
C
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C CREATION DES CARTES POUR APPEL A CALCUL - ARLQ_COUPL
C
C ----------------------------------------------------------------------
C
C
C IN  MAIL   : NOM DU MAILLAGE
C IN  MAILAR : NOM DU PSEUDO-MAILLAGE
C IN  NOM1   : NOM DE LA SD DE STOCKAGE MAILLES GROUP_MA_1
C IN  NOM2   : NOM DE LA SD DE STOCKAGE MAILLES GROUP_MA_2
C IN  TYPMAI : SD CONTENANT NOM DES TYPES ELEMENTS (&&CATA.NOMTM)
C IN  QUADRA : SD DES QUADRATURES A CALCULER
C IN  LCARA  : LONGUEUR CARACTERISTIQUE POUR TERME DE COUPLAGE (PONDERA
C              TION DES TERMES DE COUPLAGE)
C IN  TABCPL : TABLEAU DE CORRESPONDANCE
C               POUR CHAQUE PSEUDO-MAILLE -> NUM. COUPLE
C IN  NBCART : NOMBRE DE CARTES CREEES
C I/O CTARLQ : IN  -> NOM DES CARTES A CREER
C              OUT -> CARTES
C
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      CHARACTER*32 JEXNUM,JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      INTEGER     NBNOMX
      PARAMETER   (NBNOMX = 27)
C
      INTEGER       IFM,NIV
      CHARACTER*19  NGRM1,NGRM2
      INTEGER       JGRP1,JGRP2
      INTEGER       JTYP,JDIME,JCOOR,JCUMU,JCONX
      INTEGER       JQNUME,JQMAMA,JQMAFA,JQTYPM
      INTEGER       NBMA,NDIM,NBNOC1,NBNOC2
      INTEGER       NUMMC1,NUMMC2
      INTEGER       NCMPI,NCMPF,NCMPR,NCMPC
      INTEGER       IFAM,ICPL,IMA,IGM,ICMP
      INTEGER       FAMIL
      CHARACTER*8   TMS,TMC1,TMC2,NOMFAM,ELREF1,ELREF2,K8BID,GRNOM
      INTEGER       JTABCP,JTYPMM
      CHARACTER*24  GRPMA
      CHARACTER*19  CTFAMI,CTINFO
      INTEGER       JCTFV,JCTFC,JCTIV,JCTIC
      CHARACTER*19  CTREF1,CTCOO1
      CHARACTER*19  CTREF2,CTCOO2
      INTEGER       JCTRV1,JCTRC1,JCTCV1,JCTCC1
      INTEGER       JCTRV2,JCTRC2,JCTCV2,JCTCC2
      INTEGER       IRET
      INTEGER       NBGRM,NMAIN1,NMAIN2,NMASS
      CHARACTER*2   CH2
      REAL*8        CNO1(3*NBNOMX),CNO2(3*NBNOMX),R8MAEM
      LOGICAL       LINCL1,LINCL2
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('ARLEQUIN',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> *** PREPARATION CHAMPS D''ENTREE...'
      ENDIF
C
C --- INITIALISATIONS
C
      CTFAMI = CTARLQ(1)
      CTINFO = CTARLQ(2)
      CTREF1 = CTARLQ(3)
      CTCOO1 = CTARLQ(4)
      CTREF2 = CTARLQ(5)
      CTCOO2 = CTARLQ(6)
C
C --- INFO SUR LE PSEUDO-MAILLAGE
C
      CALL JEVEUO(MAILAR(1:8)//'.DIME','L',JDIME)
      NBMA = ZI(JDIME - 1 + 3)
      NDIM = ZI(JDIME - 1 + 6)
C
C --- LECTURE SD CONTENANT NOM DES TYPES ELEMENTS (&&CATA.NOMTM)
C
      CALL JEVEUO(TYPMAI,'L',JTYPMM)
C
C --- LECTURE DONNEES MAILLAGE
C
      CALL JEVEUO(MAIL(1:8)//'.TYPMAIL','L',JTYP)
      CALL JEVEUO(MAIL(1:8)//'.COORDO    .VALE','L',JCOOR)
      CALL JEVEUO(MAIL(1:8)//'.CONNEX','L',JCONX)
      CALL JEVEUO(JEXATR(MAIL(1:8)//'.CONNEX','LONCUM'),'L',JCUMU)
C
C --- LECTURE DONNEES GROUPE DE MAILLES
C
      NGRM1 = NOM1(1:10)//'.GROUPEMA'
      NGRM2 = NOM2(1:10)//'.GROUPEMA'
      CALL JEVEUO(NGRM1(1:19),'L',JGRP1)
      CALL JEVEUO(NGRM2(1:19),'L',JGRP2)
C
C --- GROUPES DE MAILLES
C
      NMAIN1 = 0
      NMAIN2 = 0
      NMASS  = 0
      GRPMA  = MAILAR(1:8)//'.GROUPEMA       '
      CALL JEEXIN(GRPMA,IRET)
       IF (IRET.NE.0) THEN
         CALL JELIRA(GRPMA,'NUTIOC',NBGRM,K8BID)
         IF ((NBGRM.GE.1).AND.(NBGRM.LE.3)) THEN
           DO 51 IGM = 1,NBGRM
            CALL JENUNO(JEXNUM(GRPMA,IGM),GRNOM)
            IF (GRNOM.EQ.'INCLU1') THEN
              CALL JELIRA(JEXNUM(GRPMA,IGM),'LONMAX',NMAIN1,K8BID)
            ELSEIF (GRNOM.EQ.'INCLU2') THEN
              CALL JELIRA(JEXNUM(GRPMA,IGM),'LONMAX',NMAIN2,K8BID)
            ELSEIF (GRNOM.EQ.'SOUS') THEN
              CALL JELIRA(JEXNUM(GRPMA,IGM),'LONMAX',NMASS ,K8BID)
            ELSE
              CALL ASSERT(.FALSE.)
            ENDIF
  51       CONTINUE
         ELSE
           CALL ASSERT(.FALSE.)
         ENDIF
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- TABLEAU DE CORRESPONDANCE NUM. ABS. MAILLE CREEE -> COUPLE
C
      CALL JEVEUO(TABCPL,'L',JTABCP)
C
C --- LECTURE DONNEES QUADRATURES
C
      CALL JEVEUO(QUADRA(1:10)//'.MAMA'  ,'L',JQMAMA)
      CALL JEVEUO(QUADRA(1:10)//'.MAFA'  ,'L',JQMAFA)
      CALL JEVEUO(QUADRA(1:10)//'.NUMERO','L',JQNUME)
      CALL JEVEUO(QUADRA(1:10)//'.TYPEMA','L',JQTYPM)
C
C --- CONSTRUCTION ET ALLOCATION DES CARTES CTFAMI ET CTINFO
C
      CALL DETRSD('CARTE',CTFAMI)
      CALL DETRSD('CARTE',CTINFO)
      CALL ALCART('V',CTFAMI,MAILAR,'NEUT_K8')
      CALL ALCART('V',CTINFO,MAILAR,'NEUT_R')
C
C --- ACCES AUX CARTES - COMPOSANTES
C
      CALL JEVEUO(CTFAMI//'.NCMP','E',JCTFC)
      CALL JEVEUO(CTINFO//'.NCMP','E',JCTIC)
C
C --- REMPLISSAGE COMPOSANTES DES CARTES
C
      NCMPI = 5
      DO 10 ICMP = 1,NCMPI
        CALL CODENT(ICMP,'G',CH2)
        ZK8(JCTIC+ICMP-1) = 'X'//CH2
  10  CONTINUE
C
      NCMPF = 1
      DO 11 ICMP = 1,NCMPF
        CALL CODENT(ICMP,'G',CH2)
        ZK8(JCTFC+ICMP-1) = 'Z'//CH2
  11  CONTINUE
C
C --- CONSTRUCTION ET ALLOCATION DES CARTES CTREFx ET CTCOOx
C
      CALL DETRSD('CARTE',CTREF1)
      CALL DETRSD('CARTE',CTCOO1)
      CALL ALCART('V',CTREF1,MAILAR,'NEUT_K8')
      CALL ALCART('V',CTCOO1,MAILAR,'NEUT_R')
      CALL DETRSD('CARTE',CTREF2)
      CALL DETRSD('CARTE',CTCOO2)
      CALL ALCART('V',CTREF2,MAILAR,'NEUT_K8')
      CALL ALCART('V',CTCOO2,MAILAR,'NEUT_R')
C
C --- ACCES AUX CARTES - COMPOSANTES
C
      CALL JEVEUO(CTREF1//'.NCMP','E',JCTRC1)
      CALL JEVEUO(CTCOO1//'.NCMP','E',JCTCC1)
      CALL JEVEUO(CTREF2//'.NCMP','E',JCTRC2)
      CALL JEVEUO(CTCOO2//'.NCMP','E',JCTCC2)
C
C --- REMPLISSAGE COMPOSANTES DES CARTES
C
      NCMPR = 1
      DO 12 ICMP = 1,NCMPR
        CALL CODENT(ICMP,'G',CH2)
        ZK8(JCTRC1+ICMP-1) = 'Z'//CH2
        ZK8(JCTRC2+ICMP-1) = 'Z'//CH2
  12  CONTINUE
C
      NCMPC = 3*NBNOMX
      DO 13 ICMP = 1,NCMPC
        CALL CODENT(ICMP,'G',CH2)
        ZK8(JCTCC1+ICMP-1) = 'X'//CH2
        ZK8(JCTCC2+ICMP-1) = 'X'//CH2
  13  CONTINUE
C
C --- ACCES AUX CARTES - VALEURS
C
      CALL JEVEUO(CTFAMI//'.VALV','E',JCTFV)
      CALL JEVEUO(CTINFO//'.VALV','E',JCTIV)
      CALL JEVEUO(CTREF1//'.VALV','E',JCTRV1)
      CALL JEVEUO(CTCOO1//'.VALV','E',JCTCV1)
      CALL JEVEUO(CTREF2//'.VALV','E',JCTRV2)
      CALL JEVEUO(CTCOO2//'.VALV','E',JCTCV2)
C
C --- REMPLISSAGE VALEURS DES CARTES
C
      DO 90 IMA = 1,NBMA
C
C --- NUMERO DU COUPLE ATTACHE A LA MAILLE
C
        ICPL   = ZI(JTABCP+IMA -1)
        IFAM   = ZI(JQMAFA+ICPL-1)
C
C --- NUMERO DE FAMILLE D'INTEGRATION
C
        FAMIL  = ZI(JQNUME+IFAM-1)
C
C --- TYPE DE LA MAILLE SUPPORT INTEGRATION
C
        TMS    = ZK8(JQTYPM+IFAM-1)
C
C --- NOM FAMILLE INTEGRATION
C
        CALL CFAMIL(TMS   ,FAMIL ,NOMFAM)
C        NOMFAM = 'ARLQ'
C
C --- REMPLISSAGE CTFAMI
C
        ZK8(JCTFV-1+1) = NOMFAM
C
C --- INFO. SUR COUPLE DE MAILLES
C
        CALL ARLICP(ICPL  ,JQMAMA,JGRP1  ,JGRP2  ,
     &              NUMMC1,NUMMC2,
     &              LINCL1,LINCL2)
C
C --- COORDONNEES -SOLIDES- DE LA MAILLE COUPLE 1
C
        CALL COSOLI(NUMMC1,ZI(JCONX),ZI(JCUMU),ZR(JCOOR),NDIM,
     &              CNO1)
        NBNOC1   = ZI(JCUMU+NUMMC1) - ZI(JCUMU+NUMMC1-1)
        TMC1     = ZK8(JTYPMM+ZI(JTYP-1+NUMMC1)-1)
        CALL ARLT2R(TMC1   ,ELREF1)
C
C --- COORDONNEES -SOLIDES- DE LA MAILLE COUPLE 2
C
        IF (NUMMC2.EQ.0) THEN
          ELREF2 = 'NONE'
        ELSE
          CALL COSOLI(NUMMC2,ZI(JCONX),ZI(JCUMU),ZR(JCOOR),NDIM,
     &                CNO2)
          NBNOC2   = ZI(JCUMU+NUMMC2) - ZI(JCUMU+NUMMC2-1)
          TMC2     = ZK8(JTYPMM+ZI(JTYP-1+NUMMC2)-1)
          CALL ARLT2R(TMC2   ,ELREF2)
        ENDIF
C
C --- REMPLISSAGE CTINFO
C
        ZR(JCTIV+1-1) = NDIM
        ZR(JCTIV+2-1) = NBNOC1
        ZR(JCTIV+3-1) = NBNOC2
        ZR(JCTIV+4-1) = LCARA
        IF (LINCL1) THEN
          ZR(JCTIV+5-1) = 1
        ELSEIF (LINCL2) THEN
          ZR(JCTIV+5-1) = 2
        ELSE
          ZR(JCTIV+5-1) = 3
        ENDIF
C
C --- REMPLISSAGE CTCOO1/CTCOO2
C
        DO 500 ICMP = 1,NCMPC
          IF (ICMP.LE.(NDIM*NBNOC1)) THEN
            ZR(JCTCV1+ICMP-1) = CNO1(ICMP)
          ELSE
            ZR(JCTCV1+ICMP-1) = R8MAEM()
          ENDIF
  500   CONTINUE
C
        IF (ELREF2.EQ.'NONE') THEN
          DO 502 ICMP = 1,NCMPC
            ZR(JCTCV2+ICMP-1) = R8MAEM()
  502     CONTINUE
        ELSE
          DO 501 ICMP = 1,NCMPC
            IF (ICMP.LE.(NDIM*NBNOC2)) THEN
              ZR(JCTCV2+ICMP-1) = CNO2(ICMP)
            ELSE
              ZR(JCTCV2+ICMP-1) = R8MAEM()
            ENDIF
  501     CONTINUE
        ENDIF
C
C --- REMPLISSAGE CTREF1/CTREF2
C
        ZK8(JCTRV1+1-1) = ELREF1
        ZK8(JCTRV2+1-1) = ELREF2
C
C -- AFFECTATION CARTES
C
        CALL NOCART(CTFAMI,3,' ','NUM',1,' ',IMA,' ',NCMPF)
        CALL NOCART(CTINFO,3,' ','NUM',1,' ',IMA,' ',NCMPI)
        CALL NOCART(CTCOO1,3,' ','NUM',1,' ',IMA,' ',NCMPC)
        CALL NOCART(CTREF1,3,' ','NUM',1,' ',IMA,' ',NCMPR)
        CALL NOCART(CTCOO2,3,' ','NUM',1,' ',IMA,' ',NCMPC)
        CALL NOCART(CTREF2,3,' ','NUM',1,' ',IMA,' ',NCMPR)
   90 CONTINUE
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> *** FIN DE PREPARATION '//
     &               'CHAMPS D''ENTREE...'
      ENDIF
C
C --- MENAGE
C
      CALL JEDEMA()
      END
