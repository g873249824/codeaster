      SUBROUTINE ATASLC(AZ,NUMEDZ,RTBLOC,ATAZ,BASEZ,NBLIG)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 17/07/2002   AUTEUR ADBHHPM P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C.======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)

C     ATASLC  --  LE BUT DE CETTE ROUTINE EST DE CREER LA MATR_ASSE
C                 DE NOM ATA.
C                 LE .VALE DE CETTE MATR_ASSE VA CONTENIR LES TERMES
C                 DU PRODUIT DE MATRICES AT*A.
C                 A EST 'CONCEPTUELLEMENT' UNE MATRICE RECTANGULAIRE
C                 DE 'HAUTEUR' P ET DE LARGEUR NEQ.
C                 A EST 'INFORMATIQUEMENT' UNE COLLECTION NUMEROTEE
C                 COMPORTANT P OBJETS QUI SONT DES VECTEURS DE REELS
C                 DE LONGUEUR NEQ.
C                 CHACUN DE CES VECTEURS EST UNE LIGNE DE LA MATRICE A.
C                 LA MATR_ASSE ATA VA DONC ETRE SYMETRIQUE ET A
C                 VALEURS REELLES. D'AUTRE PART ON VA LUI AFFECTER
C                 UN PROFIL EN LIGNE DE CIEL.

C   ARGUMENT        E/S  TYPE         ROLE
C    AZ              IN    K*     NOM DE LA COLLECTION DES VECTEURS
C                                 LIGNES (I.E. AZ EST LA MATRICE
C                                 RECTANGULAIRE POUR LAQUELLE ON VA
C                                 CALCULER LE PRODUIT AZ_T*AZ).
C    NUMEDZ         IN    K*      NOM DU NUME_DDL DECRIVANT LES
C                                 LIGNES DE LA MATRICE AZ
C    RTBLOC          IN    R8     TAILLE DES BLOCS DE LA MATR_ASSE.
C    ATAZ           OUT    K*     NOM DE LA MATR_ASSE SYMETRIQUE
C                                 A VALEURS REELLES DONT LE .VALE
C                                 CONTIENT LE PRODUIT AT*A.
C                                 LE PROFIL DE CETTE MATRICE EST
C                                 EN LIGNE DE CIEL.
C                                 CE PROFIL EST DETERMINE DANS LA
C                                 ROUTINE.
C    BASEZ           IN    K*     NOM DE LA BASE SUR LAQUELLE ON
C                                 CREE LA MATR_ASSE.
C.========================= DEBUT DES DECLARATIONS ====================
C ----- COMMUNS NORMALISES  JEVEUX
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8,MA
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR
C -----  ARGUMENTS
      CHARACTER*(*) AZ,NUMEDZ,ATAZ,BASEZ
C -----  VARIABLES LOCALES
      CHARACTER*1 K1BID,BASE
      CHARACTER*14 NUMDDL,NUMEDD
      CHARACTER*19 PFCHNO,ATA
      CHARACTER*24 KHCOL,KADIA,KABLO,KIABL,KDESC,KREFA,KCONL,KVALE,
     +             KREFE,A

C.========================= DEBUT DU CODE EXECUTABLE ==================

      CALL JEMARQ()

C --- INITIALISATIONS :
C     ---------------
C
      BASE = BASEZ
      A = AZ
      ATA = ATAZ
      NUMEDD = NUMEDZ
      PFCHNO = NUMEDD//'.NUME'
      CALL GCNCON ( '_' , NUMDDL )
      CALL DETRSD('NUME_DDL',NUMDDL)
      CALL DETRSD('MATR_ASSE',ATA)

      UN = 1.0D0

C --- RECUPERATION DU NOMBRE D'OBJETS DE LA COLLECTION A (I.E.
C --- RECUPERATION DU NOMBRE DE VECTEURS LIGNES DE LA MATRICE
C --- RECTANGULAIRE A) :
C     ----------------
C      CALL JELIRA(A,'NMAXOC',NBLIG,K1BID)

C --- RECUPERATION DE LA LONGUEUR D'UN VECTEUR DE LA COLLECTION A
C --- (I.E. DU NOMBRE DE LIGNES DE LA MATRICE RECTANGULAIRE A) :
C     -------------------------------------------------------
      CALL JELIRA(A,'LONMAX',NEQ,K1BID)

C --- AFFECTATION DU PROF_CHNO DES CHAMNOS DONT LES .VALE SONT
C --- LES COLONNES DE LA MATRICE A, A CETTE MATRICE :
C     ---------------------------------------------
      CALL JEDUPO(PFCHNO//'.DEEQ',BASE,NUMDDL//'.NUME.DEEQ',.FALSE.)
      CALL JEDUPO(PFCHNO//'.DELG',BASE,NUMDDL//'.NUME.DELG',.FALSE.)
      CALL JEDUPO(PFCHNO//'.LILI',BASE,NUMDDL//'.NUME.LILI',.FALSE.)
      CALL JEDUPO(PFCHNO//'.LPRN',BASE,NUMDDL//'.NUME.LPRN',.FALSE.)
      CALL JEDUPO(PFCHNO//'.NUEQ',BASE,NUMDDL//'.NUME.NUEQ',.FALSE.)
      CALL JEDUPO(PFCHNO//'.PRNO',BASE,NUMDDL//'.NUME.PRNO',.TRUE.)
      CALL JEDUPO(PFCHNO//'.REFN',BASE,NUMDDL//'.NUME.REFN',.FALSE.)

C --- CREATION ET AFFECTATION DU TABLEAU .HCOL DES LONGUEURS
C --- DE LIGNES :
C     =========
      KHCOL = NUMDDL//'.SLCS.HCOL'

      CALL WKVECT(KHCOL,BASE//' V I',NEQ,IDHCOL)
      DO 10 I = 1,NEQ
        ZI(IDHCOL+I-1) = 1
   10 CONTINUE


C --- ON DETERMINE LA HAUTEUR DES COLONNES DE ATA :
C     -----------------------------------------------------
      DO 50 M = 1,NBLIG
        CALL JEVEUO(JEXNUM(A,M),'L',IDLIGM)
        I1=0
        DO 40 I = 1,NEQ
          IF(ZR(IDLIGM-1+I).NE.0) THEN
            IF (I1.EQ.0) I1=I
            ZI(IDHCOL+I-1) = MAX(ZI(IDHCOL+I-1),I-I1+1)
          END IF
   40   CONTINUE
   50 CONTINUE


C --- TAILLE D'UN BLOC :
C     ----------------
      ITBLOC = NINT(RTBLOC)*1024

C --- CREATION ET AFFECTATION DU TABLEAU .ADIA DES POSITIONS
C --- DES TERMES DIAGONAUX DANS LES BLOCS :
C     ===================================
      KADIA = NUMDDL//'.SLCS.ADIA'

      CALL WKVECT(KADIA,BASE//' V I',NEQ,IDADIA)

      NBLC = 1
      ZI(IDADIA) = ZI(IDHCOL)
      NTBLC = ZI(IDHCOL)

      DO 60 IEQUA = 2,NEQ
        NTBLC = NTBLC + ZI(IDHCOL+IEQUA-1)
        IF (NTBLC.LE.ITBLOC) THEN
C          ZI(IDADIA+IEQUA-1) = ZI(IDADIA+IEQUA-1) + ZI(IDHCOL+IEQUA-1)
          ZI(IDADIA+IEQUA-1) = NTBLC
        ELSE
          NTBLC = ZI(IDHCOL+IEQUA-1)
C          ZI(IDADIA+IEQUA-1) = ZI(IDHCOL+IEQUA-1)
          ZI(IDADIA+IEQUA-1) = NTBLC
          NBLC = NBLC + 1
        END IF
   60 CONTINUE

C --- NBLC EST LE NOMBRE DE BLOCS DE LA MATRICE

C --- CREATION ET AFFECTATION DU TABLEAU .ABLO DES NUMEROS
C --- DES LIGNES DE DEBUT ET DE FIN DE BLOC :
C     ====================================
      KABLO = NUMDDL//'.SLCS.ABLO'
      CALL WKVECT(KABLO,BASE//' V I',NBLC+1,IDABLO)

      ZI(IDABLO) = 0
      IBLC = 1
      NTBLC = ZI(IDHCOL)

      DO 70 IEQUA = 2,NEQ
        NTBLC = NTBLC + ZI(IDHCOL+IEQUA-1)
        IF (NTBLC.GT.ITBLOC) THEN
          NTBLC = ZI(IDHCOL+IEQUA-1)
          ZI(IDABLO+IBLC) = IEQUA - 1
          IBLC = IBLC + 1
        END IF
   70 CONTINUE

      ZI(IDABLO+NBLC) = NEQ
      IF (NBLC.EQ.1) THEN
        ITBLOC = NTBLC
      END IF

C --- CREATION ET AFFECTATION DU TABLEAU .IABL DONNANT POUR
C --- CHAQUE LIGNE DE LA MATRICE LE BLOC AUQUEL ELLE APPARTIENT :
C     =========================================================
      KIABL = NUMDDL//'.SLCS.IABL'

      CALL WKVECT(KIABL,BASE//' V I',NEQ,IDIABL)

      IEQ = 0

      DO 90 I = 1,NBLC
        NBLIGN = ZI(IDABLO+I) - ZI(IDABLO+I-1)
        DO 80 J = 1,NBLIGN
          IEQ = IEQ + 1
          ZI(IDIABL+IEQ-1) = I
   80   CONTINUE
   90 CONTINUE

C --- CREATION ET AFFECTATION DU TABLEAU .DESC DE DESCRIPTION
C --- DE LA MATRICE :
C     =============
      KDESC = NUMDDL//'.SLCS.DESC'

      CALL WKVECT(KDESC,BASE//' V I',6,IDDESC)

      ZI(IDDESC+1-1) = NEQ
      ZI(IDDESC+2-1) = ITBLOC
      ZI(IDDESC+3-1) = NBLC

C --- CREATION ET AFFECTATION DU TABLEAU .REFA DES REFERENCES
C --- DE LA MATRICE :
C     =============
      KREFA = ATA//'.REFA'

      CALL WKVECT(KREFA,BASE//' V K24',4,IDREFA)

      CALL DISMOI('F','NOM_MAILLA',NUMEDD,'NUME_DDL',IBID,MA,IER)
      ZK24(IDREFA-1+1) = MA
      ZK24(IDREFA-1+2) = NUMDDL//'.NUME'
      ZK24(IDREFA-1+3) = NUMDDL//'.SLCS'

C --- ON AFFECTE L'ETAT 'ASSEMBLE' A LA MATRICE :
C     -----------------------------------------
      CALL JEECRA(KREFA,'DOCU',IBID,'ASSE')

C --- CREATION ET AFFECTATION DE L'OBJET .REFE REFERENCANT LE
C --- NUME_DDL DE LA MATRICE :
C     ======================
      KREFE = NUMDDL//'.SLCS.REFE'

      CALL WKVECT(KREFE,BASE//' V K24',1,IDREFE)

      ZK24(IDREFE+1-1) (1:14) = NUMDDL

C --- ON AFFECTE LE STOCKAGE EN LIGNE DE CIEL A LA MATRICE :
C     ----------------------------------------------------
      CALL JEECRA(KREFE,'DOCU',IBID,'SLCS')

C --- CREATION ET AFFECTATION DU VECTEUR .CONL DE CONDITIONNEMENT
C --- DES INCONNUES DE LAGRANGE :
C     =========================
      KCONL = ATA//'.CONL'

      CALL WKVECT(KCONL,BASE//' V R',NEQ,IDCONL)

      DO 100 IEQ = 1,NEQ
        ZR(IDCONL+IEQ-1) = UN
  100 CONTINUE

C --- CREATION ET AFFECTATION DE LA COLLECTION .VALE
C --- DES BLOCS DE LA MATRICE :
C     =========================
      KVALE = ATA//'.VALE'

      CALL JECREC(KVALE,BASE//' V R','NU','DISPERSE','CONSTANT',NBLC)

      CALL JEECRA(KVALE,'LONMAX',ITBLOC,' ')
      CALL JEECRA(KVALE,'DOCU',IBID,'MS')


C --- CALCUL DES TERMES DE LA MATRICE ATA :
C     ----------------------------------
      DO 140 I = 1,NBLC
        CALL JECROC(JEXNUM(KVALE,I))
        CALL JEVEUO(JEXNUM(KVALE,I),'E',IDVALE)
        IL1 = ZI(IDABLO+I-1) + 1
        IL2 = ZI(IDABLO+I)

        DO 130 M = 1,NBLIG
          CALL JEVEUO(JEXNUM(A,M),'L',IDLIGM)
          ITERM = 0
          DO 120 J = IL1,IL2
            LONG = ZI(IDHCOL+J-1)
            IDEC = J - LONG
            DO 110 K = 1,LONG
              ITERM = ITERM + 1
              K1 = K + IDEC
              CJK = ZR(IDLIGM+J-1)*ZR(IDLIGM+K1-1)
              ZR(IDVALE+ITERM-1) = ZR(IDVALE+ITERM-1) + CJK
  110       CONTINUE
  120     CONTINUE
  130   CONTINUE
        CALL JELIBE(JEXNUM(KVALE,I))
  140 CONTINUE

      CALL JEDEMA()

      END
