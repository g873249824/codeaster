      SUBROUTINE ARLAS4(DIME  ,LCARA ,NDML1   ,NDML2   ,PTMT    ,
     &                  CNML1   ,CNML2   ,NORM  ,TANG  ,MTEL    ,
     &                  MTMO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 13/10/2009   AUTEUR CAO B.CAO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C
C
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
      INTEGER  DIME,NDML1,NDML2
      INTEGER  PTMT(NDML2/2,*)
      INTEGER  CNML1(*)
      INTEGER  CNML2(*)
      REAL*8   LCARA
      REAL*8   NORM(DIME,*)
      REAL*8   MTEL(*),MTMO(*)
      REAL*8   TANG(DIME*(DIME-1),*)
C
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C GESTION DES RELATIONS LINEAIRES
C ASSEMBLAGE MATRICE ELEMENTAIRE ARLEQUIN
C   MAILLE COQUE / MAILLE COQUE
C
C ----------------------------------------------------------------------
C
C
C IN  DIME   : DIMENSION DE L'ESPACE
C IN  LCARA  : LONGUEUR CARACTERISTIQUE POUR TERME DE COUPLAGE
C IN  NDML1    : NOMBRE DE NOEUDS MAILLE 1
C IN  NDML2    : NOMBRE DE NOEUDS MAILLE 2
C IN  PTMT     : POINTEURS DANS C (CF ARLAS0)
C IN  CNML1    : CONNECTIVITE MAILLE 1
C IN  CNML2    : CONNECTIVITE MAILLE 2
C IN  TANG   : NORMALES LISSEES POUR COQUE (CF LISNOR)
C IN  NORM   : TANGENTES LISSEES POUR COQUE (CF LISNOR)
C IN  MTEL     : MATRICES ELEMENTAIRES (CF ARLTE)
C     I/O MTMO      : MATRICE MORSE (CF ARLFC*)
C
C MATRICE PONCTUELLE DANS MTMO : (X1.X2, X1.Y2, [X1.Z2], Y1.X2, ...,
C                              T1.X2, T1.Y2, [T1.Z2, T2.X2, ...],
C        (N X X1.*2).1, [(N X X1.*2).2, ...,] (N X Y1.*2).1, ...,
C        (N X T1.*2).1, [(N X T1.*2).2, ..., (N X T2.*2).1, ...] )
C
C ----------------------------------------------------------------------
C
      INTEGER NBVLR1,NBVLR2,NDML12,IN1,IN2
      INTEGER VLTER,VLTER2,VLTERT,VLTERR,DTR,D2N2,NOCOQ1(2,9),
     &        NOCOQ2(2,9),IND1,IND2,JND1,JND2
      INTEGER IAUX,JAUX,KAUX,PMAT,PMAT1,PMAT2,PMAT3,PMAT4,PMAT5,PMAT6,
     &        LGNM,LGNM1,LGNM2,LGNM3,LGNM4,LGNM5,LGNM6,IER
      REAL*8  MTMO1(9),VLMT2(9),MTMO2(6),VLMT3(9),MTMO3(9),VLMT4(9),
     &        MTMO4(6)
      REAL*8  TRMT1,TRMT2,TRMT3,TRMT4,VLR1,VLR2,VLR3,VLR4,S1,S2,S3,S4
C
C ----------------------------------------------------------------------
C
      IF (DIME.EQ.2) THEN
        VLTER2 = 4
        VLTERT = 2
        VLTERR = 2
        DTR = 1
        VLTER = 9
      ELSE IF (DIME.EQ.3) THEN
        VLTER2 = 9
        VLTERT = 6
        VLTERR = 9
        DTR = 6
        VLTER = 30
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF

      NBVLR1 = NDML1/2
      NBVLR2 = NDML2/2
      NDML12 = NDML1*NDML2
      D2N2 = VLTER2*NDML2

C --- ASSEMBLAGE DE LA MATRICE ELEMENTAIRE MTEL

      CALL NOCOQU(DIME,NBVLR1,NOCOQ1)
      CALL NOCOQU(DIME,NBVLR2,NOCOQ2)

      DO 10 IAUX = 1, NBVLR1

        IN1 = CNML1(IAUX)

        IND1 = NOCOQ1(2,IAUX)
        IND2 = NOCOQ1(1,IAUX)

        PMAT5 = NDML2*(IND1-1)
        PMAT6 = NDML2*(IND2-1)
        LGNM5 = NDML12 + D2N2*(IND1-1)
        LGNM6 = NDML12 + D2N2*(IND2-1)

        DO 10 JAUX = 1, NBVLR2

          LGNM = 1 + VLTER*(PTMT(JAUX,IAUX)-1)

          IN2 = CNML2(JAUX)

          JND1 = NOCOQ2(2,JAUX)
          JND2 = NOCOQ2(1,JAUX)

          PMAT1 = PMAT5 + JND1
          PMAT2 = PMAT6 + JND1
          PMAT3 = PMAT5 + JND2
          PMAT4 = PMAT6 + JND2

          LGNM1 = LGNM5 + VLTER2*(JND1-1)
          LGNM2 = LGNM6 + VLTER2*(JND1-1)
          LGNM3 = LGNM5 + VLTER2*(JND2-1)
          LGNM4 = LGNM6 + VLTER2*(JND2-1)

C ------- CALCUL DE LA TRACE DE MTEL++, MTEL-+, MTEL+- ET MTEL--

          TRMT1 = 0.D0
          TRMT2 = 0.D0
          TRMT3 = 0.D0
          TRMT4 = 0.D0

          PMAT = 1
          DO 20 KAUX = 1, DIME

            TRMT1 = TRMT1 + MTEL(LGNM1+PMAT)
            TRMT2 = TRMT2 + MTEL(LGNM2+PMAT)
            TRMT3 = TRMT3 + MTEL(LGNM3+PMAT)
            TRMT4 = TRMT4 + MTEL(LGNM4+PMAT)

            PMAT = PMAT + DIME + 1

 20       CONTINUE

C ------- CALCUL DES MATRICES PONCTUELLES MTMO1, MTMO2, MTMO3 ET MTMO4

          DO 30 KAUX = 1, VLTER2

            LGNM1 = LGNM1 + 1
            LGNM2 = LGNM2 + 1
            LGNM3 = LGNM3 + 1
            LGNM4 = LGNM4 + 1

            S1 = MTEL(LGNM1)
            S2 = MTEL(LGNM2)
            S3 = MTEL(LGNM3)
            S4 = MTEL(LGNM4)

            MTMO1(KAUX) = LCARA*(S1+S2+S3+S4)
            VLMT2(KAUX) = LCARA*(S1-S2+S3-S4)
            VLMT3(KAUX) = LCARA*(S1+S2-S3-S4)
            VLMT4(KAUX) = LCARA*(S1-S2-S3+S4)

 30       CONTINUE

          S1 = MTEL(PMAT1)
          S2 = MTEL(PMAT2)
          S3 = MTEL(PMAT3)
          S4 = MTEL(PMAT4)

          VLR1 = S1 + S2 + S3 + S4 + LCARA*(TRMT1+TRMT2+TRMT3+TRMT4)
          VLR2 = S1 - S2 + S3 - S4 + LCARA*(TRMT1-TRMT2+TRMT3-TRMT4)
          VLR3 = S1 + S2 - S3 - S4 + LCARA*(TRMT1+TRMT2-TRMT3-TRMT4)
          VLR4 = S1 - S2 - S3 + S4 + LCARA*(TRMT1-TRMT2-TRMT3+TRMT4)

          PMAT = 1
          DO 40 KAUX = 1, DIME

            MTMO1(PMAT) = MTMO1(PMAT) + VLR1
            VLMT2(PMAT) = VLMT2(PMAT) + VLR2
            VLMT3(PMAT) = VLMT3(PMAT) + VLR3
            VLMT4(PMAT) = VLMT4(PMAT) + VLR4

            PMAT = PMAT + DIME + 1

 40       CONTINUE

          CALL PRMAMA(1,VLMT2,DIME,DIME,DIME,TANG(1,IN1),DIME,DIME,
     &                DIME-1,MTMO2,DIME,DIME,DIME-1,IER)
          CALL PRMAMA(1,VLMT4,DIME,DIME,DIME,TANG(1,IN1),DIME,DIME,
     &                DIME-1,VLMT2,DIME,DIME,DIME-1,IER)

          IF (DIME.EQ.2) THEN

            VLR1 = NORM(1,IN2)
            VLR2 = NORM(2,IN2)

            MTMO3(1) = VLR1*VLMT3(2) - VLR2*VLMT3(1)
            MTMO3(2) = VLR1*VLMT3(4) - VLR2*VLMT3(3)
            MTMO4(1) = VLR1*VLMT2(2) - VLR2*VLMT2(1)

          ELSE

            PMAT = 1
            DO 50 KAUX = 1, DIME
              CALL PROVEC(NORM(1,IN2),VLMT3(PMAT),MTMO3(PMAT))
              PMAT = PMAT + DIME
 50         CONTINUE

            PMAT = 1
            DO 60 KAUX = 2, DIME
              CALL PROVEC(NORM(1,IN2),VLMT2(PMAT),MTMO4(PMAT))
              PMAT = PMAT + DIME
 60         CONTINUE

          ENDIF

C ------- ASSEMBLAGE DES MATRICES PONCTUELLES

          DO 70 KAUX = 1, VLTER2
            MTMO(LGNM) = MTMO(LGNM) + MTMO1(KAUX)
            LGNM = LGNM + 1
 70       CONTINUE

          DO 80 KAUX = 1, VLTERT
            MTMO(LGNM) = MTMO(LGNM) + MTMO2(KAUX)
            LGNM = LGNM + 1
 80       CONTINUE

          DO 90 KAUX = 1, VLTERR
            MTMO(LGNM) = MTMO(LGNM) + MTMO3(KAUX)
            LGNM = LGNM + 1
 90       CONTINUE

          DO 100 KAUX = 1, DTR
            MTMO(LGNM) = MTMO(LGNM) + MTMO4(KAUX)
            LGNM = LGNM + 1
 100      CONTINUE

 10   CONTINUE

      END
