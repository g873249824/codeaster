      SUBROUTINE CAZOCD(CHAR  ,MOTFAC,IZONE ,NZOCO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8  CHAR
      CHARACTER*16 MOTFAC
      INTEGER      IZONE,NZOCO
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE DISCRETE - LECTURE DONNEES)
C
C LECTURE DES PRINCIPALES CARACTERISTIQUES DU CONTACT (SURFACE IZONE)
C
C ----------------------------------------------------------------------
C
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  MOTFAC : MOT-CLE FACTEUR
C IN  IZONE  : INDICE DE ZONE
C IN  NZOCO  : NOMBRE DE ZONES DE CONTACT
C
C
C
C
      INTEGER      CFMMVD,ZCMDF
      CHARACTER*24 DEFICO
      INTEGER      NOC,NOCN
      CHARACTER*24 CARADF
      INTEGER      JCMDF
      LOGICAL      CFDISL
      CHARACTER*16 GLIS
      REAL*8       ALJEU
      REAL*8       COEFPT,COEFPN,COEFFF,COEFTE
      LOGICAL      LCACT ,LFROT ,LPENAC,LPENAF
      INTEGER      IARG
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      DEFICO = CHAR(1:8)//'.CONTACT'
      COEFPT = 0.D0
      COEFPN = 0.D0
      COEFFF = 0.D0
      COEFTE = 0.D0
      ALJEU  = 0.D0
      LCACT  = CFDISL(DEFICO,'CONT_ACTI')
      LFROT  = CFDISL(DEFICO,'FROTTEMENT')
      LPENAF = CFDISL(DEFICO,'FROT_PENA')
      LPENAC = CFDISL(DEFICO,'CONT_PENA')
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      CARADF = DEFICO(1:16)//'.CARADF'
      ZCMDF  = CFMMVD('ZCMDF')
      CALL JEVEUO(CARADF,'E',JCMDF )
C
C --- PARAMETRES DU FROTTEMENT
C
      IF (LFROT) THEN
        CALL GETVR8(MOTFAC,'COULOMB'       ,IZONE,IARG,1,COEFFF,NOC)
        ZR(JCMDF+ZCMDF*(IZONE-1)+4-1) = COEFFF
        CALL GETVR8(MOTFAC,'COEF_MATR_FROT',IZONE,IARG,1,COEFTE,NOC)
        ZR(JCMDF+ZCMDF*(IZONE-1)+1-1) = COEFTE
      END IF
C
C --- CARACTERISTIQUES POUR LES METHODES AVEC PENALISATION
C
      IF (LPENAC) THEN
        CALL GETVR8(MOTFAC,'E_N',IZONE,IARG,1,COEFPN,NOCN)
        IF (NOCN.EQ.0) THEN
          CALL ASSERT(.FALSE.)
        ELSE
          ZR(JCMDF+ZCMDF*(IZONE-1)+2-1) = COEFPN
        ENDIF
      ENDIF
C
      IF (LPENAF) THEN
        CALL GETVR8(MOTFAC,'E_T',IZONE,IARG,1,COEFPT,NOCN)
        IF (NOCN.EQ.0) THEN
          CALL ASSERT(.FALSE.)
        ELSE
          ZR(JCMDF+ZCMDF*(IZONE-1)+3-1) = COEFPT
        ENDIF
      ENDIF
C
C --- OPTION GLISSIERE (POUR CONTRAINTE) : UNIQUE
C
      IF (LCACT) THEN
        CALL CAZOUU(MOTFAC,NZOCO ,'GLISSIERE')
        CALL GETVTX(MOTFAC,'GLISSIERE',1 ,IARG,1,GLIS  ,NOC   )
        IF (GLIS(1:3) .EQ. 'OUI') THEN
          ZR(JCMDF+ZCMDF*(IZONE-1)+6-1) = 1.D0
          CALL CAZOUU(MOTFAC,NZOCO ,'ALARME_JEU')
          CALL GETVR8(MOTFAC,'ALARME_JEU',1 ,IARG,1,ALJEU ,NOC)
          ZR(JCMDF+ZCMDF*(IZONE-1)+5-1) = ALJEU
        ELSEIF ((GLIS(1:3) .EQ. 'NON').OR.(NOC.EQ.0)) THEN
          ZR(JCMDF+ZCMDF*(IZONE-1)+6-1) = 0.D0
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF
      ENDIF
C
      CALL JEDEMA()
C
      END
