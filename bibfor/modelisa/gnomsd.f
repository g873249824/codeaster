      SUBROUTINE GNOMSD(NOMRES,NOOJB,K1,K2)
      IMPLICIT   NONE
C     -----------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 29/10/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C BUT :
C  TROUVER UN NOM POSSIBLE POUR UN OBJET JEVEUX QUI RESPECTE :
C     - CE NOM VAUT NOOJB (DONNE EN ENTREE) SAUF POUR LES SOUS-CHAINES
C           (1:8) ET (K1:K2)
C     - LE NOM DE L'OBJET N'EXISTE PAS ENCORE DANS LES BASES OUVERTES
C     - LE NOM (1:8) EST OBTENU PAR GETRES (RESULTAT DE LA COMMANDE)
C     - LE NOM (K1:K2) EST UN NUMERO ('0001','0002', ...)

C VAR : NOOJB : NOM D'UN OBJET JEVEUX  (K24)
C IN  : NOMRES: NOM DU RESULTAT SUR LEQUEL ON SOUHAITE CREER L'OBJET
C               S'IL VAUT ' ', ON FAIT UN APPEL A GETRES
C IN  : K1,K2 : INDICES DANS NOOJB DE LA SOUS-CHAINE "NUMERO"
C     -----------------------------------------------------------------
C
      INTEGER IRET,K1,K2,NMAXSD,NDIGIT,IESSAI,INUM,N1,N2
      CHARACTER*(*) NOMRES
      CHARACTER*8 NOMU,NOMRE2
      CHARACTER*16 CONCEP,CMD
      CHARACTER*24 NOOJB,NOOJB1
C     -----------------------------------------------------------------
      CALL ASSERT(K2.GT.K1)
      CALL ASSERT(K1.GT.8)
      CALL ASSERT(K2.LE.24)

      NOMRE2=NOMRES
      IF ( NOMRE2.EQ.' ' ) THEN
        CALL GETRES(NOMU,CONCEP,CMD)
      ELSE
        NOMU=NOMRE2
      ENDIF
      NOOJB1=NOOJB
      NOOJB1(1:8)=NOMU

      NDIGIT=K2-K1+1
      NMAXSD=INT(10**NDIGIT)


C     -- SI 0 EST LIBRE C'EST GAGNE :
C     ----------------------------------------------------------
      INUM=0
      CALL CODENT(INUM,'D0',NOOJB1(K1:K2))
      CALL JEEXIN(NOOJB1,IRET)
      IF (IRET.EQ.0)GOTO 40


C     -- IL N'Y A PEUT ETRE PAS DE NOM POSSIBLE :
C        (TOUS LES NOMS SONT DEJA UTILISES)
C     ----------------------------------------------------------
      INUM=NMAXSD-1
      CALL CODENT(INUM,'D0',NOOJB1(K1:K2))
      CALL JEEXIN(NOOJB1,IRET)
      IF (IRET.GT.0) CALL U2MESI('F','MODELISA4_69',1,INUM)


C     -- ON CHERCHE UN INTERVALLE (N1,N2) CONTENANT LE NUMERO
C        INUM CHERCHE :  N1<INUM<=N2
C     ----------------------------------------------------------
      N1=0
      IESSAI=2
   10 CONTINUE
      CALL CODENT(IESSAI,'D0',NOOJB1(K1:K2))
      CALL JEEXIN(NOOJB1,IRET)
      IF (IRET.EQ.0) THEN
        N2=IESSAI
        GOTO 20

      ELSE
        N1=IESSAI
        IESSAI=MIN(2*N1,NMAXSD-1)
        GOTO 10

      ENDIF
   20 CONTINUE
      CALL ASSERT(N1.GE.0)
      CALL ASSERT(N2.LT.NMAXSD)
      CALL ASSERT(N1.LT.N2)



C     -- ON CHERCHE INUM DANS N1,N2 PAR DICHOTOMIE :
C     ----------------------------------------------
   30 CONTINUE
      IF (N1.EQ.N2-1) THEN
C       -- ON A TROUVE :
        INUM=N2
        GOTO 40

      ELSE
        IESSAI=N1+(N2-N1)/2
        CALL CODENT(IESSAI,'D0',NOOJB1(K1:K2))
        CALL JEEXIN(NOOJB1,IRET)
        IF (IRET.EQ.0) THEN
          N2=IESSAI
          GOTO 30

        ELSE
          N1=IESSAI
          GOTO 30

        ENDIF
      ENDIF



   40 CONTINUE
      CALL CODENT(INUM,'D0',NOOJB1(K1:K2))
      NOOJB=NOOJB1


      END
