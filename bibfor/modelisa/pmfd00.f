      SUBROUTINE PMFD00()
      IMPLICIT  NONE
      INCLUDE  'jeveux.h'
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 22/04/2013   AUTEUR FLEJOU J-L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C                    COMMANDE AFFE_CARA_ELEM
C
C     TRAITEMENT DES MOTS CLES :
C           COQUE  /COQUE_NCOU
C           GRILLE /COQUE_NCOU
C           POUTRE /TUYAU_NCOU
C           POUTRE /TUYAU_NSEC
C
C     CONSTRUCTION DU CHAM_ELEM (CONSTANT PAR MAILLE) CONTENANT
C     LES INFORMATIONS DE "DECOUPAGE" DES ELEMENTS DE STRUCTURE :
C     NBRE DE COUCHES (COQUE), DE SECTEURS (TUYAU), "FIBRES" (PMF),..
C
C --- ------------------------------------------------------------------
C
      CHARACTER*32 JEXNUM,JEXNOM
C
      INTEGER     NCARFI
      PARAMETER  (NCARFI=3)
C
      INTEGER  NBOCC0,NBOCC1,NBOCC2,NBOCC3,NBOCC4
      INTEGER  IRET,IBID,IFM,NIV,IARG,IASBON,IASEDI,IASMAX
      INTEGER  NBVM,NMAILP,NUMAIL,NBFIB,ICODE,IGRAND,IMA,INOMCP
      INTEGER  II,JJ,IOC,IPOS,ICMP,IER,IZONE,NBCMP,NBEC,INDIK8
      INTEGER  IRA1,IRIY1,IRIZ1,IRVA1,IRVIY1,IRVIZ1
      INTEGER  JDNM,JNF,JMP
      INTEGER  JNBFG,NBGF,JNGF,JCARFI,JPOINT,IPOINT,NGF,IG,NG,IG1
      INTEGER  NUMMAI,NBMAZA,IDESC,ILIMA,IVALE,NBFIG
C
      INTEGER  RGCMPG
C     NB DE GROUPES MAX PAR ELEMENT
C     CE NOMBRE DOIT ETRE EN ACCORD AVEC LES CATALOGUES
C     GRANDEUR_SIMPLE__.CATA ET GENER_MEPMF1.CATA !
      INTEGER        NGMXEL
      CHARACTER*2    KNGMX
      PARAMETER     (NGMXEL=10,KNGMX='10')
      INTEGER        NUGRP(NGMXEL)
C
      REAL*8      ZERO
      REAL*8      CASECT(6),CARG(6)
      REAL*8      AIRPOU,MOINOY,MOINOZ,ERRE,PRECAI
      PARAMETER  (ZERO=0.D+0)
C
      CHARACTER*1    K1BID
      CHARACTER*8    CARELE,NOMO,NOMA,K8B,MODELE,SDGF,NGRAND
      CHARACTER*16   CONCEP,CMD,LTYMCL(3)
      CHARACTER*19   CESDEC,LIGRMO,CELBID
      CHARACTER*24   MODNOM,MOMMAI,VPOINT,VNBFIB,VCARFI,VNBFIG,RNOMGF
      CHARACTER*24   K24BID
C
      INTEGER        VALMI
      REAL*8         VALMR(4)
      CHARACTER*80   VALMK(2)
C
      DATA LTYMCL/'MAILLE','GROUP_MA','TOUT'/
C --- ------------------------------------------------------------------
      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)
C
      CALL GETVID(' ','MODELE',1,IARG,1,NOMO,NBVM)
C
      CALL GETRES(CARELE,CONCEP,CMD)
C
      VNBFIB = CARELE//'.PMFNF'
C
C --- -------------------------------------------------------
      CALL GETFAC('MULTIFIBRE',NBOCC0)
      IF(NBOCC0.NE.0)THEN
         CALL GETVID(' ','GEOM_FIBRE',1,IARG,1,SDGF,IBID)
         VNBFIG = SDGF//'.NB_FIBRE_GROUPE'
         VPOINT = SDGF//'.POINTEUR'
         VCARFI = SDGF//'.CARFI'
         RNOMGF = SDGF//'.NOMS_GROUPES'
C        NOMBRE DE GROUPES TOTAL = DIMENSION DE VNBFIG
         CALL JELIRA(VNBFIG,'LONMAX',NBGF,K1BID)
         CALL JEVEUO(VNBFIG,'L',JNBFG)
         CALL JEVEUO(VCARFI,'L',JCARFI)
         CALL JEVEUO(VPOINT,'L',JPOINT)
C        NOMBRE TOTAL DE FIBRES SUR TOUS LES GROUPES
         NBFIB=0
         DO 10 IG=1,NBGF
            NBFIB=NBFIB+ZI(JNBFG-1+IG)
10       CONTINUE
      ENDIF
C
      MODNOM = NOMO//'.MODELE    .LGRF'
      CALL JEVEUO(MODNOM,'L',JDNM)
      NOMA = ZK8(JDNM)
      MOMMAI = NOMA//'.NOMMAI'
      CALL JELIRA(MOMMAI,'NOMMAX',NMAILP,K1BID)
C
C --- -------------------------------------------------------
C     S'IL N'Y A PAS D'ELEMENTS A SOUS-POINTS, ON SAUTE TOUT:
      CALL GETFAC('COQUE',NBOCC1)
      CALL GETFAC('GRILLE',NBOCC2)
      CALL GETFAC('POUTRE',NBOCC3)
      CALL GETFAC('MEMBRANE',NBOCC4)
      IF ((NBOCC0+NBOCC1+NBOCC2+NBOCC3+NBOCC4).EQ.0) GO TO 9999
C     2EME CHANCE :
      CALL GETVID(' ','MODELE',0,IARG,1,MODELE,IBID)
      LIGRMO = MODELE//'.MODELE'
      CELBID='&&PMFD00.CELBID'
      CALL ALCHML(LIGRMO,'TOU_INI_ELEM','PNBSP_I','V',CELBID,IRET,' ')
      CALL DETRSD('CHAM_ELEM',CELBID)
      IF (IRET.EQ.1) GO TO 9999
C     S'IL N'Y A PAS D'ELEMENTS PMF, ON SAUTE :
      IF (NBOCC0.EQ.0) GO TO 200
C
C --- -------------------------------------------------------
C     CONSTRUCTION DES OBJETS .PMFPT .PMFNF ET .PMFCF
      CALL WKVECT('&&PMFD00.NOMS_GROUPES','V V K24',NBGF,JNGF)
      CALL WKVECT(VNBFIB,'V V I',NMAILP*(2+NGMXEL),JNF)
C
C     DESCRIPTEUR DE LA CARTE
      CALL JEVEUO(CARELE//'.CARGENPO  .DESC','L',IDESC)
C     NUMERO DE LA GRANDEUR DANS LE CATALOGUE DE GRANDEUR
      IGRAND = ZI(IDESC)
C     NOMBRE DE ZONE MAX DANS LA CARTE
      IASMAX = ZI(IDESC+1)
C     NOMBRE DE ZONE AFFECTEE DANS LA CARTE
      IASEDI = ZI(IDESC+2)
C     NOM DE LA GRANDEUR
      CALL JENUNO(JEXNUM('&CATA.GD.NOMGD',IGRAND),NGRAND)
C     NOMBRE ET NOM DES COMPOSANTES DANS LA GRANDEUR
      CALL JELIRA(JEXNUM('&CATA.GD.NOMCMP',IGRAND),'LONMAX',NBCMP,K1BID)
      CALL JEVEUO(JEXNUM('&CATA.GD.NOMCMP',IGRAND),'L',INOMCP)
C     NOMBRE D'ENTIER CODE DANS LA CARTE
      CALL DISMOI('F','NB_EC',NGRAND,'GRANDEUR',NBEC,K8B,IER)
C
      VALMI = 0
      DO 100 IOC=1,NBOCC0
         DO 110 IG=1,NGMXEL
            NUGRP(IG)=0
110      CONTINUE
         CALL RELIEM(NOMO,NOMA,'NU_MAILLE','MULTIFIBRE',IOC,2,
     &               LTYMCL,LTYMCL,'&&PMFD00.MAILLSEP',NMAILP)
         CALL JEVEUO('&&PMFD00.MAILLSEP','L',JMP)
C ---    NOMBRE DE GROUPES A AFFECTER
         CALL GETVTX('MULTIFIBRE','GROUP_FIBRE',IOC,IARG,0,K8B,NGF)
         NGF=-NGF
         IF (NGF.GT.NGMXEL) THEN
            CALL U2MESK('F','MODELISA8_7',1,KNGMX)
         ENDIF
C ---    NOMS DES GROUPES A AFFECTER
         CALL GETVTX('MULTIFIBRE','GROUP_FIBRE',IOC,IARG,NGF,
     &               ZK24(JNGF),IBID)
C ---    NOMBRE DE FIBRES DE L'ENSEMBLE DES GROUPES
C        ON NOTE LES NUMEROS DE GROUPES
         NBFIB=0
         DO 120 IG=1,NGF
            CALL JENONU(JEXNOM(RNOMGF,ZK24(JNGF+IG-1)),NG)
            NBFIB     = NBFIB+ZI(JNBFG+NG-1)
            NUGRP(IG) = NG
120      CONTINUE
C
C        ON AFFECTE LES ELEMENTS POUTRES CONCERNES PAR CETTE OCCURENCE
C        POUR CHAQUE EL : NB DE FIBRE, NB DE GROUPES DE FIBRES
C        ET NUMERO DES GROUPES
         DO 130 JJ = 1, NMAILP
            NUMAIL     = ZI(JMP+JJ-1)
            IPOS       = JNF+(NUMAIL-1)*(2+NGMXEL)
            ZI(IPOS)   = NBFIB
            ZI(IPOS+1) = NGF
            DO 131 IG = 1, NGMXEL
               ZI(IPOS+1+IG) = NUGRP(IG)
131         CONTINUE
130      CONTINUE
C
C        INTEGRATION POUR TOUS LES GROUPES DE CETTE OCCURENCE
         DO 135 II = 1, 6
            CASECT(II)=ZERO
135      CONTINUE
         DO 140 IG = 1, NGF
            IG1    = NUGRP(IG)
            NBFIG  = ZI(JNBFG -1+IG1)
            IPOINT = ZI(JPOINT-1+IG1)
            CALL PMFITG(NBFIG,NCARFI,ZR(JCARFI+IPOINT-1),CARG)
            DO 141 II = 1, 6
               CASECT(II) = CASECT(II) + CARG(II)
141         CONTINUE
140      CONTINUE
C
C        BOUCLE SUR LES MAILLES
         DO 150 IMA = 1 , NMAILP
            NUMMAI=ZI(JMP + IMA -1)
C           RECHERCHE DE LA ZONE COMTENANT NUMMAI
            IASBON = 0
            DO 155 II = 1 , IASEDI
               ICODE = ZI(IDESC+3+2*(II-1))
               IZONE = ZI(IDESC+3+2*(II-1)+1)
C              SI C'EST UNE LISTE DE MAILLE
               IF ( ICODE.EQ.3 ) THEN
                  K24BID = CARELE//'.CARGENPO  .LIMA'
                  CALL JEVEUO(JEXNUM(K24BID,IZONE),'L',ILIMA)
                  CALL JELIRA(JEXNUM(K24BID,IZONE),'LONMAX',NBMAZA,
     &                        K1BID)
C              SI C'EST UN GROUPE DE MAILLE
               ELSEIF ( ICODE.EQ.2 )THEN
                  K24BID = NOMA//'.GROUPEMA'
                  CALL JEVEUO(JEXNUM(K24BID,IZONE),'L',ILIMA)
                  CALL JELIRA(JEXNUM(K24BID,IZONE),'LONMAX',NBMAZA,
     &                        K1BID)
C              SI C'EST TOUT LE MAILLAGE
               ELSEIF ( ICODE.EQ.1 ) THEN
                  IASBON = II
                  GOTO 160
               ELSE
                  CALL ASSERT( .FALSE. )
               ENDIF
C              MAILLE DANS LISTE OU GROUPE DE MAILLE DE CETTE ZONE
               DO 152 JJ=1,NBMAZA
                  IF (NUMMAI.EQ.ZI(ILIMA+JJ-1)) THEN
                     IASBON = II
                     GOTO 160
                  ENDIF
152            CONTINUE
155         CONTINUE
            IF (IASBON.EQ.0) THEN
               CALL JENUNO(JEXNUM(MOMMAI,NUMMAI),VALMK(1))
               CALL U2MESK('F','ALGELINE_34',1,VALMK(1))
            ENDIF
160         CONTINUE
C           RANG DES COMPOSANTES A1, IY1, IZ1 DANS LA CARTE
            IRA1  = INDIK8( ZK8(INOMCP), 'A1' , 1, NBCMP )
            IRIY1 = INDIK8( ZK8(INOMCP), 'IY1', 1, NBCMP )
            IRIZ1 = INDIK8( ZK8(INOMCP), 'IZ1', 1, NBCMP )
            IF (IRA1.EQ.0 .OR. IRIY1.EQ.0 .OR. IRIY1.EQ.0) THEN
               CALL JENUNO(JEXNUM(MOMMAI,NUMMAI),VALMK(1))
               CALL U2MESK('F','MODELISA8_3',1,VALMK)
            ENDIF
C           ENTIER CODE DE LA ZONE
            ICODE = ZI(IDESC+3+2*IASMAX+NBEC*(IASBON-1))
C           RANG DE LA VALEUR DANS L'ENTIER CODE (0 SI N'EXISTE PAS)
            IRVA1  = RGCMPG(ICODE,IRA1)
            IRVIY1 = RGCMPG(ICODE,IRIY1)
            IRVIZ1 = RGCMPG(ICODE,IRIZ1)
            IF (IRVA1.EQ.0 .OR. IRVIY1.EQ.0 .OR. IRVIZ1.EQ.0) THEN
               CALL JENUNO(JEXNUM(MOMMAI,NUMMAI),VALMK(1))
               CALL U2MESK('F','MODELISA8_3',1,VALMK)
            ENDIF
C           ON RECUPERE LES COMPOSANTES : A1, IY1, IZ1 DE CETTE ZONE
            CALL JEVEUO(CARELE//'.CARGENPO  .VALE','L',IVALE)
            AIRPOU=ZR(IVALE+(IASBON-1)*NBCMP + IRVA1  - 1)
            MOINOY=ZR(IVALE+(IASBON-1)*NBCMP + IRVIY1 - 1)
            MOINOZ=ZR(IVALE+(IASBON-1)*NBCMP + IRVIZ1 - 1)
            IF (AIRPOU.EQ.ZERO) THEN
               CALL JENUNO(JEXNUM(MOMMAI,NUMMAI),VALMK(1))
               CALL U2MESK('F','MODELISA8_1',1,VALMK)
            ENDIF
            IF (MOINOY.EQ.ZERO) THEN
               CALL JENUNO(JEXNUM(MOMMAI,NUMMAI),VALMK(1))
               VALMK(2) = 'IY'
               CALL U2MESK('F','MODELISA8_2',2,VALMK)
            ENDIF
            IF (MOINOZ.EQ.ZERO) THEN
               CALL JENUNO(JEXNUM(MOMMAI,NUMMAI),VALMK(1))
               VALMK(2) = 'IZ'
               CALL U2MESK('F','MODELISA8_2',2,VALMK)
            ENDIF
C           COMPARAISON DE LA SOMME DES AIRES DES FIBRES
            CALL GETVR8('MULTIFIBRE','PREC_AIRE',IOC,IARG,1,
     &                  PRECAI,IRET)
            ERRE=ABS(AIRPOU-CASECT(1))/AIRPOU
            IF (ERRE.GT.PRECAI) THEN
               VALMI    = IOC
               CALL JENUNO(JEXNUM(MOMMAI,NUMMAI),VALMK(1))
               VALMR(1) = AIRPOU
               VALMR(2) = CASECT(1)
               VALMR(3) = ERRE
               VALMR(4) = PRECAI
               CALL U2MESG('E','MODELISA8_4',1,VALMK,1,VALMI,4,VALMR)
            ENDIF
C           COMPARAISON DES MOMENTS D'INERTIES : IY, IZ
            CALL GETVR8('MULTIFIBRE','PREC_INERTIE',IOC,IARG,1,
     &                  PRECAI,IRET)
            ERRE=ABS(MOINOY-CASECT(5))/MOINOY
            IF (ERRE.GT.PRECAI) THEN
               VALMI    = IOC
               CALL JENUNO(JEXNUM(MOMMAI,NUMMAI),VALMK(1))
               VALMK(2) = 'IY'
               VALMR(1) = MOINOY
               VALMR(2) = CASECT(5)
               VALMR(3) = ERRE
               VALMR(4) = PRECAI
               CALL U2MESG('E','MODELISA8_5',2,VALMK,1,VALMI,4,VALMR)
            ENDIF
            ERRE=ABS(MOINOZ-CASECT(4))/MOINOZ
            IF (ERRE.GT.PRECAI) THEN
               VALMI    = IOC
               CALL JENUNO(JEXNUM(MOMMAI,NUMMAI),VALMK(1))
               VALMK(2) = 'IY'
               VALMR(1) = MOINOZ
               VALMR(2) = CASECT(4)
               VALMR(3) = ERRE
               VALMR(4) = PRECAI
               CALL U2MESG('E','MODELISA8_5',2,VALMK,1,VALMI,4,VALMR)
            ENDIF
150      CONTINUE
100   CONTINUE
      IF ( VALMI.NE.0 ) THEN
         CALL U2MESS('F','MODELISA8_6')
      ENDIF
C
C
200   CONTINUE
C
C --- -------------------------------------------------------
C     TRAITEMENT DES MOTS CLES COQUE_NCOU,TUYAU_NCOU, ...
      CESDEC = '&&PMFD00.CESDEC'
      CALL PMFD02(NOMA,CESDEC)
C
C --- -------------------------------------------------------
C     CONSTRUCTION DES CHAM_ELEM '.CANBSP' ET '.CAFIBR'
      CALL PMFD01(NOMA,CARELE,VNBFIB,VPOINT,VCARFI,VNBFIG,CESDEC,NGMXEL)
      CALL JEDETR(VNBFIB)
      CALL DETRSD('CHAM_ELEM_S',CESDEC)
      IF (NIV.EQ.2) THEN
         CALL IMPRSD('CHAMP',CARELE//'.CANBSP',6,'INFO=2')
         CALL IMPRSD('CHAMP',CARELE//'.CAFIBR',6,'INFO=2')
      END IF
9999  CONTINUE
      CALL JEDEMA()
      END
