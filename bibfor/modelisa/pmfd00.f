      SUBROUTINE PMFD00()
C MODIF MODELISA  DATE 31/08/2004   AUTEUR VABHHTS J.PELLET 
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C     COMMANDE AFFE_CARA_ELEM
C --- TRAITEMENT DES MOTS CLES AFFE_SECT ET AFFE_FIBRE
C     + TRAITEMENT DES MOTS CLES :
C           COQUE  /COQUE_NCOU
C           GRILLE /COQUE_NCOU
C           POUTRE /TUYAU_NCOU
C           POUTRE /TUYAU_NSEC
C
C       CONSTRUCTION DU CHAM_ELEM (CONSTANT PAR MAILLE) CONTENANT
C       LES INFORMATIONS DE "DECOUPAGE" DES ELEMENTS DE STRUCTURE :
C        NBRE DE COUCHES (COQUE), DE SECTEURS (TUYAU), "FIBRES" (PMF),..

C ----------------------------------------------------------------------
      IMPLICIT NONE
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER NBVAL,ITROIS,NCARFI
      PARAMETER (NBVAL=999,NCARFI=3)
      REAL*8 VAL(NBVAL),PI4,DTROIS,ZERO
      PARAMETER (PI4=0.785398163397D+0,DTROIS=3.D+0,ITROIS=3,ZERO=0.D+0)
      CHARACTER*1 K1BID
      CHARACTER*6 KNUMAI,KNBV,KIOC
      CHARACTER*8 CARELE,NOMO,NOMA,K8B,NOMAS,KTYMA,KSUDI,MODELE,KNS
      CHARACTER*16 CONCEP,CMD
      CHARACTER*16 LIMCLS(3),LTYMCL(3),PHENO
      CHARACTER*19 CESDEC,LIGRMO,CELBID
      CHARACTER*24 MODNOM
      CHARACTER*24 MLGNMA
      CHARACTER*24 MLGCNX,MLGTMS,MLGCOO
      CHARACTER*24 VPOINT,VNBFIB,VCARFI
      INTEGER NBOCCS,NBOCCP,NBOCC1,NBOCC2,NBOCC3
      INTEGER NGM,NMA,NDGM,NDMA,LMAX,LMAX1
      INTEGER NGMS,NMAS,NDGMS,NDMAS,LMAXS
      INTEGER IRET,IBID
      INTEGER NBVM,NMAILP,NBV,NMAILS,NUMAIL,NBFIB
      INTEGER IN,I,J,NO,NNO,NUMMAI,IOC,IPOS,IPOINT
      INTEGER JDLS,JDNM,JDTM,JDCO,JDNO,JMS,JPO,JCF,JNF,JMP
      REAL*8 X(4),Y(4),CENTRE(2),AXEP(2),SURF,CASECT(6)
      INTEGER NTSEG2,NTTRI3,NTQUA4,NUTYMA

      INTEGER IFM,NIV

      DATA LIMCLS/'MAILLE_SECT','GROUP_MA_SECT','TOUT_SECT'/
      DATA LTYMCL/'MAILLE','GROUP_MA','TOUT'/
C     ------------------------------------------------------------------

      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)

      CALL GETVID(' ','MODELE',1,1,1,NOMO,NBVM)
      CALL DISMOI('F','PHENOMENE',NOMO,'MODELE',IBID,PHENO,IBID)
      IF (PHENO.NE.'MECANIQUE') GO TO 9999


      CALL GETRES(CARELE,CONCEP,CMD)
      VPOINT = CARELE//'.PMFPT'
      VNBFIB = CARELE//'.PMFNF'
      VCARFI = CARELE//'.PMFCF'

      MODNOM = NOMO//'.MODELE    .NOMA'
      CALL JEVEUO(MODNOM,'L',JDNM)
      NOMA = ZK8(JDNM)
      MLGNMA = NOMA//'.NOMMAI'
      CALL JELIRA(MLGNMA,'NOMMAX',NMAILP,K1BID)


      CALL GETFAC('AFFE_SECT',NBOCCS)
      CALL GETFAC('AFFE_FIBRE',NBOCCP)


C     S'IL N'Y A PAS D'ELEMENTS A SOUS-POINTS, ON SAUTE TOUT:
C     -------------------------------------------------------
      CALL GETFAC('COQUE',NBOCC1)
      CALL GETFAC('GRILLE',NBOCC2)
      CALL GETFAC('POUTRE',NBOCC3)
      IF ((NBOCCS+NBOCCP+NBOCC1+NBOCC2+NBOCC3).EQ.0) GO TO 9999

C     -- 2EME CHANCE :
      CALL GETVID(' ','MODELE',0,1,1,MODELE,IBID)
      LIGRMO = MODELE//'.MODELE'
      CELBID='&&PMFD00.CELBID'
      CALL ALCHML(LIGRMO,'TOU_INI_ELEM','PNBSP_I','V',CELBID,IRET,' ')
      CALL DETRSD('CHAM_ELEM',CELBID)
      IF (IRET.EQ.1) GO TO 9999


C     S'IL N'Y A PAS D'ELEMENTS PMF, ON SAUTE :
C     ----------------------------------------
      IF ((NBOCCS+NBOCCP).EQ.0) GO TO 140


C --- VERIFICATION DE SYNTAXE
C --- ET CALCUL DE LONGUEUR MAXI DE MAILLE/GROUP_MA POUTRE ET SECTION

C --- POUR LES AFFE_SECT
      NDGM = 0
      NDMA = 0
      NDGMS = 0
      NDMAS = 0

      DO 10 IOC = 1,NBOCCS
        CALL GETVR8('AFFE_SECT','COOR_AXE_POUTRE',IOC,1,2,VAL,IBID)

C ---   RECHERCHE DE LONGUEUR MAXI
        CALL GETVEM(NOMA,'GROUP_MA','AFFE_SECT','GROUP_MA',IOC,1,0,K8B,
     +              NGM)
        CALL GETVEM(NOMA,'MAILLE','AFFE_SECT','MAILLE',IOC,1,0,K8B,NMA)
        NDGM = MAX(NDGM,-NGM)
        NDMA = MAX(NDMA,-NMA)
        CALL GETVEM(NOMA,'GROUP_MA_SECT','AFFE_SECT','GROUP_MA_SECT',
     +              IOC,1,0,K8B,NGMS)
        CALL GETVEM(NOMA,'MAILLE_SECT','AFFE_SECT','MAILLE_SECT',IOC,1,
     +              0,K8B,NMAS)
        NDGMS = MAX(NDGMS,-NGMS)
        NDMAS = MAX(NDMAS,-NMAS)
   10 CONTINUE
      LMAX = MAX(NDGM,NDMA)
      LMAXS = MAX(NDGMS,NDMAS)


C --- POUR LES AFFE_FIBRE MAINTENANT
      NDGM = 0
      NDMA = 0
      DO 20 IOC = 1,NBOCCP
        CALL GETVR8('AFFE_FIBRE','COOR_AXE_POUTRE',IOC,1,2,VAL,IBID)

C ---   VERIF MULTIPLE DE 3 POUR 'VALE' DANS 'AFFE_FIBRE'
        CALL GETVR8('AFFE_FIBRE','VALE',IOC,1,NBVAL,VAL,NBV)
        IF (DBLE(NBV)/DTROIS.NE.NBV/ITROIS) THEN
C ---     ON RECUPERE LE NOM EVENTUEL DE LA SECTION POUR LES MESSAGES
          CALL GETVTX('AFFE_FIBRE','NOM',IOC,1,1,KNS,IRET)
          IF(IRET.EQ.0)THEN
            CALL CODENT(IOC,'G',KIOC)
            KNS='PONCT_'//KIOC
          ENDIF
          CALL CODENT(NBV,'G',KNBV)
          CALL UTMESS('F',CMD,'AFFE_FIBRE POUR "'//KNS//
     +          '": IL Y A '//KNBV//' VALEURS POUR "VALE",'//
     +          ' CE DEVRAIT ETRE UN MULTIPLE DE 3')
        END IF
        CALL GETVEM(NOMA,'GROUP_MA','AFFE_FIBRE','GROUP_MA',IOC,1,0,K8B,
     +              NGM)
        CALL GETVEM(NOMA,'MAILLE','AFFE_FIBRE','MAILLE',IOC,1,0,K8B,NMA)
        NDGM = MAX(NDGM,-NGM)
        NDMA = MAX(NDMA,-NMA)
   20 CONTINUE
      LMAX1 = MAX(NDGM,NDMA)
      LMAX = MAX(LMAX,LMAX1)


C --- RECUPERATION DES NUMEROS DES TYPES MAILLES TRI3,QUA4
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TRIA3'),NTTRI3)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD4'),NTQUA4)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','SEG2'),NTSEG2)


C --- COMPTAGE DU NOMBRE DE FIBRES TOTAL
      LMAX1 = MAX(LMAX,LMAXS)
      CALL WKVECT('&&PMFD00.LISTE','V V K8',LMAX1,JDLS)

      NBFIB = 0
      DO 40 IOC = 1,NBOCCS
C ---   LES SECTIONS MAINTENANT
        CALL GETVID('AFFE_SECT','MAILLAGE_SECT',IOC,1,1,NOMAS,NBV)
        MLGTMS = NOMAS//'.TYPMAIL'

C ---   VERIFICATION MAILLES TRIA3 ET QUAD4 UNIQUEMENT
        CALL JEVEUO(MLGTMS,'L',JDTM)
C ---   NOMBRE DE FIBRES = NOMBRE DE MAILLES CONCERNEES
        CALL RELIEM(' ',NOMAS,'NU_MAILLE','AFFE_SECT',IOC,3,LIMCLS,
     +              LTYMCL,'&&PMFD00.MAILLSEC',NMAILS)
        NBFIB = NBFIB + NMAILS
        CALL JEVEUO('&&PMFD00.MAILLSEC','L',JMS)
        DO 30 J = 1,NMAILS
          NUMMAI = ZI(JMS+J-1)
          NUTYMA = ZI(JDTM+NUMMAI-1)
          IF (NUTYMA.NE.NTSEG2) THEN
            IF (NUTYMA.NE.NTTRI3 .AND. NUTYMA.NE.NTQUA4) THEN
              CALL CODENT(NUMMAI,'G',KNUMAI)
              CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',NUTYMA),KTYMA)
              CALL UTMESS('F',CMD,'DANS LE MAILLAGE "'//NOMAS//
     +                    '" LA MAILLE "'//KNUMAI//'" EST DE TYPE "'//
     +                    KTYMA//'" (NI TRIA3 NI QUAD4)')
            END IF
          ELSE
C ---        ON DEDUIT LES SEG2 DU NB DE FIBRES
            NBFIB = NBFIB - 1
          END IF
   30   CONTINUE
   40 CONTINUE


      DO 50 IOC = 1,NBOCCP
C ---   NOMBRE DE FIBRES PONCTUELLES
        CALL GETVR8('AFFE_FIBRE','VALE',IOC,1,NBVAL,VAL,NBV)
        NBFIB = NBFIB + NBV/ITROIS
   50 CONTINUE



C     2. CONSTRUCTION DES OBJETS .PMFPT .PMFNF ET .PMFCF
C     ----------------------------------------------------------

      CALL WKVECT(VNBFIB,'V V I',NMAILP,JNF)
      CALL WKVECT(VPOINT,'V V I',NMAILP,JPO)
      CALL WKVECT(VCARFI,'V V R',NBFIB*NCARFI,JCF)

      IPOINT = 1
C -----------------------------------------------------------------
C --- TRAITEMENT DES AFFE_SECT
C -----------------------------------------------------------------
      DO 90 IOC = 1,NBOCCS
C ---   ON RECUPERE LE NOM EVENTUEL DE LA SECTION
        CALL GETVTX('AFFE_SECT','NOM',IOC,1,1,KNS,IRET)
        IF(IRET.EQ.0)THEN
          CALL CODENT(IOC,'G',KIOC)
          KNS='SECT_'//KIOC
        ENDIF
        IF(NIV.EQ.2)WRITE(IFM,1000)KNS
C ---   ON RECUPERE LE MAILLAGE
        CALL GETVID('AFFE_SECT','MAILLAGE_SECT',IOC,1,1,NOMAS,NBV)
C ---   RECUPERATION DES COORDONNEES DE L'AXE DE LA POUTRE
        CALL GETVR8('AFFE_SECT','COOR_AXE_POUTRE',IOC,1,2,AXEP,IRET)
        IF (IRET.NE.2) THEN
          AXEP(1) = ZERO
          AXEP(2) = ZERO
        END IF
C ---   RECONSTRUCTION DES NOMS JEVEUX DU CONCEPT MAILLAGE ASSOCIE
        MLGTMS = NOMAS//'.TYPMAIL'
        MLGCNX = NOMAS//'.CONNEX'
        MLGCOO = NOMAS//'.COORDO    .VALE'

C --- RECUPERATION DES ADRESSES JEVEUX UTILES
        CALL JEVEUO(MLGTMS,'L',JDTM)
        CALL JEVEUO(MLGCOO,'L',JDCO)

C ---  ON RECUPERE LES MAILLES DE LA SECTION CONCERNEES
        CALL RELIEM(' ',NOMAS,'NU_MAILLE','AFFE_SECT',IOC,3,LIMCLS,
     +              LTYMCL,'&&PMFD00.MAILLSEC',NMAILS)
        CALL JEVEUO('&&PMFD00.MAILLSEC','L',JMS)
        NBFIB = 0
        DO 70 J = 1,NMAILS
          NUMMAI = ZI(JMS+J-1)
C ---     COORDONNEES NOEUDS
          NUTYMA = ZI(JDTM+NUMMAI-1)
          IF (NUTYMA.EQ.NTSEG2) GO TO 70
          NBFIB = NBFIB + 1
          CALL JEVEUO(JEXNUM(MLGCNX,NUMMAI),'L',JDNO)
          NNO = 3
          IF (NUTYMA.EQ.NTQUA4) NNO = 4
          DO 60 IN = 1,NNO
            NO = ZI(JDNO-1+IN)
            X(IN) = ZR(JDCO+ (NO-1)*3) - AXEP(1)
            Y(IN) = ZR(JDCO+ (NO-1)*3+1) - AXEP(2)
   60     CONTINUE
C ---     SURFACE ET CENTRE
          CALL PMFSCE(NNO,X,Y,SURF,CENTRE)
C --- STOCKAGE DES CARACTERISTIQUES DE FIBRES DANS
          IPOS = JCF + IPOINT - 1 + NCARFI* (NBFIB-1)
          ZR(IPOS) = CENTRE(1)
          ZR(IPOS+1) = CENTRE(2)
          ZR(IPOS+2) = SURF
          IF (NIV.EQ.2) WRITE (IFM,1001) NBFIB,CENTRE,SURF
   70   CONTINUE
C ---   IMPRESSION DES CARACTERISTIQUES GLOBALES
        IF (NIV.EQ.2) THEN
          CALL PMFITG(NBFIB,NCARFI,ZR(JCF+IPOINT-1),CASECT)
          WRITE(IFM,1002)KNS,CASECT
          WRITE(IFM,1003)KNS,CASECT(2)/CASECT(1),CASECT(3)/CASECT(1)
        END IF
C --- ON AFFECTE LES ELEMENTS POUTRES CONCERNES PAR CE PAQUET DE FIBRES
C --- POUR L'INSTANT : UN POINTEUR (SUR CARFIB) ET UN NB DE FIBRE POUR
C     CHAQUE EL
C ---  ON RECUPERE LES MAILLES DE LA POUTRE CONCERNEES
        CALL RELIEM(NOMO,NOMA,'NU_MAILLE','AFFE_SECT',IOC,2,LTYMCL,
     +              LTYMCL,'&&PMFD00.MAILLSEP',NMAILP)
        CALL JEVEUO('&&PMFD00.MAILLSEP','L',JMP)
        DO 80 J = 1,NMAILP
          NUMAIL = ZI(JMP+J-1)
          ZI(JPO+NUMAIL-1) = IPOINT
          ZI(JNF+NUMAIL-1) = NBFIB
   80   CONTINUE
        IPOINT = IPOINT + NBFIB*NCARFI
   90 CONTINUE
C -----------------------------------------------------------------
C --- TRAITEMENT DES AFFE_FIBRE
C -----------------------------------------------------------------
      DO 120 IOC = 1,NBOCCP
C ---   ON RECUPERE LE NOM EVENTUEL DE LA SECTION
        CALL GETVTX('AFFE_FIBRE','NOM',IOC,1,1,KNS,IRET)
        IF(IRET.EQ.0)THEN
          CALL CODENT(IOC,'G',KIOC)
          KNS='PONCT_'//KIOC
        ENDIF
        IF (NIV.EQ.2) WRITE (IFM,2000) KNS
C ---   SURFACE OU DIAMETRE
        CALL GETVTX('AFFE_FIBRE','CARA',IOC,1,1,KSUDI,IRET)
        IF (IRET.EQ.0) KSUDI = 'SURFACE '
        CALL GETVR8('AFFE_FIBRE','VALE',IOC,1,NBVAL,VAL,NBV)
C ---   RECUPERATION DES COORDONNEES DE L'AXE DE LA POUTRE
        CALL GETVR8('AFFE_FIBRE','COOR_AXE_POUTRE',IOC,1,2,AXEP,IRET)
        IF (IRET.NE.2) THEN
          AXEP(1) = ZERO
          AXEP(2) = ZERO
        END IF
C ---   CHANGER DIAMETRE EN SURFACE LE CAS ECHEANT
        NBFIB = 0
        DO 100 I = 1,NBV/3
          IF (KSUDI.EQ.'DIAMETRE') THEN
            SURF = VAL(3*I)*VAL(3*I)*PI4
          ELSE
            SURF = VAL(3*I)
          END IF
          IF (IRET.EQ.2) THEN
            CENTRE(1) = VAL(3*I-2) - AXEP(1)
            CENTRE(2) = VAL(3*I-1) - AXEP(2)
          ELSE
            CENTRE(1) = VAL(3*I-2)
            CENTRE(2) = VAL(3*I-1)
          END IF
C ---     STOCKAGE DES CARACTERISTIQUES DE FIBRES DANS
          IPOS = JCF + IPOINT - 1 + NCARFI* (I-1)
          ZR(IPOS) = CENTRE(1)
          ZR(IPOS+1) = CENTRE(2)
          ZR(IPOS+2) = SURF
          NBFIB = NBFIB + 1
          IF (NIV.EQ.2) WRITE (IFM,1001) NBFIB,CENTRE,SURF
  100   CONTINUE
C ---   IMPRESSION DES CARACTERISTIQUES GLOBALES
        IF (NIV.EQ.2) THEN
          CALL PMFITG(NBFIB,NCARFI,ZR(JCF+IPOINT-1),CASECT)
          WRITE(IFM,1002)KNS,CASECT
          WRITE(IFM,1003)KNS,CASECT(2)/CASECT(1),CASECT(3)/CASECT(1)
        END IF
C --- ON AFFECTE LES ELEMENTS POUTRES CONCERNES PAR CE PAQUET DE FIBRES
C --- POUR L'INSTANT : UN POINTEUR (SUR CARFIB) ET UN NB DE FIBRE
C     POUR CHAQUE EL
C ---    ON RECUPERE LES MAILLES DE LA POUTRE CONCERNEES
        CALL RELIEM(NOMO,NOMA,'NU_MAILLE','AFFE_FIBRE',IOC,2,LTYMCL,
     +              LTYMCL,'&&PMFD00.MAILLSEP',NMAILP)
        CALL JEVEUO('&&PMFD00.MAILLSEP','L',JMP)
        DO 110 J = 1,NMAILP
          NUMAIL = ZI(JMP+J-1)
          ZI(JPO+NUMAIL-1) = IPOINT
          ZI(JNF+NUMAIL-1) = NBFIB
  110   CONTINUE
        IPOINT = IPOINT + NBFIB*NCARFI
  120 CONTINUE

  130 CONTINUE
      CALL JEDETR('&&PMFD00.LISTE')
      CALL JEDETR('&&PMFD00.MAILLSEP')
      CALL JEDETR('&&PMFD00.MAILLSEC')
  140 CONTINUE


C     3. TRAITEMENT DES MOTS CLES COQUE_NCOU,TUYAU_NCOU, ...
C     ----------------------------------------------------------
      CESDEC = '&&PMFD00.CESDEC'
      CALL PMFD02(NOMA,CESDEC)


C     4. CONSTRUCTION DES CHAM_ELEM '.CANBSP' ET '.CAFIBR'
C     ----------------------------------------------------------
      CALL PMFD01(NOMA,CARELE,VNBFIB,VPOINT,VCARFI,CESDEC)

      CALL JEDETR(VNBFIB)
      CALL JEDETR(VPOINT)
      CALL JEDETR(VCARFI)
      CALL DETRSD('CHAM_ELEM_S',CESDEC)

      IF (NIV.EQ.2) THEN
        CALL IMPRSD('CHAMP',CARELE//'.CANBSP',6,'INFO=2')
        CALL IMPRSD('CHAMP',CARELE//'.CAFIBR',6,'INFO=2')
      END IF


 9999 CONTINUE


 1000 FORMAT(//,'DETAIL DES FIBRES SURFACIQUES DE LA SECTION "',A8,
     +     '"',/,'NUMF       Y           Z         SURF')
 1001 FORMAT(I4,3(2X,D10.4))
 1002 FORMAT(/,'CARACTERISTIQUES GLOBALES DE LA SECTION "',A8,
     +      '"',/,'(DANS LE REPERE LOCAL)',/,
     +     '   AIRE        MS/0Z       MS/OY       MQ/OZ',
     +     '       MQ/OY       MP/OYZ',/,D10.4,5(2X,D10.4))
 1003 FORMAT('POSITION DU CENTRE DE GRAVITE DE LA SECTION "',A8,'"',
     +     /,'(PAR RAPPORT A L''AXE DEFINI POUR LA POUTRE)',/,
     +     'CGY : ',D18.12,'    CGZ : ',D18.12)
 2000 FORMAT(//,'DETAIL DES FIBRES PONCTUELLES DE LA SECTION "',A8,
     +     '"',/,'NUMF       Y           Z         SURF')

      CALL JEDEMA()
      END
