      SUBROUTINE PMFD02(NOMA,CESDEC)
      IMPLICIT NONE
      CHARACTER*8    NOMA
      CHARACTER*19   CESDEC
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 12/02/2013   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C                       AFFE_CARA_ELEM
C
C       CONSTRUCTION DU CHAM_ELEM_S DE NBSP_I (CESDEC)
C          IMA ->  COQ_NCOU   TUY_NCOU   TUY_NSEC
C
C     TRAITEMENT DES MOTS CLES :
C           COQUE  / COQUE_NCOU
C           POUTRE / TUYAU_NCOU
C           POUTRE / TUYAU_NSEC
C
C ----------------------------------------------------------------------
      INCLUDE 'jeveux.h'

      INTEGER        NBOCC,IOCC,IRET,NBMA,NBCOU,NBV,NBSEC
      INTEGER        NBAP,K,I,JNCMP,JVALV,JMA,IARG
      CHARACTER*8    K8B
      CHARACTER*16   MOCLES(2),TYPMCL(2),MOCLEF(2)
      CHARACTER*19   CARTE
      CHARACTER*24   MESMAI

      DATA MOCLES/'MAILLE','GROUP_MA'/
      DATA TYPMCL/'MAILLE','GROUP_MA'/
      DATA MOCLEF/'COQUE','POUTRE'/
C     ------------------------------------------------------------------
      CALL JEMARQ()

      MESMAI = '&&PMFD02.MES_MAILLES'
      NBAP = 0
      DO 100 I = 1 , 2
         CALL GETFAC ( MOCLEF(I), NBOCC )
         NBAP = NBAP + NBOCC
         DO 105 K = 1 , NBOCC
            CALL RELIEM(' ', NOMA, 'NU_MAILLE', MOCLEF(I), K, 2,
     &                  MOCLES, TYPMCL, MESMAI, NBMA )
            IF ( NBMA .NE. 0 )  CALL JEDETR ( MESMAI )
105      CONTINUE
100   CONTINUE

      IF (NBAP.EQ.0) THEN
         CALL CESCRE('V',CESDEC,'ELEM',NOMA,'NBSP_I',1,'COQ_NCOU',
     &               -1,-1,-1)
         GO TO 9999
      END IF

      CARTE='&&PMFD02.NBSP_I'
      CALL ALCART ( 'V', CARTE, NOMA, 'NBSP_I' )
      CALL JEVEUO ( CARTE//'.NCMP', 'E', JNCMP )
      CALL JEVEUO ( CARTE//'.VALV', 'E', JVALV )

C --- MOT CLE "COQUE" :
      CALL GETFAC ( 'COQUE', NBOCC )
      DO 210 IOCC = 1,NBOCC
         CALL RELIEM(' ',NOMA,'NU_MAILLE','COQUE',IOCC,2,MOCLES,
     &               TYPMCL,MESMAI,NBMA)

         CALL GETVIS('COQUE','COQUE_NCOU',IOCC,IARG,1,NBCOU,NBV)
         ZK8(JNCMP-1+1) = 'COQ_NCOU'
         ZI(JVALV-1+1)  =  NBCOU

         CALL JEVEUO(MESMAI,'L',JMA )
         CALL NOCART(CARTE, 3, K8B, 'NUM', NBMA, K8B,
     &               ZI(JMA),' ', 1 )
         CALL JEDETR(MESMAI)
210   CONTINUE

C --- MOT CLE "POUTRE" :
      CALL GETFAC ( 'POUTRE', NBOCC )
      DO 220 IOCC = 1,NBOCC
         CALL RELIEM(' ',NOMA,'NU_MAILLE','POUTRE',IOCC,2,MOCLES,
     &               TYPMCL,MESMAI ,NBMA)

         CALL GETVIS('POUTRE','TUYAU_NCOU',IOCC,IARG,1,NBCOU,NBV)
         CALL GETVIS('POUTRE','TUYAU_NSEC',IOCC,IARG,1,NBSEC,NBV)
         ZK8(JNCMP-1+1) = 'TUY_NCOU'
         ZK8(JNCMP-1+2) = 'TUY_NSEC'
         ZI(JVALV-1+1)  =  NBCOU
         ZI(JVALV-1+2)  =  NBSEC

         CALL JEVEUO(MESMAI,'L',JMA )
         CALL NOCART(CARTE, 3, K8B, 'NUM', NBMA, K8B,
     &               ZI(JMA), ' ', 2 )
         CALL JEDETR(MESMAI)
220   CONTINUE


C --- TRANSFORME LA CARTE EN CHAM_ELEM_S
      CALL CARCES(CARTE,'ELEM',' ','V',CESDEC,'A',IRET)
      CALL DETRSD('CARTE',CARTE)

9999  CONTINUE
      CALL JEDEMA()
      END
