      SUBROUTINE CAGROU(FONREZ,CHARGZ)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*(*) FONREZ,CHARGZ
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C     TRAITER LE MOT CLE LIAISON_UNIF DE AFFE_CHAR_XXX
C     ET ENRICHIR LA CHARGE (CHARGE) AVEC LES RELATIONS LINEAIRES

C IN       : FONREZ : 'REEL' OU 'FONC' OU 'COMP'
C IN/JXVAR : CHARGZ : NOM D'UNE SD CHARGE
C ----------------------------------------------------------------------

      COMPLEX*16 BETAC,COEMUC(2)
      CHARACTER*2 TYPLAG
      CHARACTER*4 FONREE
      CHARACTER*4 TYPCOE
      CHARACTER*8 BETAF,DDL(2),NONO(2),CHARGE
      CHARACTER*8 K8BID
      CHARACTER*16 MOTFAC
      CHARACTER*19 LISREL
      CHARACTER*24 LISNOE
      REAL*8 COEMUR(2),DIRECT(6)
      INTEGER IDIM(2)
      INTEGER      IARG
C-----------------------------------------------------------------------
      INTEGER IDDL ,INO ,IOCC ,JDDL ,JLIST ,LONLIM ,LONLIS 
      INTEGER N1 ,N2 ,NLIAI 
      REAL*8 BETA 
C-----------------------------------------------------------------------
      DATA DIRECT/0.0D0,0.0D0,0.0D0,0.0D0,0.0D0,0.0D0/
C ----------------------------------------------------------------------

      CALL JEMARQ()
      FONREE = FONREZ
      CHARGE = CHARGZ

      MOTFAC = 'LIAISON_UNIF'
      TYPLAG = '12'

      TYPCOE = 'REEL'
      IF (FONREE.EQ.'COMP') THEN
        TYPCOE = 'COMP'
      END IF

      LISREL = '&&CAGROU.RLLISTE'
      LISNOE = '&&CAGROU.NOEUD'
      CALL GETFAC(MOTFAC,NLIAI)
      IF (NLIAI.EQ.0) GO TO 40

      BETAF = '&FOZERO'
      BETAC = (0.0D0,0.0D0)
      BETA = 0.0D0
      COEMUC(1) = (1.0D0,0.0D0)
      COEMUC(2) = (-1.0D0,0.0D0)
      COEMUR(1) = 1.0D0
      COEMUR(2) = -1.0D0
      IDIM(1) = 0
      IDIM(2) = 0
      LONLIM = 0

      DO 30 IOCC = 1,NLIAI

C     -- ACUISITION DE LA LISTE DES NOEUDS A LIER :
C        (CETTE LISTE EST NON REDONDANTE)
C     ---------------------------------------------
        CALL MALINO(MOTFAC,CHARGE,IOCC,LISNOE,LONLIS)
        LONLIM = MAX(LONLIM,LONLIS)
        IF (LONLIS.LE.1) THEN
          CALL U2MESS('F','MODELISA2_82')
        END IF

        CALL JEVEUO(LISNOE,'L',JLIST)

        CALL GETVTX(MOTFAC,'DDL',IOCC,IARG,0,K8BID,N1)
        IF (N1.NE.0) THEN
          N1 = -N1
        END IF

        CALL WKVECT('&&CAGROU.DDL','V V K8',N1,JDDL)

        CALL GETVTX(MOTFAC,'DDL',IOCC,IARG,N1,ZK8(JDDL),N2)

        NONO(1) = ZK8(JLIST)

        DO 20 IDDL = 1,N1
          DDL(1) = ZK8(JDDL+IDDL-1)
          DDL(2) = ZK8(JDDL+IDDL-1)
          DO 10 INO = 2,LONLIS
            NONO(2) = ZK8(JLIST+INO-1)
            CALL AFRELA(COEMUR,COEMUC,DDL,NONO,IDIM,DIRECT,2,BETA,BETAC,
     &                  BETAF,TYPCOE,FONREE,TYPLAG,0.D0,LISREL)
   10     CONTINUE
   20   CONTINUE
        CALL JEDETR(LISNOE)
        CALL JEDETR('&&CAGROU.DDL')
   30 CONTINUE

C     -- AFFECTATION DE LA LISTE_RELA A LA CHARGE :
C     ---------------------------------------------
      IF (LONLIM.GT.1) THEN
        CALL AFLRCH(LISREL,CHARGE)
      END IF

   40 CONTINUE
      CALL JEDEMA()
      END
