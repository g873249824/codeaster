      SUBROUTINE ARLMAI(MAIL  ,MAILAR,NOMARL,QUADRA,NDIM  ,
     &                  TYPMA0,NOM1  ,NOM2  ,TABCOR,TABCPL)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 12/02/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8  MAIL,MAILAR
      CHARACTER*16 TYPMA0
      CHARACTER*10 QUADRA      
      INTEGER      NDIM
      CHARACTER*8  NOMARL 
      CHARACTER*10 NOM1,NOM2   
      CHARACTER*24 TABCOR,TABCPL  
C      
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C CREATION DU PSEUDO-MAILLAGE  
C
C ----------------------------------------------------------------------
C
C
C IN  MAIL   : NOM DU MAILLAGE
C IN  MAILAR : NOM DU PSEUDO-MAILLAGE
C IN  NOMARL : NOM DE LA SD PRINCIPALE ARLEQUIN
C IN  NOM1   : NOM DE LA SD DE STOCKAGE PREMIER GROUPE 
C IN  NOM2   : NOM DE LA SD DE STOCKAGE SECOND GROUPE
C IN  TYPMA0 : SD CONTENANT NOM DES TYPES ELEMENTS (&&CATA.NOMTM)
C IN  NDIM   : NDIMNSION DU PROBLEME
C IN  QUADRA : SD DES QUADRATURES A CALCULER
C OUT TABCOR : TABLEAU DE CORRESPONDANCE 
C            POUR CHAQUE NOUVEAU NUMERO ABSOLU DANS MAILAR 
C             -> ANCIEN NUMERO ABSOLU DANS MAIL
C             -> SI NEGATIF, LA NOUVELLE MAILLE EST ISSUE D'UNE
C                DECOUPE DE LA MAILLE DE NUMERO ABSOLU ABS(NUM) DANS
C                MAIL
C OUT TABCPL : TABLEAU DE CORRESPONDANCE 
C            POUR CHAQUE PSEUDO-MAILLE -> NUM. COUPLE
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      CHARACTER*32       JEXATR,JEXNUM,JEXNOM
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C      
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      INTEGER      IFM,NIV
      INTEGER      NMAIN1,NMAIN2,NMASS,NMATOT
      INTEGER      NNOIN1,NNOIN2,CXCUMU    
      CHARACTER*8  K8BID,NOM
      INTEGER      ICPL,I
      INTEGER      IMA1,IMA2,NUMMA1,NUMMA2
      INTEGER      JQDIME,JQMAMA,JQLIMA,JQCUMU
      INTEGER      IMAIL,INOEU
      LOGICAL      LINCL1,LINCL2,LINCLU,LSSMAI
      INTEGER      ARLGEI,NTMAX,NCMAX,NBSS,NBMAR,NAPP,ULPMAI 
      INTEGER      NBNO,NBNOT ,NBMAT,NCTOT,NT
      INTEGER      IRET  
      CHARACTER*24 MAIDIM,COOVAL,NOMNOE
      INTEGER      JDIME ,JCOOR 
      INTEGER             JCOORO,       JCONXO,JCUMUO
      INTEGER      JNORMO
      CHARACTER*24 VTRAV ,GRPMA
      CHARACTER*19 NGRM1,NGRM2 
      INTEGER      JTRAVI,JTRAVR,JTRAVL,JTRAV ,IDA
      INTEGER      ITYTR3,ITYTE4
      INTEGER      NMAGR ,NNEL
      INTEGER      JGRP1 ,JGRP2,JGRP3,IGRM1,IGRM2,IGRM3
      INTEGER      JTABCO,JTABCP
C      
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('ARLEQUIN',IFM,NIV)
C
C --- AFFICHAGE
C      
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> *** CREATION DU PSEUDO-MAILLAGE...'   
      ENDIF
C
C --- NOM DES SDS TEMPORAIRES
C
      MAILAR = '&&ARL.MA'
      TABCOR = '&&ARLCP2.TABCOR'
      TABCPL = '&&ARLCP2.TABCPL'      
C
C --- DESTRUCTION DU PSEUDO-MAILLAGE S'IL EXISTE
C
      CALL DETRSD('MAILLAGE',MAILAR)      
C
C --- NUMERO DU TYPE ELEMENT TRIA3/TETRA4
C       
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TRIA3') ,ITYTR3)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TETRA4'),ITYTE4)
      IF ((ITYTR3.EQ.0).OR.(ITYTE4.EQ.0)) THEN
        CALL ASSERT(.FALSE.)
      ENDIF            
C
C --- LECTURE DONNEES QUADRATURES
C 
      CALL JEVEUO(QUADRA(1:10)//'.DIME'  ,'L',JQDIME)
      CALL JEVEUO(QUADRA(1:10)//'.LIMAMA','L',JQLIMA)      
      CALL JEVEUO(JEXATR(QUADRA(1:10)//'.LIMAMA','LONCUM'),'L',JQCUMU)
      CALL JEVEUO(QUADRA(1:10)//'.MAMA'  ,'L',JQMAMA)       
C
C --- LECTURE DONNEES TEMPORAIRES ARLEQUIN
C
      CALL JEVEUO(NOMARL(1:8)//'.TRAVR','E',JTRAVR)
      CALL JEVEUO(NOMARL(1:8)//'.TRAVI','E',JTRAVI)
      CALL JEVEUO(NOMARL(1:8)//'.TRAVL','E',JTRAVL)      
C
C --- INITIALISATIONS
C
      NGRM1 = NOM1(1:10)//'.GROUPEMA'
      NGRM2 = NOM2(1:10)//'.GROUPEMA' 
      VTRAV = '&&ARLMA1.CONNEX'     
      ULPMAI = ARLGEI(NOMARL,'ULPMAI')    
C           '
      NAPP   = ZI(JQDIME + 2 - 1 )      
      NMAIN1 = ZI(JQDIME + 3 - 1 )
      NMAIN2 = ZI(JQDIME + 4 - 1 )
      NMASS  = ZI(JQDIME + 5 - 1 )
      NNOIN1 = ZI(JQDIME + 6 - 1 )
      NNOIN2 = ZI(JQDIME + 7 - 1 ) 
C
C --- NOMBRE NOEUDS PAR ELEMENT ISSU DE LA DECOUPE (TRIANGLE OU TETRA)
C      
      IF (NDIM.EQ.2) THEN
        NNEL = 3
      ELSEIF (NDIM.EQ.3) THEN
        NNEL = 4
      ELSE
        CALL ASSERT(.FALSE.)        
      ENDIF      
C
C --- MAILLAGE INITIAL
C      
      CALL JEVEUO(MAIL(1:8)//'.COORDO    .VALE','L',JCOORO)
      CALL JEVEUO(MAIL(1:8)//'.CONNEX','L',JCONXO)
      CALL JEVEUO(JEXATR(MAIL(1:8)//'.CONNEX','LONCUM'),'L',JCUMUO)
      CALL DISMOI('F','NB_NO_MAILLA',MAIL,'MAILLAGE',NBNO,K8BID,IRET)
C
C --- LECTURE DONNEES NORMALES 
C  
      JNORMO = JCOORO
C
C --- ESTIMATION GROSSIERE DU NOMBRE D'ELEMENTS GENERES PAR SOUS-DECOUPE
C
      NCMAX  = ARLGEI(NOMARL,'NCMAX ')  
      NTMAX  = ARLGEI(NOMARL,'NTMAX ')            
      IF (NDIM.EQ.3) NCMAX = 3*NCMAX     
      NBSS   = NCMAX * NTMAX           
C
C --- ESTIMATION GROSSIERE DU NOMBRE D'ELEMENTS TOTAL
C           
      NBMAT = NMAIN1 + NMAIN2 + NMASS*NBSS
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... PSEUDO-MAILLAGE - '//
     &               ' NBR ELEMENTS A PRIORI          : ',NBMAT
      ENDIF 
C
C --- ESTIMATION GROSSIERE DE LA LONGUEUR CUMULEE DES CONNECTIVITES
C           
      NCTOT = NNOIN1 + NNOIN2 + NMASS*NBSS*3
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... PSEUDO-MAILLAGE - '//
     &               ' LONGUEUR CONNECTIVITES A PRIORI: ',NCTOT 
      ENDIF              
C
C --- ESTIMATION DU NOMBRE DE NOEUDS TOTAL
C --- NMASS*NBSS*3: SOUS-DECOUPE EN TRIANGLES (2D)
C --- NMASS*NBSS*4: SOUS-DECOUPE EN TETRA (3D)
C         
      IF (NDIM.EQ.2) THEN  
        NBNOT = NBNO + NMASS*NBSS*3
      ELSEIF (NDIM.EQ.3) THEN 
        NBNOT = NBNO + NMASS*NBSS*4
      ELSE
        CALL ASSERT(.FALSE.) 
      ENDIF
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... PSEUDO-MAILLAGE - '//
     &               ' NBR NOEUDS   A PRIORI          : ',NBNOT
      ENDIF 
C
C --- CREATIONS DES OBJETS DE BASE POUR LE PSEUDO MAILLAGE
C
      CALL INIMAI(MAILAR,'V'  ,NDIM  ,' ',NBNOT ,
     &            NBMAT ,NCTOT)
C
C --- ACCES AUX OBJETS DU PSEUDO MAILLAGE
C     
      MAIDIM  = MAILAR(1:8)//'.DIME           '  
      COOVAL  = MAILAR(1:8)//'.COORDO    .VALE'  
      NOMNOE  = MAILAR(1:8)//'.NOMNOE         '
      GRPMA   = MAILAR(1:8)//'.GROUPEMA       '   
      CALL JEVEUO(MAIDIM,'E',JDIME)
      CALL JEVEUO(COOVAL,'E',JCOOR)     
C
C --- RECOPIE DES COORDONNEES DES ANCIENS NOEUDS
C         
      CALL JEVEUO(MAIL(1:8)// '.COORDO    .VALE','L',JCOORO)
      DO 10 I = 1,3*NBNO          
        ZR(JCOOR+I-1) = ZR(JCOORO+I-1)
  10  CONTINUE 
C
C --- RECOPIE DES NOMS DES ANCIENS NOEUDS
C
      DO 11 I = 1,NBNO  
        CALL JENUNO(JEXNUM(MAIL(1:8)//'.NOMNOE',I),NOM)
        CALL JEEXIN(JEXNOM(NOMNOE,NOM),IRET)
        IF (IRET.EQ.0) THEN
          CALL JECROC(JEXNOM(NOMNOE,NOM))
        ELSE
          CALL U2MESK('F','MODELISA7_10',1,NOM)
        ENDIF
  11  CONTINUE
C
C --- TABLEAU POUR LES CONNECTIVITES DES MAILLES DECOUPEES
C
      CALL WKVECT(VTRAV ,'V V I',NCTOT,JTRAV)  
C
C --- TABLEAU DE CORRESPONDANCE NOUV. MAILLES -> ANCIENNES MAILLES
C   
      CALL WKVECT(TABCOR,'V V I',NBMAT,JTABCO) 
C
C --- TABLEAU DE CORRESPONDANCE NUM. ABS. MAILLE CREEE -> COUPLE  
C   
      CALL WKVECT(TABCPL,'V V I',NBMAT,JTABCP)        
C
C --- TABLEAUX POUR GROUPES
C
      IF (NMAIN1.NE.0) THEN
        CALL WKVECT('&&ARLMA1.GRINC1','V V I',NMAIN1,JGRP1)
      ENDIF
      IF (NMAIN2.NE.0) THEN  
        CALL WKVECT('&&ARLMA1.GRINC2','V V I',NMAIN2,JGRP2)
      ENDIF
      IF (NMASS*NBSS.NE.0) THEN
        CALL WKVECT('&&ARLMA1.GRSSMA','V V I',NMASS*NBSS,JGRP3)
      ENDIF 
C
C --- INDICES POUR REMPLIR LES TABLEAUX    
C
      INOEU  = NBNO + 1 
      IMAIL  = 1       
      IGRM1  = 1
      IGRM2  = 1
      IGRM3  = 1                
C
C --- RECOPIE DES ANCIENNES MAILLES
C        
      NBMAR  = 0
      CXCUMU = 0
      DO 30 ICPL = 1 , NAPP
C
C --- ACCES AU COUPLE
C      
        IMA1 = ZI(JQMAMA+2*(ICPL-1))
        IMA2 = ZI(JQMAMA+2*(ICPL-1)+1)      
C
C --- TYPE D'INTEGRATION
C
        CALL ARLTII(IMA1  ,IMA2   ,
     &              LINCL1,LINCL2,LINCLU,LSSMAI)
C
C --- CREATION DE LA MAILLE DANS LE PSEUDO-MAILLAGE
C                      
C ----- MAIL 1 EST INCLU DANS MAIL2
C
        IF (LINCL1) THEN
          CALL ARLMAF(MAIL  ,MAILAR,NDIM  ,NGRM1  ,IMA1  ,
     &                ZI(JCONXO)   ,ZI(JCUMUO)    ,IMAIL ,NUMMA1 ,
     &                CXCUMU)
          ZI(JGRP1 +IGRM1-1) = IMAIL 
          ZI(JTABCO+IMAIL-1) = NUMMA1
          ZI(JTABCP+IMAIL-1) = ICPL
          IMAIL = IMAIL + 1
          IGRM1 = IGRM1 + 1
C
C ----- MAIL 2 EST INCLU DANS MAIL 1
C
        ELSEIF (LINCL2) THEN
          CALL ARLMAF(MAIL  ,MAILAR,NDIM  ,NGRM2  ,IMA2  ,
     &                ZI(JCONXO)   ,ZI(JCUMUO)    ,IMAIL ,NUMMA2 ,
     &                CXCUMU)
          ZI(JGRP2 +IGRM2-1) = IMAIL 
          ZI(JTABCO+IMAIL-1) = NUMMA2
          ZI(JTABCP+IMAIL-1) = ICPL
          IMAIL = IMAIL + 1
          IGRM2 = IGRM2 + 1                       
C
C ------ MAILLES DE L'INTERSECTION
C
        ELSEIF (LSSMAI) THEN
          CALL ARLMAS(MAIL  ,NOMARL,TYPMA0,MAILAR,NDIM  ,
     &                NOM1  ,NOM2  ,IMA1  ,IMA2  ,NNEL  ,
     &                ZI(JCONXO)   ,ZI(JCUMUO),
     &                ZR(JCOORO)   ,ZR(JNORMO),
     &                IMAIL ,INOEU ,ITYTR3    ,ITYTE4,
     &                ZR(JTRAVR),ZI(JTRAVI),ZL(JTRAVL),JTRAV,
     &                NT    ,NUMMA2,CXCUMU) 
          DO 35 I = 1,NT
            ZI(JGRP3 +IGRM3-1)   = IMAIL + I - 1
            ZI(JTABCO+IMAIL+I-2) = -NUMMA2
            ZI(JTABCP+IMAIL+I-2) = ICPL
            IGRM3 = IGRM3 + 1           
 35       CONTINUE
C             
          IMAIL = IMAIL + NT 
          INOEU = INOEU + NT*NNEL   
          NBMAR = NBMAR + NT
        ENDIF          
 30   CONTINUE
C
C --- VRAI NOMBRE D'ELEMENTS
C
      NMATOT = NMAIN1 + NMAIN2 + NBMAR
      ZI(JDIME - 1 + 3) =  NMATOT
      
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... PSEUDO-MAILLAGE - '//
     &               ' SURESTIMATION NBRE MAILLES '
        WRITE(IFM,*) '<ARLEQUIN> ...... PREVU :  ',NBMAT,' MAILLES'
        WRITE(IFM,*) '<ARLEQUIN> ...... OBTENU:  ',NMAIN1+NMAIN2+NBMAR
     &                ,' MAILLES'
        WRITE(IFM,*) '<ARLEQUIN> ......... ',NMAIN1,' MAILLES 2 SUR 1'
        WRITE(IFM,*) '<ARLEQUIN> ......... ',NMAIN2,' MAILLES 1 SUR 2'
        WRITE(IFM,*) '<ARLEQUIN> ......... ',NBMAR ,' MAILLES DECOUPEES'
      ENDIF  
C
C --- VRAI NOMBRE DE NOEUDS
C
      ZI(JDIME - 1 + 1) =  INOEU-1      
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... PSEUDO-MAILLAGE - '//
     &               ' SURESTIMATION NBRE NOEUDS '
        WRITE(IFM,*) '<ARLEQUIN> ...... PREVU :  ',NBNOT,' NOEUDS'
        WRITE(IFM,*) '<ARLEQUIN> ...... OBTENU:  ',INOEU-1,' NOEUDS'
      ENDIF 
C
C --- VRAI LONGUEUR CUMULEE CONNECTIVITE
C
      ZI(JDIME - 1 + 1) =  INOEU-1      
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... PSEUDO-MAILLAGE - '//
     &               ' SURESTIMATION LONGUEUR CUMULEE CONNECTIVITE '
        WRITE(IFM,*) '<ARLEQUIN> ...... PREVU :  ',NCTOT
        WRITE(IFM,*) '<ARLEQUIN> ...... OBTENU:  ',CXCUMU
      ENDIF       
C
C --- CREATION DES GROUPES
C  
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... CREATION DES GROUPES'
      ENDIF  
      NMAGR = 0
      IF (NMAIN1.NE.0) THEN
        NMAGR  = NMAGR + 1
      ENDIF
      IF (NMAIN2.NE.0) THEN
        NMAGR  = NMAGR + 1
      ENDIF      
      IF (NBMAR.NE.0) THEN
        NMAGR  = NMAGR + 1
      ENDIF 
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... NBRE GROUPES: ',NMAGR
      ENDIF          
      CALL JECREC(GRPMA,'V V I','NOM','DISPERSE','VARIABLE',NMAGR)
      IF (NMAIN1.NE.0) THEN
        IF (NIV.GE.2) THEN
          WRITE(IFM,*) '<ARLEQUIN> ... GROUPE INTEG. SUR M1 '//
     &                 '<INCLU1> - ',NMAIN1,' MAILLES'
        ENDIF 
        CALL JECROC(JEXNOM(GRPMA,'INCLU1'))
        CALL JEECRA(JEXNOM(GRPMA,'INCLU1'),'LONMAX',NMAIN1,K8BID)
        CALL JEVEUO(JEXNOM(GRPMA,'INCLU1'),'E',IDA)
        DO 333 I = 1,NMAIN1
          ZI(IDA+I-1) = ZI(JGRP1+I-1)
 333    CONTINUE   
      ENDIF  
      IF (NMAIN2.NE.0) THEN
        IF (NIV.GE.2) THEN
          WRITE(IFM,*) '<ARLEQUIN> ... GROUPE INTEG. SUR M2 '//
     &                 '<INCLU2> - ',NMAIN2,' MAILLES'
        ENDIF       
        CALL JECROC(JEXNOM(GRPMA,'INCLU2'))
        CALL JEECRA(JEXNOM(GRPMA,'INCLU2'),'LONMAX',NMAIN2,K8BID)
        CALL JEVEUO(JEXNOM(GRPMA,'INCLU2'),'E',IDA)
        DO 334 I = 1,NMAIN2
          ZI(IDA+I-1) = ZI(JGRP2+I-1)
 334    CONTINUE   
      ENDIF 
      IF (NBMAR.NE.0) THEN
        IF (NIV.GE.2) THEN
          WRITE(IFM,*) '<ARLEQUIN> ... GROUPE INTEG. PAR SOUS-MAILLES'//
     &                 ' <SOUS> - ',NBMAR,' MAILLES'
        ENDIF      
        CALL JECROC(JEXNOM(GRPMA,'SOUS'))
        CALL JEECRA(JEXNOM(GRPMA,'SOUS'),'LONMAX',NBMAR,K8BID)
        CALL JEVEUO(JEXNOM(GRPMA,'SOUS'),'E',IDA)
        DO 335 I = 1,NBMAR        
          ZI(IDA+I-1) = ZI(JGRP3+I-1)
 335    CONTINUE   
      ENDIF                                     
C
C --- IMPRESSION DU MAILLAGE AU FORMAT GMSH
C    
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... IMPRESSION DU PSEUDO-MAILLAGE'
      ENDIF         
      CALL IRMGMS(ULPMAI,NDIM,NBNOT,MAILAR,NMAGR,'        ',.FALSE.,2)
C
C --- AFFICHAGE
C      
      IF (NIV.GE.2) THEN
C        CALL UTIMSD(IFM,2,.TRUE.,.TRUE.,MAILAR,1,'V')
        WRITE(IFM,*) '<ARLEQUIN> *** FIN DE CREATION DU PSEUDO-MAILLAGE'
      ENDIF
C
C --- MENAGE
C
      CALL JEDETR(VTRAV) 
      CALL JEDETR('&&ARLMA1.GRINC1')
      CALL JEDETR('&&ARLMA1.GRINCZ')
      CALL JEDETR('&&ARLMA1.GRSSMA')                     
C
      CALL JEDEMA()

      END
