      SUBROUTINE RCVALE( NOMMAZ,PHENOM,NBPAR,NOMPAR,VALPAR,
     &                   NBRES,NOMRES,VALRES,ICODRE,IARRET)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER            NBPAR,NBRES
      CHARACTER*(*)      PHENOM
      INTEGER   IARRET
      CHARACTER*(*)      NOMMAZ
      INTEGER        ICODRE(NBRES)
      CHARACTER*8        NOMPAR(NBPAR), NOMRES(NBRES)
      REAL*8             VALPAR(NBPAR), VALRES(NBRES)
C ----------------------------------------------------------------------
C     OBTENTION DE LA VALEUR VALRES D'UN "ELEMENT" D'UNE RELATION DE
C     COMPORTEMENT D'UN MATERIAU DONNE (NOUVELLE FORMULE RAPIDE)
C
C     ARGUMENTS D'ENTREE:
C        NOMMAT : NOM UTILISATEUR DU MATERIAU
C        PHENOM : NOM DU PHENOMENE (I.E. MOT CLE FACTEUR)
C        NBPAR  : NOMBRE DE PARAMETRES DANS NOMPAR ET VALPAR
C        NOMPAR : NOMS DES PARAMETRES(EX: 'TEMP' )
C        VALPAR : VALEURS DES PARAMETRES
C        NBRES  : NOMBRE DE RESULTATS
C        NOMRES : NOM DES RESULTATS (EX: E,NU,... )
C                 TELS QU'IL FIGURENT DANS LA COMMANDE DEFI_MATERIAU
C        IARRET : 0/1/2  COMPORTEMENT VOULU EN CAS DE PROBLEME
C           /0 : PAS DE MESSAGE D'ERREUR
C           /1 : ERREUR FATALE AVEC IDENTIFICATION DE LA MAILLE
C           /2 : ERREUR FATALE SANS IDENTIFICATION DE LA MAILLE
C           PLUS PRECISEMENT :
C           * SI LE PARAMETRE (REEL, COMPLEXE OU FONCTION)
C             N'A PAS ETE FOURNI PAR L'UTILISATEUR :
C               SI IARRET > 0  => ERREUR FATALE
C               SI IARRET = 0  => CODE RETOUR > 0
C           * SI LE PARAMETRE EST UNE FONCTION FOURNIE PAR
C             L'UTILISATEUR MAIS QUE SON EVALUATION ECHOUE :
C             => ERREUR FATALE DANS TOUS LES CAS
C
C     ARGUMENTS DE SORTIE:
C       VALRES : VALEURS DES RESULTATS APRES RECUPERATION
C                OU EVALUATION DE LA FONCTION
C       ICODRE : POUR CHAQUE RESULTAT, 0 SI ON A TROUVE, 1 SINON
C
C
C
C
      INTEGER NBMX,NBRESP,IRES,IER,NBR,NBC,NBK,IRET
      INTEGER IVALR,IVALK,NBOBJ,NBF,IR,IK
      PARAMETER        ( NBMX=30 )
      INTEGER            NBFP
      REAL*8             VALREP(NBMX)
      LOGICAL            CHANGE
      INTEGER        ICODR2(NBMX)
      CHARACTER*2        KSTOP
      CHARACTER*10       PHEN,PHEPRE
      CHARACTER*8        MATPRE,NOMREP(NBMX),NOMFOP(NBMX),K8BID
      CHARACTER*10       NOMPHE
      CHARACTER*8        NOMMAT
      SAVE  MATPRE,PHEPRE,NBFP,NBRESP,NOMREP,VALREP,ICODR2,NOMFOP

      CALL JEMARQ()
      NOMMAT = NOMMAZ
      PHEN = PHENOM
      KSTOP='F '

      CALL ASSERT(IARRET.GE.0 .AND. IARRET.LE.2)


C --- TESTS: CELA A-T-IL CHANGE ?
      CHANGE = .FALSE.
      IF (NBRES .GT. NBMX) THEN
        CALL U2MESK('F','MODELISA6_94',1,NOMMAT)
      ENDIF
      IF (NOMMAT .NE. MATPRE) CHANGE = .TRUE.
      IF (PHEN   .NE. PHEPRE) CHANGE = .TRUE.
      IF (NBRES  .NE. NBRESP) CHANGE = .TRUE.
      DO 100 IRES = 1, NBRES
        IF (NOMRES(IRES) .NE. NOMREP(IRES)) CHANGE = .TRUE.
  100 CONTINUE


      IF (.NOT.CHANGE) THEN
        DO 110 IRES = 1, NBRES
          VALRES(IRES) = VALREP(IRES)
          ICODRE(IRES) = ICODR2(IRES)
  110   CONTINUE
        IF (NBFP .EQ. 0) GOTO 9999

        DO 120 IRES = 1, NBRES
          IF (NOMFOP(IRES) .NE. ' ') THEN
            CALL FOINTE(KSTOP,NOMFOP(IRES),NBPAR,NOMPAR,VALPAR,
     &                  VALRES(IRES),IER)
            CALL ASSERT(IER.EQ.0)
            ICODRE(IRES) = 0
          ELSE
              ICODRE(IRES) = 1
          ENDIF
  120   CONTINUE


      ELSE
        NOMPHE = PHEN
        CALL JEEXIN (NOMMAT//'.'//NOMPHE//'.VALR',IRET)
        IF ( IRET .EQ. 0 ) THEN
          DO 113 IRES = 1, NBRES
            ICODRE(IRES) = 1
  113     CONTINUE
          GOTO 999
        ENDIF

        CALL JEVEUO (NOMMAT//'.'//NOMPHE//'.VALR', 'L', IVALR)
        CALL JELIRA (NOMMAT//'.'//NOMPHE//'.VALR', 'LONUTI', NBR,
     &               K8BID)
        CALL JELIRA (NOMMAT//'.'//NOMPHE//'.VALC', 'LONUTI', NBC,
     &               K8BID)
        CALL JEVEUO (NOMMAT//'.'//NOMPHE//'.VALK', 'L', IVALK)
        CALL JELIRA (NOMMAT//'.'//NOMPHE//'.VALK', 'LONUTI', NBK,
     &               K8BID)
        DO 130 IRES = 1, NBRES
          ICODRE(IRES) = 1
          NOMFOP(IRES) = ' '
  130   CONTINUE
        NBOBJ = 0
        DO 150 IR = 1, NBR
          DO 140 IRES = 1, NBRES
            IF (NOMRES(IRES) .EQ. ZK8(IVALK-1+IR)) THEN
              VALRES(IRES) = ZR(IVALR-1+IR)
              ICODRE(IRES) = 0
              NBOBJ = NBOBJ + 1
            ENDIF
  140     CONTINUE
  150   CONTINUE
        IF (NBOBJ .NE. NBRES) THEN
          NBF = (NBK-NBR-NBC)/2
          DO 170 IRES = 1, NBRES
            DO 160 IK = 1, NBF
              IF (NOMRES(IRES) .EQ. ZK8(IVALK-1+NBR+NBC+IK)) THEN
                NOMFOP(IRES) = ZK8(IVALK-1+NBR+NBC+NBF+IK)
                CALL FOINTE (KSTOP,NOMFOP(IRES),NBPAR,NOMPAR,
     &                            VALPAR,VALRES(IRES),IER)
                CALL ASSERT(IER.EQ.0)
                ICODRE(IRES) = 0
              ENDIF
  160       CONTINUE
  170     CONTINUE
        ELSE
          NBF = 0
        ENDIF

C       -- SAUVEGARDE DES VALEURS POUR LE PROCHAIN APPEL :
        MATPRE = NOMMAT
        PHEPRE = PHEN
        NBFP = NBF
        NBRESP = NBRES
        DO 180 IRES = 1, NBRESP
          NOMREP(IRES) = NOMRES(IRES)
          VALREP(IRES) = VALRES(IRES)
          ICODR2(IRES) = ICODRE(IRES)
  180   CONTINUE

      ENDIF
 999  CONTINUE
 9999 CONTINUE
C
      CALL RCVALS( IARRET, ICODRE, NBRES, NOMRES )
C
      CALL JEDEMA()
      END
