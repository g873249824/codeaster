      SUBROUTINE ACCEPT(F,NBM,METHOD,IMODE,JMODE,UFLUI,
     &                  JC,DIR,UC,UCT,L,LT)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/04/2008   AUTEUR MEUNIER S.MEUNIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C     OPERATEUR PROJ_SPEC_BASE
C     PROJECTION D UN OU PLUSIEURS SPECTRES DE TURBULENCE SUR UNE BASE
C     MODALE PERTURBEE PAR PRISE EN COMPTE DU COUPLAGE FLUIDE STRUCTURE
C-----------------------------------------------------------------------
C  IN   : IEX = 0 => ON DEMANDE L EXECUTION DE LA COMMANDE
C         IEX = 1 => ON NE FAIT QUE VERIFIER LES PRECONDITIONS
C  OUT  : IER = 0 => TOUT S EST BIEN PASSE
C         IER > 0 => NOMBRE D ERREURS RENCONTREES
C-----------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      INTEGER      NBM,I,IND,JND,IPG,JPG,IAD1,IAD2,ITAB,IMODE,JMODE
      INTEGER      ISPE,JSPE,IORIG,JORIG,NTAIL,NTAIL1,NTAIL2
      INTEGER      JPGINI,JPGFIN
      REAL*8       F,PI,DEUXPI,OMEGA,UC,KL,KT,COEGEN,DIR(3,3)
      REAL*8       D1,D2,D3,MES(3),COEH,JC,CORCOS,L,LOCAL(3),UFLUI
      REAL*8       UCT,LT,RAYON,COYANG,DIST,DTETA,JC1
      CHARACTER*1  K1BID
      CHARACTER*8  METHOD

      PARAMETER (PI=3.14159265D0)
      DATA     (LOCAL(I),I=1,3) /3*0.D0/
C
C
C-----------------------------------------------------------------------
      CALL JEMARQ()

C QUELQUES CONSTANTES
      DEUXPI=2*PI
      OMEGA=DEUXPI*F
      IF (METHOD(1:7).NE.'AU_YANG') UC=0.65D0*UFLUI
      KL = 0.1D0*OMEGA/UC
      KT = 0.55D0*OMEGA/UC

C BOUCLE SUR LES ELEMENTS DU MODELE
      JC=0.D0
      JC1=0.D0
      CALL JEVEUO('&&GROTAB.TAB','L',ITAB)
      CALL JELIRA('&&GROTAB.TAB','LONUTI',NTAIL,K1BID)
      IF (METHOD(1:7).EQ.'AU_YANG') RAYON = ZR(ITAB+NTAIL-1)
      NTAIL = NTAIL - 1
      NTAIL1=NTAIL/(6*NBM)
      NTAIL2=NTAIL/NBM
      IORIG=(IMODE-1)*NTAIL2
      JORIG=(JMODE-1)*NTAIL2
      JPGFIN=(JMODE-1)*NTAIL2+NTAIL1-1
         DO 203 IPG=(IMODE-1)*NTAIL2,(IMODE-1)*NTAIL2+NTAIL1-1
            IF (JMODE.EQ.IMODE) THEN
               JPGINI = IPG
            ELSE
               JPGINI = (JMODE-1)*NTAIL
            ENDIF
            ISPE=IPG-(IMODE-1)*NTAIL2
            IAD1=ITAB + IORIG + 6*ISPE
            DO 204 JPG=JPGINI,JPGFIN
               JSPE=JPG-(JMODE-1)*NTAIL2
               IAD2=ITAB + JORIG + 6*JSPE
C CALCUL DISTANCES INTER POINTS DE GAUSS
C ABSCISSES
               MES(1)=ZR(IAD1+1)-ZR(IAD2+1)
C ORDONNEES
               MES(2)=ZR(IAD1+2)-ZR(IAD2+2)

C COTE
               MES(3)=ZR(IAD1+3)-ZR(IAD2+3)

C COHERENCE CORCOS
               IF(METHOD(1:6).EQ.'CORCOS') THEN
                  DO 205 IND=1,3
                     LOCAL(IND)=0.D0
                     DO 206 JND=1,3
                        LOCAL(IND)=LOCAL(IND)+DIR(IND,JND)*MES(JND)
206                  CONTINUE
205               CONTINUE

                  D1=ABS(LOCAL(1))
                  D2=ABS(LOCAL(2))
                  D3=ABS(LOCAL(3))

                  COEH=CORCOS(D1,D2,LOCAL(1),UC,KT,KL,OMEGA)

C COHERENCE GENERALE
               ELSEIF(METHOD.EQ.'GENERALE') THEN
                  D1=ABS(MES(1))
                  D2=ABS(MES(2))
                  D3=ABS(MES(3))
                  COEH=COEGEN(D1,D2,D3,L,OMEGA,UC)
               ELSEIF(METHOD(1:7).EQ.'AU_YANG') THEN
                  DIST = ABS(ZR(IAD1+4)-ZR(IAD2+4))
                  DTETA= ABS(ZR(IAD1+5)-ZR(IAD2+5))
                  COEH = COYANG (DIST,DTETA,RAYON,OMEGA,UC,UCT,L,LT)
               ENDIF
               IF (JMODE.EQ.IMODE .AND. JPG.GT.IPG) THEN
                  JC1 = JC1 + COEH*ZR(IAD1)*ZR(IAD2)
               ELSE
                  JC = JC + COEH*ZR(IAD1)*ZR(IAD2)
               ENDIF
204         CONTINUE
203      CONTINUE
      IF (IMODE.EQ.JMODE) JC = JC + 2*JC1
C
      CALL JEDEMA()
      END
