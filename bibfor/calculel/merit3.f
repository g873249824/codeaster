      SUBROUTINE MERIT3(MODELE,NCHAR,LCHAR,MATE,CARA,TIME,MATEL,
     &                  PREFCH,NUMERO,BASE)
      IMPLICIT NONE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 12/02/2013   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C     ARGUMENTS:
C     ----------
      INCLUDE 'jeveux.h'
      CHARACTER*(*) LCHAR(*),MATE
      CHARACTER*8 MODELE,CARA
      CHARACTER*19 MATEL,PREFCH
      CHARACTER*24 TIME
      CHARACTER*1 BASE
      INTEGER NCHAR,NUMERO
C ----------------------------------------------------------------------

C     CALCUL DES MATRICES ELEMENTAIRES DE CONVECTION NATURELLE

C     LES RESUELEM PRODUITS S'APPELLENT :
C           PREFCH(1:8).ME000I , I=NUMERO+1,NUMERO+N

C     ENTREES:

C     LES NOMS QUI SUIVENT SONT LES PREFIXES UTILISATEUR K8:
C        MODELE : NOM DU MODELE
C        NCHAR  : NOMBRE DE CHARGES
C        LCHAR  : LISTE DES CHARGES
C        MATE   : CHAMP DE MATERIAUX
C        CARA   : CHAMP DE CARAC_ELEM
C        TIME   : CHAMPS DE TEMPSR
C        MATEL  : NOM DU MATR_ELEM (N RESUELEM) PRODUIT
C        PREFCH : PREFIXE DES NOMS DES RESUELEM STOCKES DANS MATEL
C        NUMERO : NUMERO D'ORDRE A PARTIR DUQUEL ON NOMME LES RESUELEM

C     SORTIES:
C        MATEL  : EST REMPLI.

C ----------------------------------------------------------------------

C     FONCTIONS EXTERNES:
C     -------------------

C     VARIABLES LOCALES:
C     ------------------


      CHARACTER*8 NOMCHA,LPAIN(7),LPAOUT(1)
      CHARACTER*8 VITESS
      CHARACTER*16 OPTION
      CHARACTER*24 LCHIN(7),LCHOUT(1),CHGEOM,CHCARA(18)
      CHARACTER*24 CHVITE,LIGRMO,CARTE,CONVCH
      INTEGER IRET,ILIRES
      COMPLEX*16 CBID
      LOGICAL EXICAR


C     -- ON VERIFIE LA PRESENCE PARFOIS NECESSAIRE DE CARA_ELEM
C-----------------------------------------------------------------------
      INTEGER ICHAR ,ICONV ,JVITES
C-----------------------------------------------------------------------
      CALL JEMARQ()
      IF (MODELE(1:1).EQ.' ') THEN
        CALL U2MESS('F','CALCULEL3_50')
      END IF

      CALL MEGEOM(MODELE,CHGEOM)
      CALL MECARA(CARA,EXICAR,CHCARA)

      CALL JEEXIN(MATEL//'.RERR',IRET)
      IF (IRET.GT.0) THEN
        CALL JEDETR(MATEL//'.RERR')
        CALL JEDETR(MATEL//'.RELR')
      END IF

      CHVITE = '????'

      ICONV = 0

      LPAOUT(1) = 'PMATTTR'
      LCHOUT(1) = PREFCH(1:8)//'.ME000'
      IF (LCHAR(1) (1:8).NE.'        ') THEN
        DO 10 ICHAR = 1,NCHAR
          NOMCHA = LCHAR(ICHAR)
          CONVCH = NOMCHA//'.CHTH'//'.CONVE'//'.VALE'
          CALL JEEXIN(CONVCH,IRET)
          IF (IRET.GT.0) THEN
            ICONV = ICONV + 1
            IF (ICONV.GT.1) CALL U2MESS('F','CALCULEL3_72')

            OPTION = 'RIGI_THER_CONV_D'
            CALL MEMARE('V',MATEL,MODELE,MATE,CARA,OPTION)

            CALL JEVEUO(CONVCH,'L',JVITES)
            VITESS = ZK8(JVITES)
            CHVITE = VITESS
            CARTE = '&&MERIT3'//'.CONVECT.DECENT'
            CALL MECACT('V',CARTE,'MODELE',MODELE//'.MODELE','NEUT_K24',
     &                  1,'Z1',0,0.D0,CBID,'NON')
            LPAIN(1) = 'PGEOMER'
            LCHIN(1) = CHGEOM
            LPAIN(2) = 'PMATERC'
            LCHIN(2) = MATE
            LPAIN(3) = 'PCACOQU'
            LCHIN(3) = CHCARA(7)
            LPAIN(4) = 'PTEMPSR'
            LCHIN(4) = TIME
            LPAIN(5) = 'PVITESR'
            LCHIN(5) = CHVITE
            LPAIN(6) = 'PNEUK24'
            LCHIN(6) = CARTE
            LPAIN(7) = 'PCAMASS'
            LCHIN(7) = CHCARA(12)
            ILIRES = 0
            LIGRMO = MODELE//'.MODELE'
            ILIRES = ILIRES + 1
            CALL CODENT(ILIRES+NUMERO,'D0',LCHOUT(1) (12:14))
            CALL CALCUL('S',OPTION,LIGRMO,7,LCHIN,LPAIN,1,LCHOUT,LPAOUT,
     &                  BASE,'OUI')
            CALL REAJRE(MATEL,LCHOUT(1),BASE)
          END IF
   10   CONTINUE

      END IF

C --- MENAGE
      CALL DETRSD('CARTE','&&MERIT3'//'.CONVECT.DECENT')
      CALL JEDEMA()
      END
