      SUBROUTINE PJEFMI(ELREFP,NNOP,COOR,XG,NDIM,X1,X2,LEXT,XMI)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 08/03/2011   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE PELLET J.PELLET
      IMPLICIT NONE
      CHARACTER*8 ELREFP
      INTEGER NNOP,NDIM
      REAL*8 COOR(NDIM*NNOP)
      REAL*8 XG(NDIM),X1(NDIM),X2(NDIM),XMI(NDIM)
C
C ----------------------------------------------------------------------
C BUT :
C   DETERMINER LES MEILLEURES COORDONNEES BARYCENTRIQUES ENTRE X1 ET X2
C   PERMET DE VERIFIER QUE LA ROUTINE REEREG A AMELIORE LA PRECISION
C   DES COORDONNEES BARYCENTRIQUES GROSSIERES DE LA 1ERE ETAPE DE
C   PROJ_CHAMP.
C ----------------------------------------------------------------------
C
C
C IN  ELREFP : TYPE DE L'ELEMENT
C IN  NNOP   : NOMBRE DE NOEUDS DE L'ELEMENT
C IN  COOR   : COORDONNEES DS ESPACE REEL DES NOEUDS DE L'ELEMENT
C IN  XG     : COORDONNEES DU POINT DANS L'ESPACE REEL
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  X1     : COORDONNEES DU POINT 1 DANS L'ESPACE PARA DE L'ELEMENT
C              (RESULTAT DE LA PROJECTION "GROSSIERE")
C IN  X2     : COORDONNEES DU POINT 2 DANS L'ESPACE PARA DE L'ELEMENT
C              (RESULTAT DU RAFFINEMENT REEREG.F)
C IN  LEXT   : LE POINT X2 EST "EXTERIEUR" A LA MAILLE
C OUT XMI    : "X MIEUX" : RECOPIE DE X1 OU X2 (SELON LE CAS)
C
C ----------------------------------------------------------------------
      INTEGER NBNOMX
      PARAMETER(NBNOMX=27)
      REAL*8 XR1(3),XR2(3),ZERO,D1,D2
      REAL*8 FF(NBNOMX)
      INTEGER K,IDIM,INO,NNO
      LOGICAL LEXT
C ----------------------------------------------------------------------
      ZERO=0.D0

C     -- SI LE POINT EST EXTERIEUR, ON S'INTERDIT L'EXTRAPOLATION
C        => XMI=X1
      IF (LEXT) THEN
        DO 61 K=1,NDIM
          XMI(K)=X1(K)
   61   CONTINUE
        GOTO 9999
      ENDIF

C     -- CALCUL DE XR1 : GEOMETRIE REELLE DE X1 :
      CALL ELRFVF(ELREFP,X1,NBNOMX,FF,NNO)
      CALL ASSERT(NNO.EQ.NNOP)
      CALL VECINI(NDIM,ZERO,XR1)
      DO 20 IDIM=1,NDIM
        DO 10 INO=1,NNO
          XR1(IDIM)=XR1(IDIM)+FF(INO)*COOR(NDIM*(INO-1)+IDIM)
   10   CONTINUE
   20 CONTINUE


C     -- CALCUL DE XR2 : GEOMETRIE REELLE DE X2 :
      CALL ELRFVF(ELREFP,X2,NBNOMX,FF,NNO)
      CALL VECINI(NDIM,ZERO,XR2)
      DO 40 IDIM=1,NDIM
        DO 30 INO=1,NNO
          XR2(IDIM)=XR2(IDIM)+FF(INO)*COOR(NDIM*(INO-1)+IDIM)
   30   CONTINUE
   40 CONTINUE


C     -- QUELLE EST LA MEILLEURE APPROXIMATION DE XG ?
      D1=ZERO
      D2=ZERO
      DO 50 K=1,NDIM
        D1=D1+(XR1(K)-XG(K))**2
        D2=D2+(XR2(K)-XG(K))**2
   50 CONTINUE

      IF (D1.LE.D2) THEN
        DO 60 K=1,NDIM
          XMI(K)=X1(K)
   60   CONTINUE
      ELSE
        DO 70 K=1,NDIM
          XMI(K)=X2(K)
   70   CONTINUE
      ENDIF

9999  CONTINUE
      END
