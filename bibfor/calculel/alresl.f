      SUBROUTINE ALRESL(OPT,LIGREL,NOCHOU,NOMPAR,BASE)
      IMPLICIT REAL*8 (A-H,O-Z)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 10/11/1999   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE                            VABHHTS J.PELLET
C     ARGUMENTS:
C     ----------
      INTEGER OPT
      CHARACTER*19 LIGREL,NOCHOU
      CHARACTER*8 NOMPAR
      CHARACTER*(*) BASE
C ----------------------------------------------------------------------
C     ENTREES:
C      OPT   : OPTION
C     LIGREL : NOM DE LIGREL
C     NOCHOU : NOM DU RESUELEM A ALLOUER
C      NOMPAR: NOM DU PARAMETRE
C      BASE  : 'G', 'V' OU 'L'

C     SORTIES:
C     + CREATION DU RESUELEM DE NOM NOCHOU
C       (LE RESUELEM PEUT ETRE "VIDE")

C ----------------------------------------------------------------------

C     FONCTIONS EXTERNES:
C     -------------------
      INTEGER TYPELE,NBELEM,MODATT,DIGDE2,NBGREL,INPARA,GRDEUR
      CHARACTER*1 BAS2
      CHARACTER*8 SCALAI
      CHARACTER*32 JEXNUM

C     VARIABLES LOCALES:
C     ------------------
      INTEGER NGREL,IGREL,TE,NEL,MODE,NCMPEL,IPAR
      INTEGER DESC,GD,IAD
      CHARACTER*8 SCAL
C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16,NOMOPT
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80


      CALL JEMARQ()
      BAS2 = BASE

      CALL JENUNO(JEXNUM('&CATA.OP.NOMOPT',OPT),NOMOPT)
      NGREL = NBGREL(LIGREL)
      GD = GRDEUR(NOMPAR)
      SCAL = SCALAI(GD)


C     -- LE RESUELEM DOIT-IL ETRE CREE ?
C     ----------------------------------
      IPARMX = 0
      DO 10 IGREL = 1,NGREL
        TE = TYPELE(LIGREL,IGREL)
        IPAR = INPARA(OPT,TE,'OUT',NOMPAR)
        IPARMX = MAX(IPARMX,IPAR)
   10 CONTINUE
      IF (IPARMX.EQ.0) GO TO 30


C     ----CREATION DE L'OBJET NOLI :
      CALL WKVECT(NOCHOU//'.NOLI',BAS2//' V K24',2,IAD)
      ZK24(IAD-1+1) = LIGREL
      ZK24(IAD-1+2) = NOMOPT

C     ----CREATION DE L'OBJET DESC :
      CALL WKVECT(NOCHOU//'.DESC',BAS2//' V I',2+NGREL,IDESC)
      CALL JEECRA(NOCHOU//'.DESC','DOCU',IBID,'RESL')

C     ---CREATION DE LA COLLECTION DIPERSEE RESL  :
      CALL JECREC(NOCHOU//'.RESL',BAS2//' V '//SCAL(1:4),'NU',
     +            'DISPERSE','VARIABLE',NGREL)

C     -----REMPLISSAGE DE DESC ET ALLOCATION DES ELEMENTS DE RESL:
C     ------------------------------------------------------------
      CALL JEVEUO(NOCHOU//'.DESC','E',DESC)
      ZI(DESC-1+1) = GD
      ZI(DESC-1+2) = NGREL
      DO 20 IGREL = 1,NGREL
        TE = TYPELE(LIGREL,IGREL)
        IPAR = INPARA(OPT,TE,'OUT',NOMPAR)

C        -- SI LE TYPE_ELEMENT NE CONNAIT PAS LE PARAMETRE:
        IF (IPAR.EQ.0) THEN
          ZI(DESC-1+2+IGREL) = 0
        ELSE
          MODE = MODATT(OPT,TE,'OUT',IPAR)
          ZI(DESC-1+2+IGREL) = MODE
          NCMPEL = DIGDE2(MODE)
          NEL = NBELEM(LIGREL,IGREL)
          CALL JECROC(JEXNUM(NOCHOU//'.RESL',IGREL))
          CALL JEECRA(JEXNUM(NOCHOU//'.RESL',IGREL),'LONMAX',NCMPEL*NEL,
     +                ' ')
        END IF
   20 CONTINUE
   30 CONTINUE
      CALL JEDEMA()
      END
