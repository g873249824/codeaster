      FUNCTION MINCLU(DIME  ,PREC  ,M1    ,D1    ,MM1   ,
     &                SOM1  ,M2    ,D2    ,PAN2)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 27/04/2009   AUTEUR MEUNIER S.MEUNIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C
C
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
      LOGICAL  MINCLU
      REAL*8   PREC
      INTEGER  DIME,M1,D1(*),M2,D2(*)
      REAL*8   MM1(2,DIME,*),SOM1(DIME,*)
      REAL*8   PAN2(DIME+2,*)
C
C ----------------------------------------------------------------------
C
C APPARIEMENT DE DEUX GROUPES DE MAILLE PAR LA METHODE
C BOITES ENGLOBANTES + ARBRE BSP
C
C TEST APPROCHE D'INCLUSION DE LA MAILLE M1 DANS LA MAILLE M2
C
C ----------------------------------------------------------------------
C
C
C IN  DIME  : DIMENSION DE L'ESPACE
C IN  M1    : MAILLE 1
C IN  M2    : MAILLE 2
C IN  D1    : SD BOITE.DIME ASSOCIEE A M1 (CF BOITE)
C IN  D2    : SD BOITE.DIME ASSOCIEE A M2 (CF BOITE)
C IN  MM1   : SD BOITE.MINMAX ASSOCIEE A M1 (CF BOITE)
C IN  SOM1  : SD BOITE.SOMMET ASSOCIEE A M1 (CF BOITE)
C IN  PAN2  : SD BOITE.PAN ASSOCIEE A M2 (CF BOITE)
C IN  PREC  : PRECISION POUR TESTER INCLUSION
C OUT INCLU : .TRUE. SI LES BOITES ENGLOBANT M1 SONT INCLUSES
C                  DANS LE CONVEXE (ENGLOBANT/INSCRIT) DE M2
C
C ----------------------------------------------------------------------
C
      INTEGER  I,J,K,NSOM,NPAN,P1,P2,ID(3)
      INTEGER IAUX
      REAL*8   R
C
C ----------------------------------------------------------------------
C
      MINCLU = .FALSE.
C
C====
C 1. PREMIER TEST: INCLUSION CONVEXE
C    . SI LES DEUX MAILLES SONT LINEAIRES, ON VA TESTER LE CONVEXE
C      ENGLOBANT POUR CAPTER LES CAS DE SUPERPOSITION DE MAILLE.
C    . SINON, ON SE CONTENTERA D'UNE INCLUSION STRICTE VIA LE
C      CONVEXE INSCRIT
C====
C 1.1. ==> ON REGARDE SI LES NOEUDS DE LA MAILLE M1 SONT
C          A L'INTERIEUR DE LA BOITE INSCRITE ASSOCIEE A M2.
C          DES QUE L'UN EST AU DEHORS, ON PASSE AU TEST SUIVANT.
C
      P1   = D1(2+2*M1)
      NSOM = D1(4+2*M1) - P1
      P2   = D2(1+2*M2)
      NPAN = D2(3+2*M2) - P2
      IAUX = DIME + 2
C
      DO 11 , I = 1, NSOM
C
        DO 111 , J = 1, NPAN
          R = PAN2(IAUX,P2-1+J)
          DO 112 , K = 1, DIME
            R = R + SOM1(K,P1-1+I)*PAN2(K,P2-1+J)
  112     CONTINUE
          IF ( R.GT.PREC ) THEN
            GOTO 20
          ENDIF
  111   CONTINUE
C
   11 CONTINUE
C
C 1.2. ==> ON ARRIVE ICI SI TOUS LES SOMMETS DE M1 SONT DANS LA
C          BOITE INSCRITE ASSOCIEE A M2 : ON DECLARE L'INCLUSION
C
      MINCLU = .TRUE.
C
C====
C 2. SECOND TEST: INCLUSION MINMAX
C    ABSOLUMENT INCOMPREHENSIBLE !
C====
C
   20 CONTINUE
C
      IF ( .NOT. MINCLU ) THEN
C
      DO 21 , I = 1, DIME
        ID(I) = 1
   21 CONTINUE
C
      DO 22 , I = 1, 2**DIME
        DO 221 , J = 1, NPAN
          R = PAN2(IAUX,P2-1+J)
          DO 222 , K = 1, DIME
            R = R + MM1(ID(K),K,M1)*PAN2(K,P2-1+J)
  222     CONTINUE
          IF (R.GT.PREC) THEN
            GOTO 30
          ENDIF
  221   CONTINUE
        K = 1
   23   CONTINUE
        J     = 3 - ID(K)
        ID(K) = J
        K     = K + 1
        IF (J.EQ.1) GOTO 23
   22 CONTINUE
C
      MINCLU = .TRUE.
C
      ENDIF
C
C====
C 3. FIN
C====
C
   30 CONTINUE
C
      END
