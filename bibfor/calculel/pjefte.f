      SUBROUTINE PJEFTE (RESU1, RESU2 )
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 14/12/2009   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     COMMANDE:  PROJ_CHAMP  METHODE:'ELEM'
C ----------------------------------------------------------------------
C
      IMPLICIT   NONE
C
C 0.1. ==> ARGUMENTS
C
      CHARACTER*(*) RESU1, RESU2
C
C 0.2. ==> COMMUNS
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------

      CHARACTER*32 JEXNOM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)

C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
C 0.3. ==> VARIABLES LOCALES
C

      CHARACTER*8  NOMA1,NOMA2,MODEL1,MODEL2,NCAS
      CHARACTER*16 CORRES,CORRE1,CORRE2,CORRE3
      CHARACTER*16 TYMOCL(5),MOTCLE(5)
      CHARACTER*24 GEOM2,GEOM1
      INTEGER      N1,NBOCC,IOCC,IE,IBID,NBNO2,NBMA1
      INTEGER      IAGNO2,IAGMA1

      LOGICAL      LDMAX
      REAL*8       DISTMA,R8MAEM
C----------------------------------------------------------------------
C       DESCRIPTION DE LA SD CORRESP_2_MAILLA : CORRES
C       --------------------------------------------------------
C          (CORRESPONDANCE ENTRE LES 2 MODELES MODEL1 ET MODEL2)
C  DESCRIPTION DE LA SD CORRESP_2_MAILLA :
C  SOIT NNO2= NB_NO(MAILLAGE(MODEL2))

C  CORRESP_2_MAILLA (K16)  ::= RECORD
C     '.PJEF_NO'  : S V K8 LONG=2
C     '.PJEF_NB'  : S V I  LONG=NNO2
C     '.PJEF_M1'  : S V I  LONG=NNO2
C     '.PJEF_CO'  : S V R  LONG=3*NNO2
C     '.PJEF_NU'  : S V I  LONG=LONT
C     '.PJEF_CF'  : S V R  LONG=LONT

C     '.PJEF_NO' (1) : NOM DU MAILLAGE 1 : M1
C     '.PJEF_NO' (2) : NOM DU MAILLAGE 2 : M2

C     '.PJEF_M1' (INO2) : IMA1 : NUMERO DE LA MAILLE DE M1 QUI DOIT
C                         SERVIR A L'INTERPOLATION DU NOEUD INO2 DE M2

C     '.PJEF_NB' (INO2) : NOMBRE DE NOEUDS DE IMA1

C     '.PJEF_CO' (3*(INO2-1)+1) : "KSI" DE INO2 DANS IMA1
C     '.PJEF_CO' (3*(INO2-1)+2) : "ETA" DE INO2 DANS IMA1
C     '.PJEF_CO' (3*(INO2-1)+3) : "DZETA" DE INO2 DANS IMA1
C       REMARQUES :
C        1) PJEF_CO NE SERT QUE POUR LES LIAISONS 3D/COQUE ET 3D/POUTRE
C        2) LES MAILLES SEG N'UTILISENT QUE KSI,
C           LES TRIA ET QUAD N'UTILISENT QUE KSI ET ETA

C     '.PJEF_NU' : CONTIENT LES NUMEROS DES NOEUDS DE M1 SERVANT A
C                  L'INTERPOLATION DES NOEUDS DE M2 (MIS BOUT A BOUT)
C     '.PJEF_CF' : CONTIENT LES COEFFICIENTS POUR LES NOEUDS DE
C                  M1 SERVANT A L'INTERPOLATION DES NOEUDS DE M2
C                  (MIS BOUT A BOUT)

C     -- LES OBJETS .PJEF_TR ET PJ_EF_AM N'EXISTENT QUE DANS LES
C        CORRESP_2_MAILLA (TEMPORAIRES) FAITS DE TR3
C     '.PJEF_TR' (INO2) : NUMERO DU TR3 ASSOCIE AU NOEUD INO2

C     '.PJEF_AM' (INO2) :
C          1 -> LE NOEUD INO2 EST INCLU DANS UNE MAILLE DE M1
C               ON PEUT ALORS UTILISER REEREG.F POUR AMELIORER LA
C               PRECISION DE L'INTERPOLATION
C          0 -> SINON

C    EXEMPLE D'UTILISATION :
C      ON VEUT SAVOIR COMMENT INTERPOLER INO2 A PARTIR DU MAILLAGE M1
C      SOIT NBNO1='.PJEF_NB'(INO2)
C      SOIT DECAL= SOMME POUR INO<INO2 DE '.PJEF_NB'(INO)
C      VAL2(INO2)=0
C      DO I=1,NBNO1
C        NUNO1='.PJEF_NU' (DECAL+I)
C        COEFR='.PJEF_CF' (DECAL+I)
C        VAL2(INO2)=VAL2(INO2)+COEFR*VAL1(UNO1)
C      END DO
C----------------------------------------------------------------------
C DEB ------------------------------------------------------------------
      CALL JEMARQ()
C               12   345678   9012345678901234
      CORRES = '&&PJEFTE.CORRESP'
      CORRE1 = '&&PJEFTE.CORRES1'
      CORRE2 = '&&PJEFTE.CORRES2'
      CORRE3 = '&&PJEFTE.CORRES3'

      CALL GETVID(' ','MODELE_1',1,1,1,MODEL1,N1)
      CALL GETVID(' ','MODELE_2',1,1,1,MODEL2,N1)

      CALL DISMOI('F','NOM_MAILLA',MODEL1,'MODELE',IBID,NOMA1,IE)
      CALL DISMOI('F','NOM_MAILLA',MODEL2,'MODELE',IBID,NOMA2,IE)


C     DETERMINATION DE DISTMA ET LDMAX:
C     --------------------------------------------------------
      LDMAX = .FALSE.
      DISTMA = R8MAEM()
      CALL GETVR8(' ','DISTANCE_MAX',1,0,0,DISTMA,N1)
      IF ( ABS(N1) .EQ. 1 ) THEN
        CALL GETVR8(' ','DISTANCE_MAX',1,0,1,DISTMA,N1)
        LDMAX = .TRUE.
      ENDIF


      CALL GETFAC('VIS_A_VIS',NBOCC)
      IF (NBOCC.EQ.0) THEN
C        -- CAS : TOUT:'OUI'
C        ------------------------
         CALL PJEFCA (MODEL1,' ',0,NCAS )

C        PRISE EN COMPTE DU MOT-CLE TRANSF_GEOM_[1|2]
C        --------------------------------------------
         CALL PJEFTG(1,GEOM1,NOMA1,' ',1)
         CALL PJEFTG(2,GEOM2,NOMA2,' ',1)

         IF (NCAS.EQ.'2D') THEN
            CALL PJ2DCO('TOUT',MODEL1,MODEL2,0,0,0,0,GEOM1,GEOM2,
     &                 CORRES,LDMAX,DISTMA)
         ELSE IF (NCAS.EQ.'3D') THEN
            CALL PJ3DCO('TOUT',MODEL1,MODEL2,0,0,0,0,GEOM1,GEOM2,
     &                 CORRES,LDMAX,DISTMA)
         ELSE IF (NCAS.EQ.'2.5D') THEN
            CALL PJ4DCO('TOUT',MODEL1,MODEL2,0,0,0,0,GEOM1,GEOM2,
     &                 CORRES,LDMAX,DISTMA)
         ELSE IF (NCAS.EQ.'1.5D') THEN
            CALL PJ6DCO('TOUT',MODEL1,MODEL2,0,0,0,0,GEOM1,GEOM2,
     &                 CORRES,LDMAX,DISTMA)
         ELSE
            CALL ASSERT(.FALSE.)
         END IF

      ELSE

C        -- CAS : VIS_A_VIS
C        ------------------------
         DO 30 IOCC = 1,NBOCC

C           -- RECUPERATION DE LA LISTE DE MAILLES LMA1 :
C           ----------------------------------------------
            MOTCLE(1) = 'MAILLE_1'
            TYMOCL(1) = 'MAILLE'
            MOTCLE(2) = 'GROUP_MA_1'
            TYMOCL(2) = 'GROUP_MA'
            MOTCLE(3) = 'TOUT_1'
            TYMOCL(3) = 'TOUT'
            CALL RELIEM(MODEL1,NOMA1,'NU_MAILLE','VIS_A_VIS',IOCC,3,
     &                  MOTCLE,TYMOCL,'&&PJEFTE.LIMANU1',NBMA1)
            CALL JEVEUO('&&PJEFTE.LIMANU1','L',IAGMA1)

C           -- RECUPERATION DE LA LISTE DE NOEUDS LNO2 :
C           ----------------------------------------------
            MOTCLE(1) = 'NOEUD_2'
            TYMOCL(1) = 'NOEUD'
            MOTCLE(2) = 'GROUP_NO_2'
            TYMOCL(2) = 'GROUP_NO'
            MOTCLE(3) = 'MAILLE_2'
            TYMOCL(3) = 'MAILLE'
            MOTCLE(4) = 'GROUP_MA_2'
            TYMOCL(4) = 'GROUP_MA'
            MOTCLE(5) = 'TOUT_2'
            TYMOCL(5) = 'TOUT'
            CALL RELIEM(' ',NOMA2,'NU_NOEUD','VIS_A_VIS',IOCC,5,
     &                  MOTCLE,TYMOCL,'&&PJEFTE.LINONU2',NBNO2)
            CALL JEVEUO('&&PJEFTE.LINONU2','L',IAGNO2)

C           PRISE EN COMPTE DU MOT-CLE TRANSF_GEOM_[1|2]
C           --------------------------------------------
            CALL PJEFTG(1,GEOM1,NOMA1,'VIS_A_VIS',IOCC)
            CALL PJEFTG(2,GEOM2,NOMA2,'VIS_A_VIS',IOCC)

C           -- CALCUL DU CORRESP_2_MAILLA POUR IOCC :
C           ----------------------------------------------
            CALL PJEFCA (MODEL1,'&&PJEFTE.LIMANU1',IOCC,NCAS)

            CALL DETRSD('CORRESP_2_MAILLA',CORRE1)
            IF (NCAS.EQ.'2D') THEN
               CALL PJ2DCO('PARTIE',MODEL1,MODEL2,NBMA1,ZI(IAGMA1),
     &               NBNO2,ZI(IAGNO2),GEOM1,GEOM2,CORRE1,LDMAX,DISTMA)
            ELSE IF (NCAS.EQ.'3D') THEN
               CALL PJ3DCO('PARTIE',MODEL1,MODEL2,NBMA1,ZI(IAGMA1),
     &               NBNO2,ZI(IAGNO2),GEOM1,GEOM2,CORRE1,LDMAX,DISTMA)
            ELSE IF (NCAS.EQ.'2.5D') THEN
               CALL PJ4DCO('PARTIE',MODEL1,MODEL2,NBMA1,ZI(IAGMA1),
     &               NBNO2,ZI(IAGNO2),GEOM1,GEOM2,CORRE1,LDMAX,DISTMA)
            ELSE IF (NCAS.EQ.'1.5D') THEN
               CALL PJ6DCO('PARTIE',MODEL1,MODEL2,NBMA1,ZI(IAGMA1),
     &               NBNO2,ZI(IAGNO2),GEOM1,GEOM2,CORRE1,LDMAX,DISTMA)
            ELSE
               CALL ASSERT(.FALSE.)
            END IF


C           -- SURCHARGE DU CORRESP_2_MAILLA :
C           ----------------------------------------------
            IF (IOCC.EQ.1) THEN
               CALL COPISD('CORRESP_2_MAILLA','V',CORRE1,CORRE2)
            ELSE
               CALL PJFUCO(CORRE2,CORRE1,'V',CORRE3)
               CALL DETRSD('CORRESP_2_MAILLA',CORRE2)
               CALL COPISD('CORRESP_2_MAILLA','V',CORRE3,CORRE2)
               CALL DETRSD('CORRESP_2_MAILLA',CORRE3)
            END IF

            CALL JEDETR('&&PJEFTE.LIMANU1')
            CALL JEDETR('&&PJEFTE.LINONU2')
   30    CONTINUE
         CALL COPISD('CORRESP_2_MAILLA','V',CORRE2,CORRES)
         CALL DETRSD('CORRESP_2_MAILLA',CORRE1)
         CALL DETRSD('CORRESP_2_MAILLA',CORRE2)
      END IF

C       3 -- PROJECTION DES CHAMPS DE RESU1 SUR MODEL2
C          SUIVANT LA CORRESPONDANCE CORRES
C       --------------------------------------------------------
      CALL PJEFPR(RESU1(1:8),RESU2(1:8),MODEL2,CORRES)

      CALL DETRSD('CORRESP_2_MAILLA',CORRES)
      CALL JEDETC('V',RESU2(1:8),1)


      CALL JEDEMA()
      END
