      SUBROUTINE RESLOC( MODELE, LIGREL, YAXFEM, YATHM , TBGRCA, PERMAN,
     &                   CHTIME, MATE  , SIGMAM, SIGMAP, CHSIGX, CHDEPM,
     &                   CHDEPP, CHERRM, LCHAR , NCHAR , TABIDO, CHVOIS,
     &                   CVOISX, CHELEM )
C ......................................................................
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 14/10/2008   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_20
C
C     BUT:
C         CALCUL DE L'ESTIMATEUR D'ERREUR EN RESIDU
C                     OPTION : 'ERRE_ELEM_SIGM'
C     ARGUMENTS:
C     ----------
C ENTREE :
C
C     MODELE : NOM DU MODELE
C     LIGREL : NOM DU LIGREL
C     YAXFEM : EST-CE UNE MODELISATION XFEM ?
C     YATHM  : EST-CE UNE MODELISATION THM ?
C     TBGRCA : TABLEAU GRANDEURS CARACTERISTIQUES
C              (UTILISE SI MODELISATION HM)
C     PERMAN : SI OUI, EST-CE UNE MODELISATION PERMANENTE ?
C     CHTIME : CARTE D'INSTANTS POUR L'INSTANT ACTUEL
C     MATE   : NOM DU CONCEPT CHAMP_MATERIAU
C     SIGMAM : NOM DU CHAMP DE CONTRAINTES AUX NOEUDS PAR ELEMENT
C              A L'INSTANT PRECEDENT
C     SIGMAP : NOM DU CHAMP DE CONTRAINTES AUX NOEUDS PAR ELEMENT
C              A L'INSTANT ACTUEL
C     CHDEPM : NOM DU CHAMP DE DEPLACEMENTS A L'INSTANT PRECEDENT
C     CHDEPP : NOM DU CHAMP DE DEPLACEMENTS A L'INSTANT ACTUEL
C     CHERRM : NOM DU CHAMP D'INDICATEURS A L'INSTANT PRECEDENT
C     LCHAR  : LISTE DES CHARGES
C     NCHAR  : NOMBRE DE CHARGES
C     TABIDO : TABLEAU D'ENTIERS CONTENANT DES ADRESSES
C          1 : IATYMA : ADRESSE DU VECTEUR TYPE MAILLE (NUMERO <-> NOM)
C          2 : IAGD   : ADRESSE DU VECTEUR GRANDEUR (NUMERO <-> NOM)
C          3 : IACMP  : ADRESSE DU VECTEUR NOMBRE DE COMPOSANTES
C                 (NUMERO DE GRANDEUR <-> NOMBRE DE COMPOSANTES)
C          4 : ICONX1 : ADRESSE DE LA COLLECTION CONNECTIVITE
C          5 : ICONX2 : ADRESSE DU POINTEUR DE LONGUEUR DE LA
C                       CONNECTIVITE
C     CHVOIS : NOM DU CHAMP DES VOISINS
C     CVOISX : POUR XFEM, NOM DU CHAMP DES VOISINS DES SOUS-ELEMENTS
C
C SORTIE :
C      CHELEM  : NOM DU CHAM_ELEM_ERREUR PRODUIT
C                SI CHELEM EXISTE DEJA, ON LE DETRUIT.
C ......................................................................

      IMPLICIT NONE
C DECLARATION PARAMETRES D'APPELS
      INTEGER       NCHAR
      INTEGER       TABIDO(5)
      CHARACTER*8   MODELE, LCHAR(1)
      CHARACTER*24  SIGMAM, SIGMAP, CHDEPM, CHDEPP, CHERRM
      CHARACTER*24  CHTIME, CHVOIS, CHSIGX, CVOISX, CHELEM
      CHARACTER*(*) LIGREL, MATE
      REAL*8        TBGRCA(3)
      LOGICAL       YAXFEM, YATHM, PERMAN
C
C DECLARATION VARIABLES LOCALES
C
      CHARACTER*32 JEXNOM
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'RESLOC' )
C
      INTEGER NBCMP
      PARAMETER ( NBCMP = 12 )
C
      INTEGER NBCMPC
      PARAMETER ( NBCMPC = 20 )
C
      INTEGER I,J,ICMP(NBCMP),IATYMA, IAGD, IACMP, ICONX1, ICONX2
      INTEGER IRET,IRET1,IRET2,IRET4,IRET5,IRET6,IRET7
      INTEGER JCELDP,JCELVP,JCELDM,JCELVM
      INTEGER IADE1,IAPTM1,IAVA1,NUMGD1
      INTEGER IADE2,IAPTM2,IAVA2,NUMGD2
      INTEGER IADE3,IAPTM3,IAVA3,NUMGD3
      INTEGER IAREPE
      INTEGER IBID,IER
      INTEGER NBRIN
      REAL*8  RCMP(2)
      LOGICAL      EXIGEO
      CHARACTER*1  BASE
      CHARACTER*8  LICMP(NBCMP),LPAIN(NBCMPC),LPAOUT(1),KCMP
      CHARACTER*16 OPTION
      CHARACTER*19 CARTE1,CARTE2,CARTE3,NOMGD1,NOMGD2,NOMGD3
      CHARACTER*19 PINTTO,CNSETO,LONCHA
      CHARACTER*24 CHGEOM,LCHIN(NBCMPC),LCHOUT(1),CHFOR1,CHFOR2,CHFOR3
      COMPLEX*16   CCMP
C
      INTEGER NTYCHX
      PARAMETER ( NTYCHX = 9 )
      INTEGER     ITYCHA(NTYCHX)
      CHARACTER*5 KTYCH(NTYCHX)

C DEB-------------------------------------------------------------------
C====
C 1. PREALABLES
C====
C
      BASE = 'V'
C
      CALL MEGEOM(MODELE,LCHAR(1),EXIGEO,CHGEOM)
      IF (.NOT.EXIGEO) CALL U2MESS('F','CALCULEL2_81')

C
C ------- TEST SUR LE TYPE DE CHARGEMENT DES BORDS --------------------
C
C   ATTENTION : POUR UN MEME CHARGEMENT (FORCE_FACE OU PRES_REP), SEULE
C   LA DERNIERE CHARGE EST CONSIDEREE (REGLE DE SURCHARGE ACTUELLEMENT)
C --- ON ALARME POUR LES CHARGES NON TRAITEES
C
      KTYCH(1) = 'CIMPO'
      KTYCH(2) = 'F1D2D'
      KTYCH(3) = 'F2D3D'
      KTYCH(4) = 'PRESS'
      KTYCH(5) = 'PESAN'
      KTYCH(6) = 'ROTAT'
      KTYCH(7) = 'F2D2D'
      KTYCH(8) = 'F3D3D'
      KTYCH(9) = 'FLUX'
C
      DO 11 , J = 1 , NTYCHX
        ITYCHA(J) = 0
   11 CONTINUE
C
      IRET1 = 0
      DO 12 I = 1 , NCHAR
        IRET2 = 0
        DO 121 , J = 1 , NTYCHX
          CALL EXISD('CHAMP_GD',LCHAR(I)//'.CHME.'//KTYCH(J),IRET)
          IF ( IRET.NE.0 ) THEN
            ITYCHA(J) = ITYCHA(J) + 1
            IRET2 = IRET2 + 1
          ENDIF
  121   CONTINUE
        IF ( IRET2.EQ.0 ) THEN
          CALL U2MESK('A','INDICATEUR_6',1,LCHAR(I))
          IRET1 = IRET1 + 1
        ENDIF
   12 CONTINUE
C
C     ON VERIFIE QU'UN TYPE DE CHARGE N'EST PAS PRESENT 2 FOIS
C     REMARQUE : SAUF POUR DU DIRICHLET (CIMPO)
C
      DO 13 , J = 2 , NTYCHX
        IF ( ITYCHA(J).GT.1 ) THEN
          CALL U2MESK('A','INDICATEUR_7',1,KTYCH(J))
          IRET1 = IRET1 + 1
        ENDIF
   13 CONTINUE
C
      IF ( IRET1.NE.0 ) THEN
        CALL U2MESS('F','INDICATEUR_8')
      ENDIF
C
C====
C 2. DECODAGE DES CHARGES
C====
C
      CARTE1 = ' '
      CARTE2 = ' '
      CARTE3 = ' '
      NOMGD1 = ' '
      NOMGD2 = ' '
      NOMGD3 = ' '
C
      DO 21 I = 1,NCHAR
C
C 2.1. ==> FORCES AU BORD
C
        CALL EXISD('CHAMP_GD',LCHAR(I)//'.CHME.F1D2D',IRET1)
        CALL EXISD('CHAMP_GD',LCHAR(I)//'.CHME.F2D3D',IRET2)
        IF (IRET1.NE.0) THEN
          CARTE1 = LCHAR(I)//'.CHME.F1D2D'
          CALL DISMOI('F','NOM_GD',CARTE1,'CARTE',IBID,NOMGD1,IER)
          CALL ETENCA(CARTE1,LIGREL,IRET)
          CALL ASSERT (IRET.EQ.0)
        ELSE IF (IRET2.NE.0) THEN
          CARTE1 = LCHAR(I)//'.CHME.F2D3D'
          CALL DISMOI('F','NOM_GD',CARTE1,'CARTE',IBID,NOMGD1,IER)
          CALL ETENCA(CARTE1,LIGREL,IRET)
          CALL ASSERT (IRET.EQ.0)
        END IF
C
C 2.2. ==> PRESSIONS MECANIQUES
C
        CALL EXISD('CHAMP_GD',LCHAR(I)//'.CHME.PRESS',IRET1)
        IF (IRET1.NE.0) THEN
          CARTE2 = LCHAR(I)//'.CHME.PRESS'
          CALL DISMOI('F','NOM_GD',CARTE2,'CARTE',IBID,NOMGD2,IER)
          CALL ETENCA(CARTE2,LIGREL,IRET)
          CALL ASSERT (IRET.EQ.0)
        END IF
C
C 2.3. ==> EN THM, CONDITIONS DE NEUMANN HYDRAULIQUES
C
        IF ( YATHM ) THEN
          CALL EXISD('CHAMP_GD',LCHAR(I)//'.CHME.FLUX',IRET1)
          IF (IRET1.NE.0) THEN
            CARTE3 = LCHAR(I)//'.CHME.FLUX'
            CALL DISMOI('F','NOM_GD',CARTE3,'CARTE',IBID,NOMGD3,IER)
            CALL ETENCA(CARTE3,LIGREL,IRET)
            CALL ASSERT (IRET.EQ.0)
          ENDIF
        ENDIF
C
   21 CONTINUE
C
      CALL JEVEUO(LIGREL(1:19)//'.REPE','L',IAREPE)
C
C RECUPERATION DES ADRESSES DU CHAMP DE CONTRAINTES A L'INSTANT ACTUEL
C
      CALL JEVEUO(SIGMAP(1:19)//'.CELD','L',JCELDP)
      CALL JEVEUO(SIGMAP(1:19)//'.CELV','L',JCELVP)
C
C RECUPERATION DES ADRESSES DU CHAMP DE CONTRAINTES A L'INSTANT
C PRECEDENT
C
      IF ( YATHM .AND. ( .NOT. PERMAN ) ) THEN
        CALL JEVEUO(SIGMAM(1:19)//'.CELD','L',JCELDM)
        CALL JEVEUO(SIGMAM(1:19)//'.CELV','L',JCELVM)
      ELSE
        JCELDM = -1
        JCELVM = -1
      ENDIF
C
C RECUPERATION DES ADRESSES DE LA CARTE NUMERO 1
C
      IF (CARTE1 .NE. ' ') THEN
        CALL JEVEUO (CARTE1//'.DESC','L',IADE1)
        CALL JEVEUO (CARTE1//'.VALE','L',IAVA1)
        CALL JEEXIN (CARTE1//'.PTMA',IRET)
        IF (IRET .EQ. 0) THEN
          IAPTM1 = 0
        ELSE
C            LA CARTE A ETE ETENDUE
          CALL JEVEUO (CARTE1//'.PTMA','L',IAPTM1)
        ENDIF
        CALL JENONU(JEXNOM('&CATA.GD.NOMGD',NOMGD1),NUMGD1)
      ELSE
        IADE1  = 0
        IAVA1  = 0
        IAPTM1 = 0
        NUMGD1 = 0
      ENDIF
C
C RECUPERATION DES ADRESSES DE LA CARTE NUMERO 2
C
      IF (CARTE2 .NE. ' ') THEN
        CALL JEVEUO (CARTE2//'.DESC','L',IADE2)
        CALL JEVEUO (CARTE2//'.VALE','L',IAVA2)
        CALL JEEXIN (CARTE2//'.PTMA',IRET)
        IF (IRET .EQ. 0) THEN
          IAPTM2 = 0
        ELSE
C            LA CARTE A ETE ETENDUE
          CALL JEVEUO (CARTE2//'.PTMA','L',IAPTM2)
        ENDIF
        CALL JENONU(JEXNOM('&CATA.GD.NOMGD',NOMGD2),NUMGD2)
      ELSE
        IADE2  = 0
        IAVA2  = 0
        IAPTM2 = 0
        NUMGD2 = 0
      ENDIF
C
C RECUPERATION DES ADRESSES DE LA CARTE NUMERO 3 (THM UNIQUEMENT)
C
      IF (CARTE3 .NE. ' ') THEN
        CALL JEVEUO (CARTE3//'.DESC','L',IADE3)
        CALL JEVEUO (CARTE3//'.VALE','L',IAVA3)
        CALL JEEXIN (CARTE3//'.PTMA',IRET)
C
          IF (IRET .EQ. 0) THEN
            IAPTM3 = 0
          ELSE
C          LA CARTE A ETE ETENDUE
            CALL JEVEUO (CARTE3//'.PTMA','L',IAPTM3)
          ENDIF
C
        CALL JENONU(JEXNOM('&CATA.GD.NOMGD',NOMGD3),NUMGD3)
      ELSE
        IADE3  = 0
        IAVA3  = 0
        IAPTM3 = 0
        NUMGD3 = 0
      ENDIF
C
C====
C 3. CREATION DE 2 CARTES CONTENANT DES ADRESSES D'OBJETS JEVEUX
C====
C
      IATYMA = TABIDO(1)
      IAGD   = TABIDO(2)
      IACMP  = TABIDO(3)
      ICONX1 = TABIDO(4)
      ICONX2 = TABIDO(5)
C
      LICMP(1) = 'X1'
      LICMP(2) = 'X2'
      LICMP(3) = 'X3'
      LICMP(4) = 'X4'
      LICMP(5) = 'X5'
      LICMP(6) = 'X6'
      LICMP(7) = 'X7'
      LICMP(8) = 'X8'
      LICMP(9) = 'X9'
      LICMP(10) = 'X10'
      LICMP(11) = 'X11'
      LICMP(12) = 'X12'
C
      ICMP(1) = IAREPE
      ICMP(2) = JCELDP
      ICMP(3) = JCELVP
      ICMP(4) = IATYMA
      ICMP(5) = IAGD
      ICMP(6) = IACMP
C
      ICMP(7) = IADE1
      ICMP(8) = IAVA1
      ICMP(9) = IAPTM1
      ICMP(10) = NUMGD1
C
      ICMP(11) = ICONX1
      ICMP(12) = ICONX2
C
      CALL MECACT(BASE,'&&'//NOMPRO//'.CH_FORCE','MODELE',LIGREL,
     &            'NEUT_I',NBCMP,LICMP,ICMP,RCMP,CCMP,KCMP)
C
      ICMP(2) = JCELDM
      ICMP(3) = JCELVM
C
      ICMP(5) = IADE2
      ICMP(6) = IAVA2
      ICMP(7) = IAPTM2
      ICMP(8) = NUMGD2
C
      ICMP(9)  = IADE3
      ICMP(10) = IAVA3
      ICMP(11) = IAPTM3
      ICMP(12) = NUMGD3
C
      CALL MECACT(BASE,'&&'//NOMPRO//'.CH_PRESS','MODELE',LIGREL,
     &            'NEUT_I',NBCMP,LICMP,ICMP,RCMP,CCMP,KCMP)
C
C====
C 4. CHARGEMENTS VOLUMIQUES : PESANTEUR, ROTATION OU FORCES DE VOLUME
C     ATTENTION : SI UN TYPE DE CHARGEMENT EST PRESENT DANS PLUSIEURS
C                 CHARGES, SEULE SON OCCURENCE DANS LA DERNIERE CHARGE
C                 EST CONSIDEREE
C====
C
      CHFOR1 = ' '
      CHFOR2 = ' '
C --- SI ABSENCE D'UN CHAMP DE FORCES, CREATION D'UN CHAMP NUL
      CHFOR3 = '&&'//NOMPRO//'.CH_NULLE'
      CALL MEFOR0(MODELE,CHFOR3,.FALSE.)
C
      DO 22 I = 1,NCHAR
        CALL EXISD('CHAMP_GD',LCHAR(I)//'.CHME.PESAN',IRET4)
        CALL EXISD('CHAMP_GD',LCHAR(I)//'.CHME.ROTAT',IRET5)
        CALL EXISD('CHAMP_GD',LCHAR(I)//'.CHME.F2D2D',IRET6)
        CALL EXISD('CHAMP_GD',LCHAR(I)//'.CHME.F3D3D',IRET7)
        IF (IRET4.NE.0) THEN
          CHFOR1 = LCHAR(I)//'.CHME.PESAN.DESC'
        END IF
        IF (IRET5.NE.0) THEN
          CHFOR2 = LCHAR(I)//'.CHME.ROTAT.DESC'
        END IF
        IF (IRET6.NE.0) THEN
          CHFOR3 = LCHAR(I)//'.CHME.F2D2D.DESC'
        END IF
        IF (IRET7.NE.0) THEN
          CHFOR3 = LCHAR(I)//'.CHME.F3D3D.DESC'
        END IF
   22 CONTINUE
C
C====
C 5. PARAMETRES DU CALCUL
C====
C
C ==> SI ON EST SUR UNE MODELISATION XFEM ...
C
      IF ( YAXFEM ) THEN
C
C 5.1. ==> ... ON RECUPERE LES ADRESSES DES CHAMPS PROPRES A X-FEM
C     
        PINTTO = MODELE//'.TOPOSE.PIN'
        CNSETO = MODELE//'.TOPOSE.CNS'
        LONCHA = MODELE//'.TOPOSE.LON'
C
      ENDIF
C
C ==> SI ON EST SUR UNE MODELISATION HM ...
C
      IF ( YATHM ) THEN
C
C 5.2. ==> ... ON CREE UNE CARTE CONTENANT
C              LONGUEUR ET PRESSION CARACTERISTIQUES
C
        LICMP(1) = 'X1'
        LICMP(2) = 'X2'
        RCMP(1)  = TBGRCA(1)
        RCMP(2)  = TBGRCA(2)
C
        CALL MECACT(BASE,'&&'//NOMPRO//'.GRDCA','MODELE',LIGREL,
     &              'NEUT_R',2,LICMP,ICMP,RCMP,CCMP,KCMP)
      ENDIF
C
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM
      LPAIN(2) = 'PCONTNO'
      LCHIN(2) = SIGMAP
      LPAIN(3) = 'PFRVOLU'
      LCHIN(3) = CHFOR3
      LPAIN(4) = 'PPESANR'
      LCHIN(4) = CHFOR1
      LPAIN(5) = 'PROTATR'
      LCHIN(5) = CHFOR2
      LPAIN(6) = 'PMATERC'
      LCHIN(6) = MATE
      LPAIN(7) = 'PFORCE'
      LCHIN(7) = '&&'//NOMPRO//'.CH_FORCE'
      LPAIN(8) = 'PPRESS'
      LCHIN(8) = '&&'//NOMPRO//'.CH_PRESS'
      LPAIN(9) = 'PVOISIN'
      LCHIN(9) = CHVOIS
      LPAIN(10) = 'PTEMPSR'
      LCHIN(10) = CHTIME
      NBRIN = 10
C
      IF ( YAXFEM ) THEN
C
        LPAIN(11)= 'PPINTTO'
        LCHIN(11)= PINTTO
        LPAIN(12)= 'PCNSETO'
        LCHIN(12)= CNSETO
        LPAIN(13)= 'PLONCHA'
        LCHIN(13)= LONCHA
        LPAIN(14) = 'PCVOISX'
        LCHIN(14) = CVOISX     
        LPAIN(15) = 'PSIEFSER'
        LCHIN(15) = CHSIGX
        NBRIN = 15
C
      ENDIF
C
      IF ( YATHM ) THEN
C
        LPAIN(11) = 'PDEPLAR'
        LCHIN(11) = CHDEPP
        LPAIN(12) = 'PGRDCA'
        LCHIN(12) = '&&'//NOMPRO//'.GRDCA'
        IF (PERMAN) THEN
          NBRIN = 12
        ELSE
          LPAIN(13) = 'PDEPLMR'
          LCHIN(13) = CHDEPM
          LPAIN(14) = 'PCONTNM'
          LCHIN(14) = SIGMAM
          LPAIN(15) = 'PERREM'
          LCHIN(15) = CHERRM
          NBRIN = 15
        ENDIF
C
      ENDIF
C
      LPAOUT(1) = 'PERREUR'
      LCHOUT(1) = CHELEM
      OPTION = 'ERRE_ELEM_SIGM'
C     
      CALL CALCUL('C',OPTION,LIGREL,NBRIN,LCHIN,LPAIN,1,
     &            LCHOUT,LPAOUT,'G')
      CALL EXISD('CHAMP_GD',LCHOUT(1),IRET)
      IF (IRET.EQ.0) THEN
        CALL U2MESK('A','CALCULEL2_88',1,OPTION)
        GOTO 9999
      END IF
C
C====
C 4. MENAGE FINAL
C====
C
      CALL JEDETR(CARTE1//'.PTMA')
      CALL JEDETR(CARTE2//'.PTMA')
      CALL JEDETR(CARTE3//'.PTMA')
C
      CALL DETRSD('CHAMP_GD','&&'//NOMPRO//'.CH_FORCE')
      CALL DETRSD('CHAMP_GD','&&'//NOMPRO//'.CH_PRESS')
      IF (IRET6.EQ.0 .AND. IRET7.EQ.0) THEN
        CALL DETRSD('CHAMP_GD',CHFOR3)
      END IF
C
9999  CONTINUE
C
      END
