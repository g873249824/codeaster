      SUBROUTINE CHLIGR(CHEL1Z,LIGR2Z,OPTIOZ,PARAMZ,BASE2 ,
     &                  CHEL2Z)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 23/04/2013   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*(*) LIGR2Z,CHEL2Z,CHEL1Z,BASE2,OPTIOZ,PARAMZ
C
C ----------------------------------------------------------------------
C
C BUT :
C       "CONVERTIR" UN CHAM_ELEM (CHEL1Z) EN UN AUTRE CHAM_ELEM (CHEL2Z)
C       SUR UN AUTRE LIGREL (LIGR2Z).
C       CETTE ROUTINE DEVRAIT POUVOIR TRAITER DES RESUELEM MAIS CE N'EST
C       PAS IMPLEMENTE.
C
C  ATTENTION :
C  -----------
C  1) CETTE ROUTINE NE SAIT PAS TRAITER LES ELEMENTS DE LIGR2Z DONT
C     LES MAILLES SONT TARDIVES
C  2) SUR LES ELEMENTS DE LIGRE2 QUI N'ETAIENT PAS DANS LIGRE1
C     LE CHAMP EST PROLONGE PAR 0.
C  3) IL NE FAUT PAS APPELER CETTE ROUTINE AVEC LE MEME NOM POUR
C     CHEL1 ET CHEL2
C  4) SI CHEL2 EST VIDE, LA ROUTINE N'EMET AUCUN MESSAGE, C'EST A
C     L'APPELANTDE TESTER L'EXISTENCE REELLE DU CHAMP RESULTAT
C
C ----------------------------------------------------------------------
C
C  CHEL1  IN/JXIN  K19 : NOM DU CHAM_ELEM A CONVERTIR
C  CHEL2  IN/JXOUT K19 : NOM DU CHAM_ELEM RESULTAT
C                        CHEL2 EST DETRUIT S'IL EXISTE DEJA
C  LIGR2  IN/JXIN  K19 : NOM DU LIGREL SUR LEQUEL ON VA CREER CHEL2
C  OPTIO  IN       K16 : NOM DE L'OPTION SERVANT A ALLOUER CHEL2
C  PARAM  IN       K8  : NOM DU PARAMETRE DE L'OPTION SERVANT
C                        A ALLOUER CHEL2
C  BASE2  IN       K1  : BASE DE CREATION DE CHEL2
C
C ----------------------------------------------------------------------
C
      CHARACTER*4  TYCH
      CHARACTER*19 CES,CHELV
      CHARACTER*19 LIGR1,LIGR2
      INTEGER      IBID,IRET,NNCP,JCELK,NBMA
      CHARACTER*19 CHEL2,CHEL1,OPTIO,PARAM
      CHARACTER*24 VALK(2)
      CHARACTER*8  NOMA,K8BID
      CHARACTER*16 NOMGD
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      LIGR2  = LIGR2Z
      CHEL2  = CHEL2Z
      CHEL1  = CHEL1Z
      OPTIO  = OPTIOZ
      PARAM  = PARAMZ
C
C --- MAILLAGE ATTACHE
C
      CALL DISMOI('F','NOM_MAILLA',CHEL1,'CHAM_ELEM',IBID,NOMA ,IBID)
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8BID,IBID)
      CALL DISMOI('F','NOM_LIGREL',CHEL1,'CHAM_ELEM',IBID,LIGR1,IBID)
      CALL DISMOI('F','NOM_GD',CHEL1    ,'CHAM_ELEM',IBID,NOMGD,IBID)
C
C --- TYPE DU CHAMP: RESU_ELEM INTERDIT
C
      CALL JEEXIN(CHEL1//'.DESC',IRET)
      IF (IRET.GT.0) THEN
        CALL JELIRA(CHEL1//'.DESC','DOCU',IBID,TYCH)
      ELSE
        CALL JELIRA(CHEL1//'.CELD','DOCU',IBID,TYCH)
      ENDIF
      CALL ASSERT(TYCH.EQ.'CHML')
C
C --- VERIFICATIONS SI CHAMP ELGA
C
      CALL JEVEUO(CHEL1//'.CELK','L',JCELK)
      IF (ZK24(JCELK-1+3).EQ.'ELGA') THEN
        CHELV  = '&&CHLIGR.CHELVIDE'
        CALL ALCHML(LIGR2,OPTIO,PARAM,'V',CHELV,IRET,' ')
        IF (IRET.EQ.0) THEN
          CALL CHVEPG(CHEL1 ,CHELV )
        ELSE
          GOTO 20
        ENDIF
      ENDIF
C
C --- VERIFICATIONS SI VARI_ELGA
C
      IF (NOMGD.EQ.'VARI_ELGA') THEN
        CALL CHVEVA(NBMA  ,LIGR1 ,LIGR2 ,IRET)
        IF (IRET.NE.0) THEN
          VALK(1) = CHEL1
          VALK(2) = CHEL2
          CALL U2MESK('F','CALCULEL_90',2,VALK)
        ENDIF
      ENDIF
C
C --- ON TRANSFORME LE CHAM_ELEM CHEL1 EN CHAM_ELEM_S
C
      CES    = '&&CHLIGR.CES'
      CALL CELCES(CHEL1,'V',CES)
C
C --- ON TRANSFORME LE CHAM_ELEM_S EN CHEL2
C
      CALL CESCEL(CES   ,LIGR2 ,OPTIO ,PARAM ,'CHL' ,
     &            NNCP  ,BASE2 ,CHEL2 ,'F'   ,IBID  )
      CALL DETRSD('CHAM_ELEM_S',CES)
C
   20 CONTINUE
      CALL DETRSD('CHAM_ELEM','&&CHLIGR.CHELVIDE')
      CALL JEDEMA()
      END
