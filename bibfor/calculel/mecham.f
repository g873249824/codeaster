      SUBROUTINE MECHAM(OPTION,MODELE,CARA,NH,CHGEOZ,CHCARA,CHHARZ,IRET)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER IRET,NH
      CHARACTER*(*) OPTION,MODELE,CARA
      CHARACTER*(*) CHGEOZ,CHCARA(*),CHHARZ

C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 12/02/2013   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     VERIFICATION DES CHAMPS DONNES :
C        - ON VERIFIE LES EVENTUELLES SOUS-STRUCTURES STATIQUES
C        - ON VERIFIE S'IL Y A 1 LIGREL DANS LE MODELE
C     ------------------------------------------------------------------
C IN  : OPTION : OPTION DE CALCUL
C IN  : MODELE : MODELE
C IN  : CARA   : CHAMP DE CARA_ELEM
C IN  : NH     : NUMERO D'HARMONIQUE DE FOURIER
C OUT : CHGEOZ : NOM DE CHAMP DE GEOMETRIE TROUVE
C OUT : CHCARA : NOMS DES CHAMPS DE CARACTERISTIQUES TROUVES
C OUT : CHHARZ : NOM DU CHAMP D'HARMONIQUE DE FOURIER TROUVE
C OUT : IRET  : CODE RETOUR
C                = 0 : LE MODELE CONTIENT DES ELEMENTS FINIS
C                = 1 : LE MODELE NE CONTIENT PAS D'ELEMENTS FINIS
C     ------------------------------------------------------------------
      CHARACTER*8 K8B,NOMO,NOMA,NOMACR,EXIELE
      CHARACTER*24 CHGEOM,CHHARM
      LOGICAL EXICAR
C
C-----------------------------------------------------------------------
      INTEGER IANMCR,IASSSA,IBID,IER,IMA,IEXI,NBSMA
      INTEGER NBSS
C-----------------------------------------------------------------------
      CALL JEMARQ()
      CHGEOM=' '
      CHHARM=' '

      CALL ASSERT(MODELE(1:1).NE.' ')
      NOMO=MODELE
C
C     --- ON VERIFIE LES EVENTUELLES SOUS-STRUCTURES STATIQUES:
      CALL DISMOI('F','NB_SS_ACTI',NOMO,'MODELE',NBSS,K8B,IER)
      IF (NBSS.GT.0) THEN
        CALL DISMOI('F','NB_SM_MAILLA',NOMO,'MODELE',NBSMA,K8B,IER)
        CALL DISMOI('F','NOM_MAILLA',NOMO,'MODELE',IBID,NOMA,IER)
        CALL JEVEUO(NOMA//'.NOMACR','L',IANMCR)
        CALL JEVEUO(NOMO//'.MODELE    .SSSA','L',IASSSA)
C
C        --- BOUCLE SUR LES (SUPER)MAILLES ---
        IER=0
        IF (OPTION(1:9).EQ.'MASS_MECA') THEN
          DO 10 IMA=1,NBSMA
            IF (ZI(IASSSA-1+IMA).EQ.1) THEN
              NOMACR=ZK8(IANMCR-1+IMA)
              CALL JEEXIN(NOMACR//'.MAEL_MASS_VALE',IEXI)
              IF (IEXI.EQ.0) THEN
                IER=IER+1
                CALL U2MESK('E','CALCULEL3_31',1,NOMACR)
              ENDIF
            ENDIF
   10     CONTINUE
          IF (IER.GT.0) CALL U2MESS('F','CALCULEL3_32')
        ELSEIF (OPTION(1:9).EQ.'RIGI_MECA') THEN
          DO 20 IMA=1,NBSMA
            IF (ZI(IASSSA-1+IMA).EQ.1) THEN
              NOMACR=ZK8(IANMCR-1+IMA)
              CALL JEEXIN(NOMACR//'.MAEL_RAID_VALE',IEXI)
              IF (IEXI.EQ.0) THEN
                IER=IER+1
                CALL U2MESK('E','CALCULEL3_33',1,NOMACR)
              ENDIF
            ENDIF
   20     CONTINUE
          IF (IER.GT.0) THEN
            CALL U2MESS('F','CALCULEL3_34')
          ENDIF
        ELSEIF (OPTION(1:9).EQ.'AMOR_MECA') THEN
          DO 30 IMA=1,NBSMA
            IF (ZI(IASSSA-1+IMA).EQ.1) THEN
              NOMACR=ZK8(IANMCR-1+IMA)
              CALL JEEXIN(NOMACR//'.MAEL_AMOR_VALE',IEXI)
              IF (IEXI.EQ.0) THEN
                IER=IER+1
                CALL U2MESK('E','CALCULEL6_80',1,NOMACR)
              ENDIF
            ENDIF
   30     CONTINUE
          IF (IER.GT.0) CALL U2MESS('F','CALCULEL6_81')
        ENDIF
      ENDIF

C     --- ON REGARDE S'IL Y A 1 LIGREL DANS LE MODELE ---
      CALL DISMOI('F','EXI_ELEM',NOMO,'MODELE',IBID,EXIELE,IER)
      IF (EXIELE(1:3).EQ.'OUI') THEN
        IRET=0
      ELSE
        IRET=1
      ENDIF
      IF (IRET.EQ.1 .AND. NBSS.EQ.0) CALL U2MESS('F','CALCULEL3_35')
C
C     --- SI IL N'Y A PAS D'ELEMENTS, ON SORT :
      IF (IRET.EQ.1)GOTO 40
C
      CALL MEGEOM(NOMO,CHGEOM)
      CALL MECARA(CARA,EXICAR,CHCARA)
C     --- ON CREE UN CHAMP D'HARMONIQUE DE FOURIER (CARTE CSTE) ---
      CALL MEHARM(NOMO,NH,CHHARM)
C
   40 CONTINUE
      CHGEOZ=CHGEOM
      CHHARZ=CHHARM
      CALL JEDEMA()
      END
