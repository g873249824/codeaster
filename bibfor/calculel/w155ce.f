      SUBROUTINE W155CE(NOMRES,RESU,NBORDR,LIORDR)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C ======================================================================
C     COMMANDE :  POST_CHAMP / COQU_EXCENT
C ----------------------------------------------------------------------
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8 NOMRES,RESU
      INTEGER NBORDR,LIORDR(NBORDR)
C
      INTEGER IFM,NIV
      INTEGER IRET,I,NUORDR,IBID,NOCC,IOCC
      CHARACTER*8 MODELE,CARELE,MATE,MPLAN
      CHARACTER*8 MODEAV,LPAIN(2),LPAOUT(1)
      CHARACTER*4 TSCA
      CHARACTER*16 MOTFAC,NOMSYM
      CHARACTER*19 CHIN,CHEXTR,LIGREL,RESU19,LCHIN(2),LCHOUT(1),EXCIT
      INTEGER IARG,VALI(2),IEXI
      LOGICAL LREEL,LNOEU,LDETLI,LVIDE
C     ------------------------------------------------------------------
C
      CALL JEMARQ()
C

      CALL INFMAJ()
      CALL INFNIV(IFM,NIV)
      RESU19=RESU



C     -- 1. : Y-A-T-IL QUELQUE CHOSE A FAIRE ?
C     ----------------------------------------
      CALL GETFAC('COQU_EXCENT',NOCC)
      IF (NOCC.EQ.0)GOTO 30
      CALL ASSERT(NOCC.LT.10)


      MODEAV=' '
      LDETLI=.FALSE.
      LVIDE=.TRUE.
      DO 20,IOCC=1,NOCC

C     -- 2.  : NOMSYM, MPLAN :
C     --------------------------------------------------
        MOTFAC='COQU_EXCENT'
        CALL GETVTX(MOTFAC,'NOM_CHAM',IOCC,IARG,1,NOMSYM,IBID)
        CALL ASSERT(NOMSYM.EQ.'EFGE_ELNO'.OR.NOMSYM.EQ.'EFGE_ELGA')
        CALL GETVTX(MOTFAC,'MODI_PLAN',IOCC,IARG,1,MPLAN,IBID)
        CALL ASSERT(MPLAN.EQ.'OUI')
        LNOEU=NOMSYM.EQ.'EFGE_ELNO'


C     -- 3. : BOUCLE SUR LES CHAMPS
C     --------------------------------------------------
        DO 10,I=1,NBORDR
          NUORDR=LIORDR(I)
          CALL RSEXCH(RESU19,NOMSYM,NUORDR,CHIN,IRET)
          IF (IRET.EQ.0) THEN

C         -- 3.1 : MODELE, CARELE, LIGREL :
            CALL RSLESD(RESU,NUORDR,MODELE,MATE,CARELE,EXCIT,IBID)
            IF (MODELE.NE.MODEAV) THEN
              IF (LDETLI) CALL DETRSD('LIGREL',LIGREL)
              CALL EXLIMA(' ',1,'G',MODELE,LIGREL)
              MODEAV=MODELE
C             -- SI ON CREE UN LIGREL, IL FAUT VERIFIER QUE L'ON S'EN
C                SERT VRAIMENT. SINON, IL FAUT LE DETRUIRE:
              LDETLI=.FALSE.
              IF (LIGREL(1:8).NE.MODELE) LDETLI=.TRUE.
            ENDIF

            CALL RSEXCH(NOMRES,NOMSYM,NUORDR,CHEXTR,IRET)
            CALL ASSERT(IRET.EQ.100)

            CALL JELIRA(CHIN//'.CELV','TYPE',IBID,TSCA)
            IF (TSCA.EQ.'R') THEN
              LREEL=.TRUE.
            ELSEIF (TSCA.EQ.'C') THEN
              LREEL=.FALSE.
            ELSE
              CALL ASSERT(.FALSE.)
            ENDIF

            IF (LNOEU) THEN
              IF (LREEL) THEN
                LPAIN(1)='PEFFONR'
                LPAOUT(1)='PEFFOENR'
              ELSE
                LPAIN(1)='PEFFONC'
                LPAOUT(1)='PEFFOENC'
              ENDIF
            ELSE
              IF (LREEL) THEN
                LPAIN(1)='PEFFOGR'
                LPAOUT(1)='PEFFOEGR'
              ELSE
                LPAIN(1)='PEFFOGC'
                LPAOUT(1)='PEFFOEGC'
              ENDIF
            ENDIF

            LCHIN(1)=CHIN
            LCHOUT(1)=CHEXTR

            LPAIN(2)='PCACOQU'
            LCHIN(2)=CARELE//'.CARCOQUE'

            CALL CALCUL('C','EFGE_EXCENT',LIGREL,2,LCHIN,LPAIN,1,
     &                  LCHOUT,LPAOUT,'G','OUI')

            CALL JEEXIN(LCHOUT(1)//'.CELV',IEXI)
            IF (IEXI.EQ.0) THEN
              VALI(1)=IOCC
              VALI(2)=NUORDR
              CALL U2MESI('A','CALCULEL2_19',2,VALI)
            ELSE
              LDETLI=.FALSE.
              LVIDE=.FALSE.
              CALL RSNOCH(NOMRES,NOMSYM,NUORDR)
            ENDIF
          ENDIF
   10   CONTINUE
   20 CONTINUE

      IF (LDETLI) CALL DETRSD('LIGREL',LIGREL)
      IF (LVIDE) CALL U2MESS('F','CALCULEL2_20')


   30 CONTINUE
      CALL JEDEMA()
      END
