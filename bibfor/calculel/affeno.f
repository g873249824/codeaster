      SUBROUTINE AFFENO ( IOC,INO,NOCMP,NBCMP,NCMPGD,NCMPMX,VAL,KVAL,
     &                    NBVAL,DESC,VALGLO,KVALGL,TYPE,NEC )
      IMPLICIT  NONE
      INTEGER             INO, NBCMP, NCMPMX, NEC, NBVAL, DESC(*), IOC
      REAL*8              VALGLO(*), VAL(*)
      CHARACTER*(*)       TYPE
      CHARACTER*8         NOCMP(*), KVALGL(*), NCMPGD(*), KVAL(*)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 03/10/2001   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C
C    ROUTINE UTILISEE PAR OP0052 (OPERATEUR AFFE_CHAM_NO)
C
C    CETTE ROUTINE AFFECTE LES COMPOSANTES NOCMP DU NOEUD INO AUX
C    VALEURS VAL DANS LE TABLEAU VALGLO
C    ELLE CALCULE EGALEMENT LE DESCRIPTEUR GRANDEUR DESC DU NOEUD INO
C
C IN  : IOC    : NUMERO DE L'OCCURENCE DE AFFE DANS LA COMMANDE
C IN  : INO    : NUMERO DU NOEUD
C IN  : NOCMP  : LISTE DES COMPOSANTES A AFFECTER
C IN  : NBCMP  : NOMBRE DE COMPOSANTES A AFFECTER
C IN  : NCMPGD : LISTE GLOBALE DES COMPOSANTES DE LA GRANDEUR
C IN  : NCMPMX : NOMBRE DE COMPOSANTES DE LA GRANDEUR
C IN  : VAL    : VALEURS DES COMPOSANTES A AFFECTER
C IN  : KVAL   : VALEURS DES COMPOSANTES A AFFECTER EN K*8
C IN  : NBVAL  : NOMBRE DE VALEURS
C OUT : DESC   : DESCRIPTEUR GRANDEUR DU NOEUD
C OUT : VALGLO : TABLEAU DES VALEURS DES COMPOSANTES DE LA GRANDEUR
C
C-----------------------------------------------------------------------
C
      INTEGER  NOCOAF, ICMP, J, IEC, JJ, IND, INDIK8 , IOR
C
      NOCOAF = 0
      DO 10 ICMP = 1 , NBCMP
         J = INDIK8 ( NCMPGD, NOCMP(ICMP), 1, NCMPMX )
         IF ( J .NE. 0 ) THEN
            IEC = ( J - 1 ) / 30  + 1
            JJ = J -  30 * ( IEC - 1 ) 
            DESC((INO-1)*NEC+IEC) = IOR(DESC((INO-1)*NEC+IEC),2**JJ)
         ELSE
            CALL UTDEBM('F','AFFENO','COMPOSANTE NON DEFINIE DANS '//
     +                                                 'LA GRANDEUR.')
            CALL UTIMPK('L','  COMPOSANTE: ',1,NOCMP(ICMP))
            CALL UTFINM( )
         ENDIF
         NOCOAF = NOCOAF + 1
         IND = J + (INO-1)*NCMPMX
         IF ( TYPE(1:1) .EQ. 'R' ) THEN
            VALGLO(IND) = VAL(ICMP)
         ELSE
            KVALGL(IND) = KVAL(ICMP)
         ENDIF
 10   CONTINUE
C
      IF( NOCOAF .NE. NBCMP ) THEN
        CALL UTDEBM('F','AFFENO : ',
     + 'LE NOMBRE DE COMPOSANTES AFFECTEES N''EST PAS EGAL '//
     + 'AU NOMBRE DE COMPOSANTES A AFFECTER')
        CALL UTIMPI('L','OCCURENCE DE AFFE NUMERO',1,IOC)
        CALL UTIMPI('L','NBRE DE CMP AFFECTEES : ',1,NOCOAF)
        CALL UTIMPI('L','NBRE DE CMP A AFFECTER : ',1,NBCMP)
        CALL UTFINM
      ENDIF
C
      END
