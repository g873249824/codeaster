      SUBROUTINE MANOPG(LIGREZ,MNOGAZ)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 23/06/2005   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE VABHHTS J.PELLET
C A_UTIL
      IMPLICIT NONE
      CHARACTER*(*) LIGREZ,MNOGAZ
C ------------------------------------------------------------------
C BUT: CREER LE CHAM_ELEM_S MNOGAZ QUI CONTIENDRA LA MATRICE
C      DE PASSAGE NOEUD -> POINT DE GAUSS POUR LES ELEMENTS
C      DU LIGREL
C ------------------------------------------------------------------
C     ARGUMENTS:
C LIGREZ  IN/JXIN  K19 : LIGREL
C MNOGAZ  IN/JXOUT K19 : CHAM_ELEM_S (VARI_R) DE TYPE 'ELEM'
C ------------------------------------------------------------------
C REMARQUES :
C  MNOGAZ(IMA) EST UN VECTEUR V DE REELS DE DIMENSION 2 + NBNO*NBPG
C    V(1) : NBNO
C    V(2) : NBPG
C    V(2+NBNO*(IPG-1)+INO) : MATRICE DE PASSAGE (IPG,INO)

C  ATTENTION : LES MAILLES TARDIVES SONT IGNOREES.
C-----------------------------------------------------------------------

C---- COMMUNS NORMALISES  JEVEUX
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32,JEXNOM,JEXNUM,JEXATR
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     ------------------------------------------------------------------
      INTEGER NBPGMX,NBNOMX,NBFAMX
      PARAMETER (NBPGMX=27,NBNOMX=27,NBFAMX=10)
      INTEGER IBID,NBMA,IMA,JCESD,JCESL,JCESV,IAD,JNBPG
      INTEGER ILCNX1,NBPGF(NBFAMX)
      INTEGER NEC,KFPG,NDIM,NNO,NNOS,NBFPG,INDIK8,NPG,KP,K,INO
      INTEGER JCELD,NBGREL,NEL,NBELEM,NUTE,TYPELE,IMOLO,JMOLO
      INTEGER IERD,IFAM,NUMAIL,NCMP,JMANO,IGR,IEL,IALIEL,ILLIEL
      INTEGER JNBNO,JDIME,IRET,NCPMAX
      CHARACTER*1 KBID
      CHARACTER*8 MA, FAPG(NBFAMX),NOMGD,FAMIL,ELREFE,PARAM
      CHARACTER*16 PHENO,OPTION,NOMTE,NOFPG
      CHARACTER*19 MNOGA,LIGREL,CELMOD
      CHARACTER*24 OBNBPG ,OBNBNO, OBDIME
      REAL*8 XNO(3*NBNOMX),XPG(3*NBPGMX),VOL,FF(NBNOMX)
      REAL*8 POIPG(NBNOMX)
      NUMAIL(IGR,IEL) = ZI(IALIEL-1+ZI(ILLIEL+IGR-1)+IEL-1)
C     ------------------------------------------------------------------
      CALL JEMARQ()
      MNOGA=MNOGAZ
      LIGREL=LIGREZ

      OBNBPG = '&&MANOPG.NBPG'
      OBNBNO = '&&MANOPG.NBNO'
      OBDIME = '&&MANOPG.DIME'

      CALL DISMOI('F','NOM_MAILLA',LIGREL,'LIGREL',IBID,MA,IBID)
      CALL DISMOI('F','NB_MA_MAILLA',MA,'MAILLAGE',NBMA,KBID,IBID)
      CALL DISMOI('F','PHENOMENE',LIGREL,'LIGREL',IBID,PHENO,IBID)
      CALL JEVEUO(JEXATR(MA//'.CONNEX','LONCUM'),'L',ILCNX1)


C     1. ON RECUPERE LES NOMBRES DE POINTS DE GAUSS ET DE NOEUDS :
C     ------------------------------------------------------------
      IF (PHENO.EQ.'MECANIQUE') THEN
        OPTION='RAPH_MECA'
        PARAM='PCONTMR'
      ELSE IF (PHENO.EQ.'THERMIQUE') THEN
        OPTION='FLUX_ELGA_TEMP'
        PARAM='PFLUX_R'
      ELSE
        CALL UTMESS('F','MANOPG','A FAIRE  ...')
      END IF
      CALL NBPTCA(LIGREL,OPTION,PARAM,OBNBPG,OBNBNO)
      CALL JEVEUO(OBNBPG,'L',JNBPG)
      CALL JEVEUO(OBNBNO,'L',JNBNO)


C     2. ALLOCATION DU CHAM_ELEM_S MNOGA :
C     ---------------------------------------------------------------
      CALL WKVECT(OBDIME,'V V I',NBMA,JDIME)
      NCPMAX=0
      DO 77, IMA=1,NBMA
        ZI(JDIME-1+IMA)= ZI(JNBPG-1+IMA)*ZI(JNBNO-1+IMA) +2
        NCPMAX=MAX(NCPMAX,ZI(JDIME-1+IMA))
77    CONTINUE
      CALL CESCRE('V',MNOGA,'ELEM',MA,'VARI_R',-NCPMAX,' ',
     &             -1,-1,ZI(JDIME))


C     3. ALLOCATION D'UN CHAMP MODELE POUR DETERMINER LES FAMILLES
C        DE POINTS DE GAUSS UTILISEES.
C     ---------------------------------------------------------------
      CELMOD='&&MANOPG.CELMOD'
      CALL ALCHML(LIGREL,OPTION,PARAM,'V',CELMOD,IRET,' ')
      CALL JEVEUO(CELMOD//'.CELD','L',JCELD)


C     4. REMPLISSAGE DE MNOGA :
C     ---------------------------------------------------------------
      CALL JEVEUO(MNOGA//'.CESD','L',JCESD)
      CALL JEVEUO(MNOGA//'.CESL','E',JCESL)
      CALL JEVEUO(MNOGA//'.CESV','E',JCESV)

      CALL JELIRA(LIGREL//'.LIEL','NMAXOC',NBGREL,KBID)
      CALL JEVEUO(LIGREL//'.LIEL','L',IALIEL)
      CALL JEVEUO(JEXATR(LIGREL//'.LIEL','LONCUM'),'L',ILLIEL)

      DO 1, IGR=1,NBGREL
        NEL = NBELEM(LIGREL,IGR)
        NUTE = TYPELE(LIGREL,IGR)
        CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',NUTE),NOMTE)
        IMOLO = ZI(JCELD-1+ZI(JCELD-1+4+IGR)+2)
        IF (IMOLO.EQ.0) GO TO 1


C       4.1 DETERMINATION DE LA FAMILLE DE POINTS DE GAUSS :
C       -----------------------------------------------------------
        CALL JEVEUO(JEXNUM('&CATA.TE.MODELOC',IMOLO),'L',JMOLO)
        CALL DISMOI('F','NOM_GD',CELMOD,'CHAMP',IBID,NOMGD,IERD)
        CALL DISMOI('F','NB_EC',NOMGD,'GRANDEUR',NEC,KBID,IERD)
        KFPG = ZI(JMOLO-1+4+NEC+1)
        CALL JENUNO(JEXNUM('&CATA.TM.NOFPG',KFPG),NOFPG)
        ELREFE=NOFPG(1:8)
        FAMIL=NOFPG(9:16)

C       4.2 APPEL AUX ROUTINES ELRACA ET ELRFVF :
C       ------------------------------------------
        CALL ELRACA(ELREFE,NDIM,NNO,NNOS,NBFPG,FAPG,NBPGF,XNO,VOL)
        CALL ASSERT(NBFPG.LE.10)
        CALL ASSERT(NNO.LE.27)
        IFAM = INDIK8(FAPG,FAMIL,1,NBFPG)
        CALL ASSERT(IFAM.GT.0)
        CALL ELRAGA(ELREFE,FAMIL,NDIM,NPG,XPG,POIPG)
        CALL ASSERT(NPG.LE.27)

C       4.3 ECRITURE DANS MANOPG :
C       ------------------------------------------
        DO 2,IEL = 1,NEL
          IMA=NUMAIL(IGR,IEL)

          NCMP= (NNO*NPG)+2
          CALL CESEXI('C',JCESD,JCESL,IMA,1,1,NCMP,IAD)
          CALL ASSERT(IAD.LT.0)
          CALL CESEXI('C',JCESD,JCESL,IMA,1,1,1,IAD)
          CALL ASSERT(IAD.LT.0)
          IAD=-IAD
          DO 17,K=1,(NNO*NPG)+2
            ZL(JCESL-1+IAD-1+K) = .TRUE.
17        CONTINUE

          JMANO=JCESV-1+IAD

          ZR(JMANO-1+1) = NNO
          ZR(JMANO-1+2) = NPG

          DO 20 KP = 1,NPG
            CALL ELRFVF(ELREFE,XPG(NDIM*(KP-1)+1),NBNOMX,FF,NNO)
            DO 10 INO = 1,NNO
              ZR(JMANO-1+2+NNO*(KP-1)+INO) = FF(INO)
   10       CONTINUE
   20     CONTINUE

   2    CONTINUE
   1  CONTINUE


      CALL DETRSD('CHAMP',CELMOD)
      CALL JEDETR(OBNBPG)
      CALL JEDETR(OBNBNO)
      CALL JEDETR(OBDIME)

      CALL JEDEMA()
      END
