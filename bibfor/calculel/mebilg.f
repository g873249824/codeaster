      SUBROUTINE MEBILG(OPTIOZ,RESULT,MODELE,DEPLA1,DEPLA2,THETA,MATE,
     &                  NCHAR,LCHAR,SYMECH,EXTIM,TIME,INDI,INDJ,NBPRUP,
     &                  NOPRUP)
      IMPLICIT NONE

      CHARACTER*8 MODELE,LCHAR(*),RESULT,SYMECH
      CHARACTER*16 OPTIOZ,NOPRUP(*)
      CHARACTER*24 DEPLA1,DEPLA2,MATE,THETA
      REAL*8 TIME
      INTEGER INDI,INDJ,NCHAR,NBPRUP
      LOGICAL EXTIM
C ......................................................................
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 16/06/2004   AUTEUR DURAND C.DURAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C     - FONCTION REALISEE:   CALCUL DU G BILINEAIRE EN 3D

C IN   OPTION  --> CALC_G_BILI
C IN   RESULT  --> NOM UTILISATEUR DU RESULTAT ET TABLE
C IN   MODELE  --> NOM DU MODELE
C IN   DEPLA1  --> CHAMP DE DEPLACEMENT U
C IN   THETA   --> CHAMP THETA
C IN   MATE    --> CHAMP DE MATERIAUX
C IN   SYMECH  --> SYMETRIE DU CHARGEMENT
C IN   EXTIM   --> VRAI SI L'INSTANT EST DONNE
C IN   TIME    --> INSTANT DE CALCUL
C IN   INDI    --> INDICE I DU DEPLACEMENT U DANS LA SD
C IN   INDJ    --> INDICE J DU DEPLACEMENT U DANS LA SD
C ......................................................................

      INTEGER I,IRET,IBID,IERD,INIT,NIV,IFM
      INTEGER IADRMA
      INTEGER JTEMP,NCHIN,IVAL(2)

      REAL*8 G,RVAL(1)

      COMPLEX*16 CBID

      LOGICAL EXIGEO,EXITHE,EXITRF

      CHARACTER*8 NOMA,K8B
      CHARACTER*8 LPAIN(6),LPAOUT(1),REPK
      CHARACTER*16 OPTION
      CHARACTER*24 CHGEOM,CHTEMP,CHTREF
      CHARACTER*24 LCHIN(6),LCHOUT(1),LIGRMO
      CHARACTER*24 OBJ1,TEMPE,OBVALE

C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------

      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      CALL JEMARQ()
      OPTION = OPTIOZ
      CALL INFNIV(IFM,NIV)

C- RECUPERATION DU CHAMP GEOMETRIQUE

      CALL MEGEOM(MODELE,' ',EXIGEO,CHGEOM)
      NOMA = CHGEOM

C- RECUPERATION DE L'ETAT INITIAL

      CALL GETFAC('ETAT_INIT',INIT)
      IF (INIT.NE.0) THEN
        CALL UTMESS('F','MEBILG','CALC_G_BILI : CHAMP INITIAL'//
     &              ' IMPOSSIBLE')
      END IF

C- RECUPERATION (S'ILS EXISTENT) DES CHAMP DE TEMPERATURES (T,TREF)

      TEMPE = ' '
      CHTEMP = '&&MEBILG.CH_TEMP_R'
      DO 10 I = 1,NCHAR
        CALL JEEXIN(LCHAR(I)//'.CHME.TEMPE.TEMP',IRET)
        IF (IRET.NE.0) THEN
          CALL JEVEUO(LCHAR(I)//'.CHME.TEMPE.TEMP','L',JTEMP)
          TEMPE = ZK8(JTEMP)
        END IF
   10 CONTINUE
      CALL METREF(MATE,NOMA,EXITRF,CHTREF)
      CALL METEMP(NOMA,TEMPE,EXTIM,TIME,CHTREF,EXITHE,CHTEMP(1:19))
      CALL DISMOI('F','ELAS_F_TEMP',MATE,'CHAM_MATER',IBID,REPK,IERD)
      IF (REPK.EQ.'OUI') THEN
        IF (.NOT.EXITHE) THEN
          CALL UTMESS('F','MEBILG',
     &                'LE MATERIAU DEPEND DE LA TEMPERATURE'//
     &                '! IL N''Y A PAS DE CHAMP DE TEMPERATURE '//
     &                '! LE CALCUL EST IMPOSSIBLE ')
        END IF
        IF (.NOT.EXITRF) THEN
          CALL UTMESS('A',' MEBILG',
     &                'LE MATERIAU DEPEND DE LA TEMPERATURE'//
     &                ' IL N''Y A PAS DE TEMPERATURE DE REFERENCE'//
     &                ' ON PRENDRA DONC LA VALEUR 0')
        END IF
      END IF

C OBJET DECRIVANT LE MAILLAGE

      OBJ1 = MODELE//'.MODELE    .NOMA'
      CALL JEVEUO(OBJ1,'L',IADRMA)
      NOMA = ZK8(IADRMA)

      LPAOUT(1) = 'PGTHETA'
      LCHOUT(1) = '&&FICGELE'

      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM
      LPAIN(2) = 'PDEPLAU'
      LCHIN(2) = DEPLA1
      LPAIN(3) = 'PTHETAR'
      LCHIN(3) = THETA
      LPAIN(4) = 'PMATERC'
      LCHIN(4) = MATE
      LPAIN(5) = 'PTEMPER'
      LCHIN(5) = CHTEMP
      LPAIN(6) = 'PDEPLAV'
      LCHIN(6) = DEPLA2

      LIGRMO = MODELE//'.MODELE'
      NCHIN = 6

      CALL CALCUL('S',OPTION,LIGRMO,NCHIN,LCHIN,LPAIN,1,LCHOUT,LPAOUT,
     &            'V')

C  SOMMATION DES FIC ET G ELEMENTAIRES

      CALL MESOMM(LCHOUT(1),1,IBID,G,CBID,0,IBID)

      IF (SYMECH.NE.'SANS') G = 2.D0*G

C IMPRESSION DE G ET ECRITURE DANS LA TABLE RESU

      IF (NIV.GE.2) THEN
        OBVALE = LCHOUT(1) (1:19)//'.VALE'
        CALL JEIMPO(IFM,OBVALE,' ',
     &              'OBJET CONTENANT LA VALEUR DE G SUR CHAQUE ELEMENT')
      END IF

      RVAL(1) = G
      IVAL(1) = INDI
      IVAL(2) = INDJ
      CALL TBAJLI(RESULT,NBPRUP,NOPRUP,IVAL,RVAL,CBID,K8B,0)

      CALL DETRSD('CHAMP_GD',CHTEMP)
      CALL JEDETR('&&MEBILG.VALG')

      CALL JEDEMA()
      END
