      SUBROUTINE  SRLIMA(MO,MAIL2D,MAIL3D,MAILTO,NBMA2D)
      IMPLICIT NONE
      INTEGER NBMA2D
      CHARACTER*8 MO
      CHARACTER*24 MAIL2D,MAIL3D,MAILTO
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 05/02/2013   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C     BUT: CONSTRUIRE 3 LISTES DE MAILLES A PARTIR DES DONNEES
C          UTILISATEUR :
C          LISTE DES MAILLES 2D
C          LISTE DES MAILLES 3D SOUS-JACENTES
C          LISTE DE L'ENSEMBLE DES MAILLES 2D + MAILLES 3D SOUS-JACENTES
C
C     IN  MO     : NOM DU MODELE
C     IN  MAIL2D : NOM OBJET JEVEUX CONTENANT LA LISTE DES MAILLES 2D
C     IN  MAIL3D : NOM OBJET JEVEUX CONTENANT LA LISTE DES MAILLES 2D
C     IN  MAILTO : NOM OBJET JEVEUX CONTENANT LA LISTE DES MAILLES 2D+3D
C     OUT NBMA2D : NOMBRE DE MAILLES 2D TROUVEES == NB MAILLES 3D
C
C ----------------------------------------------------------------------
C
      INCLUDE 'jeveux.h'

      INTEGER      JTYMA,JMA2D,NUMA,JCOOR,JMA3D
      INTEGER      IMA,IBID,IRET
      INTEGER      NBMA,NBMAMO,JLIMA,NBMAT,JMATO

      CHARACTER*8  MA,TYPMA,NOMA,LIMOCL(3),TYMOCL(3),K8B,VALK(2)
      CHARACTER*24 MESMAI,LIMAMO
      CHARACTER*32 JEXNUM

      DATA LIMOCL/'TOUT','MAILLE','GROUP_MA'/
      DATA TYMOCL/'TOUT','MAILLE','GROUP_MA'/
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()

C --- ON RECUPERE LES MAILLES DE PEAU
      CALL DISMOI('F','NOM_MAILLA',MO,'MODELE',IBID,MA,IRET)
      CALL DISMOI('F','NB_MA_MAILLA',MA,'MAILLAGE',NBMAT,K8B,IRET)
      MESMAI = '&&SRLIMA.MAILLU'
      CALL RELIEM(MO,MA,'NU_MAILLE',' ',0,3,LIMOCL,TYMOCL,MESMAI,NBMA)

C --- ON NE GARDE QUE LES MAILLES SURFACIQUES
      CALL UTFLMD(MA,MESMAI,2,' ',NBMA2D,MAIL2D)
      IF (NBMA2D.GT.0) THEN
        CALL JEVEUO(MAIL2D,'L',JMA2D)
      ELSE
        CALL U2MESS('F','CALCULEL5_54')
      ENDIF

C --- ON RECHERCHE LA MAILLES 3D SUPPORT DE CHAQUE MAILLE 2D FOURNIE
C --- IL FAUT SE PROTEGER DES MAILLES QUI NE FONT PAS PARTIE DU MODELE :
      LIMAMO = '&&SRLIMA.LIMAIL'
      CALL UTMAMO (MO,NBMAMO,LIMAMO)
      CALL JEVEUO(LIMAMO,'L',JLIMA)
C
      CALL JEVEUO (MA//'.COORDO    .VALE', 'L', JCOOR)
      CALL UTMASU(MA,'3D',NBMA2D,ZI(JMA2D),MAIL3D,ZR(JCOOR),
     &            NBMAMO,ZI(JLIMA),.TRUE.)
      CALL JEVEUO(MAIL3D,'L',JMA3D)
C
      CALL WKVECT(MAILTO,'V V I',NBMA2D*2,JMATO)
C
      DO 20 IMA=1,NBMA2D
        ZI(JMATO-1+IMA)        = ZI(JMA2D-1+IMA)
        ZI(JMATO-1+NBMA2D+IMA) = ZI(JMA3D-1+IMA)
 20   CONTINUE
C
      CALL JEDETR(MESMAI)
      CALL JEDETR(LIMAMO)
C
      CALL JEDEMA()
C
      END
