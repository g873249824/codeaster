      SUBROUTINE UTERSA(NDIM,IFLUP,IFLUM,INO,MNO,JNO,IVOIS,MA,IEL,
     &                  NBNV,NBSV,IAVALP,IAVALM,NSOMM,JAC,LTHETA,VALTHE,
     &                  VALUNT,NIV,IFM,ITYP,XN,YN,ZN,TERM22,AUX,
     &                  JAD,JADV,NOE)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE  BOITEAU O.BOITEAU
C TOLE CRP_21
C-----------------------------------------------------------------------
C    - FONCTION REALISEE: UTILITAIRE DE CALCUL DE L'ERREUR DUE AU TERME
C                         DE SAUT. POUR AERER TE0003.
C
C IN NDIM : DIMENSION DU CALCUL
C IN IFLUP/M : ADRESSE JEVEUX DU FLUX +/-
C IN INO/JNO/MNO : NUMERO DES NOEUDS DE L'ARETE (INO NUMERO FACE EN 3D)
C IN MA : MAILLAGE
C IN IVOIS : ADRESSE JEVEUX DE VOISIN
C IN IEL : INDICE DE LA MAILLE VOISINE DANS LE IGREL.
C IN NBNV : NOMBRE DE NOEUDS DE LA MAILLE VOISINE.
C IN NBSV : CODE DE LA MAILLE VOISINE (2D POUR MAILLE SYMETRIQUE).
C IN IAVALP/M : ADRESSE JEVEUX DES FLUX + ET - DU VOISIN
C IN NSOMM  : NBRE DE SOMMETS DE L'ARETE OU DE LA FACE.
C IN JAC  : JACOBIEN
C IN LTHETA/VALTHE/UNT : PARAMETRES THETA METHODE
C IN NIV/IFM : PARAMETRES D'IMPRESSION
C IN ITYP : TYPE DE FACE (EN 3D)
C IN XN/YN/ZN : COMPOSANTES DE LA NORMALE AUX POINTS D'INTEGRATION
C IN NOE : TABLEAU NUMEROS DE NOEUDS PAR FACE ET PAR TYPE D'ELEMENT 3D
C OUT TERM22 : CONTRIBUTION DE L'ERREUR
C OUT AUX : TERME DE NORMALISATION
C OUT JAD/JADV : ADRESSE JEVEUX DE LA MAILLE ET DE SA VOISINE
C   -------------------------------------------------------------------
C     SUBROUTINES APPELLEES:
C       JEVEUX: JEVEUO,JEXNUM.
C       TRI: INDIIS.
C     FONCTIONS INTRINSEQUES:
C       AUCUNE.
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C       24/09/01 (OB): CREATION POUR SIMPLIFIER TE0003.F.
C       11/09/01 (OB): MODIF. REPARTITION ERREUR/NORMALISATION
C----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE

C DECLARATION PARAMETRES D'APPELS
      INCLUDE 'jeveux.h'
      INTEGER IFLUP,IFLUM,NDIM,INO,MNO,JNO,IVOIS,NSOMM,ITYP,IEL,
     &        NBNV,IAVALP,IAVALM,NBSV,JAD,JADV,NOE(9,6,3),NIV,IFM
      REAL*8 JAC(9),TERM22,AUX,VALTHE,VALUNT,XN(9),YN(9),ZN(9)
      CHARACTER*8 MA
      LOGICAL LTHETA
      

C DECLARATION VARIABLES LOCALES
      INTEGER IJ,IN,IINO,I1,INOV0,JNOV0,MNOV,NCHER,INOV,
     &        JNOV,INDIIS,IINOV
      REAL*8 AUX1,AUX2,AUX3,AUX4,TERM23
      
C INIT.
      TERM22 = 0.D0
      AUX = 0.D0
      CALL JEVEUO(JEXNUM(MA//'.CONNEX',ZI(IVOIS)),'L',JAD)
      CALL JEVEUO(JEXNUM(MA//'.CONNEX',ZI(IVOIS+INO)),'L',JADV)
                  
      IF (NDIM.EQ.2) THEN
C CAS 2D

C NUMERO LOCALE (INOV) DANS LA MAILLE VOISINE DU PREMIER NOEUD (DE
C NUMERO GLOBALE NCHER) DE NUM LOCAL INO ET COMMUN AUX DEUX EFS.
        NCHER = ZI(JAD-1+INO)
        INOV = INDIIS(ZI(JADV),NCHER,1,NBNV)
        IF (NIV.EQ.2) WRITE(IFM,*)'INOV LOCAL/GLOBAL',INOV,NCHER
C NUMERO LOCALE (JNOV)  DU NOEUD VOISIN DE JNO
        NCHER = ZI(JAD-1+JNO)
        JNOV = INDIIS(ZI(JADV),NCHER,1,NBNV)
        IF (NIV.EQ.2) WRITE(IFM,*)'JNOV LOCAL/GLOBAL',JNOV,NCHER
        
C POINT DE DEPART: INO
        IJ = IFLUP+(INO-1)*NDIM
        I1 = IAVALP+NDIM*((IEL-1)*NBNV+INOV-1)
        AUX2 = -VALTHE*(ZR(IJ)*XN(1)+ZR(IJ+1)*YN(1))
        AUX1 = VALTHE*(ZR(I1)*XN(1)+ZR(I1+1)*YN(1))
        TERM23 = AUX1 + AUX2         
        IF (NIV.EQ.2) WRITE(IFM,*)' ZR INO P',AUX1,AUX2
        IF (LTHETA) THEN
          IJ = IJ+IFLUM-IFLUP
          I1 = I1+IAVALM-IAVALP
          AUX4 = -VALUNT*(ZR(IJ)*XN(1)+ZR(IJ+1)*YN(1))
          AUX3 = VALUNT*(ZR(I1)*XN(1)+ZR(I1+1)*YN(1))
          AUX1 = AUX1 + AUX3
          AUX2 = AUX2 + AUX4
          TERM23 = TERM23 + AUX3 + AUX4                
          IF (NIV.EQ.2) WRITE(IFM,*)' ZR INO M',AUX3,AUX4
        ENDIF
        TERM22 = JAC(1)*TERM23*TERM23
        AUX1 = (AUX1-AUX2)*0.5D0
        AUX = JAC(1)*AUX1*AUX1
        
C POINT EXTREME: JNO
        IJ = IFLUP+(JNO-1)*NDIM
        I1 = IAVALP+NDIM*((IEL-1)*NBNV+JNOV-1)
        AUX2 = -VALTHE*(ZR(IJ)*XN(2)+ZR(IJ+1)*YN(2))
        AUX1 = VALTHE*(ZR(I1)*XN(2)+ZR(I1+1)*YN(2))
        TERM23 = AUX1 + AUX2                
        IF (NIV.EQ.2) WRITE(IFM,*)' ZR JNO P',AUX1,AUX2
        IF (LTHETA) THEN
          IJ = IJ+IFLUM-IFLUP
          I1 = I1+IAVALM-IAVALP
          AUX4 = -VALUNT*(ZR(IJ)*XN(2)+ZR(IJ+1)*YN(2))
          AUX3 = VALUNT*(ZR(I1)*XN(2)+ZR(I1+1)*YN(2))
          AUX1 = AUX1 + AUX3
          AUX2 = AUX2 + AUX4
          TERM23 = TERM23 + AUX3 + AUX4                
          IF (NIV.EQ.2) WRITE(IFM,*)' ZR JNO M',AUX3,AUX4
        ENDIF
        TERM22 = TERM22 + JAC(2)*TERM23*TERM23
        AUX1 = (AUX1-AUX2)*0.5D0
        AUX = AUX + JAC(2)*AUX1*AUX1
              
C POINT MILIEU SI NECESSAIRE: MNO
        IF (NSOMM.EQ.3) THEN
C TESTS POUR CALCULER MNOV AFIN DE TRAITER LES MAILLES VOISINES
C COMPORTANT DES ORIENTATIONS LOCALES IDENTIQUES (MAILLAGE SYMETRISE)
          IF ((INOV.EQ.NBSV).AND.(JNOV.EQ.1)) THEN
            INOV0 = 0
          ELSE
            INOV0 = INOV
          ENDIF
          IF ((JNOV.EQ.NBSV).AND.(INOV.EQ.1)) THEN
            JNOV0 = 0
          ELSE
            JNOV0 = JNOV
          ENDIF
          IF (INOV0.LT.JNOV0) THEN
            MNOV = INOV + NBSV
          ELSE
            MNOV = JNOV + NBSV
          ENDIF
          IJ = IFLUP+(MNO-1)*NDIM
          I1 = IAVALP+NDIM*((IEL-1)*NBNV+MNOV-1)
          AUX2 = -VALTHE*(ZR(IJ)*XN(3)+ZR(IJ+1)*YN(3))
          AUX1 = VALTHE*(ZR(I1)*XN(3)+ZR(I1+1)*YN(3))
          TERM23 = AUX1 + AUX2                
          IF (NIV.EQ.2) THEN
            WRITE(IFM,*)' INOV0/JN0V0/MNOV ',INOV0,JNOV0,MNOV
            WRITE(IFM,*)' ZR MNO P',AUX1,AUX2
          ENDIF
          IF (LTHETA) THEN
            IJ = IJ+IFLUM-IFLUP
            I1 = I1+IAVALM-IAVALP
            AUX4 = -VALUNT*(ZR(IJ)*XN(3)+ZR(IJ+1)*YN(3))
            AUX3 = VALUNT*(ZR(I1)*XN(3)+ZR(I1+1)*YN(3))
            AUX1 = AUX1 + AUX3
            AUX2 = AUX2 + AUX4
            TERM23 = TERM23 + AUX3 + AUX4                
            IF (NIV.EQ.2) WRITE(IFM,*)' ZR MNO M',AUX3,AUX4
          ENDIF
          TERM22 = TERM22 + JAC(3)*TERM23*TERM23
          AUX1 = (AUX1-AUX2)*0.5D0
          AUX = AUX + JAC(3)*AUX1*AUX1        
        ENDIF
      ELSE
      
C CAS 3D
        DO 100 IN=1,NSOMM

C NOEUD COURANT
          IINO = NOE(IN,INO,ITYP)
          NCHER = ZI(JAD-1+IINO)
          IF (NIV.EQ.2) WRITE(IFM,*)'NOEUD COURANT LOCAL/GLOBAL',
     &      IINO,NCHER           
C NOEUD VOISIN CORRESPONDANT
          IINOV = INDIIS(ZI(JADV),NCHER,1,NBNV)
          IF (NIV.EQ.2) WRITE(IFM,*)'SON VOISIN LOCAL',IINOV           

          IJ = IFLUP+(IINO-1)*NDIM
          I1 = IAVALP+NDIM*((IEL-1)*NBNV+IINOV-1)
          AUX2 = -VALTHE*(ZR(IJ)*XN(IN)+ZR(IJ+1)*YN(IN)+ZR(IJ+2)*
     &           ZN(IN))
          AUX1 = VALTHE*(ZR(I1)*XN(IN)+ZR(I1+1)*YN(IN)+ZR(I1+2)*
     &           ZN(IN))
          TERM23 = AUX1 + AUX2         
          IF (NIV.EQ.2) WRITE(IFM,*)' ZR IINO P/IN ',AUX1,AUX2,IN
          IF (LTHETA) THEN
            IJ = IJ+IFLUM-IFLUP
            I1 = I1+IAVALM-IAVALP
            AUX4 = -VALUNT*(ZR(IJ)*XN(IN)+ZR(IJ+1)*YN(IN)+ZR(IJ+2)*
     &           ZN(IN))
            AUX3 = VALUNT*(ZR(I1)*XN(IN)+ZR(I1+1)*YN(IN)+ZR(I1+2)*
     &           ZN(IN))
            AUX1 = AUX1 + AUX3
            AUX2 = AUX2 + AUX4
            TERM23 = TERM23 + AUX3 + AUX4                
            IF (NIV.EQ.2) WRITE(IFM,*)' ZR IINO M    ',AUX3,AUX4
          ENDIF
          TERM22 = TERM22 + TERM23*TERM23*JAC(IN)
          AUX1 = (AUX1-AUX2)*0.5D0
          AUX = AUX + AUX1*AUX1*JAC(IN)
  100   CONTINUE
  
      ENDIF
      
      END
