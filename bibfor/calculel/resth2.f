      SUBROUTINE RESTH2(MODELE,LIGREL,LCHAR,NCHAR,MA,
     &  CARTEF,NOMGDF,CARTEH,NOMGDH,CARTET,NOMGDT,CARTES,NOMGDS,
     &  CHGEOM,CHSOUR,PSOURC)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 12/02/2013   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE BOITEAU O.BOITEAU
C-----------------------------------------------------------------------
C    - FONCTION REALISEE:  PREPARATION DU CALCUL DE L'ESTIMATEUR
C                          D'ERREUR EN RESIDU SUR LE PROBLEME THERMIQUE.
C
C IN MODELE  : NOM DU MODELE
C IN LIGREL  : NOM DU LIGREL
C IN LCHAR   : LISTE DES CHARGES
C IN NCHAR   : NOMBRE DE CHARGES
C OUT MA     : NOM DU MAILLAGE
C OUT CARTEF/NOMGDF: INFO SUR LE FLUX RETENU
C OUT CARTEH/NOMGDH: INFO SUR L'ECHANGE RETENU
C OUT CARTET/NOMGDT: INFO SUR LA TEMP_EXT RETENUE
C OUT CARTES/NOMGDS: INFO SUR LA SOURCE RETENUE
C OUT CHGEOM : CHAMP GEOMETRIE
C OUT CHSOUR : CHAMP SOURCE
C OUT PSOURC : NOM DU PARAMETRE ASSOCIE A SOURCE
C
C   -------------------------------------------------------------------
C     SUBROUTINES APPELLEES:
C       MESSAGE:U2MESS,U2MESG.
C       JEVEUX:JEMARQ,JEDEMA,JEDETR,MEGEOM,DISMOI,EXISD,ETENCA.
C       ELEMENTS FINIS:CALCUL,RESVOI.
C
C     FONCTIONS INTRINSEQUES:
C       AUCUNE.
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C       22/08/02 (OB): CREATION DU A LA SEPARATION DE RESTHE, EN UNE
C          PARTIE PRELIMINAIRE (RESTH2) HORS DE LA BOUCLE EN TEMPS ET
C          UNE PARTIE (RESTHE) CALCUL DEPENDANT DU TEMPS.
C----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE

C DECLARATION PARAMETRES D'APPELS
      INTEGER NCHAR
      CHARACTER*8 MODELE,LCHAR(1),MA,PSOURC
      CHARACTER*19 CARTEF,CARTEH,CARTET,CARTES,NOMGDF,NOMGDH,NOMGDT,
     &             NOMGDS
      CHARACTER*24 LIGREL,CHGEOM,CHSOUR

C DECLARATION VARIABLES LOCALES
      INTEGER I,IBID,IER,IRETF,IRETH,IRETT,IRETS,NBIN,NBOUT,IRETEP
      CHARACTER*1 BASE
      CHARACTER*8 LPAIN(1),LPAOUT(1)
      CHARACTER*16 OPT
      CHARACTER*19 CARTF,CARTH,CARTT,CARTS,CARTEP
      CHARACTER*24 LCHIN(1),LCHOUT(1)

C DEBUT DE LA SUBROUTINE
      CALL JEMARQ()

      CALL ASSERT(LIGREL(1:8).EQ.MODELE)

C RECHERCHE DU NOM DU CHAMP GEOMETRIE DANS LA SD MODELE OU CHARGE -----
C SURCOUCHE DE LA REQUETE D'EXISTENCE JEEXIN/JEVEUO DU DESCRIPTEUR DE
C MODELE//'MODELE.NOMA.COORDO': RESULTAT DANS CHGEOM. ON S'ASSURE DE
C LA COHERENCE AVEC CHARGE//'CHTH.MODEL.NOMO'.
      BASE = 'V'
      CALL MEGEOM(MODELE,CHGEOM)

C RECHERCHE DES ELTS FINIS CONTIGUS ET REMPLISSAGE DU CHAMELEM DE TYPE
C VOISIN VIA L'OPTION DE CALCUL 'INIT_MAIL_VOIS'.
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) =  CHGEOM
      LPAOUT(1) = 'PVOISIN'
      LCHOUT(1) = '&&RESTHER.VOISIN'
      OPT = 'INIT_MAIL_VOIS'
      NBIN = 1
      NBOUT = 1
      CALL CALCUL('S',OPT,LIGREL,NBIN,LCHIN,LPAIN,NBOUT,
     &     LCHOUT,LPAOUT,BASE,'OUI')

C SURCOUCHE DE DISMMO RENVOYANT LE NOM DU MAILLAGE (MA) VIA UN JEVEUO
C SUR MODELE//'.MODELE.NOMA'
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE',IBID,MA,IER)
C REMPLISSAGE DU CHAM_ELEM '&&RESTHER.VOISIN' PAR LES NUMEROS ET LES
C TYPES DE MAILLES VOISINES.
      CALL RESVOI(MODELE,MA,'&&RESTHER.VOISIN')

C BOUCLE SUR LES CHARGEMENTS -----------------------------------------
C
C ATTENTION: POUR UN MEME TYPE DE CHARGEMENT, SEULE LA DERNIERE
C OCCURENCE EST CONSERVEE.

      NOMGDF = ' '
      NOMGDH = ' '
      NOMGDT = ' '
      NOMGDS = ' '
      CARTEF = ' '
      CARTEH = ' '
      CARTET = ' '
      CARTES = ' '
      CHSOUR = ' '
      IRETF = 0
      IRETH = 0
      IRETT = 0
      IRETS = 0
      IRETEP = 0
C OPTION DE CALCUL PAR DEFAUT
      PSOURC='PSOURCR'

C BOUCLE SUR LES AFFE_CHAR_THER
      DO 10 I = 1,NCHAR
C INIT.
        CARTF = LCHAR(I)//'.CHTH.FLURE'
        CARTH = LCHAR(I)//'.CHTH.COEFH'
        CARTT = LCHAR(I)//'.CHTH.T_EXT'
        CARTS = LCHAR(I)//'.CHTH.SOURE'
        CARTEP = LCHAR(I)//'.CHTH.HECHP'
C DETERMINE L'EXISTENCE DES SD DE TYPE CHAMP_GD ET DE NOM CARTE.
        CALL EXISD('CHAMP_GD',CARTF,IRETF)
        CALL EXISD('CHAMP_GD',CARTH,IRETH)
        CALL EXISD('CHAMP_GD',CARTT,IRETT)
        CALL EXISD('CHAMP_GD',CARTS,IRETS)
        CALL EXISD('CHAMP_GD',CARTEP,IRETEP)
        IF (IRETEP.NE.0) THEN
          CALL U2MESS('A', 'CALCULEL6_42')
        ENDIF
        IF (((IRETH.EQ.0).AND.(IRETT.NE.0)).OR.
     &    ((IRETT.EQ.0).AND.(IRETH.NE.0)))
     &    CALL ASSERT(.FALSE.)

C TRAITEMENT DES CHARGEMENTS DE TYPE FLUX_REP/FLUN
        IF (IRETF.NE.0 ) THEN
C SURCOUCHE DE DISMCA RENVOYANT LE NOM DE LA SD (NOMGD...) VIA UN
C JEVEUO/JENUNO SUR CARTF//'.DESC'.
          CALL DISMOI('F','NOM_GD',CARTF,'CARTE',IBID,NOMGDF,IER)

C EXTENSION DE LA CARTE CARTEF VIA CARTEF//'.PTMA' ET '.PTMS' SUR 'V'
          CALL ETENCA(CARTF,LIGREL,IER)
          CALL ASSERT(IER.EQ.0)

C SEULE CARTE FLUN CONSERVEE (REGLE SURCHARGE USUELLE DE LA DERNIERE)
          IF (CARTEF.NE.' ') THEN
            CALL U2MESK('I', 'CALCULEL6_43',1,'FLUX LINEAIRE')
            CALL JEDETR(CARTEF//'.PTMA')
            CALL JEDETR(CARTEF//'.PTMS')
          ENDIF
          CARTEF = CARTF
        ENDIF

C TRAITEMENT DES CHARGEMENTS DE TYPE ECHANGE/COEF_H
        IF (IRETH.NE.0 ) THEN
          CALL DISMOI('F','NOM_GD',CARTH,'CARTE',IBID,NOMGDH,IER)
          CALL ETENCA(CARTH,LIGREL,IER)
          CALL ASSERT(IER.EQ.0)

C TRAITEMENT DES CHARGEMENTS DE TYPE ECHANGE/TEMP_EXT
          CALL DISMOI('F','NOM_GD',CARTT,'CARTE',IBID,NOMGDT,IER)
          CALL ETENCA(CARTT,LIGREL,IER)
          CALL ASSERT(IER.EQ.0)

C SEULE CARTE FLUN CONSERVEE (REGLE SURCHARGE USUELLE DE LA DERNIERE)
          IF (CARTEH.NE.' ') THEN
            CALL U2MESK('I', 'CALCULEL6_43',1,'ECHANGE')
            CALL JEDETR(CARTEH//'.PTMA')
            CALL JEDETR(CARTEH//'.PTMS')
            CALL JEDETR(CARTET//'.PTMA')
            CALL JEDETR(CARTET//'.PTMS')
          ENDIF
          CARTEH = CARTH
          CARTET = CARTT
        ENDIF

C TRAITEMENT DES SOURCES VOLUMIQUES
        IF (IRETS.NE.0) THEN
          CHSOUR = CARTS//'.DESC'
          CALL DISMOI('F','NOM_GD',CARTS,'CARTE',IBID,NOMGDS,IER)
C SEULE CARTE FLUN CONSERVEE (REGLE SURCHARGE USUELLE DE LA DERNIERE)
          IF (CARTES.NE.' ') THEN
            CALL U2MESK('A', 'CALCULEL6_43',1,'SOURCE')
          ENDIF
C OPTION DE CALCUL POUR SOURCE VARIABLE
          IF (NOMGDS(1:6).EQ.'SOUR_F') PSOURC='PSOURCF'
          CARTES = CARTS
        ENDIF

C FIN BOUCLE AFFE_CHAR_THER
   10 CONTINUE

      CALL JEDEMA()

      END
