      SUBROUTINE CRCNCT(BASE,NOMCH,MAILLA,GD,NBCMP,LICMP,RCMP)
      IMPLICIT NONE
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     ARGUMENTS:
C     ----------
      INCLUDE 'jeveux.h'
      INTEGER NBCMP
      CHARACTER*(*) BASE,NOMCH,MAILLA,GD
      CHARACTER*(*) LICMP(NBCMP)
      REAL*8 RCMP(NBCMP)
C ----------------------------------------------------------------------
C     BUT:
C     ----
C      CREER UN CHAM_NO A REPRESENTATION CONSTANTE D'UNE GRANDEUR
C      DONNEE AVEC DES COMPOSANTES IDENTIQUES SUR TOUS LES NOEUDS.
C
C     ENTREES:
C     --------
C     BASE  (K1)  : BASE OU L'ON VEUT CREER LE CHAM_NO
C     MAILLA(K8)  : NOM DU MAILLAGE SUR LEQUEL ON VEUT CREER LE CHAM_NO
C     GD    (K8)  : NOM DE LA GRANDEUR ASSICIEE AU CHAMP
C     NBCMP (I)   : NOMBRE DE CMPS DE LICMP(*)
C     LICMP (LK8) : LISTE DES CMPS QUE L'ON VEUT SUR TOUS LES NOEUDS
C     RCMP  (LR)  : VALEURS DES CMPS QUE L'ON VEUT EN TOUS LES NOEUDS
C
C     SORTIES:
C     --------
C      LE CHAM_NO NOMCH EST CREE.
C
C ----------------------------------------------------------------------
      CHARACTER*19 CH19
      CHARACTER*8 MAILL2,GD2,NOCMP
      CHARACTER*1 BAS2
      CHARACTER*4 TYSCA
      CHARACTER*24 VALK(2)

      CHARACTER*8 KBID
C
C
C
C-----------------------------------------------------------------------
      INTEGER IADESC ,IANCMP ,IAREFE ,IAVALE ,IBID ,ICMP ,IEC
      INTEGER IED ,IGD ,IIEC ,INO ,ITROU ,NBCMP2 ,NBNO
      INTEGER NEC, INDIK8
C-----------------------------------------------------------------------
      CALL JEMARQ()
      GD2= GD
      MAILL2=MAILLA
      CH19=NOMCH
      BAS2=BASE
C
C
C     VERIFICATION DES ARGUMENTS D'APPEL :
C     ------------------------------------
      CALL JENONU(JEXNOM('&CATA.GD.NOMGD',GD2),IGD)
      IF (IGD.EQ.0) CALL U2MESK('F','CALCULEL2_21',1,GD2)
      CALL JEVEUO(JEXNUM('&CATA.GD.NOMCMP',IGD),'L',IANCMP)
      CALL JELIRA(JEXNUM('&CATA.GD.NOMCMP',IGD),'LONMAX',NBCMP2,KBID)
      DO 1, ICMP=1,NBCMP
        NOCMP=LICMP(ICMP)
        ITROU=INDIK8(ZK8(IANCMP),NOCMP,1,NBCMP2)
        IF (ITROU.EQ.0) THEN
           VALK(1) = NOCMP
           VALK(2) = GD2
           CALL U2MESK('F','CALCULEL2_22', 2 ,VALK)
        ENDIF
 1    CONTINUE
      CALL DISMOI('F','NB_EC',GD2,'GRANDEUR',NEC,KBID,IED)
      CALL DISMOI('F','TYPE_SCA',GD2,'GRANDEUR',IBID,TYSCA,IED)
      IF (TYSCA(1:1).NE.'R') CALL U2MESK('F','CALCULEL2_23',1,GD2)
C
C
C     ALLOCATION DU CHAM_NO :
C     -----------------------
      CALL DISMOI('F','NB_NO_MAILLA',MAILL2,'MAILLAGE',NBNO,KBID,IED)
      CALL WKVECT(CH19//'.VALE',BAS2//' V R',NBCMP*NBNO,IAVALE)
      CALL WKVECT(CH19//'.DESC',BAS2//' V I',NEC+2,IADESC)
      CALL WKVECT(CH19//'.REFE',BAS2//' V K24',4,IAREFE)
C
C     OBJET: .REFE
C     ------------
      ZK24(IAREFE-1+1)=MAILL2
C
C     OBJET: .DESC
C     ------------
      CALL JEECRA(CH19//'.DESC','DOCU',IBID,'CHNO')
      ZI(IADESC-1+1)=IGD
      ZI(IADESC-1+2)=-NBCMP
      DO 2, ICMP=1,NBCMP
        NOCMP=LICMP(ICMP)
        ITROU=INDIK8(ZK8(IANCMP),NOCMP,1,NBCMP2)
        IEC=(ITROU-1)/30 +1
        IIEC=ITROU-(IEC-1)*30
        ZI(IADESC-1+2+IEC)=IOR(ZI(IADESC-1+2+IEC),2**IIEC)
 2    CONTINUE
C
C     OBJET: .VALE
C     ------------
      DO 3, ICMP=1,NBCMP
        DO 4, INO=1,NBNO
          ZR(IAVALE-1+(INO-1)*NBCMP+ICMP)=RCMP(ICMP)
 4      CONTINUE
 3    CONTINUE
C
C
      CALL JEDEMA()
      END
