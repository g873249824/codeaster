      SUBROUTINE ETENC2(CARTZ,IRET)
      IMPLICIT NONE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 04/09/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C     ARGUMENTS:
C     ----------
      INCLUDE 'jeveux.h'
      CHARACTER*19 CART
      CHARACTER*(*) CARTZ
      INTEGER IRET
C ----------------------------------------------------------------------
C     ENTREES:
C     CARTZ  : NOM DE LA CARTE A ETENDRE
C     SORTIES:
C     ON CREE L'OBJET  CART.PTMA

C     IRET : CODE RETOUR :
C              0 --> OK
C              1 --> LA CARTE CONCERNE AUSSI DES MAILLES TARDIVES
C ----------------------------------------------------------------------
      CHARACTER*24 VALK
      CHARACTER*1 K1BID
      INTEGER NBMA,NBEDIT,IGD,CODE,IENT,I,II,NB
      INTEGER DESC,GRPMA,LIMA
      INTEGER JPTMA,IRET0,IBID
      INTEGER NOLI
      INTEGER VALI(3)
      CHARACTER*8 MA,KBID

      CALL JEMARQ()
      CART = CARTZ

      IRET = 0
      CALL JEEXIN(CART//'.DESC',IRET0)
      IF (IRET0.LE.0) CALL U2MESS('F','CALCULEL2_49')

      CALL JEVEUO(CART//'.NOLI','L',NOLI)
      CALL JEVEUO(CART//'.DESC','L',DESC)


C     1- ALLOCATION DE .PTMA :
C     ----------------------------------------
      CALL DISMOI('F','NOM_MAILLA',CART,'CARTE',IBID,MA,IBID)
      CALL DISMOI('F','NB_MA_MAILLA',MA,'MAILLAGE',NBMA,KBID,IBID)

      IF (NBMA.GT.0) THEN
        CALL JEEXIN(CART//'.PTMA',IBID)
C       -- LA CARTE EST DEJA ETENDUE:
        IF (IBID.GT.0) GO TO 50
        CALL WKVECT(CART//'.PTMA','V V I',NBMA,JPTMA)
      END IF


C     2- REMPLISSAGE DE .PTMA :
C     ----------------------------------------
      NBEDIT = ZI(DESC-1+3)
      DO 40 IGD = 1,NBEDIT
        CODE = ZI(DESC-1+3+2*IGD-1)
        IENT = ZI(DESC-1+3+2*IGD)

C        ------ GROUPE PREDEFINI "TOUT":
        IF (CODE.EQ.1) THEN
          DO 10 I = 1,NBMA
            ZI(JPTMA-1+I) = IGD
   10     CONTINUE
          GO TO 40
        END IF
        IF ((CODE.EQ.-1)) IRET = 1

C        ------- GROUPE DE MAILLES DU MAILLAGE:
        IF (CODE.EQ.2) THEN
          CALL JELIRA(JEXNUM(MA//'.GROUPEMA',IENT),'LONUTI',NB,K1BID)
          CALL JEVEUO(JEXNUM(MA//'.GROUPEMA',IENT),'L',GRPMA)
          DO 20 I = 1,NB
            II = ZI(GRPMA-1+I)
            ZI(JPTMA-1+II) = IGD
   20     CONTINUE
          GO TO 40
        END IF

C        ------- LISTE TARDIVE DE MAILLES ASSOCIEE A LA CARTE:
        IF (ABS(CODE).EQ.3) THEN
          CALL JELIRA(JEXNUM(CART//'.LIMA',IENT),'LONMAX',NB,K1BID)
          CALL JEVEUO(JEXNUM(CART//'.LIMA',IENT),'L',LIMA)
          IF (CODE.GT.0) THEN
            DO 30 I = 1,NB
              II = ZI(LIMA-1+I)
              IF (II.LE.0) THEN
                VALK = CART
                VALI (1) = IENT
                VALI (2) = I
                VALI (3) = II
                CALL U2MESG('F', 'CALCULEL5_85',1,VALK,3,VALI,0,0.D0)
              END IF
              ZI(JPTMA-1+II) = IGD
   30       CONTINUE
          ELSE
            IRET = 1
          END IF
          GO TO 40
        END IF
   40 CONTINUE
   50 CONTINUE


      CALL JEDEMA()
      END
