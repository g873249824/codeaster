      SUBROUTINE ELREF4(ELRZ,FAMIL,NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDE,
     &                  JGANO)
      IMPLICIT NONE
      CHARACTER*(*) ELRZ,FAMIL
      INTEGER NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDE,JGANO
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 08/09/2003   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE VABHHTS J.PELLET

C ----------------------------------------------------------------------
C BUT: RECUPERER DANS UNE ROUTINE TE00IJ LES ADRESSES DANS ZR
C      DES COORDONNEES ET POIDS DES POINTS DE GAUSS AINSI QUE DES
C      VALEURS DES FONCTIONS DE FORME (ET LEURS DERIVEES)
C ----------------------------------------------------------------------
C   IN   ELRZ : NOM DE L'ELREFA (K8) OU ' '
C                 SI ELRZ=' ' : ON PREND L'ELREFA "PRINCIPAL"
C        FAMIL  : NOM (LOCAL) DE LA FAMILLE DE POINTS DE GAUSS :
C                 'STD','RICH',...
C   OUT  NDIM   : DIMENSION DE L'ESPACE (=NB COORDONNEES)
C        NNO    : NOMBRE DE NOEUDS DU TYPE_MAILLE
C        NNOS   : NOMBRE DE NOEUDS SOMMETS DU TYPE_MAILLE
C        NPG1   : NOMBRE DE POINTS DE GAUSS
C        IPOIDS : ADRESSE DANS ZR DU TABLEAU POIDS(IPG)
C        IVF    : ADRESSE DANS ZR DU TABLEAU FF(INO,IPG)
C        IDFDE  : ADRESSE DANS ZR DU TABLEAU DFF(IDIM,INO,IPG)
C        JGANO  : ADRESSE DANS ZR DE LA MATRICE DE PASSAGE
C                      GAUSS -> NOEUDS (DIM= 2+NNO*NPG)
C                 ATTENTION : LES 2 1ERS TERMES SONT LES
C                             DIMMENSIONS DE LA MATRICE: NNO ET NPG

C   -------------------------------------------------------------------
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32,JEXNUM
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN DECLARATIONS NORMALISEES  JEVEUX ---------------------
      CHARACTER*16 OPTION,NOMTE,NOMTM,PHENO,MODELI
      COMMON /CAKK01/OPTION,NOMTE,NOMTM,PHENO,MODELI
      INTEGER NUTE,JNBELR,JNOELR,IACTIF,JPNLFP,JNOLFP,NBLFPG
      COMMON /CAII11/NUTE,JNBELR,JNOELR,IACTIF,JPNLFP,JNOLFP,NBLFPG


      CHARACTER*8 ELRF,FAMI2,FAPG(20)
      CHARACTER*16 NOFGPG
      CHARACTER*32 NOFLPG
      INTEGER NBFPG,NBPG(20),JVR,DECAL,IFAM,LONFAM
      INTEGER INDK32,INDIK8,NUFPG,NUFGPG,NUFLPG
      REAL*8 VOL,X(3*27)

C     -- POUR FAIRE DES "SAVE" ET GAGNER EN PERFORMANCE :
      INTEGER MAXSAV
      PARAMETER (MAXSAV=5)
      INTEGER NBSAV
      COMMON /CAII13/NBSAV
      INTEGER ADDSAV(5,8),K1,K2,NUSAV
      CHARACTER*32 NOMSAV(MAXSAV)
      SAVE NOMSAV,ADDSAV
C DEB ------------------------------------------------------------------

      FAMI2 = FAMIL
      IF (ELRZ.EQ.' ') THEN
        CALL ELREF1(ELRF)
        CALL ASSERT(ELRF.NE.'XXXXXXXX')
      ELSE
        ELRF = ELRZ
      END IF
      NOFLPG = NOMTE//ELRF//FAMI2


C     -- POUR GAGNER DU TEMPS, ON REGARDE SI LA FAMILLE A ETE SAUVEE:
C     ---------------------------------------------------------------
      NUSAV = INDK32(NOMSAV,NOFLPG,1,NBSAV)
      IF (NUSAV.GT.0) THEN
        NDIM = ADDSAV(NUSAV,1)
        NNO = ADDSAV(NUSAV,2)
        NNOS = ADDSAV(NUSAV,3)
        NPG1 = ADDSAV(NUSAV,4)
        IPOIDS = ADDSAV(NUSAV,5)
        IVF = ADDSAV(NUSAV,6)
        IDFDE = ADDSAV(NUSAV,7)
        JGANO = ADDSAV(NUSAV,8)
        GO TO 40
      END IF


C     -- CALCUL DE NUFPG :
C     --------------------
      NUFLPG = INDK32(ZK32(JPNLFP),NOFLPG,1,NBLFPG)
      CALL ASSERT(NUFLPG.GT.0)
      NUFGPG = ZI(JNOLFP-1+NUFLPG)
      CALL JENUNO(JEXNUM('&CATA.TM.NOFPG',NUFGPG),NOFGPG)
      CALL ASSERT(NOFGPG(1:8).EQ.ELRF)
      CALL ELRACA(ELRF,NDIM,NNO,NNOS,NBFPG,FAPG,NBPG,X,VOL)
      CALL ASSERT(NBFPG.LT.20)
      NUFPG = INDIK8(FAPG,NOFGPG(9:16),1,NBFPG)
      CALL ASSERT(NUFPG.GT.0)


      CALL JEVEUO('&INEL.'//ELRF//'.ELRA_R','L',JVR)

      DECAL = 0
      DO 10,IFAM = 1,NUFPG - 1
        NPG1 = NBPG(IFAM)
        LONFAM = NPG1* (NDIM+1) + NPG1*NNO* (NDIM+1) + 2 + NPG1*NNO
        DECAL = DECAL + LONFAM
   10 CONTINUE

      NPG1 = NBPG(NUFPG)

      IPOIDS = JVR + DECAL
      IVF = IPOIDS + NPG1* (NDIM+1)
      IDFDE = IVF + NPG1*NNO
      JGANO = IDFDE + NPG1*NNO*NDIM


C     -- ON SAUVEGARDE LES VALEURS CALCULEES :
C     ----------------------------------------
C     -- ON DECALE TOUT LE MONDE VERS LE BAS:
      NBSAV = MIN(NBSAV+1,MAXSAV)
      DO 30,K1 = NBSAV - 1,1,-1
        NOMSAV(K1+1) = NOMSAV(K1)
        DO 20,K2 = 1,8
          ADDSAV(K1+1,K2) = ADDSAV(K1,K2)
   20   CONTINUE
   30 CONTINUE

C     -- ON RECOPIE LES NOUVELLES VALEURS EN POSITION 1 :
      NOMSAV(1) = NOFLPG
      ADDSAV(1,1) = NDIM
      ADDSAV(1,2) = NNO
      ADDSAV(1,3) = NNOS
      ADDSAV(1,4) = NPG1
      ADDSAV(1,5) = IPOIDS
      ADDSAV(1,6) = IVF
      ADDSAV(1,7) = IDFDE
      ADDSAV(1,8) = JGANO


   40 CONTINUE


      END
