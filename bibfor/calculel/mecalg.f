      SUBROUTINE MECALG(OPTIOZ,RESULT,MODELE,DEPLA,THETA,MATE,NCHAR,
     &                  LCHAR,SYMECH,COMPOR,EXITIM,TIME,IORD,NBPRUP,
     &                  NOPRUP,NOPASE,DLAGDE)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 11/09/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------

C     - FONCTION REALISEE:   CALCUL DU TAUX DE RESTITUTION D'ENERGIE
C                          ET DE SA DERIVEE PAR RAPPORT A UNE VARIATION
C                          DE DOMAINE.

C     - ARGUMENTS   :

C IN/OUT    OPTION       --> CALC_G    (G SI CHARGES REELLES)
C                        --> CALC_G_F  (G SI CHARGES FONCTIONS)
C                        --> CALC_DG   (G + DG SI CHARGES REELLES)
C                        --> CALC_DG_F (G + DG SI CHARGES FONCTIONS)
C IN    RESULT       --> NOM UTILISATEUR DU RESULTAT ET TABLE
C IN    MODELE       --> NOM DU MODELE
C IN    DEPLA        --> CHAMP DES DEPLACEMENTS
C IN    THETA        --> CHAMP THETA (DE TYPE CHAM_NO)
C IN    MATE         --> CHAMP DU MATERIAU
C IN    NCHAR        --> NOMBRE DE CHARGES
C IN    LCHAR        --> LISTE DES CHARGES
C IN    SYMECH       --> SYMETRIE DU CHARGEMENT
C IN    EXITIM       --> VRAI SI L'INSTANT EST DONNE
C IN    TIME         --> INSTANT DE CALCUL
C IN    IORD         --> NUMERO D'ORDRE DE LA SD
C IN    NOPASE       --> NOM DU PARAMETRE SENSIBLE (SI DG)
C IN    DLAGDE       --> DERIVEE LAGRANGIENNE DU DEPLACEMENT (SI DG)
C   -------------------------------------------------------------------
C     SUBROUTINES APPELLEES:
C       JEVEUX: JEMARQ,DISMOI,RSEXCH,JEEXIN,JEVEUO,METREF,METEMP,
C               MTEMP2,MECACT,JEIMPO,DETRSD.
C       PARAMETRE: GETFAC,GETVID.
C       MESSAGE: INFNIV,UTMESS.
C       ELEMENTS FINIS: MEGEOM,CALCUL,MESOMM.
C       DIVERS: GCHARG.

C     FONCTIONS INTRINSEQUES:
C       AUCUNE.
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C      11/12/00 (OB): TOILETTAGE FORTRAN, INTRODUCTION DU CALCUL DE DG.
C----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE

C DECLARATION PARAMETRES D'APPELS
      CHARACTER*8 MODELE,LCHAR(*),RESULT,SYMECH
      CHARACTER*8 NOPASE
      CHARACTER*16 OPTIOZ,NOPRUP(*)
      CHARACTER*24 DEPLA,MATE,COMPOR,THETA,DLAGDE
      REAL*8 TIME
      INTEGER IORD,NCHAR,NBPRUP
      LOGICAL EXITIM

C     ----------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C ---------------- FIN COMMUNS NORMALISES  JEVEUX  --------------------

C DECLARATION VARIABLES LOCALES

      CHARACTER*6 NOMPRO
      PARAMETER (NOMPRO='MECALG')

      INTEGER IBID,IRET,JTEMP,NRES,IFM,NIV
      INTEGER I,IERD,NCHIN,INCR,INIT,NSIG,NDEP
      REAL*8 G,DG,VAL(3)
      COMPLEX*16 CBID
      LOGICAL EXIGEO,EXITHE,EXITRF,FONC,EPSI
      CHARACTER*8 RESU,NOMA,REPK,LPAIN(23),LPAOUT(2),K8B
      CHARACTER*16 OPTION
      CHARACTER*24 LIGRMO,TEMPE,CHGEOM,LCHIN(23),LCHOUT(2),DLAGTE
      CHARACTER*24 CHTIME,CHTEMP,CHTREF,CHVOLU
      CHARACTER*24 CF1D2D,CF2D3D,CHPRES,CHROTA,CHPESA
      CHARACTER*24 PAVOLU,PA1D2D,PA2D3D,PAPRES,PEPSIN,OBVALE
      CHARACTER*24 CHSIG,CHEPSP,CHVARI,CHSIGI,CHDEPI,CHEPSI,TYPE
      CHARACTER*24 THETAS


      CALL JEMARQ()
      CHTIME = ' '
      OPTION = OPTIOZ
      CALL INFNIV(IFM,NIV)

C INITIALISATIONS
      G = 0.D0
      DG = 0.D0

C- RECUPERATION DU CHAMP GEOMETRIQUE

      CALL MEGEOM(MODELE,' ',EXIGEO,CHGEOM)
      NOMA = CHGEOM(1:8)

C- RECUPERATION DU COMPORTEMENT

      CALL GETFAC('COMP_INCR',INCR)
      IF (INCR.NE.0) THEN
        CALL GETVID(' ','RESULTAT',0,1,1,RESU,NRES)
        CALL DISMOI('F','TYPE_RESU',RESU,'RESULTAT',IBID,TYPE,IERD)
        IF (TYPE.NE.'EVOL_NOLI') THEN
          CALL UTMESS('F',NOMPRO,'RESULTAT N''EST PAS EN EVOL_NOLI')
        END IF
        CALL RSEXCH(RESU,'SIEF_ELGA',IORD,CHSIG,IRET)
        IF (IRET.NE.0) THEN
          CALL UTMESS('F',NOMPRO,'CHAMP SIEF_ELGA NON TROUVE')
        END IF
        CALL RSEXCH(RESU,'EPSP_ELNO',IORD,CHEPSP,IRET)
        IF (IRET.NE.0) THEN
          CALL UTMESS('F',NOMPRO,'CHAMP EPSP_ELNO NON TROUVE')
        END IF
        CALL RSEXCH(RESU,'VARI_ELNO_ELGA',IORD,CHVARI,IRET)
        IF (IRET.NE.0) THEN
          CALL UTMESS('F',NOMPRO,'CHAMP VARI_ELNO_ELGA NON TROUVE')
        END IF
      END IF

C- RECUPERATION DE L'ETAT INITIAL

      CALL GETFAC('ETAT_INIT',INIT)
      IF (INIT.NE.0) THEN
        CALL GETVID('ETAT_INIT','SIGM',1,1,1,CHSIGI,NSIG)
        CALL GETVID('ETAT_INIT','DEPL',1,1,1,CHDEPI,NDEP)
        IF ((NSIG.EQ.0) .AND. (NDEP.EQ.0)) THEN
          CALL UTMESS('F',NOMPRO,'AUCUN CHAMP INITIAL TROUVE')
        END IF
      END IF

C- RECUPERATION (S'ILS EXISTENT) DES CHAMP DE TEMPERATURES (T,TREF)

      TEMPE = ' '
      CHTEMP = '&&'//NOMPRO//'.CH_TEMP_R'
      DO 10 I = 1,NCHAR
        CALL JEEXIN(LCHAR(I)//'.CHME.TEMPE.TEMP',IRET)
        IF (IRET.NE.0) THEN
          CALL JEVEUO(LCHAR(I)//'.CHME.TEMPE.TEMP','L',JTEMP)
          TEMPE = ZK8(JTEMP)
        END IF
   10 CONTINUE
      CALL METREF(MATE,NOMA,EXITRF,CHTREF)
      CALL METEMP(NOMA,TEMPE,EXITIM,TIME,CHTREF,EXITHE,CHTEMP(1:19))
      CALL DISMOI('F','ELAS_F_TEMP',MATE,'CHAM_MATER',IBID,REPK,IERD)
      IF (REPK.EQ.'OUI') THEN
        IF (.NOT.EXITHE) THEN
          CALL UTMESS('F',NOMPRO,'LE MATERIAU DEPEND DE LA TEMPERATURE'
     &                //'! IL N''Y A PAS DE CHAMP DE TEMPERATURE '//
     &                '! LE CALCUL EST IMPOSSIBLE ')
        END IF
        IF (.NOT.EXITRF) THEN
          CALL UTMESS('A',NOMPRO,'LE MATERIAU DEPEND DE LA TEMPERATURE'
     &                //' IL N''Y A PAS DE TEMPERATURE DE REFERENCE'//
     &                ' ON PRENDRA DONC LA VALEUR 0')
        END IF

C FLAG D'ERREUR SI LE MATERIAU DEPEND DE LA TEMPERATURE
        IF (OPTION.EQ.'CALC_DG') THEN
          CALL UTMESS('F',NOMPRO,'LE MATERIAU DEPEND DE LA TEMPERATURE'
     &                //' L''OPTION CALC_DG NE PREND PAS EN COMPTE'//
     &                ' CETTE POSSIBILITE')
        END IF
      END IF

C RECUPERATION DE LA DERIVEE LAGRANGIENNE DU CHAMP DE TEMPERATURE
C DANS LA SD DU MOT-CLE CHARGE
      IF (OPTION.EQ.'CALC_DG') THEN

        IBID = 0
        CALL RSEXC2(1,1,NOPASE,'THETA',IBID,THETAS,OPTION,IRET)
        IF (IRET.GT.0) THEN
          CALL UTMESS('F',NOMPRO,'LE CHAMP DE THETA SENSIBILITE '//
     &                'EST INEXISTANT DANS LA SD '//NOPASE//' !')
        END IF

        DLAGTE = '&&'//NOMPRO//'.DLAG_TEMP'
        CALL MTEMP2(NOMA,TEMPE,EXITIM,TIME,CHTREF,NOPASE,EXITHE,DLAGTE)

      END IF

C - TRAITEMENT DES CHARGES

      CALL GCHARG(MODELE,NCHAR,LCHAR,CHVOLU,CF1D2D,CF2D3D,CHPRES,CHEPSI,
     &            CHPESA,CHROTA,FONC,EPSI)
      IF (FONC) THEN
        PAVOLU = 'PFFVOLU'
        PA1D2D = 'PFF1D2D'
        PA2D3D = 'PFF2D3D'
        PAPRES = 'PPRESSF'
        PEPSIN = 'PEPSINF'
        IF (OPTION.EQ.'CALC_DG') THEN
          OPTION = 'CALC_DG_F'
        ELSE
          OPTION = 'CALC_G_F'
        END IF
      ELSE
        PAVOLU = 'PFRVOLU'
        PA1D2D = 'PFR1D2D'
        PA2D3D = 'PFR2D3D'
        PAPRES = 'PPRESSR'
        PEPSIN = 'PEPSINR'
        OPTION = OPTIOZ
      END IF

      LPAOUT(1) = 'PGTHETA'
      LCHOUT(1) = '&&'//NOMPRO//'.CH_G'
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM
      LPAIN(2) = 'PDEPLAR'
      LCHIN(2) = DEPLA
      LPAIN(3) = 'PTHETAR'
      LCHIN(3) = THETA
      LPAIN(4) = 'PMATERC'
      LCHIN(4) = MATE
      LPAIN(5) = 'PTEMPER'
      LCHIN(5) = CHTEMP
      LPAIN(6) = 'PTEREF'
      LCHIN(6) = CHTREF
      LPAIN(7) = PAVOLU(1:8)
      LCHIN(7) = CHVOLU
      LPAIN(8) = PA1D2D(1:8)
      LCHIN(8) = CF1D2D
      LPAIN(9) = PA2D3D(1:8)
      LCHIN(9) = CF2D3D
      LPAIN(10) = PAPRES(1:8)
      LCHIN(10) = CHPRES
      LPAIN(11) = 'PPESANR'
      LCHIN(11) = CHPESA
      LPAIN(12) = 'PROTATR'
      LCHIN(12) = CHROTA
      LPAIN(13) = PEPSIN(1:8)
      LCHIN(13) = CHEPSI
      LPAIN(14) = 'PCOMPOR'
      LCHIN(14) = COMPOR

      LIGRMO = MODELE//'.MODELE'
      NCHIN = 14
      IF ((OPTION.EQ.'CALC_G_F') .OR. (OPTION.EQ.'CALC_DG_F')) THEN
        CHTIME = '&&'//NOMPRO//'.CH_INST_R'
        CALL MECACT('V',CHTIME,'MODELE',LIGRMO,'INST_R  ',1,'INST   ',
     &              IBID,TIME,CBID,K8B)
        LPAIN(NCHIN+1) = 'PTEMPSR'
        LCHIN(NCHIN+1) = CHTIME
        NCHIN = NCHIN + 1
      END IF

      IF (INCR.NE.0) THEN
        LPAIN(NCHIN+1) = 'PCONTRR'
        LCHIN(NCHIN+1) = CHSIG
        LPAIN(NCHIN+2) = 'PDEFOPL'
        LCHIN(NCHIN+2) = CHEPSP
        LPAIN(NCHIN+3) = 'PVARIPR'
        LCHIN(NCHIN+3) = CHVARI
        NCHIN = NCHIN + 3
      END IF
      IF (INIT.NE.0) THEN
        IF (NSIG.NE.0) THEN
          LPAIN(NCHIN+1) = 'PSIGINR'
          LCHIN(NCHIN+1) = CHSIGI
          NCHIN = NCHIN + 1
        END IF
        IF (NDEP.NE.0) THEN
          LPAIN(NCHIN+1) = 'PDEPINR'
          LCHIN(NCHIN+1) = CHDEPI
          NCHIN = NCHIN + 1
        END IF
      END IF

C SI DG:
C INCORPORATION DES CHAMPS CONTENANT LES DERIVEES LAGRANGIENNES DU
C DEPLACEMENT (DLAGDE), DE LA TEMPERATURE (DLAGTE) ET LES VALEURS
C DU CHAMP THETA ASSOCIE A CETTE DERIVATION (THETAS)
      IF (OPTION(1:7).EQ.'CALC_DG') THEN
        LPAIN(NCHIN+1) = 'PDLAGDE'
        LCHIN(NCHIN+1) = DLAGDE
        LPAIN(NCHIN+2) = 'PVECTTH'
        LCHIN(NCHIN+2) = THETAS
        LPAIN(NCHIN+3) = 'PDLAGTE'
        LCHIN(NCHIN+3) = DLAGTE
        NCHIN = NCHIN + 3
        LPAOUT(2) = 'PDGTHET'
        LCHOUT(2) = '&&'//NOMPRO//'.CH_DG'
      END IF

C-  SOMMATION DES G ELEMENTAIRES

      CALL CALCUL('S',OPTION,LIGRMO,NCHIN,LCHIN,LPAIN,2,LCHOUT,LPAOUT,
     &            'V')
      CALL MESOMM(LCHOUT(1),1,IBID,G,CBID,0,IBID)
      IF (OPTION(1:7).EQ.'CALC_DG') CALL MESOMM(LCHOUT(2),1,IBID,DG,
     &    CBID,0,IBID)
      IF (SYMECH.NE.'SANS') THEN
        G = 2.D0*G
        DG = 2.D0*DG
      END IF

C- IMPRESSION DE G ET ECRITURE DANS LA TABLE RESULT

      IF (NIV.GE.2) THEN
        OBVALE = LCHOUT(1) (1:19)//'.VALE'
        IF (OPTION(1:7).EQ.'CALC_DG') THEN
          CALL JEIMPO('MESSAGE',OBVALE,' ',
     &         'OBJET CONTENANT LA VALEUR DE G ET DG SUR CHAQUE ELEMENT'
     &                )
        ELSE
          CALL JEIMPO('MESSAGE',OBVALE,' ',
     &               'OBJET CONTENANT LA VALEUR DE G SUR CHAQUE ELEMENT'
     &                )
        END IF
      END IF

      IF (NBPRUP.EQ.1) THEN
        CALL TBAJLI(RESULT,NBPRUP,NOPRUP,IBID,G,CBID,K8B,0)
      ELSE
        VAL(1) = TIME
        VAL(2) = G
        IF (OPTION(1:7).EQ.'CALC_DG') VAL(3) = DG
        CALL TBAJLI(RESULT,NBPRUP,NOPRUP,IORD,VAL,CBID,K8B,0)
      END IF

      CALL DETRSD('CHAMP_GD',CHTEMP)
      IF ((OPTION.EQ.'CALC_G_F') .OR.
     &    (OPTION.EQ.'CALC_DG_F')) CALL DETRSD('CHAMP_GD',CHTIME)

C DESTRUCTION DES SD. PROVISOIRES (SI DG)
      IF (OPTION(1:7).EQ.'CALC_DG') CALL DETRSD('CHAMP_GD',DLAGTE)

      CALL JEDEMA()
      END
