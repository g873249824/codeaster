      SUBROUTINE ARLAP1(DIME  ,TABARC,JARBLI,
     &                  BOI1SD,JBO1MM,JBO1PA,
     &                  BOI2SD,JBO2MM,JBO2PA,
     &                  NVIS0 ,JLIRES,
     &                  NORMAL,JGRMAM,GRMALC,
     &                  MAIL  ,CONNEX,LONCUM,COORD,
     &                  TYPMAS,TYPMAI,
     &                  NBMA1 ,TABMA1,
     &                  NBMA2 ,TABMA2,
     &                  NHAPP ,TABCOL,NBMAC ,
     &                  TABFIL,TALIEN,TAVIVI,JTRAVR,JVOISI,
     &                  NAPP  ,NAPT                        )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 20/10/2009   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
C TOLE CRP_21
      CHARACTER*8   MAIL,TYPMAS(*)
      INTEGER       JARBLI,BOI1SD(*),BOI2SD(*),DIME
      INTEGER       TYPMAI(*),TABARC(*),JLIRES,NVIS0
      INTEGER       NBMA1,TABMA1(NBMA1),NBMA2,TABMA2(NBMA2)
      INTEGER       NBMAC,NHAPP,JGRMAM,GRMALC(*)
      INTEGER       CONNEX(*),LONCUM(*)
      INTEGER       JBO1MM,JBO1PA,JBO2MM,JBO2PA,TALIEN(NBMA1)
      LOGICAL       TABCOL(NBMA1),TABFIL(NBMA2)
      REAL*8        COORD(3,*),NORMAL(DIME,*)
      INTEGER       TAVIVI(NBMA1),JTRAVR,JVOISI(2),NAPP,NAPT
C
C ----------------------------------------------------------------------
C
C APPARIEMENT DE DEUX GROUPES DE MAILLE PAR LA METHODE
C BOITES ENGLOBANTES + ARBRE BSP
C
C ARLEQUIN -- ROUTINE D'APPARIEMENT
C ***                   ** *
C
C ----------------------------------------------------------------------
C
C
C APPARIEMENT DE NOMBO1 SUR NOMBO2, PAR FILTRAGE DES MAILLES SUR UNE
C ZONE DE COLLAGE (ZONE RESTREINTE DE GRMA2)
C
C
C IN  DIME   : DIMENSION DE L'ESPACE (2 OU 3)
C IN  TABARC : ARBORESCENCE DE L'ARBRE D'APPARIEMENT (VOIR BISSEC.F)
C IN  JARBLI : POINTEUR VERS VECTEUR LISTE DE MAILLES
C IN  BOI1SD : SD BOITE 1
C IN  JBO1MM : POINTEUR SUR LES BOITES ENGLOBANTES DU GROUPE 1
C IN  JBO1PA : POINTEUR SUR LES PANS DES MAILLES DU GROUPE 1
C IN  BOI2SD : SD BOITE 2
C IN  JBO2MM : POINTEUR SUR LES BOITES ENGLOBANTES DU GROUPE 2
C IN  JBO2PA : POINTEUR SUR LES PANS DES MAILLES DU GROUPE 2
C IN  NVIS0  : LONGUEUR DE L'OBJET .LISTE.RESERVE (CF ARLAP0)
C IN  JLIRES : POINTEUR DANS L'OBJET .LISTE.RESERVE (CF ARLAP0)
C IN  NORMAL : COORDONNEES DES NORMALES (CF LISNOR)
C IN  JGRMAM : POINTEUR SUR LE GRAPHE MAILLE-MAILLE ATTACHE A GRMA2
C              (VOIR ROUTINE GRMAMA)
C                 MAILLE -> LISTE DES MAILLES VOISINES
C                 (IE. DIFFERENTES ET PARTAGEANT UN NOEUD)
C IN  GRMALC : LONGUEUR CUMULEE DU GRAPHE MAILLE-MAILLE ATTACHE A GRMA2
C IN  MAIL   : NOM UTILISATEUR DU MAILLAGE
C IN  CONNEX : CONNEXITE DES MAILLES
C IN  LONCUM : LONGUEUR CUMULEE DE CONNEX
C IN  COORD  : COORDONNEES DES NOEUDS
C IN  TYPMAS : TABLEAU CONTENANT NOM DES TYPES ELEMENTS ASTER
C              (&&CATA.NOMTM)
C IN  TYPMAI : TABLEAU CONTENANT NUMERO DES ELEMENTS DU MAILLAGE MAIL
C IN  NBMA1  : NOMBRE DE MAILLES DANS GRMA1
C IN  TABMA1 : MAILLES GROUP_MA_1
C              LISTE CONTENANT LES NUMEROS ABSOLUS DES MAILLES
C              A APPARIER (LONG: NBMA1)
C IN  NBMA2  : NOMBRE DE MAILLES DANS GRMA2
C IN  TABMA2 : MAILLES GROUP_MA_2
C              LISTE CONTENANT LES NUMEROS ABSOLUS DES MAILLES
C              A APPARIER (LONG: NBMA2)
C IN  NHAPP  : ECHANTILLONNAGE DE LA FRONTIERE
C IN  TABCOL : TABLEAU INDIQUANT SI MAILLE A COLLER, DE LONGUEUR NBMA1
C IN  NBMAC  : NOMBRE DE MAILLES DE LA ZONE DE COLLAGE
C IN  TABFIL : TABLEAU INDIQUANT SI MAILLE APPARIEE
C              -> TRUE , SI MAILLE NON APPARIEE
C              -> FALSE, SI MAILLE APPARIEE
C IN  TALIEN : TABLEAU .LISTE.ENTETE (CF ARLAP0) DE LONGUEUR NBMA1
C OUT TAVIVI : TABLEAU .NVISAVIS (CF ARLAP0) DE LONGUEUR NBMA1
C IN  JTRAVR : POINTEUR SUR LE VECTEUR DE TRAVAIL TRAVR (CF ARLAP0)
C IN  JVOISI : POINTEURS SUR .VOISIN1 ET .VOISIN2 (CF ARLAP0)
C OUT NAPP   : NOMBRE DE COUPLES D'APPARIEMENT
C OUT NAPT   : NOMBRE TOTAL DE COUPLES D'APPARIEMENT
C
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      CHARACTER*32  JEXNUM
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      CHARACTER*8   TYPEMA,NOMMAI
      INTEGER       JARLIA
      INTEGER       JTRAUX
      INTEGER       NVIS,NMACAN
      INTEGER       NBEV
      INTEGER       LCOQUE
      INTEGER       NVOIS(2),NUMA,IMA1,IMA2
      INTEGER       NNOH,DEFDIM,DIMPL2,IAUX,JAUX,JLIMA,KAUX,LAUX,PMAT0,
     &              PMAT1
      INTEGER       NOEARE(48),NOEPAN(60)
      INTEGER       NSOM,NPAN,NARE
      REAL*8        CNOEUD(81),VLR
      LOGICAL       LINTER,MINTER
      REAL*8        DDOT
      INTEGER       NBSOM,NBARE,NBPAN
      INTEGER       IFM,NIV
C
      INTEGER      NBVLI, NBVLR, NBVLK
      PARAMETER   ( NBVLI = 1 , NBVLR = 1 , NBVLK = 1 )
      INTEGER      VALI(NBVLI)
      REAL*8       VALR(NBVLR)
      CHARACTER*24 VALK(NBVLK)

      CHARACTER*6  NOMPRO
      PARAMETER   (NOMPRO='ARLAP1')
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('ARLEQUIN',IFM,NIV)
C
      DEFDIM  = 2*DIME
      DIMPL2  = DIME+2
      NAPP = 0
      NAPT = 0
C
      DO 10 , IMA1 = 1, NBMA1
C
        IF (.NOT.TABCOL(IMA1)) GOTO 11
C
C 1. ==> CARACTERISTIQUES DE LA MAILLE DU DOMAINE 1
C
        NUMA = TABMA1(IMA1)
        CALL JENUNO(JEXNUM(MAIL(1:8)//'.NOMMAI',NUMA),NOMMAI)
        TYPEMA = TYPMAS(TYPMAI(NUMA))
C --- EXTENSION EVENTUELLE DES COQUES
        CALL TMACOQ(TYPEMA,DIME,LCOQUE)
C --- COORDONNEES DES SOMMETS
        IF (LCOQUE.EQ.0) THEN
          CALL COSOLI(NUMA,CONNEX,LONCUM,COORD,
     &                DIME,CNOEUD                          )
        ELSE
          CALL COCOQU(NUMA      ,CONNEX,LONCUM,COORD,
     &                'VARIABLE',
     &                0.D0      ,NORMAL,DIME      ,CNOEUD)
        ENDIF
C --- NOMBRE DE SOMMETS DE LA MAILLE
        NSOM = NBSOM(TYPEMA)
C --- NOMBRE D'ARETES DE LA MAILLE
        NARE = NBARE(TYPEMA)
C --- NUMEROS DES NOEUDS DEFINISSANT LES ARETES DE LA MAILLE
        CALL NOARE(TYPEMA,NOEARE)
C
        IF (DIME.EQ.3) THEN
C --- NOMBRE DE PANS DE LA MAILLE
          NPAN = NBPAN(TYPEMA)
C --- NUMEROS DES NOEUDS DEFINISSANT LES PANS DE LA MAILLE
          CALL NOPAN (TYPEMA,NOEPAN)
        ELSE
          NPAN = 0
        ENDIF
C
C 2. ==> ECHANTILLONNAGE DE LA FRONTIERE DE LA MAILLE
C
        CALL ECHMAP(NOMMAI,TYPEMA,DIME  ,CNOEUD,NSOM  ,
     &              NOEARE,NARE  ,NOEPAN,NPAN  ,NHAPP ,
     &              ZR(JTRAVR),NNOH)
C
C 3. ==> APPARIEMENT PONCTUEL ?
C
        NVIS = 0
        DO 30 , IAUX = 1, NBMA2
          TABFIL(IAUX) = .TRUE.
 30     CONTINUE
C
        JTRAUX = JTRAVR - DIME
C
C --- POUR CHAQUE POINT ECHANTILLONNE DE LA FRONTIERE DE LA MAILLE 1
C
        DO 32 , IAUX = 1, NNOH
C
C --- QUELLES MAILLES POSSIBLEMENT APPARIEES AVEC CE POINT ?
C
          JTRAUX = JTRAUX + DIME
          CALL CERNE(ZR(JTRAUX),DIME,TABARC,ZR(JBO2PA),NMACAN,JLIMA)
          JARLIA = JARBLI - 1 + JLIMA
C
C --- EXPLORATION DES MAILLES CANDIDATES DANS LE DOMAINE 2
C
          DO 321 , JAUX = 1, NMACAN
            IMA2 = ZI(JARLIA)
            JARLIA = JARLIA + 1
C
C --- SI LA MAILLE EST DEJA APPARIEE, ON PASSE A LA SUIVANTE
C
            IF (.NOT.TABFIL(IMA2)) THEN
              GOTO 321
            ENDIF
C
C --- SI LE POINT EST AU DEHORS DE LA BOITE MINMAX, ON PASSE
C     A LA MAILLE SUIVANTE
C
            PMAT0 = JBO2MM + DEFDIM*(IMA2-1)
C
            DO 322 , KAUX = 1, DIME
              VLR = ZR(JTRAUX-1+KAUX)
              IF ( (VLR.LT.ZR(PMAT0)) .OR. (VLR.GT.ZR(PMAT0+1)) ) THEN
                GOTO 321
              ENDIF
              PMAT0 = PMAT0 + 2
 322        CONTINUE
C
C --- SI LE POINT EST AU DEHORS DE LA BOITE ENGLOBANTE, ON PASSE
C     A LA MAILLE SUIVANTE
C
            PMAT0 = BOI2SD(2*IMA2+1)
            NBEV  = BOI2SD(2*IMA2+3) - PMAT0
            PMAT0 = JBO2PA + DIMPL2*(PMAT0-1)
C
            DO 323 , KAUX = 1, NBEV
              VLR = DDOT(DIME,ZR(PMAT0),1,ZR(JTRAUX),1)
     &              + ZR(PMAT0+DIME)
              IF ( VLR.GT.0.D0 ) THEN
                GOTO 321
              ENDIF
              PMAT0 = PMAT0 + DIMPL2
 323        CONTINUE
C
C ---  STOCKAGE : LE POINT EST DANS LA BOITE ENGLOBANTE DE LA MAILLE
C
            IF (NIV.GE.2) THEN
              VALI(1) = IAUX
              NUMA    = TABMA2(IMA2)
              CALL JENUNO(JEXNUM(MAIL(1:8)//'.NOMMAI',NUMA),NOMMAI)
              VALK(1) = NOMMAI
              CALL ARLDBG(NOMPRO,NIV,IFM,1,1,VALI,0,VALR,1,VALK)
            ENDIF
            TABFIL(IMA2) = .FALSE.
            ZI(JLIRES)      = IMA2
C
            IF (NVIS.EQ.0) THEN
              ZI(JLIRES+1) = 0
            ELSE
              ZI(JLIRES+1) = TALIEN(IMA1)
            ENDIF
C
            TALIEN(IMA1) = JLIRES
            NVIS         = NVIS  + 1
            JLIRES       = JLIRES + 2
            NVIS0        = NVIS0 - 1
C
            IF (NVIS0.LT.0) THEN
              CALL U2MESS('F','ARLEQUIN_13')
            ENDIF
C
            GOTO 32
C IL FAUDRAIT CERTAINEMENT SUPPRIMER CE GOTO 32 : A CAUSE DES
C EVENTUELLES COINCIDENCES, IL FAUDRAIT CONTINUER SUR LES MAILLES.
C SINON, ON RISQUE DE S'ARRETER PARCE QU'UN SOMMET EST LE MEME.
C
 321     CONTINUE
C
  32   CONTINUE
C
C 4. ==> CAS DE L'INCLUSION
C
        IF ( NVIS.EQ.0 ) THEN

C --- POINT MILIEU DE LA PREMIERE MAILLE DOMAINE 2

C --- CARACTERISTIQUES DE LA MAILLE
          NUMA   = TABMA2(1)
          TYPEMA = TYPMAS(TYPMAI(NUMA))

C --- EXTENSION EVENTUELLE DES COQUES
          CALL TMACOQ(TYPEMA,DIME,LCOQUE)

C --- COORDONNEES DES SOMMETS
          IF (LCOQUE.EQ.0) THEN
            CALL COSOLI(NUMA  ,CONNEX,LONCUM,COORD,DIME,
     &                  CNOEUD)
          ELSE
            CALL COCOQU(NUMA  ,CONNEX,LONCUM,COORD,'VARIABLE',
     &                  0.D0  ,NORMAL,DIME ,CNOEUD)
          ENDIF
C
C --- CALCUL DES COORDONNEES BARYCENTRE DE LA MAILLE
C     REMARQUE : ON ECRASE LES COORDONNEES DU PREMIER SOMMET
C
          DO 41 , KAUX = 1, DIME
            DO 411 LAUX = 1, NSOM - 1
              CNOEUD(KAUX) = CNOEUD(KAUX) + CNOEUD(LAUX*DIME+KAUX)
 411        CONTINUE
            CNOEUD(KAUX) = CNOEUD(KAUX) / DBLE(NSOM)
  41      CONTINUE
C
C --- SI LE BARYCENTRE EST AU DEHORS DE LA BOITE MINMAX, ON PASSE
C     A LA SUITE
C
          PMAT0 = JBO1MM + DEFDIM*(IMA1-1)
C
          DO 42 , KAUX = 1, DIME
            VLR = CNOEUD(KAUX)
            IF ((VLR.LT.ZR(PMAT0)).OR.(VLR.GT.ZR(PMAT0+1))) GOTO 61
            PMAT0 = PMAT0 + 2
  42      CONTINUE
C
C --- SI LE BARYCENTRE EST AU DEHORS DE LA BOITE ENGLOBANTE, ON PASSE
C     A LA SUITE
C
          PMAT0 = BOI1SD(2*IMA1+1)
          NBEV  = BOI1SD(2*IMA1+3)- PMAT0
          PMAT0 = JBO1PA + DIMPL2*(PMAT0-1)
C
          DO 43 , KAUX = 1, NBEV
            VLR = DDOT(DIME,ZR(PMAT0),1,CNOEUD,1) + ZR(PMAT0+DIME)
            IF (VLR.GT.0.D0) GOTO 61
            PMAT0 = PMAT0 + DIMPL2
  43      CONTINUE
C
C --- STOCKAGE : LE BARYCENTRE EST DANS LA BOITE ENGLOBANTE DE LA MAILLE
C
          IF (NIV.GE.2) THEN
            VALK(1) = NOMMAI
            CALL ARLDBG(NOMPRO,NIV,IFM,2,0,VALI,0,VALR,1,VALK)
          ENDIF
          TABFIL(1)    = .FALSE.
          ZI(JLIRES)   = 1
          ZI(JLIRES+1) = 0
          TALIEN(IMA1) = JLIRES
          JLIRES       = JLIRES + 2
          NVIS         = 1

        ENDIF
C
C 5. ==> PARCOURS DU VOISINAGE
C
        IF ((NBMA2.EQ.1).OR.(NVIS.EQ.0)) GOTO 61
C
C 5.1. ==> INITIALISATION PARCOURS DU VOISINAGE
C
        KAUX = 1
        LAUX = 2
        PMAT0 = JVOISI(1)
        PMAT1 = TALIEN(IMA1)
        NVOIS(1) = NVIS

        DO 51 , IAUX = 1, NVIS
          ZI(PMAT0-1+IAUX) = ZI(PMAT1)
          PMAT1 = ZI(PMAT1+1)
  51    CONTINUE
C
C 5.2. ==> PARCOURS
C
 52     CONTINUE

        NVOIS(LAUX) = 0
        NMACAN = NVOIS(KAUX)
        PMAT0     = JVOISI(KAUX)

        DO 521 , IAUX = 1, NMACAN

          JAUX = ZI(PMAT0)
          PMAT0 = PMAT0 + 1

          PMAT1 = GRMALC(JAUX)
          NBEV  = GRMALC(JAUX+1) - PMAT1
          PMAT1 = JGRMAM - 1 + PMAT1

          DO 522 , JAUX = 1, NBEV

            IMA2 = ZI(PMAT1)
            PMAT1 = PMAT1 + 1

            IF (TABFIL(IMA2)) THEN

              TABFIL(IMA2) = .FALSE.

              LINTER = MINTER(DIME,IMA1,IMA2,BOI1SD,BOI2SD,
     &                        ZR(JBO1MM),ZR(JBO2MM),ZR(JBO1PA),
     &                        ZR(JBO2PA))

              IF (LINTER) THEN

                ZI(JVOISI(LAUX)+NVOIS(LAUX)) = IMA2
                NVOIS(LAUX)  = NVOIS(LAUX) + 1
                ZI(JLIRES)   = IMA2
                ZI(JLIRES+1 )= TALIEN(IMA1)
                TALIEN(IMA1) = JLIRES
                JLIRES = JLIRES + 2
                NVIS   = NVIS + 1

                NVIS0 = NVIS0 - 1
                IF (NVIS0.LT.0) CALL U2MESS('F','ARLEQUIN_13')
C
              ENDIF
C
            ENDIF
C
 522      CONTINUE
C
 521    CONTINUE
C
        KAUX = 3 - KAUX
        LAUX = 3 - LAUX
C
        IF (NVOIS(KAUX).NE.0) GOTO 52
C
C 6. ==> LA SUITE
C
  61    CONTINUE

        IF (NVIS.NE.0) THEN
          TAVIVI(IMA1) = NVIS
          NAPP              = NAPP + NVIS
          NAPT              = NAPT + NVIS
          GOTO 10
        ENDIF

        TABCOL(IMA1) = .FALSE.
        NBMAC = NBMAC - 1
C
  11    CONTINUE

        TAVIVI(IMA1) = 1
        NAPT         = NAPT + 1

  10  CONTINUE

      CALL JEDEMA()
C
      END
