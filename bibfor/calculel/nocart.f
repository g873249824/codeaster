      SUBROUTINE NOCART(CHINZ,CODE,GROUPZ,MODEZ,NMA,LIMANO,LIMANU,
     &                  NMLIGZ, NCMP)
      IMPLICIT REAL*8 (A-H,O-Z)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 17/01/97   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C     ARGUMENTS:
C     ----------
      INTEGER CODE,NMA,NCMP,LIMANU(*)
      CHARACTER*3 MODE
      CHARACTER*8 GROUPE
      CHARACTER*(*) LIMANO(*)
      CHARACTER*8 LIMANZ
      CHARACTER*24 CHIN,NOMLIG
      CHARACTER*(*) CHINZ, NMLIGZ, GROUPZ, MODEZ
C ----------------------------------------------------------------------
C     ENTREES:
C     CHINZ : NOM DE LA CARTE A ENRICHIR
C     CODE : 1: 'TOUT' LES MAILLES DU MAILLAGE.
C           -1: 'TOUT' LES MAILLES SUPPL. D'1 LIGREL.
C            2: 1 GROUPE NOMME DE MAILLES DU MAILLAGE.
C            3: 1 LISTE TARDIVE DE MAILLES DU MAILLAGE.
C           -3: 1 LISTE TARDIVE DE MAILLES TARDIVES D'1 LIGREL.
C     GROUPZ : NOM D' 1 GROUPE DE MAILLES DU MAILLAGE
C              ( UNIQUEMENT SI CODE= 2)
C     MODEZ : 'NOM' OU 'NUM' :
C             SI 'NOM' ON UTILISE LA LISTE LIMANO ( NOMS DES MAILLES)
C                 ( UNIQUEMENT SI CODE= 3)
C             SI 'NUM' ON UTILISE LA LISTE LIMANU ( NUMERO DES MAILLES)
C                 ( UNIQUEMENT SI CODE= 3 OU -3)
C     NMA  : NOMBRE DE MAILLES DANS LIMANO OU LIMANU
C                 ( UNIQUEMENT SI CODE= 3 OU -3)
C     LIMANO : NOMS DES MAILLES DU GROUPE_TARDIF (CODE=3)
C     LIMANU : NUMEROS DES MAILLES DU GROUPE_TARDIF (CODE=3 OU -3)
C     NMLIGZ : NOM DU LIGREL OU SONT EVENTUELLEMENT DEFINIES LES MAILLES
C         TARDIVES QUE L'ON VEUT AFFECTER.
C         NOMLIG EST NON_BLANC UNIQUEMENT SI CODE=-3 OU CODE=-1
C     NCMP : NOMBRE DE COMPOSANTES DECRITES
C            DANS CHIN.NCMP ET CHIN.VALV
C            -- REMARQUE : ON PEUT SUR-DIMENSIONNER NCMP A CONDITION
C                          QUE LA LISTE DES NOMS DE CMPS CONTIENNE DES
C                          "BLANCS". LES CMPS REELLEMENT NOTEES SONT
C                          LES COMPOSANTES NON-BLANCHES.
C
C     SORTIES:
C     ON ENRICHIT LE CONTENU DE LA CARTE CHIN
C
C ----------------------------------------------------------------------
C
C     FONCTIONS EXTERNES:
C     -------------------
      INTEGER NBEC
      CHARACTER*8 SCALAI
      CHARACTER*32 JEXNUM,JEXNOM
C
C     VARIABLES LOCALES:
C     ------------------
      INTEGER NEC,NEDIT,NGDMX,IADDG,GR,DIM,I,NUMERO,GD
      INTEGER DESC,LIMA
      CHARACTER*8 SCAL
      INTEGER NOMA,NOLI
      CHARACTER*8 MA
C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
C
C
      CALL JEMARQ()
      CHIN   = CHINZ
      NOMLIG = NMLIGZ
      GROUPE = GROUPZ
      MODE   = MODEZ
C
      CALL JEVEUO(CHIN(1:19)//'.NOMA','L',NOMA)
      MA = ZK8(NOMA-1+1)
C
      CALL JEVEUO(CHIN(1:19)//'.DESC','E',DESC)
      NGDMX = ZI(DESC-1+2)
      NEDIT = ZI(DESC-1+3) + 1
      IF (NEDIT.GT.NGDMX) THEN
         CALL UTMESS('F',' NOCART ','ERREUR PRGRMEUR DANS NOCART'//
     +               'LE NOMBRE D''APPELS A NOCART EST TROP GRAND')
      END IF
      ZI(DESC-1+3) = NEDIT
      CALL JEVEUO(CHIN(1:19)//'.NOLI','E',NOLI)
      IF ((CODE.EQ.-1) .OR. (CODE.EQ.-3)) THEN
         IF (NOMLIG(1:8).NE.'        ') THEN
            ZK24(NOLI-1+NEDIT) = NOMLIG
         ELSE
            CALL UTMESS('F',' NOCART ','1')
         END IF
      END IF
      GD = ZI(DESC-1+1)
      NGDMX = ZI(DESC-1+2)
      NEC = NBEC(GD)
C
C     APPEL A EDITGD QUI REMPLIT VALE ET MET A JOUR    LE DESC_GRANDEUR
C
      IADDG = 3 + 2*NGDMX + (NEDIT-1)*NEC + 1
      CALL EDITGD(CHIN,NCMP,GD,NEDIT,ZI(DESC-1+IADDG))

C
C     MISE A JOUR DE DESC ET LIMA:
C
      ZI(DESC-1+3+2*NEDIT-1) = CODE
      DIM = 0
      IF (ABS(CODE).EQ.1) THEN
C        -- ON NOTE LE NUMERO D'ENTITE CONVENTIONNEL RELATIF
C        -- A "TOUT":   9999
         ZI(DESC-1+3+2*NEDIT) = 9999
      ELSE IF (CODE.EQ.2) THEN
         CALL JENONU(JEXNOM(MA//'.GROUPEMA',GROUPE),GR)
         ZI(DESC-1+3+2*NEDIT) = GR
      ELSE IF (ABS(CODE).EQ.3) THEN
         ZI(DESC-1+3+2*NEDIT) = NEDIT
         DIM = NMA
      ELSE
         CALL UTMESS('F',' NOCART ','2')
      END IF
C
C     RECOPIE DANS LIMA LES NUMEROS DE MAILLES DU GROUPE TARDIF:
C
      IF (DIM.LT.0) THEN
         CALL UTMESS('F',' NOCART ','3')
      END IF
      CALL JECROC(JEXNUM(CHIN(1:19)//'.LIMA',NEDIT))
      IF (DIM.EQ.0) THEN
C --- ON CREE 1 OBJET DE DIMENSION 1 POUR NE PAS PLANTER JEVEUX???
         CALL JEECRA(JEXNUM(CHIN(1:19)//'.LIMA',NEDIT),'LONMAX',1,' ')
      ELSE
         CALL JEECRA(JEXNUM(CHIN(1:19)//'.LIMA',NEDIT),'LONMAX',DIM,' ')
      END IF
      CALL JEVEUO(JEXNUM(CHIN(1:19)//'.LIMA',NEDIT),'E',LIMA)
      DO 100 I = 1,DIM
         IF (MODE.EQ.'NUM') THEN
C             MAILLES NUMEROTEES ( DU MAILLAGE (>0) OU TARDIVES(<0) )
            ZI(LIMA-1+I) = LIMANU(I)
         ELSE IF (MODE.EQ.'NOM') THEN
C             MAILLES NOMMEES DU MAILLAGE
            LIMANZ = LIMANO(I)
            CALL JENONU(JEXNOM(MA//'.NOMMAI',LIMANZ),NUMERO)
            ZI(LIMA-1+I) = NUMERO
         ELSE
            CALL UTMESS('F',' NOCART ','4')
         END IF
  100 CONTINUE
  110 CONTINUE
      CALL JEDEMA()
      END
