      SUBROUTINE VRCREF(MODELE,CHMAT,CARELE,CHVREF)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 08/10/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8 MODELE,CHMAT,CARELE
      CHARACTER*19 CHVREF
C ======================================================================
C   BUT : FABRIQUER LE CHAMP DE VARIABLES DE COMMANDE DE "REFERENCE"
C   ARGUMENTS :
C   MODELE (K8)  IN/JXIN : SD MODELE
C   CHMAT  (K8)  IN/JXIN : SD CHAM_MATER
C   CARELE (K8)  IN/JXIN : SD CARA_ELEM (SOUS-POINTS)
C   CHVREF (K19) IN/JXOUT: SD CHAM_ELEM/ELGA (VARC DE "REFERENCE")
C ----------------------------------------------------------------------


      CHARACTER*8 MODELS,CHMATS,CARELS
      CHARACTER*19 CHVRES
      INTEGER N1,IAD,ISP,IPT,JCESV
      INTEGER K,K2,NBMA,NCMP,ICMP,JCESL1,JCESV1,JCESD1
      INTEGER JCESD,JCESL,IMA,NBPT,NBSP,NBCVRC,JCVVAR,IBID
      INTEGER JDCLD,JDCLL,JDCLV,NNCP,IRET,JCVRC
      CHARACTER*8 KBID,VARC,NOMA1,NOMA2
      CHARACTER*19  DCELI,CELMOD,CART1,CES1,LIGRMO,CSVREF
      CHARACTER*24 VALK(4)
      REAL*8 VALREF
      LOGICAL AVRC
      SAVE MODELS,CHMATS,CARELS,CHVRES
      DATA MODELS/' '/,CHMATS/' '/,CARELS/' '/,CHVRES/' '/
C ----------------------------------------------------------------------

      CALL JEMARQ()

C     -- SI LE CHAMP A PRODUIRE EXISTE DEJA ET QUE LES ARGUMENTS
C        SONT LES MEMES QUE LA FOIS PRECEDENTE, ON SORT RAPIDEMENT :
      CALL EXISD('CHAM_ELEM',CHVREF,IRET)
      IF (IRET.GT.0) THEN
        IF (MODELE.NE.MODELS) GOTO 5
        IF (CHMAT .NE.CHMATS) GOTO 5
        IF (CARELE.NE.CARELS) GOTO 5
        IF (CHVREF.NE.CHVRES) GOTO 5
        GOTO 9999
      ENDIF
5     CONTINUE
C     -- SAUVERGARDE DES ARGUMENTS POUR LE PROCHAIN APPEL
      MODELS=MODELE
      CHMATS=CHMAT
      CARELS=CARELE
      CHVRES=CHVREF


C     -- ON SE PROTEGE DES MODELES QUI NE CONNAISSENT PAS LES VRC :
      CELMOD='&&VRCREF.CELMOD'
      LIGRMO=MODELE//'.MODELE'
      CALL JEEXIN(LIGRMO//'.LIEL',IRET)
      IF (IRET.EQ.0) GOTO 9999
      CALL ALCHML(LIGRMO,'INIT_VARC','PVARCPR','V',CELMOD,IRET,' ')
      CALL DETRSD('CHAMP',CELMOD)
      IF (IRET.EQ.1) GOTO 9999


      CALL JEEXIN(CHMAT//'.CVRCVARC',IRET)
C     AVRC : .TRUE. SI AFFE_MATERIAU/AFFE_VARC EST UTILISE
      AVRC=.FALSE.
      IF (IRET.GT.0) THEN
        CALL JEVEUO(CHMAT//'.CVRCVARC','L',JCVRC)
        CALL JELIRA(CHMAT//'.CVRCVARC','LONMAX',NBCVRC,KBID)
        DO 10 K=1,NBCVRC
          IF (ZK8(JCVRC-1+K).NE.' ') THEN
            AVRC=.TRUE.
            GOTO 11
          ENDIF
10     CONTINUE

11     CONTINUE
      ENDIF



C     -- CAS : PAS DE AFFE_VARC :
C     ------------------------------------------
      IF (.NOT.AVRC) GOTO 9999

C     -- CAS AFFE_VARC  :
C     ------------------------
      CALL JEVEUO(CHMAT//'.CVRCVARC','L',JCVVAR)
      CALL JELIRA(CHMAT//'.CVRCVARC','LONMAX',NBCVRC,KBID)


C     0. VERIFICATION DE LA COHERENCE DE MODELE  ET CHMAT (FICHE 19507)
C     ------------------------------------------------------------------
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE',IBID,NOMA1,IBID)
      CALL DISMOI('F','NOM_MAILLA',CHMAT//'.CHAMP_MAT','CHAMP',IBID,
     &            NOMA2,IBID)
      IF (NOMA1.NE.NOMA2) THEN
        VALK(1)=NOMA1
        VALK(2)=NOMA2
        CALL U2MESK('F','CALCULEL4_23',2,VALK)
      ENDIF


C     1. ALLOCATION DE CSVREF
C     ------------------------------------------
      DCELI='&&VRCREF.DCELI'
      CSVREF='&&VRCREF.CSVREF'
      CALL CESVAR(CARELE,' ',LIGRMO,DCELI)

C     -- MODIFICATION DE DCELI : TOUTES LES MAILLES ONT
C        NBCVRC COMPOSANTES.
      CALL JEVEUO(DCELI//'.CESD','L',JDCLD)
      CALL JEVEUO(DCELI//'.CESL','L',JDCLL)
      CALL JEVEUO(DCELI//'.CESV','E',JDCLV)
      NBMA = ZI(JDCLD-1+1)

      DO 170,IMA = 1,NBMA
        NBPT = ZI(JDCLD-1+5+4* (IMA-1)+1)
        NBSP = MAX(1,ZI(JDCLD-1+5+4* (IMA-1)+2))
        CALL ASSERT(NBPT.EQ.1)
        CALL ASSERT(NBSP.EQ.1)
        CALL CESEXI('C',JDCLD,JDCLL,IMA,1,1,2,IAD)
        IF (IAD.GT.0) ZI(JDCLV-1+IAD)=NBCVRC
170   CONTINUE

      CALL ALCHML(LIGRMO,'INIT_VARC','PVARCPR','V',CELMOD,IRET,DCELI)
      CALL ASSERT(IRET.EQ.0)
      CALL DETRSD('CHAMP',DCELI)
      CALL CELCES(CELMOD,'V',CSVREF)
      CALL DETRSD('CHAMP',CELMOD)

      CALL JELIRA(CSVREF//'.CESV','LONMAX',N1,KBID)

      CALL JEVEUO(CSVREF//'.CESD','L',JCESD)
      CALL JEVEUO(CSVREF//'.CESL','E',JCESL)
      CALL JEVEUO(CSVREF//'.CESV','E',JCESV)
      CALL JELIRA(CSVREF//'.CESL','LONMAX',N1,KBID)
      DO 777, K=1,N1
        ZL(JCESL-1+K)=.FALSE.
777   CONTINUE



C     2. REMPLISSAGE DE CSVREF.CESV :
C     ------------------------------------------
      VARC=' '
      DO 1, K=1,NBCVRC
        IF (ZK8(JCVVAR-1+K).EQ.VARC) GO TO 1
        VARC=ZK8(JCVVAR-1+K)
        CART1 = CHMAT//'.'//VARC//'.1'
        CES1='&&VRCREF.CES1'
        CALL  CARCES(CART1,'ELEM',' ','V',CES1,'A',IRET)
        CALL ASSERT(IRET.EQ.0)

        CALL JEVEUO(CES1//'.CESD','L',JCESD1)
        CALL JEVEUO(CES1//'.CESV','L',JCESV1)
        CALL JEVEUO(CES1//'.CESL','L',JCESL1)

        NBMA = ZI(JCESD-1+1)
        CALL ASSERT(NBMA.EQ.ZI(JCESD1-1+1))

C       -- CALCUL DE NCMP
        NCMP=0
        DO 69, K2=K,NBCVRC
          IF (ZK8(JCVVAR-1+K2).EQ.VARC) NCMP=NCMP+1
69      CONTINUE

        DO 70,IMA = 1,NBMA
          NBPT = ZI(JCESD-1+5+4* (IMA-1)+1)
          NBSP = MAX(1,ZI(JCESD-1+5+4* (IMA-1)+2))

          CALL CESEXI('C',JCESD1,JCESL1,IMA,1,1,1,IAD)
          IF (IAD.LE.0) GO TO 70
          VALREF=ZR(JCESV1-1+IAD)

          DO 60,IPT = 1,NBPT
            DO 50,ISP = 1,NBSP
              DO 51,ICMP = 1,NCMP
                CALL CESEXI('C',JCESD,JCESL,IMA,IPT,ISP,K-1+ICMP,IAD)
                CALL ASSERT(IAD.LE.0)
                IF (IAD.EQ.0) GOTO 51
                IAD=-IAD
                ZL(JCESL-1+IAD)=.TRUE.
                ZR(JCESV-1+IAD)=VALREF
51          CONTINUE
50          CONTINUE
60        CONTINUE
70      CONTINUE
        CALL DETRSD('CHAMP',CES1)
1     CONTINUE


C     3. RECOPIE DU CHAMP SIMPLE DANS LE CHAMP CHVREF
C     -----------------------------------------------------

C     LE CHAMP DE TEMP_REF PEUT CONTENIR DES VALEURS "VIDES"
C     ON LES TRANSFORME EN "NAN"
      CALL JUVINN(CSVREF//'.CESV')
      CALL CESCEL(CSVREF,LIGRMO,'INIT_VARC','PVARCPR','NAN',NNCP,'V',
     &            CHVREF,'F',IBID)
      CALL DETRSD('CHAM_ELEM_S',CSVREF)

9999  CONTINUE
      CALL JEDEMA()
      END
