      SUBROUTINE RESGRA(MAT   ,MATF  ,VCINE ,NITER ,EPSI  ,
     &                  CRITER,NSECM ,RSOLU ,SOLVEU,ISTOP ,
     &                  IRET  )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 02/10/2012   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_4
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*(*) MAT   ,MATF  ,VCINE
      INTEGER       NITER ,NSECM
      REAL*8        EPSI  ,RSOLU(*)
      CHARACTER*19  CRITER,SOLVEU
      INTEGER       ISTOP ,IRET
C----------------------------------------------------------------------
C     ROUTINE DE HAUT NIVEAU DE RESOLUTION PAR UNE METHODE DE GRADIENT
C     CONJUGUE (GCPC)
C----------------------------------------------------------------------
C IN/JXIN  K19 MAT    : MATR_ASSE PREMIER MEMBRE DU SYSTEME LINEAIRE
C IN/JXIN  K19 MATF   : MATR_ASSE DE PRECONDITIONNEMENT
C IN/JXIN  K*  VCINE  : CHAMP ASSOCIE AUX CHARGES CINEMATIQUES (OU ' ')
C IN       I   NITER  : NOMBRE MAXIMUM D'ITERATIONS
C IN       R   EPSI   : PARAMETRE D'ERREUR
C IN/JXOUT K19 CRITER : SD_CRITER (CRITERES DE CONVERGENCE)
C IN       I   NSECM  : NOMBRE DE SECONDS MEMBRES
C IN/OUT   R   RSOLU(*,NSECM)  :
C        EN ENTREE : VECTEUR DE REELS CONTENANT LES SECONDS MEMBRES
C        EN SORTIE : VECTEUR DE REELS CONTENANT LES SOLUTIONS
C IN       K19 SOLVEU : SD_SOLVEUR
C IN       I   ISTOP  : COMPORTEMENT EN CAS D'ERREUR
C OUT      I   IRET   : CODE RETOUR
C----------------------------------------------------------------------
C----------------------------------------------------------------------
      COMPLEX*16 CBID
C----------------------------------------------------------------------
C     VARIABLES LOCALES
C----------------------------------------------------------------------
      CHARACTER*19 KSTOC,KSTOCF
      CHARACTER*19 VCIN19,MATAS,MATFAC,SMBR
      CHARACTER*4 TYPE
      CHARACTER*24 PRECON
      INTEGER IFM,NIV,IER,IDVALC,IDIN,IDIP,JSMDE,NEQ,NBLC,ISLVK
      INTEGER IBID,IDAC,IDINPC,IDIPPC,IDACPC,IDW1,IDW2,IDW3
      INTEGER JREFA,JREFAF,K,LMAT,KDEB,IEQ,IDW4,ISMBR

C----------------------------------------------------------------------
C     DEBUT
      CALL JEMARQ()

      CALL INFNIV(IFM,NIV)

C     0- INITIALISATIONS :
C     -----------------------------------
      MATAS=MAT
      MATFAC=MATF

      CALL JEVEUO(SOLVEU//'.SLVK','L',ISLVK)
      PRECON=ZK24(ISLVK-1+2)


C     1- MATRICE :
C     -----------------------------------
      CALL MTDSCR(MATAS)
      CALL JEVEUO(MATAS//'.&INT','L',LMAT)
      NEQ=ZI(LMAT+2)


C     3- SI CHARGE CINEMATIQUE :
C     -----------------------------
      IF (VCINE.NE.' ') THEN
        VCIN19=VCINE
        CALL JEEXIN(VCIN19//'.VALE',IER)
        IF (IER.EQ.0) CALL U2MESK('F','ALGELINE3_34',1,VCIN19)
        CALL JEVEUO(VCIN19//'.VALE','L',IDVALC)
        DO 10,K=1,NSECM
          KDEB=(K-1)*NEQ+1
          CALL CSMBGG(LMAT,RSOLU(KDEB),ZR(IDVALC),CBID,CBID,'R')
   10   CONTINUE
      ENDIF


C     4- MISE A L'ECHELLE DES "LAGR" DANS LE SECOND MEMBRE :
C     ------------------------------------------------------
      CALL MRCONL('MULT',LMAT,0,'R',RSOLU,NSECM)


C     5- RECUPERATION DE LA MATRICE ASSEMBLEE :
C     ------------------------------------------------
      CALL JEVEUO(MATAS//'.REFA','L',JREFA)
      KSTOC=ZK24(JREFA-1+2)(1:14)//'.SMOS'
      CALL JEEXIN(KSTOC//'.SMDI',IER)
      IF (IER.EQ.0) CALL U2MESK('F','ALGELINE3_21',1,MATAS)
      CALL JEVEUO(KSTOC//'.SMDI','L',IDIN)
      CALL JEVEUO(KSTOC//'.SMHC','L',IDIP)
      CALL JEVEUO(KSTOC//'.SMDE','L',JSMDE)
      NEQ=ZI(JSMDE-1+1)
      IF (NITER.EQ.0)NITER=NEQ/2
      NBLC=ZI(JSMDE-1+3)
      IF (NBLC.NE.1) CALL U2MESS('F','ALGELINE3_22')
      CALL JELIRA(JEXNUM(MATAS//'.VALM',1),'TYPE',IBID,TYPE)
      IF (TYPE.NE.'R') CALL U2MESS('F','ALGELINE3_37')

      CALL JEVEUO(JEXNUM(MATAS//'.VALM',1),'L',IDAC)


C     6- RECUPERATION DE LA MATRICE DE PRECONDITIONNEMENT:
C     -----------------------------------------------------
      IF (PRECON(1:8).EQ.'LDLT_INC') THEN
        CALL JEEXIN(MATFAC//'.REFA',IER)
        IF (IER.EQ.0) CALL U2MESS('F','ALGELINE3_38')

        CALL JEVEUO(MATFAC//'.REFA','L',JREFAF)
        KSTOCF=ZK24(JREFAF-1+2)(1:14)//'.SMOS'
        CALL JEVEUO(KSTOCF//'.SMDI','L',IDINPC)
        CALL JEVEUO(KSTOCF//'.SMHC','L',IDIPPC)
        CALL JEVEUO(JEXNUM(MATFAC//'.VALM',1),'L',IDACPC)
      ENDIF


C     7- CREATION DE 3 VECTEURS DE TRAVAIL
C     ------------------------------------------------
      CALL WKVECT('&&RESGRA.W1','V V R',NEQ,IDW1)
      CALL WKVECT('&&RESGRA.W2','V V R',NEQ,IDW2)
      CALL WKVECT('&&RESGRA.W3','V V R',NEQ,IDW3)



C     9- RESOLUTION EFFECTIVE ---
C     ---------------------------------
      DO 30,K=1,NSECM
        CALL WKVECT('&&RESGRA.W4','V V R',NEQ,IDW4)

C        ---- SOLUTION POUR MUMPS
        SMBR='&&RESGRA.SMBR      '
        CALL WKVECT(SMBR//'.VALE','V V R',NEQ,ISMBR)

        KDEB=(K-1)*NEQ+1
        CALL GCPC(NEQ       ,ZI(IDIN)  ,ZI4(IDIP)  ,ZR(IDAC),ZI(IDINPC),
     &           ZI4(IDIPPC),ZR(IDACPC),RSOLU(KDEB),ZR(IDW4),ZR(IDW1)  ,
     &           ZR(IDW2)   ,ZR(IDW3)  ,0          ,NITER   ,EPSI      ,
     &           CRITER     ,SOLVEU    ,MATAS      ,SMBR    ,ISTOP     ,
     &           IRET       )
        DO 20,IEQ=1,NEQ
          RSOLU(KDEB-1+IEQ)=ZR(IDW4-1+IEQ)
   20   CONTINUE
        CALL JEDETR('&&RESGRA.W4')
        CALL JEDETR(SMBR//'.VALE')
   30 CONTINUE


C     10- MISE A L'ECHELLE DES LAGRANGES DANS LA SOLUTION :
C     -----------------------------------------------------
      CALL MRCONL('MULT',LMAT,0,'R',RSOLU,NSECM)


      CALL JEDETR('&&RESGRA.W1')
      CALL JEDETR('&&RESGRA.W2')
      CALL JEDETR('&&RESGRA.W3')

      CALL JEDEMA()
      END
