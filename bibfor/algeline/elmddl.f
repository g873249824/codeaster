      SUBROUTINE ELMDDL(RAIDE ,NEQ   ,DDL   ,NDDLE ,NBDDL ,
     &                  VECDDL)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 04/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRS_1404
C
      IMPLICIT NONE
      CHARACTER*19 RAIDE
      INTEGER      NEQ,NBDDL,VECDDL(NEQ),NDDLE
      CHARACTER*8  DDL(NDDLE)
C
C ----------------------------------------------------------------------
C
C CONSTRUCTION D'UN TABLEAU D'ENTIERS REPERANT LA POSITION DES DDL
C EXCLUS DE LA RECHERCHE DE VALEURS PROPRES
C
C ----------------------------------------------------------------------
C
C IN  RAIDEUR : NOM DE LA MATRICE DE "RAIDEUR"
C IN  NEQ     : NPMBRE DE DDL
C IN  DDL     : NOM DU DDL A ELIMINER
C IN  NDDLE   : NOMBRE DE TYPES DE DDL EXCLUS
C OUT NBDDL   : NOMBRE DE DDL A ELIMINER
C OUT VECDDL  : POSITION DES DDL A ELIMINER
C
C ----------------------------------------------------------------------
C
      INTEGER      IBID,IERD,IEQ,IFM,NIV,I,INTER(NEQ)
      CHARACTER*14 NUME
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)
C
C --- INITIALISATIONS
C
      NBDDL = 0
      CALL DISMOI('F','NOM_NUME_DDL',RAIDE,'MATR_ASSE',IBID,NUME,IERD)
      DO 5 IEQ = 1,NEQ
        VECDDL(IEQ) = 1
   5  CONTINUE    
C
C --- CALCUL DU NOMBRE DE DDL A ELIMINER
C
      IF (NDDLE.GT.0) THEN
        DO 10 I = 1,NDDLE
C
C ------- RECUPERATION DES POSITIONS DES DDL
C
          CALL PTEDDL('NUME_DDL',NUME,1,DDL(I),NEQ,INTER)
C
C ------- CALCUL DU NOMBRE DE 'DDL': NBDDL
C
          DO 20 IEQ = 1,NEQ
            NBDDL = NBDDL + INTER(IEQ)
  20      CONTINUE
C
C ------- STOP SI ON CHERCHE A ELIM UN DDL ABSENT DE LA MODELISATION
C
          IF (NBDDL.EQ.0) THEN
            CALL ASSERT(.FALSE.)
          ENDIF
C
C ------- INVERSION : INTER = 0 SI DDL TROUVE ET 1 SINON
C
          DO 30 IEQ = 1,NEQ
            INTER(IEQ) = ABS(INTER(IEQ)-1)
  30      CONTINUE
C
          DO 40 IEQ = 1,NEQ
            VECDDL(IEQ) = VECDDL(IEQ)*INTER(IEQ)
  40      CONTINUE
C
  10    CONTINUE
      ENDIF
C
C --- IMPRESSION DES DDL
C
      IF (NIV.GE.1) THEN
        IF (NBDDL.GT.0) THEN
          WRITE (IFM,9060)
          WRITE (IFM,9050) NBDDL
          WRITE (IFM,9000) 
          DO 6 I = 1,NDDLE
            WRITE (IFM,9010) DDL(I)
   6      CONTINUE 
          WRITE (IFM,9060)
        ELSE
          WRITE (IFM,9001) 
        ENDIF  
      ENDIF
C
C ----------------------------------------------------------------------
C
      CALL JEDEMA()
C
 9000 FORMAT ('DDL_EXCLUS:')
 9001 FORMAT ('PAS DE DDL_EXCLUS')
 9010 FORMAT (12X,A8,/)
 9050 FORMAT ('NOMBRE DE DDL_EXCLUS:',10X,I7,/)
 9060 FORMAT (72('-'))
C
      END
