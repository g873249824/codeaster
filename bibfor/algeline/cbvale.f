      SUBROUTINE CBVALE(NBCOMB,TYPCST,CONST,TYPMAT,LMAT,TYPRES,LRES,
     &                  DDLEXC)
      IMPLICIT REAL*8 (A-H,O-Z)
      INTEGER NBCOMB,LMAT(*),LRES
      CHARACTER*(*) TYPCST(*),TYPMAT(*),TYPRES,DDLEXC
      REAL*8 CONST(*)
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 28/02/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     COMBINAISON LINEAIRE DES .VALM DES MATRICES
C       *  LES MATRICES SONT SUPPOSEES ETRE DE MEME STOCKAGE
C          MAIS PEUVENT ETRE A ELEMENTS REELS OU COMPLEXES
C       *  LES SCALAIRES SONT REELS OU COMPLEXES
C     -----------------------------------------------------------------
C IN  I  NBCOMB = NOMBRE DE MATRICES A COMBINER
C IN  K* TYPCST = TYPE DES CONSTANTES (R OU C OU I)
C IN  R  CONST  = TABLEAU DE R*8    DES COEFICIENTS
C IN  K* TYPMAT = TYPE DES MATRICES   (R OU C)
C IN  I  LMAT = TABLEAU DES POINTEURS DES MATRICES
C IN  K* TYPRES = TYPE DES MATRICES   (R OU C)
C IN  I  LRES = POINTEUR DE MATRICE RESULTAT
C IN  K* DDLEXC = NOM DES DDLS A EXCLURE (CONCRETEMENT IL S'AGIT
C                                         DES LAGRANGE)

C     -----------------------------------------------------------------

C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

C     -----------------------------------------------------------------
C     LGBLOC = LONGUEUR DES BLOCS
      INTEGER LGBLOC,TYPSYM,MATSYM,HCOL
C     -----------------------------------------------------------------
      CHARACTER*1 CH1,CLAS
      CHARACTER*4 CBID
      CHARACTER*19 MATRES,MATI,MATSTO
      CHARACTER*24 VALE,VALER
      CHARACTER*8 NOMDDL
      CHARACTER*14 NUME
      CHARACTER*19 NOMA
      CHARACTER*24 MAT1

      REAL*8 ZERO
      COMPLEX*16 CZERO,C8CST
C     -----------------------------------------------------------------
      CALL JEMARQ()
      ZERO = 0.D0
      CZERO = DCMPLX(ZERO,ZERO)


      NOMDDL = DDLEXC
      MATRES = ZK24(ZI(LRES+1))
      NEQ = ZI(LRES+2)
      CALL JELIRA(MATRES//'.REFA','CLAS',IBID,CLAS)
      VALER = MATRES//'.VALM'
      LGBLOC = ZI(LRES+14)


      MAT1 = ZK24(ZI(LMAT(1)+1))
      NOMA = MAT1
      MXDDL = 1

C     I) RECUPERATION DU NOM DE LA NUMEROTATION ASSOCIEE AUX MATRICES
      CALL DISMOI('F','NOM_NUME_DDL',NOMA,'MATR_ASSE',IBID,NUME,IERD)

C     II) RECUPERATION DES POSITIONS DES DDL
      CALL WKVECT('&&CBVALE','V V I',NEQ*MXDDL,LDDL)
      CALL PTEDDL('NUME_DDL',NUME,MXDDL,NOMDDL,NEQ,ZI(LDDL))

      TYPSYM = ZI(LRES+4)
      CALL ASSERT(TYPSYM.EQ.0 .OR. TYPSYM.EQ.1)
C     TYPSYM=0 -> NON SYMETRIQUE ; TYPSYM=1 ->  SYMETRIQUE

C --- AFFECTATION DE L'ETAT DE LA PREMIERE MATRICE COMBINER
C --- A LA MATRICE RESULTAT

      CALL MTDSC2(ZK24(ZI(LRES+1)),'SMDI','L',JSMDI)
      CALL JEVEUO(ZK24(ZI(LRES+1))(1:19)//'.REFA','L',JREFA)
      CALL JEVEUO(ZK24(JREFA-1+2)(1:14)//'.SMOS.SMHC','L',JSMHC)

C --- BOUCLE SUR LES BLOCS DE LA MATRICE RESULTAT


        CALL JEVEUO(JEXNUM(VALER,1),'E',IATRES)
        IF (TYPSYM.EQ.0) THEN
          CALL JEVEUO(JEXNUM(VALER,2),'E',IATREI)
        END IF
        IF (TYPRES(1:1).EQ.'R') THEN
          DO 10 IVAL = IATRES,IATRES + LGBLOC - 1
            ZR(IVAL) = ZERO
   10     CONTINUE

          IF (TYPSYM.EQ.0) THEN
            DO 20 IVAL = IATREI,IATREI + LGBLOC - 1
              ZR(IVAL) = ZERO
   20       CONTINUE
          END IF
        ELSE IF (TYPRES(1:1).EQ.'C') THEN
          DO 30 IVAL = IATRES,IATRES + LGBLOC - 1
            ZC(IVAL) = CZERO
   30     CONTINUE

        ELSE
          CH1 = TYPRES(1:1)
          CALL UTMESS('F','CBVALE_1','TYPE DE MATRICE RESULTAT "'//CH1//
     &                '" INCONNU.')
        END IF

C --- BOUCLE SUR LES MATRICES A COMBINER ---
        ICONST = 1
        DO 40 IMAT = 1,NBCOMB

C ---    NOM DE LA MATRICE A COMBINER

          MATI = ZK24(ZI(LMAT(IMAT)+1))
          VALE = MATI//'.VALM'
          CALL JEVEUO(JEXNUM(VALE,1),'L',IATMAT)
          MATSYM = ZI(LMAT(IMAT)+4)
          IF (MATSYM.EQ.0) THEN
            CALL JEVEUO(JEXNUM(VALE,2),'E',IATMAI)
          END IF

C --- LA MATRICE RESULTAT EST REELLE

          IF (TYPRES(1:1).EQ.'R') THEN

C ---          LA MATRICE A COMBINER EST REELLE

            IF (TYPMAT(IMAT) (1:1).EQ.'R') THEN

C ---              CAS D'UN MATRICE SYMETRIQUE

                IF (TYPSYM.EQ.1) THEN
                  CALL RRRSSM(NEQ,ZI(JSMHC),ZI(JSMDI),
     &                        ZI(LDDL),CONST(ICONST),ZR(IATMAT),
     &                        ZR(IATRES))

C ----             CAS D'UN MATRICE NON-SYMETRIQUE

                ELSE IF (TYPSYM.EQ.0) THEN
                  IF (MATSYM.EQ.1) THEN
                    CALL RRRNSM(NEQ,ZI(JSMHC),
     &                          ZI(JSMDI),ZI(LDDL),CONST(ICONST),
     &                          ZR(IATMAT),ZR(IATRES),ZR(IATREI))
                  ELSE IF (MATSYM.EQ.0) THEN
                    CALL RRRNNM(NEQ,ZI(JSMHC),
     &                          ZI(JSMDI),ZI(LDDL),CONST(ICONST),
     &                          ZR(IATMAT),ZR(IATMAI),ZR(IATRES),
     &                          ZR(IATREI))
                  END IF
                END IF


C ---      CAS OU LA MATRICE RESULTANTE EST REELLE ET LA MATRICE
C ---      A COMBINER EST COMPLEXE

            ELSE IF (TYPMAT(IMAT) (1:1).EQ.'C') THEN

C ---        LE COEFFICIENT MULTIPLICATEUR EST REEL

              IF (TYPCST(IMAT) (1:1).EQ.'R') THEN
                  CALL RCRSSM(NEQ,ZI(JSMHC),ZI(JSMDI),
     &                        ZI(LDDL),CONST(ICONST),ZC(IATMAT),
     &                        ZR(IATRES))

C ---        LE COEFFICIENT MULTIPLICATEUR EST IMAGINAIRE PUR

              ELSE IF (TYPCST(IMAT) (1:1).EQ.'I') THEN
                  CALL RCISSM(NEQ,ZI(JSMHC),ZI(JSMDI),
     &                        ZI(LDDL),CONST(ICONST),ZC(IATMAT),
     &                        ZR(IATRES))

C ---          LE COEFFICIENT MULTIPLICATEUR EST DE TYPE INCONNU

              ELSE
                CH1 = TYPCST(IMAT) (1:1)
                CALL UTMESS('F','CBVALE_2','TYPE INCONNU: '//CH1)
              END IF

C ---       LA MATRICE A COMBINER EST DE TYPE INCONNU

            ELSE
              CH1 = TYPMAT(IMAT) (1:1)
              CALL UTMESS('F','CBVALE_3','TYPE INCONNU: '//CH1)
            END IF

C --- LA MATRICE RESULTAT EST COMPLEXE

          ELSE IF (TYPRES(1:1).EQ.'C') THEN

C ---        LA MATRICE A COMBINER EST REELLE

            IF (TYPMAT(IMAT) (1:1).EQ.'R') THEN

C ---          LE COEFFICIENT MULTIPLICATEUR EST REEL

              IF (TYPCST(IMAT) (1:1).EQ.'R') THEN
                  CALL CRRSSM(NEQ,ZI(JSMHC),ZI(JSMDI),
     &                        ZI(LDDL),CONST(ICONST),ZR(IATMAT),
     &                        ZC(IATRES))

C ---          LE COEFFICIENT MULTIPLICATEUR EST COMPLEXE

              ELSE IF (TYPCST(IMAT) (1:1).EQ.'C') THEN
                C8CST = DCMPLX(CONST(ICONST),CONST(ICONST+1))
                  CALL CRCSSM(NEQ,ZI(JSMHC),ZI(JSMDI),
     &                        ZI(LDDL),C8CST,ZR(IATMAT),ZC(IATRES))

C ---          LE COEFFICIENT MULTIPLICATEUR EST DE TYPE INCONNU

              ELSE
                CH1 = TYPCST(IMAT) (1:1)
                CALL UTMESS('F','CBVALE_4','TYPE INCONNU: '//CH1)
              END IF

C ---        LA MATRICE A COMBINER EST COMPLEXE

            ELSE IF (TYPMAT(IMAT) (1:1).EQ.'C') THEN

C ---          LE COEFFICIENT MULTIPLICATEUR EST REEL

              IF (TYPCST(IMAT) (1:1).EQ.'R') THEN
                  CALL CCRSSM(NEQ,ZI(JSMHC),ZI(JSMDI),
     &                        ZI(LDDL),CONST(ICONST),ZC(IATMAT),
     &                        ZC(IATRES))

C ---          LE COEFFICIENT MULTIPLICATEUR EST REEL

              ELSE IF (TYPCST(IMAT) (:1).EQ.'C') THEN
                C8CST = DCMPLX(CONST(ICONST),CONST(ICONST+1))
                  CALL CCCSSM(NEQ,ZI(JSMHC),ZI(JSMDI),
     &                        ZI(LDDL),C8CST,ZC(IATMAT),ZC(IATRES))

C ---          LE COEFFICIENT MULTIPLICATEUR EST DE TYPE INCONNU

              ELSE
                CH1 = TYPCST(IMAT) (1:1)
                CALL UTMESS('F','CBVALE_5','TYPE INCONNU: '//CH1)
              END IF

C ---        LA MATRICE A COMBINER EST DE TYPE INCONNU

            ELSE
              CH1 = TYPMAT(IMAT) (1:1)
              CALL UTMESS('F','CBVALE_6','TYPE INCONNU: '//CH1)
            END IF
          END IF
          ICONST = ICONST + 1
          IF (TYPCST(IMAT) (1:1).EQ.'C') ICONST = ICONST + 1
          CALL JELIBE(JEXNUM(VALE,1))
          IF (MATSYM.EQ.0) THEN
            CALL JELIBE(JEXNUM(VALE,2))
          END IF
C ---   FIN DE LA BOUCLE SUR LES MATRICES A COMBINER
   40   CONTINUE
        CALL JELIBE(JEXNUM(VALER,1))
        IF (TYPSYM.EQ.0) THEN
          CALL JELIBE(JEXNUM(VALER,2))
        END IF
C --- FIN DE LA BOUCLE SUR LES BLOCS DE LA MATRICE RESULTAT


      CALL JEDETR('&&CBVALE')


      CALL JEDEMA()
      END
