      SUBROUTINE CMREST(NOMAIN,NOMAOU)
      IMPLICIT NONE
      CHARACTER*8 NOMAIN,NOMAOU
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 03/03/2008   AUTEUR FLEJOU J-L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C     OPERATEUR CREA_MAILLAGE
C     MOT-CLE: RESTREINT
C
C     BUT: RESTREINDRE UN MAILLAGE A DES GROUPES DE MAILLES/NOEUDS
C
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32,JEXATR
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER NBMAOU,NBNOIN,IRET,JNUMA,JWK1,JCONX1,JCONX2,IMA,NUMA,NBNO
      INTEGER INO,NUNO,JDIM,ITYPOU,ITYPIN,JADIN,JADOU,IBID,JCORIN,JCOROU
      INTEGER IAD,NTGEO,NBNOOU,NBNOMX,JWK2,NBGMA,JGMA,IGM,NBMA,NBMAIN
      INTEGER JWK3,NBGMIN,JGMANV,NBGMNV,K,INDIIS,JNMPG,NMPG,NBGNO
      INTEGER JGNO,NBGNIN,JGNONV,JNNPG,NBGNNV,IGN,NNPG,JNUGN,NUMGNO
      CHARACTER*4 DOCU
      CHARACTER*8 K8B,NOMMA,NOMNO,NOMGMA,TTGRMA,TTGRNO,NOMGNO
      CHARACTER*24 NUMIN,NOMMAI,NOMNOE,GRPNOE,COOVAL,COOREF,COODSC
      CHARACTER*24 GRPMAI,CONNEX,TYPMAI,DIMIN,DIMOU,NUNIN
      LOGICAL PREMS

      CALL JEMARQ()
C
C
C -1- PRELIMINAIRES
C     ============
C
C --- RECUPERATION DES MAILLES PRESENTES DANS RESTREINT/GROUP_MA :
      NUMIN  = '&&CMREST.NUM_MAIL_IN'
      CALL RELIEM ( ' ', NOMAIN, 'NU_MAILLE','RESTREINT', 1, 1,
     &             'GROUP_MA','GROUP_MA', NUMIN, NBMAOU )
      CALL JEVEUO(NUMIN,'L',JNUMA)

      CALL DISMOI('F','NB_NO_MAILLA',NOMAIN,'MAILLAGE',NBNOIN,K8B,IRET)
      CALL DISMOI('F','NB_MA_MAILLA',NOMAIN,'MAILLAGE',NBMAIN,K8B,IRET)

C --- CREATION DE TABLEAUX DE TRAVAIL:
C     ZI(JWK1) :
C     - DIMENSIONNE AU NOMBRE DE NOEUDS DU MAILLAGE IN
C     - CORRESPONDANCE : NUMEROS DES NOEUDS MAILLAGE IN => MAILLAGE OUT
C     - EX: ZI(JWK1+INO1-1)=INO2
C         -> SI INO2!=0:LE NOEUD INO1 DU MAILLAGE IN CORRESPOND AU NOEUD
C                       INO2 DU MAILLAGE OUT.
C         -> SI INO2=0: LE NOEUD INO1 DU MAILLAGE IN N'EST PAS PRESENT
C                       DANS LE MAILLAGE OUT.
      CALL WKVECT('&&CMREST_WORK_1','V V I',NBNOIN,JWK1)

C     ZI(JWK2) : (L'INVERSE DE ZI(JWK1))
C     - DIMENSIONNE AU NOMBRE DE NOEUDS DU MAILLAGE IN
C     - CORRESPONDANCE : NUMEROS DES NOEUDS MAILLAGE OUT => MAILLAGE IN
C     - EX: ZI(JWK1+INO1-1)=INO2
C        -> LE NOEUD INO1 DU MAILLAGE OUT CORRESPOND AU NOEUD
C           INO2 DU MAILLAGE IN.
      CALL WKVECT('&&CMREST_WORK_2','V V I',NBNOIN,JWK2)

C     ZI(JWK3) :
C     - DIMENSIONNE AU NOMBRE DE MAILLES DU MAILLAGE IN
C     - CORRESPONDANCE : NUMEROS DES MAILLES MAILLAGE IN => MAILLAGE OUT
C     - EX: ZI(JWK3+IMA1-1)=IMA2
C         -> SI IMA2!=0:LA MAILLE IMA1 DU MAILLAGE IN CORRESPOND A
C                       LA MAILLE IMA2 DU MAILLAGE OUT.
C         -> SI IMA2=0: LA MAILLE IMA1 DU MAILLAGE IN N'EST PAS PRESENTE
C                       DANS LE MAILLAGE OUT.
      CALL WKVECT('&&CMREST_WORK_3','V V I',NBMAIN,JWK3)

C --- INITIALISATIONS DES TABLEAUX DE TRAVAIL
      DO 10 INO=1,NBNOIN
         ZI(JWK1+INO-1)=0
         ZI(JWK2+INO-1)=0
10    CONTINUE
      DO 15 IMA=1,NBMAIN
         ZI(JWK3+IMA-1)=0
15    CONTINUE

C ---  REMPLISSAGE DES TABLEAUX DE TRAVAIL
      CALL JEVEUO(NOMAIN// '.CONNEX','L',JCONX1)
      CALL JEVEUO(JEXATR(NOMAIN//'.CONNEX','LONCUM'),'L',JCONX2)
      NBNOOU=0
      DO 20 IMA=1,NBMAOU
         NUMA=ZI(JNUMA+IMA-1)
         ZI(JWK3+NUMA-1)=IMA
         NBNO=ZI(JCONX2+NUMA)-ZI(JCONX2+NUMA-1)
         DO 21 INO=1,NBNO
            NUNO=ZI(JCONX1-1+ZI(JCONX2+NUMA-1)+INO-1)
            IF(ZI(JWK1+NUNO-1).EQ.0)THEN
               NBNOOU=NBNOOU+1
               ZI(JWK1+NUNO-1)=NBNOOU
               ZI(JWK2+NBNOOU-1)=NUNO
            ENDIF
21       CONTINUE
20    CONTINUE

C
C -2- CREATION DU NOUVEAU MAILLAGE
C     ============================
C
      NOMMAI  = NOMAOU// '.NOMMAI         '
      NOMNOE  = NOMAOU// '.NOMNOE         '
      GRPNOE  = NOMAOU// '.GROUPENO       '
      GRPMAI  = NOMAOU// '.GROUPEMA       '
      CONNEX  = NOMAOU// '.CONNEX         '
      TYPMAI  = NOMAOU// '.TYPMAIL        '
      COOVAL  = NOMAOU// '.COORDO    .VALE'
      COODSC  = NOMAOU// '.COORDO    .DESC'
      COOREF  = NOMAOU// '.COORDO    .REFE'

C --- OBJET .DIME
      DIMIN  = NOMAIN//'.DIME'
      DIMOU  = NOMAOU//'.DIME'
      CALL JEDUPO(DIMIN ,'G',DIMOU,.FALSE.)
      CALL JEVEUO(DIMOU ,'E',JDIM)
      ZI(JDIM-1 + 1) = NBNOOU
      ZI(JDIM-1 + 3) = NBMAOU

C --- OBJET .NOMMAI
      CALL JECREO(NOMMAI,'G N K8')
      CALL JEECRA(NOMMAI,'NOMMAX',NBMAOU,' ')
      DO 40 IMA=1,NBMAOU
         CALL JENUNO(JEXNUM(NOMAIN// '.NOMMAI',ZI(JNUMA+IMA-1)),NOMMA)
         CALL JECROC(JEXNOM(NOMMAI,NOMMA))
40    CONTINUE

C --- OBJET .TYPMAIL
      CALL WKVECT(TYPMAI,'G V I',NBMAOU,ITYPOU)
      CALL JEVEUO(NOMAIN//'.TYPMAIL','L',ITYPIN)
      DO 50 IMA = 1,NBMAOU
         ZI(ITYPOU-1+IMA) = ZI(ITYPIN-1+ZI(JNUMA+IMA-1))
50    CONTINUE

C --- OBJET .CONNEX
      CALL JECREC(CONNEX,'G V I','NU','CONTIG','VARIABLE',NBMAOU)
      CALL DISMOI('F','NB_NO_MAX','&CATA','CATALOGUE',NBNOMX,K8B,IRET)

      CALL JEECRA(CONNEX,'LONT',NBNOMX*NBMAOU,' ')
      DO 60 IMA = 1, NBMAOU
         CALL JELIRA(JEXNUM(NOMAIN//'.CONNEX',ZI(JNUMA+IMA-1)),
     &               'LONMAX',NBNO,K8B)
         CALL JEECRA(JEXNUM(CONNEX,IMA),'LONMAX',NBNO,K8B)
         CALL JEVEUO(JEXNUM(NOMAIN// '.CONNEX',ZI(JNUMA+IMA-1)),
     &               'L',JADIN)
         CALL JEVEUO(JEXNUM(CONNEX,IMA),'E',JADOU)
         DO 61 INO = 1, NBNO
            ZI(JADOU+INO-1) = ZI(JWK1+ZI(JADIN+INO-1)-1)
61       CONTINUE
60    CONTINUE

C --- OBJET .NOMNOE
      CALL JECREO(NOMNOE,'G N K8')
      CALL JEECRA(NOMNOE,'NOMMAX',NBNOOU,' ')
      DO 70 INO=1,NBNOOU
         CALL JENUNO(JEXNUM(NOMAIN// '.NOMNOE',ZI(JWK2+INO-1)),NOMNO)
         CALL JECROC(JEXNOM(NOMNOE,NOMNO))
70    CONTINUE

C --- OBJET .COORDO.VALE
      CALL JECREO(COOVAL,'G V R')
      CALL JEECRA(COOVAL,'LONMAX',NBNOOU*3,' ')
      CALL JELIRA(NOMAIN// '.COORDO    .VALE','DOCU',IBID,DOCU)
      CALL JEECRA(COOVAL,'DOCU',IBID,DOCU)
      CALL JEVEUO(NOMAIN// '.COORDO    .VALE','L',JCORIN)
      CALL JEVEUO(COOVAL,'E',JCOROU)
      DO 80 INO=1,NBNOIN
         IF(ZI(JWK1+INO-1).NE.0)THEN
            ZR(JCOROU+3*(ZI(JWK1+INO-1)-1)  )=ZR(JCORIN+3*(INO-1)  )
            ZR(JCOROU+3*(ZI(JWK1+INO-1)-1)+1)=ZR(JCORIN+3*(INO-1)+1)
            ZR(JCOROU+3*(ZI(JWK1+INO-1)-1)+2)=ZR(JCORIN+3*(INO-1)+2)
         ENDIF
80    CONTINUE

C --- OBJET COORDO.DESC
      CALL JECREO(COODSC,'G V I')
      CALL JEECRA(COODSC,'LONMAX',3,' ')
      CALL JEECRA(COODSC,'DOCU',0,'CHNO')
      CALL JEVEUO(COODSC,'E',IAD)
      CALL JENONU(JEXNOM('&CATA.GD.NOMGD','GEOM_R'),NTGEO)
      ZI(IAD)   =  NTGEO
      ZI(IAD+1) = -3
      ZI(IAD+2) = 14

C --- OBJET COORDO.REFE
      CALL WKVECT(COOREF,'G V K24',2,IAD)
      ZK24(IAD) = NOMAOU

C --- OBJET .GROUPEMA
      CALL GETVTX('RESTREINT','TOUT_GROUP_MA',1,1,1,TTGRMA,IRET)
      IF(TTGRMA.EQ.'NON')THEN
C       'TOUT_GROUP_MA'='NON'
         CALL GETVTX('RESTREINT','GROUP_MA',1,1,0,K8B,NBGMA)
         NBGMA=-NBGMA
         CALL WKVECT('&&CMREST_GRMA_FOURNIS','V V K8',NBGMA,JGMA)
         CALL GETVTX('RESTREINT','GROUP_MA',1,1,NBGMA,ZK8(JGMA),IRET)
         CALL JECREC(GRPMAI,'G V I','NO','DISPERSE','VARIABLE',NBGMA)
         DO 90 IGM = 1,NBGMA
            NOMGMA=ZK8(JGMA+IGM-1)
            CALL JECROC(JEXNOM(GRPMAI,NOMGMA))
            CALL JELIRA(JEXNOM(NOMAIN//'.GROUPEMA',NOMGMA),'LONMAX',
     &                  NBMA,K8B)
            CALL JEECRA(JEXNOM(GRPMAI,NOMGMA),'LONMAX',NBMA,K8B)
            CALL JEVEUO(JEXNOM(NOMAIN//'.GROUPEMA',NOMGMA),'L',JADIN)
            CALL JEVEUO(JEXNOM(GRPMAI,NOMGMA),'E',JADOU)
            DO 91 IMA = 1, NBMA
               ZI(JADOU+IMA-1) = ZI(JWK3+ZI(JADIN+IMA-1)-1)
91          CONTINUE
90       CONTINUE
      ELSE
C       TOUT_GROUP_MA='OUI'
         CALL JELIRA(NOMAIN//'.GROUPEMA','NOMUTI',NBGMIN,K8B)
         CALL WKVECT('&&CMREST_GRMA_NON_VIDES','V V I',NBGMIN,JGMANV)
         CALL WKVECT('&&CMREST_NB_MA_PAR_GRMA','V V I',NBGMIN,JNMPG)
         NBGMNV=0
         DO 100 IGM=1,NBGMIN
            CALL JEVEUO(JEXNUM(NOMAIN//'.GROUPEMA',IGM),'L',JADIN)
            CALL JELIRA(JEXNUM(NOMAIN//'.GROUPEMA',IGM),'LONMAX',
     &                  NBMA,K8B)
            NMPG=0
            PREMS=.TRUE.
            DO 101 IMA=1,NBMA
               IF(ZI(JWK3+ZI(JADIN+IMA-1)-1).NE.0)THEN
                  IF(PREMS)THEN
                     NBGMNV=NBGMNV+1
                     ZI(JGMANV+NBGMNV-1)=IGM
                     PREMS=.FALSE.
                  ENDIF
                  NMPG=NMPG+1
               ENDIF
101         CONTINUE
            IF( .NOT. PREMS) ZI(JNMPG+NBGMNV-1)=NMPG
100      CONTINUE
         CALL JECREC(GRPMAI,'G V I','NO','DISPERSE','VARIABLE',NBGMNV)
         DO 110 IGM=1,NBGMNV
            CALL JENUNO(JEXNUM(NOMAIN//'.GROUPEMA',ZI(JGMANV+IGM-1)),
     &                  NOMGMA)
            CALL JECROC(JEXNOM(GRPMAI,NOMGMA))
            CALL JELIRA(JEXNOM(NOMAIN//'.GROUPEMA',NOMGMA),'LONMAX',
     &                  NBMA,K8B)
            CALL JEECRA(JEXNOM(GRPMAI,NOMGMA),'LONMAX',
     &                  ZI(JNMPG+IGM-1),K8B)
            CALL JEVEUO(JEXNOM(NOMAIN//'.GROUPEMA',NOMGMA),'L',JADIN)
            CALL JEVEUO(JEXNOM(GRPMAI,NOMGMA),'E',JADOU)
            K=0
            DO 111 IMA = 1, NBMA
               IF(ZI(JWK3+ZI(JADIN+IMA-1)-1).NE.0)THEN
                  K=K+1
                  ZI(JADOU+K-1) = ZI(JWK3+ZI(JADIN+IMA-1)-1)
               ENDIF
111         CONTINUE
110      CONTINUE
      ENDIF

C --- OBJET .GROUPENO
      CALL GETVTX('RESTREINT','TOUT_GROUP_NO',1,1,1,TTGRNO,IRET)
      CALL GETVTX('RESTREINT','GROUP_NO',1,1,0,K8B,NBGNO)

      IF (NBGNO.NE.0)THEN
         NBGNO  = -NBGNO
         NUNIN  = '&&CMREST.GRP_NOEU_IN'
         CALL WKVECT(NUNIN,'V V K8',NBGNO,JNUGN)
         CALL GETVTX('RESTREINT','GROUP_NO',1,1,NBGNO,ZK8(JNUGN),IRET)
      ENDIF

C     SI 'TOUT_GROUP_NO'='NON' ET 'GROUP_NO' ABSENT => ON SORT
      IF(TTGRNO.EQ.'NON'.AND.NBGNO.EQ.0)GOTO 555

      IF(TTGRNO.EQ.'NON')THEN
C       'TOUT_GROUP_MA'='NON' ET 'GROUP_NO' PRESENT
         CALL WKVECT('&&CMREST_GRNO_NON_VIDES','V V I',NBNOOU,JGNONV)
         CALL WKVECT('&&CMREST_NB_NO_PAR_GRNO','V V I',NBNOOU,JNNPG)
         NBGNNV=0
         DO 200 IGN=1,NBGNO
            CALL JENONU(JEXNOM(NOMAIN//'.GROUPENO',ZK8(JNUGN+IGN-1)),
     &                  NUMGNO)
            CALL JEVEUO(JEXNOM(NOMAIN//'.GROUPENO',ZK8(JNUGN+IGN-1)),
     &                  'L',JADIN)
            CALL JELIRA(JEXNOM(NOMAIN//'.GROUPENO',ZK8(JNUGN+IGN-1)),
     &                  'LONMAX',NBNO,K8B)
            NNPG=0
            PREMS=.TRUE.
            DO 201 INO=1,NBNO
               IF(ZI(JWK1+ZI(JADIN+INO-1)-1).NE.0)THEN
                  IF(PREMS)THEN
                     NBGNNV=NBGNNV+1
                     ZI(JGNONV+NBGNNV-1)=NUMGNO
                     PREMS=.FALSE.
                  ENDIF
                  NNPG=NNPG+1
               ENDIF
201         CONTINUE
            IF(.NOT. PREMS) ZI(JNNPG+NBGNNV-1)=NNPG
200      CONTINUE

      ELSE
C       TOUT_GROUP_NO='OUI'
         CALL JELIRA(NOMAIN//'.GROUPENO','NOMUTI',NBGNIN,K8B)
         CALL WKVECT('&&CMREST_GRNO_NON_VIDES','V V I',NBGNIN,JGNONV)
         CALL WKVECT('&&CMREST_NB_NO_PAR_GRNO','V V I',NBGNIN,JNNPG)
         NBGNNV=0
         DO 210 IGN=1,NBGNIN
            CALL JEVEUO(JEXNUM(NOMAIN//'.GROUPENO',IGN),'L',JADIN)
            CALL JELIRA(JEXNUM(NOMAIN//'.GROUPENO',IGN),'LONMAX',
     &                  NBNO,K8B)
            NNPG=0
            PREMS=.TRUE.
            DO 211 INO=1,NBNO
               IF(ZI(JWK1+ZI(JADIN+INO-1)-1).NE.0)THEN
                  IF(PREMS)THEN
                     NBGNNV=NBGNNV+1
                     ZI(JGNONV+NBGNNV-1)=IGN
                     PREMS=.FALSE.
                  ENDIF
                  NNPG=NNPG+1
               ENDIF
211         CONTINUE
            IF(.NOT. PREMS)ZI(JNNPG+NBGNNV-1)=NNPG
210      CONTINUE
      ENDIF

C     SI AUCUN GROUPE DE NOEUD N'EST A CREER, ON SORT
      IF(NBGNNV.EQ.0)GOTO 555

      CALL JECREC(GRPNOE,'G V I','NO','DISPERSE','VARIABLE',NBGNNV)
      DO 220 IGN=1,NBGNNV
         CALL JENUNO(JEXNUM(NOMAIN//'.GROUPENO',ZI(JGNONV+IGN-1)),
     &               NOMGNO)
         CALL JECROC(JEXNOM(GRPNOE,NOMGNO))
         CALL JELIRA(JEXNOM(NOMAIN//'.GROUPENO',NOMGNO),'LONMAX',
     &               NBNO,K8B)
         CALL JEECRA(JEXNOM(GRPNOE,NOMGNO),'LONMAX',
     &               ZI(JNNPG+IGN-1),K8B)
         CALL JEVEUO(JEXNOM(NOMAIN//'.GROUPENO',NOMGNO),'L',JADIN)
         CALL JEVEUO(JEXNOM(GRPNOE,NOMGNO),'E',JADOU)
         K=0
         DO 221 INO = 1, NBNO
            IF(ZI(JWK1+ZI(JADIN+INO-1)-1).NE.0)THEN
               K=K+1
               ZI(JADOU+K-1) = ZI(JWK1+ZI(JADIN+INO-1)-1)
            ENDIF
221      CONTINUE
220   CONTINUE
C
C
C
555   CONTINUE
C
      CALL JEDETR('&&CMREST_WORK_1')
      CALL JEDETR('&&CMREST_WORK_2')
      CALL JEDETR('&&CMREST_WORK_3')
      CALL JEDETR('&&CMREST_GRMA_FOURNIS')
      CALL JEDETR('&&CMREST_GRNO_FOURNIS')
      CALL JEDETR('&&CMREST_GRMA_NON_VIDES')
      CALL JEDETR('&&CMREST_NB_MA_PAR_GRMA')
      CALL JEDETR(NUNIN)
      CALL JEDETR(NUMIN)

      CALL JEDEMA()

      END
