      SUBROUTINE VPECST (IFM,TYPRES,OMGMIN,OMGMAX,NBFRE1,NBFRE2,
     &                   NBFREQ,NBLAGR,TYPEP,TYPCON,DIMC1,ZIMC1)
      IMPLICIT NONE
      INTEGER             IFM,NBFRE1,NBFRE2,NBFREQ,NBLAGR
      REAL*8              OMGMIN,OMGMAX,DIMC1
      COMPLEX*16          ZIMC1
      CHARACTER*1         TYPEP
      CHARACTER*8         TYPCON
      CHARACTER*16        TYPRES
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 16/10/2012   AUTEUR ALARCON A.ALARCON 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C   PRINTING OF THE NUMBER OF THE EIGENVALUES THAT BELONG TO A CHOOSEN
C   PATTERN
C     ------------------------------------------------------------------
C IN  OMGMIN : R8 : PULSATION MIN
C IN  OMGMAX : R8 : PULSATION MAX
C IN  NBFRE1 : IS : NB DE PULSATION INFERIEURE A OMGMIN
C IN  NBFRE2 : IS : NB DE PULSATION INFERIEURE A OMGMAX
C IN/OUT NBFREQ : IS : OUTPUT: NBRE DE FREQ DANS LA BANDE OMGMIN OMGMAX
C                    INPUT: NUMERO DE LA BANDE (SEULEMENT SI TYPEP='D')
C IN  NBLAGR : IS : NB DE DDLS DE LAGRANGE
C IN  TYPRES : TX : TYPE DE CALCUL (DYNAMIQUE OU FLAMBEMENT)
C IN  TYPEP  : K1 : TYPE D EIGENVALUE-PROBLEM
C    'R' GEP REEL POUR MODE_ITER_SIMULT/BANDE OU INFO_MODE SUR UNE
C        SEULE BANDE OU MODE_ITER_INV
C    'F' IDEM 'R' MAIS SANS CALCUL NBFREQ, IL EST DONNE EN INPUT. 
C        UTILISE POUR OPTION 'SIMULT/'BANDE' AVEC TABLE ISSUE
C        D'INFO_MODE
C    'D' GEP REEL POUR INFO_MODE SUR LA BANDE NUMERO NFREQ
C    'C' GEP COMPLEXE OU QEP POUR INFO_MODE
C    'S' GEP REEL POUR TEST DE STURM POSTTRAITEMENT MODE_ITER_SIMULT
C IN  TYPCON : K8 : TYPE DE CONTOUR (LICITE SI TYPEP='C')
C IN  DIMC1  : R8 : DIMENSION CHARACTERISTIQUE REEL N 1 DU CONTOUR 
C                   (LICITE SI TYPEP='C')
C IN  ZIMC1  : C16: DIMENSION CHARACTERISTIQUE COMPLEXE N 1 DU CONTOUR 
C                   (LICITE SI TYPEP='C')
C     ------------------------------------------------------------------
C     REMARQUE:  NBFRE1 ET NBFRE2  SONT CALCULES PAR VPSTUR
C     ------------------------------------------------------------------
      REAL*8  FREQOM,FMIN,FMAX
      INTEGER IBANDE
C     ------------------------------------------------------------------
C
C   --- ON RECUPERE LE NUMERO DE LA BANDE SI NECESSAIRE
      IF (TYPEP.EQ.'D') IBANDE=NBFREQ

C   --- WE ARE ON THE REEL PLANE (STURM TEST)
      IF ((TYPEP.EQ.'R').OR.(TYPEP.EQ.'S').OR.(TYPEP.EQ.'D').OR.
     &    (TYPEP.EQ.'F')) THEN

C   --- TEST DE STURM
        IF (TYPEP.NE.'F') THEN
          IF (TYPRES.EQ.'DYNAMIQUE') THEN
            NBFREQ = ABS( NBFRE2 - NBFRE1 )
          ELSE
            IF ((OMGMIN *OMGMAX).GE.0.D0) THEN
              NBFREQ=ABS(NBFRE2-NBFRE1)
            ELSE
              NBFREQ=ABS(NBFRE2+NBFRE1-2*NBLAGR)
            ENDIF
          ENDIF
        ENDIF
        IF (NBFREQ.GT.9999) THEN
          CALL U2MESS('A','ALGELINE3_64')
          WRITE(IFM,*)' NOMBRE DE VALEURS PROPRES : ',NBFREQ
        ENDIF
        
C   --- AFFICHAGE SI MODE_ITER_SIMULT+BANDE OU MODE_ITER_INV+
C   --- SEPARE/AJUSTE OU INFO_MODE+MIN/MAX
        IF ((TYPEP.EQ.'R').OR.(TYPEP.EQ.'F')) THEN
          IF (TYPRES.EQ.'DYNAMIQUE') THEN
            FMIN=FREQOM(OMGMIN)
            FMAX=FREQOM(OMGMAX)
            IF (TYPEP.EQ.'R') THEN
              WRITE(IFM,900)
            ELSE IF (TYPEP.EQ.'F') THEN
              WRITE(IFM,1900)
            ENDIF
            IF (NBFREQ.EQ.0)  THEN
              WRITE(IFM,901)FMIN,FMAX
            ELSE IF (FMIN.EQ.0.D0)  THEN
              WRITE(IFM,902)NBFREQ,FMAX
            ELSE
              WRITE(IFM,903)FMIN,FMAX,NBFREQ
            ENDIF
          ELSE
            IF (TYPEP.EQ.'R') THEN
              WRITE(IFM,800)
            ELSE IF (TYPEP.EQ.'F') THEN
              WRITE(IFM,1800)
            ENDIF
            IF (NBFREQ.EQ.0)  THEN
              WRITE(IFM,801) OMGMIN, OMGMAX
            ELSE IF (OMGMIN .EQ. 0.D0 )  THEN
              WRITE(IFM,802)NBFREQ,OMGMAX
            ELSE
              WRITE(IFM,803)OMGMIN,OMGMAX,NBFREQ
            ENDIF
          ENDIF

C   --- AFFICHAGE SI INFO_MODE+LISTE
        ELSE IF (TYPEP.EQ.'D') THEN
          IF (TYPRES.EQ.'DYNAMIQUE') THEN
            FMIN=FREQOM(OMGMIN)
            FMAX=FREQOM(OMGMAX)
            IF (IBANDE.EQ.1) WRITE(IFM,900)
            IF (NBFREQ.EQ.0)  THEN
              WRITE(IFM,1901)IBANDE,FMIN,FMAX
            ELSE
              WRITE(IFM,1903)IBANDE,FMIN,FMAX,NBFREQ
            ENDIF
          ELSE
            IF (IBANDE.EQ.1) WRITE(IFM,800)
            IF (NBFREQ.EQ.0)  THEN
              WRITE(IFM,1801)IBANDE,OMGMIN,OMGMAX
            ELSE
              WRITE(IFM,1803)IBANDE,OMGMIN,OMGMAX,NBFREQ
            ENDIF
          ENDIF

        ELSE IF (TYPEP.EQ.'S') THEN
C   --- AFFICHAGE DEDIE DS UNE DES ROUTINES APPELLANTES         
        ENDIF

C   --- WE ARE ON THE COMPLEX PLANE (APM TEST)
      ELSE IF (TYPEP.EQ.'C') THEN
        NBFREQ=NBFRE2
        IF (NBFREQ .GT. 9999) THEN
          CALL U2MESS('A','ALGELINE3_64')
          WRITE(IFM,*)' NOMBRE DE VALEURS PROPRES : ',NBFREQ
        ENDIF
        WRITE(IFM,910)
        IF (NBFREQ.EQ.0)  THEN
          IF (TYPCON(1:6).EQ.'CERCLE')
     &      WRITE(IFM,911)DBLE(ZIMC1),DIMAG(ZIMC1),DIMC1
        ELSE
          IF (TYPCON(1:6).EQ.'CERCLE')
     &      WRITE(IFM,912)DBLE(ZIMC1),DIMAG(ZIMC1),DIMC1,NBFREQ
        ENDIF      

C   --- ILLEGAL OPTION
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
    
      IF (TYPEP.NE.'S') WRITE(IFM,950)
C     ------------------------------------------------------------------
  800 FORMAT(/,72('-'),/,'   VERIFICATION DU SPECTRE DE CHARGES ',
     &'CRITIQUES (METHODE DE STURM)',/)
 1800 FORMAT(/,72('-'),/,'   VERIFICATION DU SPECTRE DE CHARGES ',
     &'CRITIQUES (METHODE DE STURM - '/
     &'RESULTAT ISSU D''UN INFO_MODE PRECEDENT)',/)
C
  801 FORMAT(1X,'PAS DE CHARGE CRITIQUE DANS LA BANDE (',1PE10.3,',',
     &                                                 1PE10.3,') ')
 1801 FORMAT(1X,'PAS DE CHARGE CRITIQUE DANS LA BANDE N ',I3,' DE '/
     &       ' BORNES (',1PE10.3,',',1PE10.3,') ')
C
  802 FORMAT(1X,I4,' CHARGES CRITIQUES INFERIEURES A ',1PE10.3,' HZ')
C
  803 FORMAT(1X,'LE NOMBRE DE CHARGES CRITIQUES DANS LA BANDE (',
     &                               1PE10.3,',',1PE10.3,') EST ',I4)
 1803 FORMAT(1X,'LE NOMBRE DE CHARGES CRITIQUES DANS LA BANDE N ',I3,
     &       ' DE BORNES (',1PE10.3,',',1PE10.3,') EST ',I4)
C
  900 FORMAT(/,72('-'),/,'   VERIFICATION DU SPECTRE DE FREQUENCES ',
     &'(METHODE DE STURM)',/)
 1900 FORMAT(/,72('-'),/,'   VERIFICATION DU SPECTRE DE FREQUENCES ',
     &'(METHODE DE STURM - '/
     &'RESULTAT ISSU D''UN INFO_MODE PRECEDENT)',/)
C
  901 FORMAT(1X,'PAS DE FREQUENCE DANS LA BANDE (',1PE10.3,',',
     &                                                 1PE10.3,') ')
 1901 FORMAT(1X,'PAS DE FREQUENCE DANS LA BANDE N ',I3,' DE BORNES '/
     &       '(',1PE10.3,',',1PE10.3,')')
C
  902 FORMAT(1X,I4,' FREQUENCES PROPRES INFERIEURES A ',1PE10.3,' HZ')
C
  903 FORMAT(1X,'LE NOMBRE DE FREQUENCES DANS LA BANDE (',1PE10.3,
     &                                         ',',1PE10.3,') EST ',I4) 
 1903 FORMAT(1X,'LE NOMBRE DE FREQUENCES DANS LA BANDE N ',I3,' DE '/
     &       'BORNES (',1PE10.3,',',1PE10.3,') EST ',I4)
C
  910 FORMAT(/,72('-'),/,'   VERIFICATION DU SPECTRE EN FREQUENCE ',
     &  '(METHODE DE L''ARGUMENT PRINCIPAL)',/)
  911 FORMAT(1X,'PAS DE FREQUENCE DANS LE DISQUE CENTRE EN (',1PE10.3,
     &  ',',1PE10.3')',/,' ET DE RAYON ',1PE10.3)
  912 FORMAT(1X,'LE NOMBRE DE FREQUENCES DANS LE DISQUE CENTRE EN (',
     &  1PE10.3,',',1PE10.3')',/,' ET DE RAYON ',1PE10.3,' EST ',I4)
C
  950 FORMAT(72('-'),/)
C     ------------------------------------------------------------------
      END
