      SUBROUTINE VPTABL(TABMOD,TYPEVP,FMIN,FMAX,PRECDC,NFREQ,EFFMIN,
     &                  EFFMAX)
      IMPLICIT NONE
      REAL*8       FMIN,FMAX,PRECDC,EFFMIN,EFFMAX
      INTEGER      NFREQ
      CHARACTER*9  TYPEVP
      CHARACTER*19 TABMOD
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C DANS LA TABLE TABMOD RECUPERE LE NBRE DE FREQUENCES CORRESPONDANT A
C L'INTERVALLE FREMIN/FREMAX. ON DECALE LES BORNES SUIVANT LA REGLE
C PRATIQUEE DS LES OPERATEURS MODAUX VIA LA PARAMETRE
C VERIF_MODE/PREC_SHIFT
C IN TABMOD    : K19 : TABLE ISSUE DE INFO_MODE
C IN TYPEVP    : K9  : TYPE DE PROBLEME, FREQ OU CHAR_CRIT
C IN FMIN/FMAX : R8  : BORNES DE LA BANDE FREQUENTIELLE DEMANDEE
C IN PRECDC    : R8  : DECALAGE EXTERNE POUR ENGLOBER LA BANDE
C OUT NFREQ    : IN  : NBRE DE MODES DANS LA BANDE
C OUT EFFMIN/EFFMAX : R8  : BORNES DE LA BANDE FREQUENTIELLE EFFECTIVE
C-----------------------------------------------------------------------
C RESPONSABLE ALARCON A.ALARCON

C VARIABLES LOCALES
      INTEGER      IER,NBVAL1,NBVAL2,NBVAL3,JOBJ1,JOBJ2,JOBJ3,JOBJ4,
     &             JOBJ5,I,LXLGUT,IFM,NIV,IBID,LL,NBVAL4,NBVAL5
      REAL*8       VR(2),LPREC(2),RDENOM,EPS,R8PREM
      COMPLEX*16   CBID
      CHARACTER*4  TYPVAL
      CHARACTER*8  K8BID,LCRIT(2)
      CHARACTER*10 LCRPA(2)
      CHARACTER*16 LIPACR(2),PARA
      CHARACTER*19 TABMOF
      CHARACTER*24 VALK(2),NOMOB1,NOMOB2,NOMOB3,NOMOB4,NOMOB5
      LOGICAL      LEXIST
      INCLUDE 'jeveux.h'


C --- INITS.
      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)
      VALK(1)=TABMOD
      TABMOF='&&VPTABL.TABMOF'
      NOMOB1='&&VPTABL.NB_MODE'
      NOMOB2='&&VPTABL.BORNE_MIN'
      NOMOB3='&&VPTABL.BORNE_MAX'
      NOMOB4='&&VPTABL.EFFEC_MIN'
      NOMOB5='&&VPTABL.EFFEC_MAX'
      NFREQ=-9999
      LL=LXLGUT(TYPEVP)
      EPS=100.D0*R8PREM()

C --- TESTS DE VALIDITE DE LA CARTE
      CALL TBEXIP(TABMOD,TYPEVP(1:LL)//'_MIN',LEXIST,K8BID)
      IF ((LEXIST.AND.(K8BID(1:1).NE.'R')).OR.(.NOT.LEXIST))
     &  CALL U2MESK('F','ALGELINE2_23', 1 ,VALK)

      CALL TBEXIP(TABMOD,TYPEVP(1:LL)//'_MAX',LEXIST,K8BID)
      IF ((LEXIST.AND.(K8BID(1:1).NE.'R')).OR.(.NOT.LEXIST))
     &  CALL U2MESK('F','ALGELINE2_23', 1 ,VALK)

      CALL TBEXIP(TABMOD,'NB_MODE',LEXIST,K8BID)
      IF ((LEXIST.AND.(K8BID(1:1).NE.'I')).OR.(.NOT.LEXIST))
     &  CALL U2MESK('F','ALGELINE2_23', 1 ,VALK)

      CALL TBEXIP(TABMOD,'BORNE_MIN_EFFECT',LEXIST,K8BID)
      IF ((LEXIST.AND.(K8BID(1:1).NE.'R')).OR.(.NOT.LEXIST))
     &  CALL U2MESK('F','ALGELINE2_23', 1 ,VALK)

      CALL TBEXIP(TABMOD,'BORNE_MAX_EFFECT',LEXIST,K8BID)
      IF ((LEXIST.AND.(K8BID(1:1).NE.'R')).OR.(.NOT.LEXIST))
     &  CALL U2MESK('F','ALGELINE2_23', 1 ,VALK)

C --- EXTRACTION DES LIGNES REPONDANT AU CRITERE DS LA TABLE
C --- INTERMEDIAIRE: TABMOF
      LIPACR(1)=TYPEVP(1:LL)//'_MIN'
      LIPACR(2)=TYPEVP(1:LL)//'_MAX'
      LCRPA(1)='GE'
      LCRPA(2)='LE'
      VR(1)=FMIN-EPS
      VR(2)=FMAX+EPS
      LPREC(1)=PRECDC
      LPREC(2)=PRECDC
      LCRIT(1)='RELA'
      LCRIT(2)='RELA'

      CALL TBEXTB(TABMOD,'V',TABMOF,2,LIPACR,LCRPA,IBID,VR,CBID,K8BID,
     &            LPREC,LCRIT,IER)

C --- PB EXTRACTION: PAS DE LIGNE CORRESPONDANT AUX CRITERES
      IF ((IER.EQ.1).OR.(IER.EQ.2)) THEN

        CALL U2MESG('F','ALGELINE2_24',1,VALK,0,0,2,VR)

      ELSE

C --- EXTRACTION DES COLONNES 'MIN','MAX' ET 'NB_MODE' + VERIFICATIONS
        PARA='NB_MODE'
        CALL TBEXVE(TABMOF,PARA,NOMOB1,'V',NBVAL1,TYPVAL)
        IF ((TYPVAL.NE.'I').OR.(NBVAL1.EQ.0))
     &    CALL U2MESK('F','ALGELINE2_23',1,VALK)
        PARA=LIPACR(1)
        CALL TBEXVE(TABMOF,PARA,NOMOB2,'V',NBVAL2,TYPVAL)
        IF ((TYPVAL.NE.'R').OR.(NBVAL2.EQ.0))
     &    CALL U2MESK('F','ALGELINE2_23',1,VALK)
        PARA=LIPACR(2)
        CALL TBEXVE(TABMOF,PARA,NOMOB3,'V',NBVAL3,TYPVAL)
        IF ((TYPVAL.NE.'R').OR.(NBVAL3.EQ.0))
     &    CALL U2MESK('F','ALGELINE2_23',1,VALK)
        PARA='BORNE_MIN_EFFECT'
        CALL TBEXVE(TABMOF,PARA,NOMOB4,'V',NBVAL4,TYPVAL)
        IF ((TYPVAL.NE.'R').OR.(NBVAL4.EQ.0))
     &    CALL U2MESK('F','ALGELINE2_23',1,VALK)
        PARA='BORNE_MAX_EFFECT'
        CALL TBEXVE(TABMOF,PARA,NOMOB5,'V',NBVAL5,TYPVAL)
        IF ((TYPVAL.NE.'R').OR.(NBVAL5.EQ.0))
     &    CALL U2MESK('F','ALGELINE2_23',1,VALK)

C --- VERIF NBRE DE LIGNES EGAUX
        IF ((NBVAL1.NE.NBVAL2).OR.(NBVAL1.NE.NBVAL3).OR.
     &      (NBVAL2.NE.NBVAL3)) CALL U2MESK('F','ALGELINE2_23',1,VALK)
        IF ((NBVAL4.NE.NBVAL5).OR.(NBVAL1.NE.NBVAL4).OR.
     &      (NBVAL1.NE.NBVAL5)) CALL U2MESK('F','ALGELINE2_23',1,VALK)

C --- VERIF PAS DE TROU DS LES OBJETS
        CALL JEVEUO(NOMOB2,'L',JOBJ2)
        CALL JEVEUO(NOMOB3,'L',JOBJ3)
        CALL JEVEUO(NOMOB4,'L',JOBJ4)
        CALL JEVEUO(NOMOB5,'L',JOBJ5)

C POUR DEBUGGAGE: LISTE DES BORNES DES INTERVALLES SELECTIONNES
C       DO I=1,NBVAL1
C         WRITE(6,*)I,ZR(JOBJ2+I-1),ZR(JOBJ3+I-1),
C     &              ZR(JOBJ4+I-1),ZR(JOBJ5+I-1)
C       ENDDO

C --- VERIF DES BORNES EXTREMES
        IF (ABS(FMIN).GT.EPS) THEN
          RDENOM=ABS(FMIN)
        ELSE
          RDENOM=1.D0
        ENDIF
        IF (ABS(ZR(JOBJ2)-FMIN)/RDENOM.GT.PRECDC)
     &    CALL U2MESK('F','ALGELINE2_25',1,VALK)

        IF (ABS(FMAX).GT.EPS) THEN
          RDENOM=ABS(FMAX)
        ELSE
          RDENOM=1.D0
        ENDIF
        IF (ABS(ZR(JOBJ3+NBVAL3-1)-FMAX)/RDENOM.GT.PRECDC)
     &    CALL U2MESK('F','ALGELINE2_25',1,VALK)

C --- VERIF DES BORNES INTERNES (INITIALES ET EFFECTIVES)
        DO 10 I=1,NBVAL3-1
          IF (ABS(ZR(JOBJ3+I-1)).GT.EPS) THEN
            RDENOM=ABS(ZR(JOBJ3+I-1))
          ELSE
            RDENOM=1.D0
          ENDIF
          IF (ABS(ZR(JOBJ3+I-1)-ZR(JOBJ2+I))/RDENOM
     &        .GT.PRECDC) CALL U2MESK('F','ALGELINE2_25',1,VALK)
   10   CONTINUE
        DO 15 I=1,NBVAL5-1
          IF (ABS(ZR(JOBJ5+I-1)).GT.EPS) THEN
            RDENOM=ABS(ZR(JOBJ5+I-1))
          ELSE
            RDENOM=1.D0
          ENDIF
          IF (ABS(ZR(JOBJ5+I-1)-ZR(JOBJ4+I))/RDENOM
     &        .GT.PRECDC) CALL U2MESK('F','ALGELINE2_25',1,VALK)
   15   CONTINUE

C --- SOMME DES NB_MODES DES BANDES SELECTIONNEES
        CALL JEVEUO(NOMOB1,'L',JOBJ1)
        NFREQ=0
        DO 20 I=1,NBVAL1
          NFREQ=NFREQ+ZI(JOBJ1+I-1)
   20   CONTINUE

C --- RECUPERATION DES BORNES EFFECTIVES
        EFFMIN=ZR(JOBJ4)
        EFFMAX=ZR(JOBJ5+NBVAL5-1)

        CALL JEDETR(NOMOB1)
        CALL JEDETR(NOMOB2)
        CALL JEDETR(NOMOB3)
        CALL JEDETR(NOMOB4)
        CALL JEDETR(NOMOB5)
      ENDIF
      CALL JEDETR(TABMOF)

      CALL JEDEMA()
      END
