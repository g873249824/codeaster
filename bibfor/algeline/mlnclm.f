      SUBROUTINE MLNCLM(N,P,FRONTL,FRONTU,ADPER,T1,T2,AD,EPS,IER)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 07/01/2002   AUTEUR JFBHHUC C.ROSE 
C RESPONSABLE JFBHHUC C.ROSE
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C VERSION COMPLEXE DE MLNFLM
C     VERSION AVEC APPEL A CGEMV POUR LES PRODUITS MATRICE-VECTEUR
C     AU DELA D' UN CERTAIN SEUIL
C     CGEMV EST APPEL A TRAVERS LA FONCTION C CGEMW POUR CAR CGEMV
C     NECESSITE DES ARGUMENTS ENTIER INTEGER*4 REFUSES PAR ASTER
      IMPLICIT NONE
      INTEGER N,P,ADPER(*),AD(*),IER
      COMPLEX*16 FRONTL(*),FRONTU(*),T1(*),T2(*)
      REAL*8 EPS
C
      INTEGER I,K,LDA,NN,KK
      INTEGER SEUIN,SEUIK
      PARAMETER(SEUIN=1500,SEUIK=300)
      LDA= N
      DO 30 K = 1,P
C     CALCUL DE L
         DO 10 I = 1,K - 1
            AD(I) = ADPER(I) + K - I
            T1(I) = FRONTU(AD(I))
            T2(I) = FRONTL(AD(I))
 10      CONTINUE
         IF (K.GT.1) THEN
C     PRODUIT MATRICE-VECTEUR
C     CALL SSPMVC(N-K+1,K-1,FRONTL,AD,T1,FRONTL(ADPER(K)))
C
            NN= N-K+1
            KK= K-1
            IF(NN.LT.SEUIN.OR.KK.LT.SEUIK) THEN
               CALL SSPMVC(NN,KK,FRONTL,AD,T1,FRONTL(ADPER(K)))
            ELSE
               CALL CGEMW(NN,KK,FRONTL(K),LDA,T1,FRONTL(ADPER(K)))
            END IF
         ENDIF
C     DIVISION PAR LE TERME
C     DIAGONAL
            IF (ABS(FRONTL(ADPER(K))).LE.EPS) THEN
               IER = K
               GO TO 40
            END IF
CRAY DIR$ IVDEP DIRECTIVE INHIBEE CAR DEPENDANCE AVANT
            DO 20 I = 1,N - K
               FRONTL(ADPER(K)+I) = FRONTL(ADPER(K)+I)/FRONTL(ADPER(K))
 20         CONTINUE
C
C     CALCUL DE U
            IF (K.GT.1) THEN
C     PRODUIT MATRICE-VECTEUR
C     CALL SSPMVC(N-K+1,K-1,FRONTU,AD,T2,FRONTU(ADPER(K)))
C
               NN= N-K+1
               KK= K-1
               IF(NN.LT.SEUIN.OR.KK.LT.SEUIK) THEN
                  CALL SSPMVC(NN,KK,FRONTU,AD,T2,FRONTU(ADPER(K)))
               ELSE
                  CALL CGEMW(NN,KK,FRONTU(K),LDA,T2,FRONTU(ADPER(K)))
               ENDIF
            END IF
C
 30      CONTINUE
 40      CONTINUE
         END
