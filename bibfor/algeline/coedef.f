      SUBROUTINE COEDEF(IMOD,FREMOD,NBM,YOUNG,POISS,RHO,ICOQ,NBNO,
     &                  NUMNO,NUNOE0,NBNOTO,COORDO,IAXE,KEC,GEOM,
     &                  DEFM,DRMAX,TORCO,TCOEF)
      IMPLICIT REAL*8 (A-H,O-Z)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 11/07/2005   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C CARACTERISATION DES DEFORMEES DES MODES PRIS EN COMPTE POUR LE
C COUPLAGE : DETERMINATION DES COEFFICIENTS DE LA DEFORMEE AXIALE
C APPELANT : MODCOQ
C-----------------------------------------------------------------------
C  IN : IMOD   : INDICE DU MODE CONSIDERE
C  IN : FREMOD : FREQUENCE DU MODE CONSIDERE
C  IN : NBM    : NOMBRE DE MODES PRIS EN COMPTE POUR LE COUPLAGE
C  IN : YOUNG  : MODULE D'YOUNG DU MATERIAU STRUCTUREL
C  IN : POISS  : COEFFICIENT DE POISSON DU MATERIAU STRUCTUREL
C  IN : RHO    : MASSE VOLUMIQUE DU MATERIAU STRUCTUREL
C  IN : ICOQ   : INDICE CARACTERISANT LA COQUE SUR LAQUELLE ON TRAVAILLE
C                ICOQ=1 COQUE INTERNE   ICOQ=2 COQUE EXTERNE
C  IN : NBNO   : NOMBRE DE NOEUDS APPARTENANT AU GROUPE DE NOEUDS SUR
C                LEQUEL ON VA EXTRAIRE LES DEPLACEMENTS
C  IN : NUMNO  : LISTE DES NUMEROS DES NOEUDS APPARTENANT A CE GROUPE
C  IN : NUNOE0 : NUMERO DU NOEUD CORRESPONDANT AU DEPLACEMENT RADIAL
C                MAXIMUM SUR LE MAILLAGE
C  IN : NBNOTO : NOMBRE TOTAL DE NOEUDS DU MAILLAGE
C  IN : COORDO : LISTE DES COORDONNEES DE TOUS LES NOEUDS DU MAILLAGE
C  IN : IAXE   : INDICE CARACTERISANT L'AXE DE REVOLUTION DES COQUES
C                IAXE = 1 : AXE X DU REPERE GLOBAL
C                IAXE = 2 : AXE Y DU REPERE GLOBAL
C                IAXE = 3 : AXE Z DU REPERE GLOBAL
C  IN : KEC    : INDICE CARACTERISTIQUE DU SENS DE L'ECOULEMENT
C                KEC =  1 : ECOULEMENT DANS LE SENS CROISSANT DU
C                PARAMETRE LE LONG DE L'AXE DE REVOLUTION DES COQUES
C                KEC = -1 : ECOULEMENT DANS LE SENS DECROISSANT
C  IN : GEOM   : VECTEUR DE GRANDEURS GEOMETRIQUES CARACTERISTIQUES
C  IN : DEFM   : TABLEAU DES DEFORMEES MODALES (DDL DE TRANSLATION DANS
C                LES DEUX DIRECTIONS ORTHOGONALES A L'AXE DE REVOLUTION)
C  IN : DRMAX  : VALEUR DU DEPLACEMENT RADIAL MAXIMUM
C  IN : TORCO  : TABLEAU CONTENANT LES ORDRES DE COQUE ET DEPHASAGES
C OUT : TCOEF  : TABLEAU CONTENANT LES COEFFICIENTS DES DEFORMEES
C-----------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      INTEGER  IMOD,NBM,ICOQ,NBNO,NUMNO(NBNO),NUNOE0,NBNOTO,IAXE,KEC,
     &         IRET
      REAL*8   FREMOD,YOUNG,POISS,RHO,COORDO(3,NBNOTO),GEOM(9),DET
      REAL*8   DEFM(2,NBNOTO,NBM),DRMAX,TORCO(4,NBM),TCOEF(10,NBM)
C
      REAL*8   R8PI
      REAL*8   LONG,LONG2,LONG4
C
C-----------------------------------------------------------------------
      CALL JEMARQ()
C
C
C --- 1.INITIALISATIONS
C
      PI = R8PI()
      TOLE = 100.D0*R8PREM()
C
      IF (IAXE.EQ.1) THEN
        IDIR1 = 2
        IDIR2 = 3
      ELSE IF (IAXE.EQ.2) THEN
        IDIR1 = 3
        IDIR2 = 1
      ELSE
        IDIR1 = 1
        IDIR2 = 2
      ENDIF
C
      LONG = GEOM(3)
      Z0 = GEOM(4)
      Z1 = GEOM(5)
      IF (ICOQ.EQ.1) THEN
        EPAIS = GEOM(6)
        RAYON = GEOM(8)
      ELSE
        EPAIS = GEOM(7)
        RAYON = GEOM(9)
      ENDIF
C
      ILIGNE = 1
      IF (ICOQ.EQ.2) ILIGNE = 3
      ITAB = 5*(ICOQ-1)
C
C
C --- 2.DETERMINATION DU NOMBRE D'ONDES
C
      ORCOQ = TORCO(ILIGNE,IMOD)
C
C-----SI MODE DE POUTRE
      IF (ORCOQ.EQ.1.D0) THEN
        RAPSI = 2.D0/(RAYON*RAYON + EPAIS*EPAIS/4.D0)
        XX = DBLE(SQRT(RHO/YOUNG*RAPSI))
        TCOEF(1+ITAB,IMOD) = LONG*DBLE(SQRT(2.D0*PI*FREMOD*XX))
C-----SINON (MODE DE COQUE)
      ELSE
        ORCQ2 = ORCOQ*ORCOQ
        ORCQ4 = ORCQ2*ORCQ2
        RAYO2 = RAYON*RAYON
        RAYO4 = RAYO2*RAYO2
        LONG2 = LONG *LONG
        LONG4 = LONG2*LONG2
        X = 2.D0*PI*FREMOD
        X = X*X*(1.D0+1.D0/ORCQ2)
        A = (1.D0-POISS*POISS)*RHO*X/YOUNG
        Y = (ORCQ2-1.D0)*(ORCQ2-1.D0)
        B = EPAIS*EPAIS*Y/(12.D0*RAYO4)
        Z = LONG4*ORCQ4*(A-B)/RAYO2
        IF (Z.LE.0.D0) THEN
          CALL UTMESS('F','COEDEF','VALEUR NEGATIVE OU NULLE POUR '//
     &      'LA PUISSANCE QUATRIEME DU NOMBRE D ONDES => VALEUR '//
     &      'DE L ORDRE DE COQUE MAL DETERMINEE => IL FAUT AFFINER '//
     &      'LE MAILLAGE SUR LES COQUES : REDUIRE LE PAS ANGULAIRE '//
     &      'POUR DEFINIR PLUS DE NOEUDS SUR LES CONTOURS')
        ELSE
          Z = DBLE(SQRT(Z))
          TCOEF(1+ITAB,IMOD) = DBLE(SQRT(Z))
        ENDIF
      ENDIF
C
C
C --- 3.EXTRACTION DE LA GENERATRICE SUR LAQUELLE ON VA CALCULER LES
C ---   DEPLACEMENTS RADIAUX
C
C --- 3.1.RECUPERATION DES DEUX COORDONNEES DEFINISSANT LA GENERATRICE
C ---     ET DE L'AZIMUT CORRESPONDANT
C
      XGEN = COORDO(IDIR1,NUNOE0)
      YGEN = COORDO(IDIR2,NUNOE0)
      THETAG = TORCO(ILIGNE+1,IMOD)
C
C --- 3.2.EXTRACTION DES NOEUDS APPARTENANT A LA GENERATRICE
C
      CALL WKVECT('&&COEDEF.TEMP.NOGEN','V V I',NBNO,INOGEN)
      NBNOGE = 0
      DO 20 INO = 1,NBNO
        NUMNOE = NUMNO(INO)
        XNO = COORDO(IDIR1,NUMNOE)
        DIFX = DBLE(ABS(XNO-XGEN))
        IF (DIFX.LT.TOLE) THEN
          YNO = COORDO(IDIR2,NUMNOE)
          DIFY = DBLE(ABS(YNO-YGEN))
          IF (DIFY.LT.TOLE) THEN
            ZNO = COORDO(IAXE,NUMNOE)
            IF (ZNO.GE.Z0 .AND. ZNO.LE.Z1) THEN
              NBNOGE = NBNOGE + 1
              ZI(INOGEN+NBNOGE-1) = NUMNOE
            ENDIF
          ENDIF
        ENDIF
  20  CONTINUE
C
      IF (NBNOGE.LT.4) CALL UTMESS('F','COEDEF','NOMBRE DE NOEUDS SUR'//
     &  ' LA GENERATRICE INFERIEUR A 4 DONC INSUFFISANT POUR'//
     &  ' DETERMINER LES COEFFICIENTS DE LA DEFORMEE AXIALE')
C
C
C --- 4.CREATION D'UNE DISCRETISATION LE LONG DE LA GENERATRICE
C ---   SIMULTANEMENT ON CREE UNE DISTRIBUTION EN DR
C
      CALL WKVECT('&&COEDEF.TEMP.ZAXE','V V R',NBNOGE,IZAXE)
      CALL WKVECT('&&COEDEF.TEMP.DR  ','V V R',NBNOGE,IDR)
C
      DO 30 INO = 1,NBNOGE
        NUMNOE = ZI(INOGEN+INO-1)
        IF (KEC.EQ.1) THEN
          ZR(IZAXE+INO-1) = COORDO(IAXE,NUMNOE) - Z0
        ELSE
          ZR(IZAXE+INO-1) = Z1 - COORDO(IAXE,NUMNOE)
        ENDIF
        DX = DEFM(1,NUMNOE,IMOD)
        DY = DEFM(2,NUMNOE,IMOD)
        ZR(IDR+INO-1) = DBLE(COS(THETAG))*DX+DBLE(SIN(THETAG))*DY
  30  CONTINUE
C
C
C --- 5.DETERMINATION DES COEFFICIENTS DE LA DEFORMEE AXIALE
C
C --- 5.1.CALCUL DES COEFFICIENTS DU TRIANGLE SUPERIEUR DE LA MATRICE A
C ---    (PAR COLONNES DESCENDANTES) ET DES COEFFICIENTS DU SECOND
C ---     MEMBRE B
C
      CALL WKVECT('&&COEDEF.TEMP.COEA','V V R',10,ICOEA)
      CALL WKVECT('&&COEDEF.TEMP.VECB','V V R',4 ,IVECB)
C
      DELTAI = TCOEF(1+ITAB,IMOD)
C
      DO 40 IZ = 1,NBNOGE
        ZZ = DELTAI*ZR(IZAXE+IZ-1)/LONG
        DR = ZR(IDR+IZ-1)
        COSIZ  = DBLE(COS(ZZ))
        SINIZ  = DBLE(SIN(ZZ))
        COSHIZ = DBLE(COSH(ZZ))
        SINHIZ = DBLE(SINH(ZZ))
        ZR(ICOEA)   = ZR(ICOEA)   + COSIZ  * COSIZ
        ZR(ICOEA+1) = ZR(ICOEA+1) + SINIZ  * COSIZ
        ZR(ICOEA+2) = ZR(ICOEA+2) + SINIZ  * SINIZ
        ZR(ICOEA+3) = ZR(ICOEA+3) + COSIZ  * COSHIZ
        ZR(ICOEA+4) = ZR(ICOEA+4) + SINIZ  * COSHIZ
        ZR(ICOEA+5) = ZR(ICOEA+5) + COSHIZ * COSHIZ
        ZR(ICOEA+6) = ZR(ICOEA+6) + COSIZ  * SINHIZ
        ZR(ICOEA+7) = ZR(ICOEA+7) + SINIZ  * SINHIZ
        ZR(ICOEA+8) = ZR(ICOEA+8) + COSHIZ * SINHIZ
        ZR(ICOEA+9) = ZR(ICOEA+9) + SINHIZ * SINHIZ
        ZR(IVECB)   = ZR(IVECB)   + DR * COSIZ
        ZR(IVECB+1) = ZR(IVECB+1) + DR * SINIZ
        ZR(IVECB+2) = ZR(IVECB+2) + DR * COSHIZ
        ZR(IVECB+3) = ZR(IVECB+3) + DR * SINHIZ
  40  CONTINUE
C
C --- 5.2.CREATION DE LA MATRICE A
C
      CALL WKVECT('&&COEDEF.TEMP.MATA','V V R',16,IMATA)
C
      DO 50 IC = 1,4
        IDEC1 = IC*(IC-1)/2
        DO 51 IL = 1,IC
          IDEC = IDEC1 + IL
          ZR(IMATA+4*(IC-1)+IL-1) = ZR(ICOEA+IDEC-1)
  51    CONTINUE
  50  CONTINUE
      DO 60 IC = 1,3
        DO 61 IL = IC+1,4
          IDEC = IL*(IL-1)/2+IC
          ZR(IMATA+4*(IC-1)+IL-1) = ZR(ICOEA+IDEC-1)
  61    CONTINUE
  60  CONTINUE
C
C --- 5.3.RESOLUTION DU SYSTEME LINEAIRE
C
      CALL MGAUSS('NFVP',ZR(IMATA),ZR(IVECB),4,4,1,DET,IRET)
C
C --- 5.4.CALCUL D'UNE ERREUR RELATIVE SUR LA NORME DES DR
C
      SOMM1 = 0.D0
      SOMM2 = 0.D0
C
      DO 80 IZ = 1,NBNOGE
        DR = ZR(IDR+IZ-1)
        SOMM1 = SOMM1 + DR * DR
        ZZ = DELTAI*ZR(IZAXE+IZ-1)/LONG
        COSIZ  = DBLE(COS(ZZ))
        SINIZ  = DBLE(SIN(ZZ))
        COSHIZ = DBLE(COSH(ZZ))
        SINHIZ = DBLE(SINH(ZZ))
        FONC   = ZR(IVECB)*COSIZ    + ZR(IVECB+1)*SINIZ
     &         + ZR(IVECB+2)*COSHIZ + ZR(IVECB+3)*SINHIZ
        SOMM2 = SOMM2 + (DR-FONC) * (DR-FONC)
  80  CONTINUE
C
      ERR = DBLE(SQRT(SOMM2/SOMM1)) * 100.D0
C
C --- 5.5.DEDUCTION DES COEFFICIENTS DE LA DEFORMEE AXIALE
C
      IF (KEC.EQ.1) THEN
         ZZNOE0 = DELTAI * (COORDO(IAXE,NUNOE0)-Z0) / LONG
      ELSE
         ZZNOE0 = DELTAI * (Z1-COORDO(IAXE,NUNOE0)) / LONG
      ENDIF
      DRNOE0 = ZR(IVECB)  *DBLE(COS(ZZNOE0))
     &       + ZR(IVECB+1)*DBLE(SIN(ZZNOE0))
     &       + ZR(IVECB+2)*DBLE(COSH(ZZNOE0))
     &       + ZR(IVECB+3)*DBLE(SINH(ZZNOE0))
      IF (DBLE(ABS(DRNOE0)).LT.DRMAX*TOLE) CALL UTMESS('F','COEDEF',
     &  'DEPLACEMENT RADIAL MAXIMUM NUL SUR LA GENERATRICE')
      DO 90 IB = 1,4
        TCOEF(1+ITAB+IB,IMOD) = ZR(IVECB+IB-1)*DRMAX/DRNOE0
  90  CONTINUE
C
C
C --- 6.IMPRESSION DES RESULTATS DANS LE FICHIER MESSAGE
C
 500  FORMAT('DETERMINATION DES COEFFICIENTS DE LA DEFORMEE AXIALE')
 501  FORMAT('ERREUR RELATIVE SUR LA NORME DES DEPLACEMENTS RADIAUX : ',
     &        G13.6,1X,'%')
 502  FORMAT('L ORDRE DE COQUE EST PEUT-ETRE MAL IDENTIFIE. LA BASE ',
     &       'MODALE EST TROP RICHE')
 503  FORMAT('OU LE NOMBRE DE NOEUDS DU MAILLAGE SUR UNE CIRCONFERENCE',
     &       ' EST TROP FAIBLE.')
C
      IFM = IUNIFI('MESSAGE')
      WRITE(IFM,500)
      WRITE(IFM,501) ERR
      IF (ERR.GT.1.D0) THEN
        WRITE(IFM,502)
        WRITE(IFM,503)
      ENDIF
C
      CALL JEDETR('&&COEDEF.TEMP.ZAXE')
      CALL JEDETR('&&COEDEF.TEMP.DR  ')
      CALL JEDETR('&&COEDEF.TEMP.COEA')
      CALL JEDETR('&&COEDEF.TEMP.VECB')
      CALL JEDETR('&&COEDEF.TEMP.MATA')
      CALL JEDETR('&&COEDEF.TEMP.NOGEN')
      CALL JEDEMA()
C
      END
