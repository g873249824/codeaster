      SUBROUTINE CALSVD(NM,M,N,A,W,MATU,U,MATV,V,IERR)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE  CRP_4
C-----------------------------------------------------------------------
C
C DESCRIPTION :   CALCUL DE LA DECOMPOSITION AUX VALEURS SINGULIERES
C -----------                          T
C                             A = U S V
C
C                 D'UNE MATRICE REELLE RECTANGULAIRE (M,N)
C                 APPEL A LA ROUTINE LAPACK : DGESDD
C
C IN     : NM   : INTEGER , SCALAIRE
C                 PREMIERE DIMENSION DES TABLEAUX A, U ET V, DECLAREE
C                 DANS L'APPELANT, NM >= MAX(M,N)
C IN     : M    : INTEGER , SCALAIRE
C                 NOMBRE DE LIGNES DES MATRICES A ET U
C                 NOMBRE DE COLONNES DE U
C IN     : N    : INTEGER , SCALAIRE
C                 NOMBRE DE COLONNES DE A
C                  = ORDRE DE LA MATRICE V
C IN     : A    : REAL*8 , TABLEAU DE DIMENSION(NM,N)
C                 CONTIENT LA MATRICE RECTANGULAIRE A DONT ON VEUT
C                 CALCULER LA DECOMPOSITION AUX VALEURS SINGULIERES
C                 LE CONTENU EST INCHANGE EN SORTIE : LE TABLEAU EST
C                 RECOPIE DANS U EN DEBUT DE CALCUL
C OUT    : W    : REAL*8 , VECTEUR DE DIMENSION N
C                 CONTIENT LES MIN(N,M) VALEURS SINGULIERES DE A
C                 LES VALEURS SINGULIERES SONT ORDONNEES:
C                 (ORDRE DECROISSANT)
C IN     : MATU : LOGICAL , SCALAIRE
C                 MATU = .TRUE.  INDIQUE QUE LA MATRICE U EST DESIREE
C                 MATU = .FALSE. SINON
C OUT    : U    : REAL*8 , TABLEAU DE DIMENSION (NM,M)
C                 SI MATU = .TRUE. LE TABLEAU U CONTIENT LA MATRICE U
C                 (MATRICE (M,N) A COLONNES ORTHOGONALES)
C                 SI MATU = .FALSE. LE TABLEAU U N'EST PAS REMPLI
C IN     : MATV : LOGICAL , SCALAIRE
C                 MATV = .TRUE.  INDIQUE QUE LA MATRICE V EST DESIREE
C                 MATV = .FALSE. SINON
C OUT    : V    : REAL*8 , TABLEAU DE DIMENSION (NM,N)
C                 SI MATV = .TRUE. LE TABLEAU V CONTIENT LA MATRICE V
C                 (MATRICE CARREE D'ORDRE N ORTHOGONALE)
C                 SI MATV = .FALSE. V EST INUTILE
C OUT    : IERR : INTEGER , SCALAIRE , CODE RETOUR
C                 IERR  = 0 : OK
C                 IERR /= 0 : PB LORS DE LA DECOMPOSITION
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
C ARGUMENTS
C ---------
      INCLUDE 'jeveux.h'
      INTEGER     NM, M, N, IERR
      REAL*8      A(NM,N), W(N), U(NM,M), V(NM,N)
      LOGICAL     MATU, MATV
C
C VARIABLES LOCALES
C -----------------
      INTEGER*4    IERR1
      INTEGER      JWORK,JIWORK,JVT,NM1,NM2,LDVT,I,J,LWORK
      CHARACTER*1  CODE
      PARAMETER (NM1=20)
      REAL*8      VT(NM1*NM1)
C     JE DOUBLE LA TAILLE DE WORK POUR DE MEILLEURS PERFS :
      REAL*8      WORK(2*(7*NM1**2 + 4*NM1))
      INTEGER*4   IWORK(8*NM1)
      LOGICAL ALLOC ,SAFE



C
      CALL MATFPE(-1)

      SAFE=.TRUE.
C     LE BOOLEEN "SAFE" PERMET DE BASCULER ENTRE LES 2 ROUTINES LAPACK
C     SAFE=.TRUE.  => DGESVD
C     SAFE=.FALSE. => DGESDD


C -- REMARQUES DE JP QUI A REMPLACE LE TEXTE DE CETTE ROUTINE PAR
C    DES APPELS A LA ROUTINE LAPACK DGESDD :
C     1) LA ROUTINE CALSVD UTLISE V ET DGESDD UTILISE SA TRANSPOSEE: VT
C ----------------------------------------------------------------------

      NM2=MAX(N,M)
      LDVT=NM2
      LWORK=2*(7*NM2**2 + 4*NM2)


C     -- POUR NE PAS ALLOUER D'OBJETS JEVEUX POUR LES PETITS CAS :
C     ------------------------------------------------------------
      IF (NM1.GE.NM2)THEN
          ALLOC=.FALSE.
      ELSE
          ALLOC=.TRUE.
          CALL WKVECT('&&CALSVD.VT','V V R',NM2*NM2,JVT)
          CALL WKVECT('&&CALSVD.WORK','V V R',LWORK,JWORK)
          CALL WKVECT('&&CALSVD.IWORK','V V S',8*NM2,JIWORK)
      END IF

      IF (MATU.OR.MATV) THEN
        CODE='A'
      ELSE
        CODE='N'
      END IF

C     -- APPEL A LA ROUTINE LAPACK (DGESDD):
C     ------------------------------------------------------------
      IF (.NOT.ALLOC) THEN
        IF (SAFE) THEN
          CALL DGESVD(CODE,CODE,M,N,A,NM,W,U,NM,VT,LDVT,WORK,LWORK,
     &               IERR1)
        ELSE
          CALL DGESDD(CODE,M,N,A,NM,W,U,NM,VT,LDVT,WORK,LWORK,
     &               IWORK,IERR1)
        END IF
        IF (MATV) THEN
          DO 1, I=1,NM
            DO 2, J=1,N
               V(I,J)=VT((I-1)*LDVT+J)
 2          CONTINUE
 1        CONTINUE
        END IF

      ELSE
        IF (SAFE) THEN
         CALL DGESVD(CODE,CODE,M,N,A,NM,W,U,NM,ZR(JVT),LDVT,
     &               ZR(JWORK),LWORK,IERR1)
        ELSE
          CALL DGESDD(CODE,M,N,A,NM,W,U,NM,ZR(JVT),LDVT,
     &               ZR(JWORK),LWORK,ZI4(JIWORK),IERR1)
        END IF
        IF (MATV) THEN
          DO 3, I=1,NM
            DO 4, J=1,N
               V(I,J)=ZR(JVT-1+(I-1)*LDVT+J)
 4          CONTINUE
 3        CONTINUE
        END IF
      END IF

      IERR=IERR1

      IF (ALLOC) THEN
          CALL JEDETR('&&CALSVD.VT')
          CALL JEDETR('&&CALSVD.WORK')
          CALL JEDETR('&&CALSVD.IWORK')
      END IF

C
      CALL MATFPE(1)

      END
