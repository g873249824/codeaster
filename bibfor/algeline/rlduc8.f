      SUBROUTINE RLDUC8 ( NOMMAT, HCOL, ADIA , ABLO ,
     &                     NEQ   , NBBLOC , XSOL , NBSOL )
      IMPLICIT REAL*8 (A-H,O-Z)
C
      INCLUDE 'jeveux.h'
      CHARACTER*(*)       NOMMAT
      INTEGER             HCOL(*),ADIA(*),ABLO(*)
      COMPLEX*16          XSOL (NEQ,*)

C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ======================================================================
C BUT : RESOLUTION DU SYSTEME A COEFFICIENTS COMPLEXES :  A * X = B
C       ELLE CORRESPOND AU SOLVEUR 'LDLT' + COMPLEXE + NON SYM
C       (CETTE ROUTINE EST LA SOEUR JUMELLE DE RLDUR8)
C ======================================================================
C     RESOLUTION DU SYSTEME A COEFFICIENTS COMPLEXES :  A * X = B
C     LA MATRICE EST NON SYMETRIQUE ET A ETE FACTORISEE SOUS FORME L*D*U
C     LA RESOLUTION EST EN PLACE
C
C     ON PEUT RESOUDRE SUR UNE SOUS-MATRICE DE A :
C     ON PREND LES NEQ PREMIERES LIGNES ET COLONNES (NEQ PEUT ETRE
C     INFERIEUR A LA DIMENSION DE LA MATRICE).
C
C     ON PEUT RESOUDRE NBSOL SYSTEMES D'UN COUP A CONDITION
C     QUE LES VECETURS SOIENT CONSECUTIFS EN MEMOIRE :
C     XSOL EST UN VECTEUR DE NBSOL*NEQ REELS
C     ------------------------------------------------------------------
C
C IN  NOMMAT  :    : NOM UTILISATEUR DE LA MATRICE A FACTORISER
C IN  HCOL    : IS : HCOL DE LA MATRICE
C             HCOL(I) RENVOIE LA HAUTEUR DE LA I-EME COLONNE
C IN  ADIA    : IS : ADRESSE DU TERME DIAGONALE DANS SON BLOC
C             ADIA(I) RENVOIE L'ADRESSE DE LA I-EME LIGNE DANS SON BLOC
C IN  ABLO    :  :   POINTEUR DE BLOC
C             ABLO(I+1) RENVOIE LE NO DE LA DERNIERE LIGNE DU I-EME BLOC
C
C IN  NEQ     : IS : NOMBRE D'EQUATIONS PRISES EN COMPTE
C                    C'EST AUSSI LA DIMENSION DES VECTEURS XSOL.
C IN  NBBLOC  : IS : NOMBRE DE BLOC DE LA MATRICE
C VAR XSOL    : C8 : EN ENTREE LES SECONDS MEMBRES
C                    EN ENTREE LES SOLUTIONS
C IN  NBSOL   : IS : NOMBRE DE SOLUTIONS / SECONDS MEMBRES
C     ------------------------------------------------------------------
C  LES TABLEAUX RELATIFS AUX BLOCS ONT ETE CONSTRUITS A PARTIR
C  DES NBBLOCS PREMIERS BLOCS (I.E LES BLOCS SUP)
C  MAIS ILS RESTENT VALABLES POUR LES NBBLOCS BLOCS SUIVANTS
C   (I.E LES BLOCS INF) PUISQUE LES POFILS SUP ET INF SONT IDENTIQUES
C     ------------------------------------------------------------------
C
C     --- RAPPEL SOMMAIRE DE L'ALGORITHME ------------------------------
C
C     POUR I = 1,2, ... ,N
C     !  ACC = 0
C     !  POUR K = 1,2, ... ,I-1
C     !  !  ACC = ACC +  K(I,K)* X(K)
C     !  FIN_POUR
C     !  X(I) = X(I)-ACC/K(I,I)
C     FIN_POUR
C
C     POUR I = N,N-1,... ,1
C     !  ACC = 0
C     !  POUR K = I+1, ... ,N
C     !  !  ACC = ACC +  K(K,I)* X(K)
C     !  FIN_POUR
C     !  X(I) = X(I) - ACC
C     FIN_POUR
C     ------------------------------------------------------------------
C
C     REFERENCE (HISTORIQUE) :
C     (1) P.D. CROUT,
C         A SHORT METHOD FOR EVALUATING DETERMINANTS AND SOLVING SYSTEMS
C         OF LINEAR EQUATIONS WITH REAL OR COMPLEX COEFFICIENTS.
C         AIEE TRANSACTION VOL 60, PP 1235-1240  (1941)
C     ------------------------------------------------------------------
C
C
C
C     ------------------------------------------------------------------
      CHARACTER*6      PGC, PGCANC
      COMMON  /NOMAJE/ PGC
C
C     ------------------------------------------------------------------
      COMPLEX*16       C8VAL
      CHARACTER*24     NOMDIA, UALF
C     ------------------------------------------------------------------
      DATA  UALF  /'                   .UALF'/
      DATA  NOMDIA/'                   .&VDI'/
C     ------------------------------------------------------------------
C
      CALL JEMARQ()
      PGCANC       = PGC
      PGC          = 'RLDUC8'
      UALF(1:19)   = NOMMAT
      NOMDIA(1:19) = NOMMAT
C
C     --- CREATION/RAPPEL D'UN TABLEAU POUR STOCKER LA DIAGONALE -------
      CALL WKVECT(NOMDIA,'V V C',NEQ,LDIAG)
C
C     ------------------------------------------------------------------
C     --- PREMIERE  PARTIE : RESOLUTION DESCENDANTE ---
C     --- ET REMPLI LE TABLEAU DIAGONAL POUR L'ETAPE SUIVANTE
C     --- DANS L'ETAPE DE RESOLUTION DESCENDANTE, SEULE EST
C     --- CONCERNEE LA PARTIE INFERIEURE DE LA MATRICE
C
      DO 100 IBLOC = 1 , NBBLOC
         IF (ABLO(IBLOC).GE.NEQ) GO TO 101
C
C        -- IDERBL EST LE NUMERO DU DERNIER BLOC A TRAITER:
C
C-     ACCES EN LECTURE AU BLOC INF COURANT
C
         IDERBL=IBLOC+NBBLOC
         CALL JEVEUO( JEXNUM(UALF,IBLOC+NBBLOC),'L',LMAT )
         DO 110 IEQUA = ABLO(IBLOC)+1 ,ABLO(IBLOC+1)
            IF (IEQUA.GT.NEQ) GO TO 111
            ILONG = HCOL (IEQUA)
C-  ADRESSE DANS LE BLOC INF DU TERME DIAGONAL CORRESPONDANT A
C-  L'EQUATION IEQUA
            IADIA = LMAT + ADIA(IEQUA) - 1
C-  ADRESSE DANS LE BLOC INF DU PREMIER TERME NON NUL SUR LA
C-  LIGNE IEQUA
            IDE   = IADIA - ILONG + 1
C-  INDICE DU PREMIER TERME NON NUL SUR LA LIGNE IEQUA
            IXX   = IEQUA - ILONG + 1
            ZC(LDIAG+IEQUA-1) = ZC(IADIA)
            DO 120 ISOL = 1, NBSOL
               C8VAL = (0.D0 , 0.D0)
               DO 130 I = 0, ILONG - 2
                  C8VAL = C8VAL + XSOL(IXX+I,ISOL) * ZC(IDE+I)
  130          CONTINUE
               XSOL(IEQUA,ISOL) = XSOL(IEQUA,ISOL) - C8VAL
               XSOL(IEQUA,ISOL) = XSOL(IEQUA,ISOL)/ZC(LDIAG+IEQUA-1)
  120       CONTINUE
  110    CONTINUE
  111    CONTINUE
         CALL JELIBE(JEXNUM(UALF,IBLOC+NBBLOC))
  100 CONTINUE
  101 CONTINUE
C
C
C     --- DEUXIEME  PARTIE : RESOLUTION REMONTANTE ---
C     --- UTILISATION DES BLOCS SUP DE LA MATRICE   ---
      IDERBL = NBBLOC + NBBLOC
      DO 300 IBLOC = IDERBL , NBBLOC+1 , -1
         CALL JEVEUO(JEXNUM(UALF,IBLOC-NBBLOC),'L',LMAT)
         DO 310 IEQUA = ABLO(IBLOC-NBBLOC+1), ABLO(IBLOC-NBBLOC)+1, -1
            IF (IEQUA.GT.NEQ) GO TO 310
            ILONG  = HCOL(IEQUA)
C-  ADRESSE DANS LE BLOC SUP DU TERME DIAGONAL CORRESPONDANT A
C-  L'EQUATION IEQUA
            IADIA  = LMAT + ADIA(IEQUA) - 1
C-  ADRESSE DANS LE BLOC SUP DU PREMIER TERME NON NUL SUR LA
C-  COLONNE IEQUA
            IDE   = IADIA - ILONG + 1
C-  INDICE DU PREMIER TERME NON NUL SUR LA COLONNE IEQUA
            IXX   = IEQUA - ILONG + 1
            DO 320 ISOL=1,NBSOL
               C8VAL   = - XSOL(IEQUA,ISOL)
               IF (C8VAL .NE. 0 ) THEN
                 DO 330 I = 0, ILONG - 2
                    XSOL(IXX+I,ISOL)=XSOL(IXX+I,ISOL)+C8VAL*ZC(IDE+I)
C                    XSOL(IXX+I,ISOL)=XSOL(IXX+I,ISOL)+C8VAL*ZC(IDE+I)/
C     1              ZC(LDIAG+IXX+I-1)
 330             CONTINUE
               ENDIF
 320        CONTINUE
 310     CONTINUE
         CALL JELIBE(JEXNUM(UALF,IBLOC-NBBLOC))
 300  CONTINUE
C
      CALL JEDETR(NOMDIA)
      PGC = PGCANC
C
      CALL JEDEMA()
      END
