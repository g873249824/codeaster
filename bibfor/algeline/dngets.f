      SUBROUTINE DNGETS
     &  (ISHIFT, WHICH, KEV, NP, RITZR, RITZI, BOUNDS, SHIFTR,
     &   SHIFTI )
C---------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 20/09/2002   AUTEUR D6BHHJP J.P.LEFEBVRE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     SUBROUTINE ARPACK CALCULANT NP SHIFTS DU RESTART DE IRAM.
C---------------------------------------------------------------------
C BEGINDOC
C
C NAME: DNGETS
C
C DESCRIPTION:
C  GIVEN THE EIGENVALUES OF THE UPPER HESSENBERG MATRIX H,
C  COMPUTES THE NP SHIFTS AMU THAT ARE ZEROS OF THE POLYNOMIAL OF
C  DEGREE NP WHICH FILTERS OUT COMPONENTS OF THE UNWANTED EIGENVECTORS
C  CORRESPONDING TO THE AMU'S BASED ON SOME GIVEN CRITERIA.
C
C  NOTE: CALL THIS EVEN IN THE CASE OF USER SPECIFIED SHIFTS IN ORDER
C  TO SORT THE EIGENVALUES, AND ERROR BOUNDS OF H FOR LATER USE.
C
C
C ARGUMENTS
C  ISHIFT  INTEGER.  (INPUT)
C          METHOD FOR SELECTING THE IMPLICIT SHIFTS AT EACH ITERATION.
C          ISHIFT = 0: USER SPECIFIED SHIFTS
C          ISHIFT = 1: EXACT SHIFT WITH RESPECT TO THE MATRIX H.
C
C  WHICH   CHARACTER*2.  (INPUT)
C          SHIFT SELECTION CRITERIA.
C          'LM' -> WANT THE KEV EIGENVALUES OF LARGEST MAGNITUDE.
C          'SM' -> WANT THE KEV EIGENVALUES OF SMALLEST MAGNITUDE.
C          'LR' -> WANT THE KEV EIGENVALUES OF LARGEST REAL PART.
C          'SR' -> WANT THE KEV EIGENVALUES OF SMALLEST REAL PART.
C          'LI' -> WANT THE KEV EIGENVALUES OF LARGEST IMAGINARY PART.
C          'SI' -> WANT THE KEV EIGENVALUES OF SMALLEST IMAGINARY PART.
C
C  KEV   INTEGER.  (INPUT/OUTPUT)
C     INPUT: KEV+NP IS THE SIZE OF THE MATRIX H.
C     OUTPUT: POSSIBLY INCREASES KEV BY ONE TO KEEP COMPLEX CONJUGATE
C     PAIRS TOGETHER.
C
C  NP   INTEGER.  (INPUT/OUTPUT)
C       NUMBER OF IMPLICIT SHIFTS TO BE COMPUTED.
C       OUTPUT: POSSIBLY DECREASES NP BY ONE TO KEEP COMPLEX CONJUGATE
C       PAIRS TOGETHER.
C
C  RITZR,  REAL*8 ARRAY OF LENGTH KEV+NP.  (INPUT/OUTPUT)
C  RITZI   ON INPUT, RITZR AND RITZI CONTAIN THE REAL AND IMAGINARY
C       PARTS OF THE EIGENVALUES OF H.
C       ON OUTPUT, RITZR AND RITZI ARE SORTED SO THAT THE UNWANTED
C       EIGENVALUES ARE IN THE FIRST NP LOCATIONS AND THE WANTED
C       PORTION IS IN THE LAST KEV LOCATIONS.  WHEN EXACT SHIFTS ARE
C       SELECTED, THE UNWANTED PART CORRESPONDS TO THE SHIFTS TO
C       BE APPLIED. ALSO, IF ISHIFT .EQ. 1, THE UNWANTED EIGENVALUES
C       ARE FURTHER SORTED SO THAT THE ONES WITH LARGEST RITZ VALUES
C       ARE FIRST.
C
C  BOUNDS  REAL*8 ARRAY OF LENGTH KEV+NP.  (INPUT/OUTPUT)
C          ERROR BOUNDS CORRESPONDING TO THE ORDERING IN RITZ.
C
C  SHIFTR, SHIFTI  *** USE DEPRECATED AS OF VERSION 2.1. ***
C
C
C ENDDOC
C-----------------------------------------------------------------------
C BEGINLIB
C
C ROUTINES CALLED:
C     DSORTC  ARPACK SORTING ROUTINE.
C
C INTRINSIC FUNCTIONS
C     ABS
C
C AUTHOR
C     DANNY SORENSEN               PHUONG VU
C     RICHARD LEHOUCQ              CRPC / RICE UNIVERSITY
C     DEPT. OF COMPUTATIONAL &     HOUSTON, TEXAS
C     APPLIED MATHEMATICS
C     RICE UNIVERSITY
C     HOUSTON, TEXAS
C
C REVISION HISTORY:
C     XX/XX/92: VERSION ' 2.1'
C
C FILE: NGETS.F   SID: 2.3   DATE OF SID: 4/20/96   RELEASE: 2
C
C ASTER INFORMATION
C 07/01/2000 TOILETTAGE DU FORTRAN SUIVANT LES REGLES ASTER,
C            DISPARITION DE SECOND,
C            COMMON TIMING REMPLACE PAR COMMON INFOR,
C            IMPLICIT NONE.
C
C ENDLIB
C-----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE

C     %-----------------------------%
C     | INCLUDE FILES FOR DEBUGGING |
C     %-----------------------------%

      INTEGER LOGFIL, NDIGIT, MGETV0,
     &  MNAUPD, MNAUP2, MNAITR, MNEIGH, MNAPPS, MNGETS, MNEUPD
      COMMON /DEBUG/
     &  LOGFIL, NDIGIT, MGETV0,
     &  MNAUPD, MNAUP2, MNAITR, MNEIGH, MNAPPS, MNGETS, MNEUPD

C     %------------------%
C     | SCALAR ARGUMENTS |
C     %------------------%

      CHARACTER*2 WHICH
      INTEGER    ISHIFT, KEV, NP
C
C     %-----------------%
C     | ARRAY ARGUMENTS |
C     %-----------------%

      REAL*8 BOUNDS(KEV+NP), RITZR(KEV+NP), RITZI(KEV+NP),
     &  SHIFTR(1), SHIFTI(1)

C     %------------%
C     | PARAMETERS |
C     %------------%

      REAL*8 ZERO
      PARAMETER (ZERO = 0.0D+0)

C     %---------------%
C     | LOCAL SCALARS |
C     %---------------%

      INTEGER    MSGLVL

C     %-----------------------%
C     | EXECUTABLE STATEMENTS |
C     %-----------------------%

C     %-------------------------------%
C     | INITIALIZE TIMING STATISTICS  |
C     | & MESSAGE LEVEL FOR DEBUGGING |
C     %-------------------------------%
C
      MSGLVL = MNGETS

C     %----------------------------------------------------%
C     | LM, SM, LR, SR, LI, SI CASE.                       |
C     | SORT THE EIGENVALUES OF H INTO THE DESIRED ORDER   |
C     | AND APPLY THE RESULTING ORDER TO BOUNDS.           |
C     | THE EIGENVALUES ARE SORTED SO THAT THE WANTED PART |
C     | ARE ALWAYS IN THE LAST KEV LOCATIONS.              |
C     | WE FIRST DO A PRE-PROCESSING SORT IN ORDER TO KEEP |
C     | COMPLEX CONJUGATE PAIRS TOGETHER                   |
C     %----------------------------------------------------%

      IF (WHICH .EQ. 'LM') THEN
         CALL DSORTC ('LR', .TRUE., KEV+NP, RITZR, RITZI, BOUNDS)
      ELSE IF (WHICH .EQ. 'SM') THEN
         CALL DSORTC ('SR', .TRUE., KEV+NP, RITZR, RITZI, BOUNDS)
      ELSE IF (WHICH .EQ. 'LR') THEN
         CALL DSORTC ('LM', .TRUE., KEV+NP, RITZR, RITZI, BOUNDS)
      ELSE IF (WHICH .EQ. 'SR') THEN
         CALL DSORTC ('SM', .TRUE., KEV+NP, RITZR, RITZI, BOUNDS)
      ELSE IF (WHICH .EQ. 'LI') THEN
         CALL DSORTC ('LM', .TRUE., KEV+NP, RITZR, RITZI, BOUNDS)
      ELSE IF (WHICH .EQ. 'SI') THEN
         CALL DSORTC ('SM', .TRUE., KEV+NP, RITZR, RITZI, BOUNDS)
      END IF

      CALL DSORTC (WHICH, .TRUE., KEV+NP, RITZR, RITZI, BOUNDS)

C     %-------------------------------------------------------%
C     | INCREASE KEV BY ONE IF THE ( RITZR(NP),RITZI(NP) )    |
C     | = ( RITZR(NP+1),-RITZI(NP+1) ) AND RITZ(NP) .NE. ZERO |
C     | ACCORDINGLY DECREASE NP BY ONE. IN OTHER WORDS KEEP   |
C     | COMPLEX CONJUGATE PAIRS TOGETHER.                     |
C     %-------------------------------------------------------%

      IF (       ( RITZR(NP+1) - RITZR(NP) ) .EQ. ZERO
     &     .AND. ( RITZI(NP+1) + RITZI(NP) ) .EQ. ZERO ) THEN
         NP = NP - 1
         KEV = KEV + 1
      END IF

      IF ( ISHIFT .EQ. 1 ) THEN

C        %-------------------------------------------------------%
C        | SORT THE UNWANTED RITZ VALUES USED AS SHIFTS SO THAT  |
C        | THE ONES WITH LARGEST RITZ ESTIMATES ARE FIRST        |
C        | THIS WILL TEND TO MINIMIZE THE EFFECTS OF THE         |
C        | FORWARD INSTABILITY OF THE ITERATION WHEN THEY SHIFTS |
C        | ARE APPLIED IN SUBROUTINE DNAPPS.                     |
C        | BE CAREFUL AND USE 'SR' SINCE WE WANT TO SORT BOUNDS! |
C        %-------------------------------------------------------%

         CALL DSORTC ( 'SR', .TRUE., NP, BOUNDS, RITZR, RITZI )
      END IF
C
      IF (MSGLVL .GT. 0) THEN
         CALL IVOUT (LOGFIL, 1, KEV, NDIGIT, '_NGETS: KEV IS')
         CALL IVOUT (LOGFIL, 1, NP, NDIGIT, '_NGETS: NP IS')
         CALL DVOUT (LOGFIL, KEV+NP, RITZR, NDIGIT,
     &        '_NGETS: EIGENVALUES OF CURRENT H MATRIX -- REAL PART')
         CALL DVOUT (LOGFIL, KEV+NP, RITZI, NDIGIT,
     &        '_NGETS: EIGENVALUES OF CURRENT H MATRIX -- IMAG PART')
         CALL DVOUT (LOGFIL, KEV+NP, BOUNDS, NDIGIT,
     &      '_NGETS: RITZ ESTIMATES OF THE CURRENT KEV+NP RITZ VALUES')
      END IF

C     %---------------%
C     | END OF DNGETS |
C     %---------------%

      END
