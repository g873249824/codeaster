      SUBROUTINE DEELPO(CAELEM,NOMA,NUMAIL,PHIE)
      IMPLICIT NONE
      INTEGER      NUMAIL
      REAL*8       PHIE
      CHARACTER*8  CAELEM,NOMA
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 14/01/2013   AUTEUR FLEJOU J-L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C RECUPERATION DU DIAMETRE EXTERIEUR D'UN ELEMENT DE POUTRE
C-----------------------------------------------------------------------
C  IN : CAELEM : NOM DU CONCEPT DE TYPE CARA_ELEM
C  IN : NOMA   : NOM DU CONCEPT DE TYPE MAILLAGE
C  IN : NUMAIL : NUMERO DE LA MAILLE CORRESPONDANTE
C  OUT: PHIE   : DIAMETRE EXTERIEUR SUR L'ELEMENT
C-----------------------------------------------------------------------
C
      INCLUDE 'jeveux.h'
      CHARACTER*32 JEXNUM,JEXNOM
C
      INTEGER        IAS,IASBON,IASEDI,IASMAX,ICAD,ICAV,ICMP,IER
      INTEGER        ICODE,IGLMA,IGRAND,IMA,INOMCP,IRANG1
      INTEGER        IRANG2,IRANV1,IRANV2,IRET,NBCMP,NBMA
      INTEGER        NUENTI,NUMA,NBEC,INDIK8
C
      LOGICAL        EXISDG
C
      REAL*8         R8PREM,DIFR,R1,R2,TOLR
C
      CHARACTER*1    K1BID
      CHARACTER*8    NOMCMP(2),NOMAIL,K8BID
      CHARACTER*19   CARTE
      CHARACTER*24   CADESC,CAVALE,CALIMA,GPMAMA,NOMAMA
C
      DATA NOMCMP /'R1      ','R2      '/
C
C-----------------------------------------------------------------------
      CALL JEMARQ()
C
C-----1.ACCES AUX OBJETS UTILES
C
      GPMAMA = NOMA//'.GROUPEMA'
C
      CARTE  = CAELEM//'.CARGEOPO'
      CADESC = CARTE//'.DESC'
      CAVALE = CARTE//'.VALE'
      CALIMA = CARTE//'.LIMA'
      CALL JEEXIN(CADESC,IRET)
      IF (IRET.EQ.0) CALL U2MESS('F','ALGELINE_33')
      CALL JEVEUO(CADESC,'L',ICAD)
      CALL JEVEUO(CAVALE,'L',ICAV)
C
C     DETERMINATION DU NUMERO D'ASSOCIATION CORRESPONDANT DANS LA CARTE
      IASMAX = ZI(ICAD+1)
      IASEDI = ZI(ICAD+2)
      IASBON = 0
C
      DO 10 IAS = 1,IASEDI
         ICODE  = ZI(ICAD+3+2*(IAS-1))
         NUENTI = ZI(ICAD+3+2*(IAS-1)+1)
C        SI AFFECATION SUR UN GROUPE DE MAILLE
         IF (ICODE.EQ.2) THEN
            CALL JEVEUO(JEXNUM(GPMAMA,NUENTI),'L',IGLMA)
            CALL JELIRA(JEXNUM(GPMAMA,NUENTI),'LONMAX',NBMA,K1BID)
C        SI AFFECATION SUR UNE LISTE DE MAILLE
         ELSE IF (ICODE.EQ.3) THEN
            CALL JEVEUO(JEXNUM(CALIMA,NUENTI),'L',IGLMA)
            CALL JELIRA(JEXNUM(CALIMA,NUENTI),'LONMAX',NBMA,K1BID)
         ENDIF
C        RECHERCHE DE LA MAILLE
         DO 30 IMA = 1,NBMA
            NUMA = ZI(IGLMA+IMA-1)
            IF (NUMA.EQ.NUMAIL) THEN
               IASBON = IAS
               GOTO 40
            ENDIF
30       CONTINUE
C
10    CONTINUE
C
      IF (IASBON.EQ.0) THEN
         NOMAMA = NOMA//'.NOMMAI'
         CALL JENUNO(JEXNUM(NOMAMA,NUMAIL),NOMAIL)
         CALL U2MESK('F','ALGELINE_34',1,NOMAIL)
      ENDIF
C
40    CONTINUE
C
C     EXTRACTION DES RAYONS EXTERIEURS AUX DEUX EXTREMITES DE L'ELEMENT
C       SI LE RAYON EXTERIEUR EST CONSTANT SUR L'ELEMENT, ON DEDUIT
C       LE DIAMETRE EXTERIEUR
C
      IGRAND = ZI(ICAD)
      CALL JELIRA(JEXNUM('&CATA.GD.NOMCMP',IGRAND),'LONMAX',NBCMP,K1BID)
      CALL JEVEUO(JEXNOM('&CATA.GD.NOMCMP','CAGEPO'),'L',INOMCP)
C     NOMBRE D'ENTIERS CODES DANS LA CARTE
      CALL DISMOI('F','NB_EC','CAGEPO','GRANDEUR',NBEC,K8BID,IER)
      IRANG1 = INDIK8( ZK8(INOMCP) , NOMCMP(1) , 1 , NBCMP )
      IRANG2 = INDIK8( ZK8(INOMCP) , NOMCMP(2) , 1 , NBCMP )
      IF (IRANG1.EQ.0 .OR. IRANG2.EQ.0) CALL U2MESS('F','ALGELINE_35')
      ICODE = ZI(ICAD-1+3+2*IASMAX+NBEC*(IASBON-1)+1)
      IRANV1 = 0
      DO 61 ICMP = 1,IRANG1
         IF ( EXISDG(ICODE,ICMP) ) IRANV1 = IRANV1 + 1
61    CONTINUE
      IRANV2 = 0
      DO 62 ICMP = 1,IRANG2
         IF ( EXISDG(ICODE,ICMP) ) IRANV2 = IRANV2 + 1
62    CONTINUE
      IF (IRANV1.EQ.0 .OR. IRANV2.EQ.0) CALL U2MESS('F','ALGELINE_36')
C
      R1 = ZR(ICAV+NBCMP*(IASBON-1)+IRANV1-1)
      R2 = ZR(ICAV+NBCMP*(IASBON-1)+IRANV2-1)
      IF (R1.EQ.0.D0 .OR. R2.EQ.0.D0) CALL U2MESS('F','ALGELINE_37')
      TOLR = R8PREM()
      DIFR = DBLE(ABS(R1-R2))
      IF (DIFR.GT.R1*TOLR) CALL U2MESS('F','ALGELINE_38')
C
      PHIE = 2.D0*R1
C
      CALL JEDEMA()
C
      END
