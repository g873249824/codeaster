      SUBROUTINE XSIGTH(NDIM,NNOP,NFH,IGEOM,LONCH,INST,NBSIG,SIGTH)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 29/04/2013   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
C
      INTEGER       NDIM,NNOP,NFH,IGEOM,NBSIG,LONCH(10)
      REAL*8        SIGTH(*),INST
C
C ----------------------------------------------------------------------
C FONCTION REALISEE:
C
C      CALCUL DES CONTRAINTES THERMIQUES POUR LES ELEMENTS X-FEM
C
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  NNOP    : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C IN  NFH     : NOMBRE DE FONCTIONS HEAVYSIDE
C IN  IGEOM   : COORDONEES DES NOEUDS
C IN  PINTT   : COORDONNÉES DES POINTS D'INTERSECTION
C IN  LONCH   : LONGUEURS DES CHAMPS UTILISÉES
C IN  SIGMA   : CONTRAINTES DE CAUCHY AUX POINTS DE GAUSS DES SOUS-ÉLTS
C IN  NBSIG   : NOMBRE DE CONTRAINTES ASSOCIE A L'ELEMENT
C IN  PMILT   : COORDONNEES DES POINTS MILIEUX
C IN  INST    : INSTANT
C IN  NBSIG   : DIMENSION DU TENSEUR DES CONTRAINTES
C
C OUT SIGTH   : CONTRAINTES THERMIQUES
C
C ----------------------------------------------------------------------
C
      REAL*8       R8BI7(7),R8BI3(3),EPSTH(6),D(36)
      INTEGER      NSE,IDECPG,IDEBS,IRET,IPG,I,ISE,IN,INO,NPG,J
      INTEGER      IMATE,IRESE,NNO,IG,IBID,KPG
      LOGICAL      ISELLI
      CHARACTER*2  K2BID
      CHARACTER*8  ELREFP,ELRESE(6),FAMI(6)
      CHARACTER*16 PHENOM,OPTION
C
      PARAMETER    (OPTION='CHAR_MECA_TEMP_R')
      DATA          ELRESE /'SE2','TR3','TE4','SE3','TR6','TE4'/
      DATA          FAMI   /'BID','XINT','XINT','BID','XINT','XINT'/
C
C ----------------------------------------------------------------------
C
      CALL ELREF1(ELREFP)
C
C     INITIALISATION DES VECTEURS BIDONS
      CALL VECINI(7,0.D0,R8BI7)
      CALL VECINI(3,0.D0,R8BI3)

C     ON AUTORISE UNIQUEMENT L'ISOTROPIE
      CALL JEVECH('PMATERC','L',IMATE)
      CALL RCCOMA(ZI(IMATE),'ELAS',1,PHENOM,IRET)
      CALL ASSERT(IRET.EQ.0 .AND. PHENOM.EQ.'ELAS')
      CALL TECACH('ONO','PCAMASS','L',1,IBID,IRET)
      CALL ASSERT(IRET.NE.0)
      R8BI7(1) = 1.D0
C
C     SOUS-ELEMENT DE REFERENCE : RECUP DE NNO ET NPG
      IF (.NOT.ISELLI(ELREFP).AND. NDIM.LE.2) THEN
        IRESE=3
      ELSE
        IRESE=0
      ENDIF
      CALL ELREF5(ELRESE(NDIM+IRESE),FAMI(NDIM+IRESE),IBID,NNO,
     &            IBID,NPG,IBID,IBID,IBID,IBID,IBID,IBID)
C
C     RÉCUPÉRATION DE LA SUBDIVISION DE L'ÉLÉMENT EN NSE SOUS ELEMENT
      NSE=LONCH(1)
C
C       BOUCLE SUR LES NSE SOUS-ELEMENTS
      DO 110 ISE=1,NSE
C
C       DEBUT DE LA ZONE MÉMOIRE DE SIGMA CORRESPONDANTE
        IDECPG = NPG   * (ISE-1)
        IDEBS  = NBSIG * IDECPG
        IF (NDIM .EQ. 3) THEN
          CALL ASSERT(NBSIG.EQ.6)
        ELSEIF (NDIM.EQ.2) THEN
          CALL ASSERT(NBSIG.EQ.4)
        ENDIF
C
C       BOUCLE SUR LES POINTS DE GAUSS DU SOUS-ELEMENT
        DO 120 KPG=1,NPG
C
C         NUMERO DU PG DANS LE FAMILLE 'XFEM'
          IPG = IDECPG + KPG
C
C         CALCUL DES DEFORMATIONS THERMIQUES EPSTH
          CALL VECINI( 6,0.D0,EPSTH)
          CALL EPSTMC('XFEM',NDIM,INST,'+',IPG,1,
     &                R8BI3,R8BI7,ZI(IMATE),OPTION,EPSTH)
C
C         CALCUL DE LA MATRICE DE HOOKE (MATERIAU ISOTROPE)
          CALL VECINI(36,0.D0,D    )
          CALL DMATMC('XFEM',K2BID, ZI(IMATE), INST,'+',IPG,1,
     &                R8BI7, R8BI3, NBSIG, D)
C
C         CONTRAINTES THERMIQUES AU PG COURANT
          DO 60 I = 1, NBSIG
             DO 70 J = 1, NBSIG
                SIGTH(IDEBS+NBSIG*(KPG-1)+I) 
     &          = SIGTH(IDEBS+NBSIG*(KPG-1)+I)+D(J+(I-1)*NBSIG)*EPSTH(J)
  70         CONTINUE
  60      CONTINUE
C
 120    CONTINUE
C
 110  CONTINUE
C
      END
