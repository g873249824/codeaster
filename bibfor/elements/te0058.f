      SUBROUTINE TE0058(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*16      OPTION,NOMTE
C.......................................................................
C
C     BUT: CALCUL DES VECTEURS ELEMENTAIRES EN THERMIQUE
C          CORRESPONDANT AU FLUX IMPOSE (FONCTION)
C          SUR DES FACES D'ELEMENTS ISOPARAMETRIQUES 3D
C
C          OPTION : 'CHAR_THER_FLUN_F'
C
C     ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................
C
      CHARACTER*8        NOMPAR(4)
      REAL*8             NX,NY,NZ,SX(9,9),SY(9,9),SZ(9,9),JAC,THETA
      REAL*8             VALPAR(4),XX,YY,ZZ,FLUX
      INTEGER            IPOIDS,IVF,IDFDX,IDFDY,IGEOM,ITEMPS
      INTEGER            NDIM,NNO,IPG,NPG1,IVECTT,IFLUX,IDEC,JDEC,KDEC
      INTEGER            LDEC,NNOS,JGANO
C
C-----------------------------------------------------------------------
      INTEGER I ,IER ,INO ,J ,JNO
      REAL*8 FLUN ,FLUNP1
C-----------------------------------------------------------------------
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDX,JGANO)
      IDFDY  = IDFDX  + 1
C
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PFLUXNF','L',IFLUX)
      CALL JEVECH('PTEMPSR','L',ITEMPS)
      CALL JEVECH('PVECTTR','E',IVECTT)
C
      THETA     = ZR(ITEMPS+2)
      NOMPAR(1) = 'X'
      NOMPAR(2) = 'Y'
      NOMPAR(3) = 'Z'
      NOMPAR(4) = 'INST'
C
      DO 10 I = 1,NNO
        ZR(IVECTT+I-1) = 0.0D0
 10   CONTINUE
C
C    CALCUL DES PRODUITS VECTORIELS OMI X OMJ
C
      DO 1 INO = 1,NNO
        I = IGEOM + 3*(INO-1) -1
        DO 2 JNO = 1,NNO
          J = IGEOM + 3*(JNO-1) -1
          SX(INO,JNO) = ZR(I+2) * ZR(J+3) - ZR(I+3) * ZR(J+2)
          SY(INO,JNO) = ZR(I+3) * ZR(J+1) - ZR(I+1) * ZR(J+3)
          SZ(INO,JNO) = ZR(I+1) * ZR(J+2) - ZR(I+2) * ZR(J+1)
 2      CONTINUE
 1    CONTINUE
C
C    BOUCLE SUR LES POINTS DE GAUSS
C
      DO 101 IPG=1,NPG1
        KDEC = (IPG-1)*NNO*NDIM
        LDEC = (IPG-1)*NNO
C
C    CALCUL DE FLUN
C
        XX = 0.D0
        YY = 0.D0
        ZZ = 0.D0
        DO 12 I = 1,NNO
          XX = XX + ZR(IGEOM+3*I-3) * ZR(IVF+LDEC+I-1)
          YY = YY + ZR(IGEOM+3*I-2) * ZR(IVF+LDEC+I-1)
          ZZ = ZZ + ZR(IGEOM+3*I-1) * ZR(IVF+LDEC+I-1)
 12     CONTINUE
        VALPAR(1) = XX
        VALPAR(2) = YY
        VALPAR(3) = ZZ
        VALPAR(4) = ZR(ITEMPS)
        CALL FOINTE('FM',ZK8(IFLUX),4,NOMPAR,VALPAR,FLUNP1,IER)
        IF ( THETA .NE. 1.0D0 ) THEN
          VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
          CALL FOINTE('FM',ZK8(IFLUX),4,NOMPAR,VALPAR,FLUN,IER)
        ELSE
          FLUN = 0.0D0
        ENDIF
        FLUX = THETA*FLUNP1 + (1.0D0-THETA)*FLUN
C
        NX = 0.0D0
        NY = 0.0D0
        NZ = 0.0D0
C
C   CALCUL DE LA NORMALE AU POINT DE GAUSS IPG
C
        DO 102 I=1,NNO
          IDEC = (I-1)*NDIM
          DO 102 J=1,NNO
            JDEC = (J-1)*NDIM
            NX = NX + ZR(IDFDX+KDEC+IDEC) *ZR(IDFDY+KDEC+JDEC) *SX(I,J)
            NY = NY + ZR(IDFDX+KDEC+IDEC) *ZR(IDFDY+KDEC+JDEC) *SY(I,J)
            NZ = NZ + ZR(IDFDX+KDEC+IDEC) *ZR(IDFDY+KDEC+JDEC) *SZ(I,J)
 102    CONTINUE
C
C   CALCUL DU JACOBIEN AU POINT DE GAUSS IPG
C
        JAC = SQRT(NX*NX + NY*NY + NZ*NZ)
CCDIR$ IVDEP
        DO 103 I=1,NNO
          ZR(IVECTT+I-1) = ZR(IVECTT+I-1) + JAC *
     &    ZR(IPOIDS+IPG-1) * FLUX * ZR(IVF+LDEC+I-1)
 103    CONTINUE
C
 101    CONTINUE
      END
