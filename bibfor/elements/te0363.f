       SUBROUTINE TE0363(OPTION,NOMTE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 01/09/2009   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT   NONE
      CHARACTER*16 OPTION,NOMTE
C
C ----------------------------------------------------------------------
C  CONTACT XFEM GRANDS GLISSEMENTS
C  REACTUALISATION DU STATUT DE CONTACT
C
C  OPTION : 'XCVBCA' (X-FEM MISE À JOUR DU STATUT DE CONTACT)
C
C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX --------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX --------------------
C
      CHARACTER*8   TYPMA,ESC,MAIT,ELC
      INTEGER      NDIM,NDDL,NNE,NNES,NNM,NNC
      INTEGER      JPCPO,JPCAI
      INTEGER      IGEOM,IGEOMM,IDEPM,IDEPL
      INTEGER      INDCO,MEMCO,INDNOR,IGLISS
      INTEGER      JOUT1,JOUT2,JOUT3
      INTEGER      NFAES,INCOCA
      REAL*8       JEU,TAU1(3),TAU2(3),NORM(3)
      REAL*8       GEOME(3),GEOMM(3),COORE(3),COORM(3)
      REAL*8       DEPLME(9),DEPLMM(6),DEPLE(9),DEPLM(6)
      REAL*8       FFPC(8),FFES(8),FFMA(8),COOR(2)
      REAL*8       PREC,NOOR,R8PREM,RBID
      INTEGER      I,J,IN,PL
      INTEGER      CFACE(5,3),XOULA
      PARAMETER    (PREC=1.D-16)
      LOGICAL      MALIN
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INFOS SUR LA MAILLE DE CONTACT
C
      CALL XMELET(NOMTE,TYPMA,ESC,MAIT,ELC,
     &            NDIM,NDDL,NNE,NNM,NNC,MALIN)
      CALL ASSERT (NDDL.LE.120)
C
C --- INITIALISATIONS
C
      CALL VECINI(NDIM,0.D0,GEOME)
      CALL VECINI(NDIM,0.D0,GEOMM)
      CALL VECINI(2*NDIM,0.D0,DEPLM)
      CALL VECINI(2*NDIM,0.D0,DEPLMM)
      CALL VECINI(3*NDIM,0.D0,DEPLE)
      CALL VECINI(3*NDIM,0.D0,DEPLME)
C --- INITIALISATION DE LA VARIABLE DE TRAVAIL
      INCOCA=0
C
C --- RECUPERATION DES DONNEES DE LA CARTE CONTACT 'POINT' (VOIR XMCART)
C
      CALL JEVECH('PCAR_PT','L',JPCPO)
C --- LES COORDONNEES ESCLAVE DANS L'ELEMENT DE CONTACT
      COOR(1) = ZR(JPCPO-1+1)
      COOR(2) = ZR(JPCPO-1+10)
      TAU1(1)  = ZR(JPCPO-1+4)
      TAU1(2)  = ZR(JPCPO-1+5)
      TAU1(3)  = ZR(JPCPO-1+6)
      TAU2(1)  = ZR(JPCPO-1+7)
      TAU2(2)  = ZR(JPCPO-1+8)
      TAU2(3)  = ZR(JPCPO-1+9)
      INDCO    = NINT(ZR(JPCPO-1+11))
      INDNOR   = NINT(ZR(JPCPO-1+17))
      IGLISS   = NINT(ZR(JPCPO-1+20))
      MEMCO    = NINT(ZR(JPCPO-1+21))
C --- LE NUMERO DE LA FACETTE DE CONTACT ESCLAVE
      NFAES    = NINT(ZR(JPCPO-1+22))
C --- LES COORDONNEES ESCLAVE ET MAITRES DANS L'ELEMENT PARENT
      COORE(1)  = ZR(JPCPO-1+24)
      COORE(2)  = ZR(JPCPO-1+25)
      COORE(3)  = ZR(JPCPO-1+26)
      COORM(1)  = ZR(JPCPO-1+27)
      COORM(2)  = ZR(JPCPO-1+28)
      COORM(3)  = ZR(JPCPO-1+29)
C      
C --- RECUPERATION DES DONNEES DE LA CARTE CONTACT AINTER (VOIR XMCART)
C
      CALL JEVECH('PCAR_AI','L',JPCAI)     
C
C --- RECUPERATION DE LA GEOMETRIE ET DES CHAMPS DE DEPLACEMENT
C
      CALL JEVECH('PGEOMER','E',IGEOM)
      CALL JEVECH('PDEPL_P','E',IDEPL)
      CALL JEVECH('PDEPL_M','L',IDEPM)
C      
C --- RECUPERATION DES CHAMPS DE SORTIE
C
      CALL JEVECH('PINCOCA','E',JOUT1)
      CALL JEVECH('PINDCOT','E',JOUT2)
      CALL JEVECH('PINDMEM','E',JOUT3)
C      
C --- NOMBRE DE NOEUDS ESCLAVES SOMMETES
C
      IF (MALIN) THEN
C --- ON EST EN LINEAIRE
        NNES=NNE
      ELSE
C --- ON EST EN QUADRATIQUE
        NNES=NNE/2
      ENDIF
C
C --- ON CONSTRUIT LA MATRICE DE CONNECTIVITÉ CFACE (MAILLE ESCLAVE), CE
C --- QUI SUIT N'EST VALABLE QU'EN 2D POUR LA FORMULATION QUADRATIQUE,
C --- EN 3D ON UTILISE SEULEMENT LA FORMULATION AUX NOEUDS SOMMETS,
C --- CETTE MATRICE EST DONC INUTILE, ON NE LA CONSTRUIT PAS !!!
C
      CFACE(1,1)=1
      CFACE(1,2)=2
C
C --- REACTUALISATION DE LA GEOMETRIE
C
      CALL XMREAC(NDIM,NNE,NNES,NNM,
     &            IGEOM,IDEPM)
C
C --- FONCTIONS DE FORMES POUR LE POINT DE CONTACT
C
      CALL ELRFVF(ELC,COOR,NNC,FFPC,NNC)
C
C --- FONCTIONS DE FORMES DU POINTS DE CONTACT DANS L'ELEMENT PARENT 
C
      CALL ELRFVF(ESC,COORE,NNES,FFES,NNES)
C
C --- FONCTIONS DE FORMES DE LA PROJ DU PT DE CONTACT DANS L'ELE PARENT
C
      CALL ELRFVF(MAIT,COORM,NNM,FFMA,NNM)
C
C --- CALCUL DES COORDONNÉES RÉELLES DU POINT DE CONTACT
C
      DO 120 I = 1,NDIM
        DO 130 J = 1,NNES
          GEOME(I) = GEOME(I) + 
     &           FFES(J)*ZR(IGEOM-1+NDIM*(J-1)+I)
 130    CONTINUE
 120  CONTINUE
C
C --- CALCUL DES COORDONNÉES RÉELES DU PROJETE DU POINT DE CONTACT
C
      DO 140 I = 1,NDIM
        DO 150 J = 1,NNM
          GEOMM(I) = GEOMM(I) +
     &           FFMA(J)*ZR(IGEOM+NDIM*NNE-1+NDIM*(J-1)+I)
 150    CONTINUE
 140  CONTINUE
C
C --- SI ON EST EN LINEAIRE, ON IMPOSE QUE LE NB DE NOEUDS DE CONTACTS
C --- ET LES FFS DES LAGRANGES DE CONTACT SONT IDENTIQUES A CEUX
C --- DES DEPLACEMENTS DANS LA MAILLE ESCLAVE POUR LE CALCUL DES CONTRIB
C
      IF (MALIN) THEN
        NNC=NNE
        DO 155 I=1,NNE
          FFPC(I)=FFES(I)
 155    CONTINUE
      ENDIF
C
C --- CALCUL DE LA NORMALE
C
      IF (NDIM.EQ.2) THEN
        CALL MMNORM(NDIM,TAU1,TAU2,NORM,NOOR)
      ELSEIF (NDIM.EQ.3) THEN
        CALL PROVEC(TAU1,TAU2,NORM)
        CALL NORMEV(NORM,NOOR)
      ENDIF
      IF (NOOR.LE.R8PREM()) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
C 
C --- CALCUL DES DEPLACEMENTS DU POINT DE CONTACT ET DE SON PROJETE
C 
      DO 180 I = 1,2*NDIM
        DO 190 J = 1,NNES
          DEPLE(I)  = DEPLE(I)  +
     &                FFES(J)*ZR(IDEPL+(J-1)*(3*NDIM)+I-1)
          DEPLME(I) = DEPLME(I) +
     &                FFES(J)*ZR(IDEPM+(J-1)*(3*NDIM)+I-1)
 190    CONTINUE
 180  CONTINUE
C 
      DO 200 I = 1,2*NDIM
        DO 210 J = 1,NNM
          DEPLM(I)  = DEPLM(I)  + 
     & FFMA(J)*ZR(IDEPL+NNES*3*NDIM+(NNE-NNES)*NDIM+2*NDIM*(J-1)+I-1)
          DEPLMM(I) = DEPLMM(I) +
     & FFMA(J)*ZR(IDEPM+NNES*3*NDIM+(NNE-NNES)*NDIM+2*NDIM*(J-1)+I-1)
 210    CONTINUE
 200  CONTINUE
C
C --- CALCUL DES REACTIONS DE CONTACT (PRESSION ET FROTTEMENT)
C
      DO 220 I=1,NNC
C --- XOULA NE SERT PLUS A RIEN AVEC LA FORMULATION NOEUDS SOMMETS !!!
        IN=XOULA(CFACE,NFAES,I,JPCAI,TYPMA)
        CALL XPLMA2(NDIM,NNE,NNES,IN,PL)
        DEPLE(2*NDIM+1)=DEPLE(2*NDIM+1)+FFPC(I)*ZR(IDEPL-1+PL)
        DEPLME(2*NDIM+1)=DEPLME(2*NDIM+1)+FFPC(I)*ZR(IDEPM-1+PL)
 220  CONTINUE
C
C --- EVALUATION DES JEUX - CAS DU CONTACT
C
      CALL XMMJEU(NDIM,NORM,GEOME,GEOMM,DEPLE,DEPLM,
     &            DEPLME,DEPLMM,RBID,JEU)
C
C --- NOEUDS EXCLUS PAR PROJECTION HORS ZONE
C
      IF (INDNOR .EQ. 1) THEN
        ZR(JOUT2)= 0.D0
        GO TO 999
      ENDIF
C
C --- SI LE CONTACT A ETE POSTULE, ON TESTE LA VALEUR DE LA PRESSION
C --- DE CONTACT
C
      IF (INDCO.EQ.1) THEN
        IF (DEPLE(2*NDIM+1).GT.-1.D-3) THEN
          IF ((IGLISS.EQ.1).AND.(MEMCO.EQ.1)) THEN
            ZR(JOUT2) = 1.D0
            ZI(JOUT3) = 1
          ELSE IF (IGLISS.EQ.0) THEN
            ZR(JOUT2) = 0.D0
            INCOCA = 1
          ENDIF
        ELSE
          ZR(JOUT2) = 1.D0
          ZI(JOUT3) = 1
        END IF
C
C --- SI LE NON-CONTACT A ETE POSTULÉ, ON TESTE LA VALEUR DU JEU
C
      ELSE IF (INDCO .EQ. 0) THEN
        IF (JEU.GT.PREC) THEN
          ZR(JOUT2) = 1.D0
          ZI(JOUT3) = 1
          INCOCA = 1
        ELSE
          ZR(JOUT2) = 0.D0
        END IF
C
      ELSE
        CALL U2MESS('F','DVP_98')
      ENDIF
C
 999  CONTINUE
C 
C --- ENREGISTREMENT DU CHAMP DE SORTIE
C
      ZI(JOUT1)=INCOCA
C
      CALL JEDEMA()
      END
