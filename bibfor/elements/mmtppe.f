      SUBROUTINE MMTPPE(TYPMAE,TYPMAM,NDIM  ,NNE   ,NNM   ,
     &                  NNL   ,NBDM  ,DELTAT,THETA ,LAXIS ,
     &                  LDYNA ,LFOVIT,TYPBAR,JEUSUP,PRFUSU,
     &                  FFE   ,FFM   ,FFL   ,NORM  ,TAU1  ,
     &                  TAU2  ,MPROJN,MPROJT,JACOBI,WPG   ,
     &                  DLAGRF,DVITE ,COEFCR,COEFCS)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 18/04/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C TOLE CRP_21
C
      IMPLICIT NONE
      CHARACTER*8  TYPMAE,TYPMAM
      INTEGER      NDIM,NNE,NNM,NNL,NBDM
      REAL*8       FFE(9),FFM(9),FFL(9)
      REAL*8       TAU1(3),TAU2(3)
      REAL*8       NORM(3)
      REAL*8       MPROJT(3,3),MPROJN(3,3)
      INTEGER      TYPBAR
      LOGICAL      LAXIS,LDYNA,LFOVIT
      REAL*8       JACOBI,WPG
      REAL*8       PRFUSU,JEUSUP
      REAL*8       DELTAT,THETA
      REAL*8       DLAGRF(2),DVITE(3)
      REAL*8       COEFCR,COEFCS
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - CALCUL)
C
C PREPARATION DES CALCULS DES MATRICES - CALCUL DES QUANTITES 
C CAS POIN_ELEM
C
C ----------------------------------------------------------------------
C
C
C IN  TYPMAE : NOM DE LA MAILLE ESCLAVE
C IN  TYPMAM : NOM DE LA MAILLE MAITRE
C IN  NDIM   : DIMENSION DE LA MAILLE DE CONTACT
C IN  NNE    : NOMBRE DE NOEUDS DE LA MAILLE ESCLAVE
C IN  NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C IN  NNL    : NOMBRE DE NOEUDS PORTANT UN LAGRANGE DE CONTACT/FROTT
C IN  NBDM   : NOMBRE DE COMPOSANTES/NOEUD DES DEPL+LAGR_C+LAGR_F
C IN  DELTAT : INCREMENT DE TEMPS
C IN  THETA  : COEFFICIENT SCHEMA THETA
C IN  LCOMPL : .TRUE. SI COMPLIANCE ACTIVEE
C IN  LAXIS  : .TRUE. SI AXISYMETRIE
C IN  LDYNA  : .TRUE. SI DYNAMIQUE
C IN  LFOVIT : .TRUE. SI FORMULATION EN VITESSE
C IN  TYPBAR : ARETE OU POINT EN FOND DE FISSURE
C IN  JEUSUP : JEU SUPPLEMENTAIRE PAR DIST_ESCL/DIST_MAIT
C IN  PRFUSU : JEU SUPPLEMENTAIRE PAR PROFONDEUR D'USURE
C OUT FFE    : FONCTIONS DE FORMES DEPL_ESCL
C OUT FFM    : FONCTIONS DE FORMES DEPL_MAIT
C OUT FFL    : FONCTIONS DE FORMES LAGR.
C OUT NORM   : NORMALE
C OUT TAU1   : PREMIER VECTEUR TANGENT
C OUT TAU2   : SECOND VECTEUR TANGENT
C OUT MPROJT : MATRICE DE PROJECTION TANGENTE
C OUT MPROJT : MATRICE DE PROJECTION NORMALE
C OUT JACOBI : JACOBIEN DE LA MAILLE AU POINT DE CONTACT
C OUT WPG    : POIDS DU POINT INTEGRATION DU POINT DE CONTACT
C OUT DVITE  : SAUT DE "VITESSE" [[DELTA X]]
C OUT DLAGRF : INCREMENT DEPDEL DES LAGRANGIENS DE FROTTEMENT
C 
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      INTEGER      JPCF
      INTEGER      JGEOM,JDEPDE,JDEPM
      INTEGER      JACCM,JVITM,JVITP,I
      REAL*8       GEOMAE(9,3),GEOMAM(9,3)
      REAL*8       GEOMM(3),GEOME(3)
      REAL*8       DDEPLE(3),DDEPLM(3)
      REAL*8       DEPLME(3),DEPLMM(3)
      REAL*8       DFFE(2,9),DDFFE(3,9)
      REAL*8       DFFM(2,9),DDFFM(3,9)
      REAL*8       DFFL(2,9),DDFFL(3,9) 
      REAL*8       XPC,YPC,XPR,YPR
      REAL*8       DLAGRC,JEU
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()    
C
C --- RECUPERATION DES DONNEES DU CHAM_ELEM DU CONTACT
C
      CALL JEVECH('PCONFR','L',JPCF )
      XPC      = ZR(JPCF-1+1)
      YPC      = ZR(JPCF-1+2)
      XPR      = ZR(JPCF-1+3)
      YPR      = ZR(JPCF-1+4)
      TAU1(1)  = ZR(JPCF-1+5)
      TAU1(2)  = ZR(JPCF-1+6)
      TAU1(3)  = ZR(JPCF-1+7)
      TAU2(1)  = ZR(JPCF-1+8)
      TAU2(2)  = ZR(JPCF-1+9)
      TAU2(3)  = ZR(JPCF-1+10)
      WPG      = ZR(JPCF-1+11)
C
C --- RECUPERATION DE LA GEOMETRIE ET DES CHAMPS DE DEPLACEMENT
C
      CALL JEVECH('PGEOMER','E',JGEOM )
      CALL JEVECH('PDEPL_P','E',JDEPDE)
      CALL JEVECH('PDEPL_M','L',JDEPM ) 
      IF (LDYNA) THEN
        CALL JEVECH('PVITE_P','L',JVITP )
        CALL JEVECH('PVITE_M','L',JVITM )
        CALL JEVECH('PACCE_M','L',JACCM )
      ENDIF
C
C --- FONCTIONS DE FORMES ET DERIVEES
C
      CALL MMFORM(NDIM  ,TYPMAE,TYPMAM,NNE   ,NNM   ,
     &            XPC   ,YPC   ,XPR   ,YPR   ,FFE   ,
     &            DFFE  ,DDFFE ,FFM   ,DFFM  ,DDFFM ,
     &            FFL   ,DFFL  ,DDFFL )
C
C --- MODIFICATIONS DES FF ET DFF POUR ELEMENTS DE BARSOUM
C
      IF (TYPBAR.NE.0) THEN
        CALL MMMFFM(TYPMAE,XPC   ,YPC   ,TYPBAR,FFE   ,
     &              DFFE  )
        CALL MMMFFM(TYPMAM,XPR   ,YPR   ,TYPBAR,FFM   ,
     &              DFFM  )
        CALL MMMFFM(TYPMAE,XPC   ,YPC   ,TYPBAR,FFL   ,
     &              DFFL  )
      ENDIF
C
C --- JACOBIEN POUR LE POINT DE CONTACT
C
      CALL MMMJAC(TYPMAE,JGEOM ,FFE   ,DFFE  ,LAXIS ,
     &            NNE   ,NDIM  ,JACOBI)
C
C --- CALCUL DE LA NORMALE ET DES MATRICES DE PROJECTION
C
      CALL MMCALN(NDIM  ,TAU1  ,TAU2  ,NORM  ,MPROJN,
     &            MPROJT)
C
C --- REACTUALISATION DE LA GEOMETRIE (MAILLAGE+DEPMOI)
C
      CALL MMREAC(NBDM  ,NDIM  ,NNE   ,NNM   ,JGEOM ,
     &            JDEPM ,GEOMAE,GEOMAM)
C
C --- CALCUL DES COORDONNEES ACTUALISEES
C
      CALL MMGEOM(NDIM  ,NNE   ,NNM   ,FFE   ,FFM   ,
     &            GEOMAE,GEOMAM,GEOME ,GEOMM )
C
C --- CALCUL DES INCREMENTS - LAGRANGE DE CONTACT ET FROTTEMENT
C
      CALL MMLAGM(NBDM  ,NDIM  ,NNL   ,JDEPDE,FFL   ,
     &            DLAGRC,DLAGRF)
C
C --- MISE A JOUR DES CHAMPS INCONNUS INCREMENTAUX - DEPLACEMENTS
C
      CALL MMDEPM(NBDM  ,NDIM  ,NNE   ,NNM   ,JDEPM ,
     &            JDEPDE,FFE   ,FFM   ,DDEPLE,DDEPLM ,
     &            DEPLME,DEPLMM)
C
C --- CALCUL DU JEU
C
      CALL MMMJEU(NDIM,  JEUSUP,PRFUSU,NORM  ,GEOME ,
     &            GEOMM ,DDEPLE,DDEPLM,JEU   )
C
C --- CALCUL DU SAUT DE "VITESSE" [[DELTA X]]
C
      DO 12 I = 1,3
        DVITE(I) = DDEPLE(I) - DDEPLM(I)
   12 CONTINUE
C
C --- COEFFICIENTS MODIFIES POUR FORMULATION EN THETA-VITESSE
C
      IF (LFOVIT) THEN
        COEFCR = COEFCR/DELTAT/THETA
        COEFCS = COEFCS/DELTAT/THETA
      ENDIF
C
      CALL JEDEMA()
C
      END
