      SUBROUTINE TE0354 ( OPTION , NOMTE )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 18/09/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16 OPTION,NOMTE

C ----------------------------------------------------------------------
C                   SOURCE THERMIQUE NON LINEAIRE
C
C    - FONCTION REALISEE:  CALCUL DES MATRICES ELEMENTAIRES
C                          OPTION : 'CHAR_THER_SOURNL'
C                                   'RESI_THER_SOURNL'
C                                   'MTAN_THER_SOURNL'
C
C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................

      INTEGER NNOMAX
      PARAMETER (NNOMAX=27)

      LOGICAL AXI,RESI,LTEATT
C      INTEGER NDIM,NNO,NPG,NNOS,G,I,OS,OSM,M,N,IW,IVF,IDFDE,IRET,JGANO
      INTEGER NDIM,NNO,NPG,NNOS,G,OS,OSM,M,N,IW,IVF,IDFDE,IRET,JGANO
      INTEGER IGEOM,ITEMPS,IVECT,IMATR,ISOUR,ITHER
      REAL*8  DFDX(NNOMAX),DFDY(NNOMAX),DFDZ(NNOMAX),W,RG,THETA,SOUR,TG
      REAL*8  DSDT,COEF,COEFOP,DDOT
C      CHARACTER*8 ELREFE,FCT
      CHARACTER*8 ELREFE


C    LECTURE DES PARAMETRES COMMUNS
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PTEMPSR','L',ITEMPS)
      CALL JEVECH('PSOURNL','L',ISOUR)
      IF (ZK8(ISOUR)(1:7).EQ.'&FOZERO') GOTO 9999
      THETA = ZR(ITEMPS+2)

C    LECTURE DES PARAMETRES SPECIFIQUES A CHAQUE OPTION
      IF (OPTION(1:4).EQ.'CHAR') THEN
        RESI = .TRUE.
        COEFOP = 1-THETA
        CALL JEVECH('PTEMPER','L',ITHER)
        CALL JEVECH('PVECTTR','E',IVECT)
      ELSE IF (OPTION(1:4).EQ.'RESI') THEN
        RESI = .TRUE.
        COEFOP = -THETA
        CALL JEVECH('PTEMPEI','L',ITHER)
        CALL JEVECH('PRESIDU','E',IVECT)
      ELSE
        RESI = .FALSE.
        COEFOP = -THETA
        CALL JEVECH('PTEMPEI','L',ITHER)
        CALL JEVECH('PMATTTR','E',IMATR)
      END IF

C    ACCES AUX CARACTERISTIQUES DE L'ELEMENT FINI
      CALL ELREF1(ELREFE)
      CALL ELREF4(ELREFE,'RIGI',NDIM,NNO,NNOS,NPG,IW,IVF,IDFDE,JGANO)
      AXI = LTEATT(' ','AXIS','OUI')

      DO 100 G=1,NPG
        OS = (G-1)*NNO

C      CALCUL DU POIDS DU POINT DE GAUSS
        IF (NDIM.EQ.2) THEN
          CALL DFDM2D(NNO,G,IW,IDFDE,ZR(IGEOM),DFDX,DFDY,W)
          IF (AXI) THEN
            RG = DDOT(NNO,ZR(IGEOM),2,ZR(IVF+OS),1)
            W  = W*RG
          END IF
        ELSE
          CALL DFDM3D(NNO,G,IW,IDFDE,ZR(IGEOM),DFDX,DFDY,DFDZ,W)
        END IF

C      CALCUL DE LA TEMPERATURE AU POINT DE GAUSS
        TG = DDOT(NNO,ZR(ITHER),1,ZR(IVF+OS),1)

C      CALCUL DU RESIDU
        IF (RESI) THEN

C        CALCUL DE LA SOURCE
          CALL FOINTE('FM',ZK8(ISOUR),1,'TEMP',TG,SOUR,IRET)
          COEF = W*SOUR*COEFOP

C        CONTRIBUTION AU RESIDU
          CALL DAXPY(NNO,COEF,ZR(IVF+OS),1,ZR(IVECT),1)

C      CALCUL DE LA MATRICE TANGENTE (STOCKAGE SYMETRIQUE)
        ELSE

C        CALCUL DE LA DERIVEE DE LA SOURCE PAR RAPPORT A LA TEMPERATURE
          CALL FODERI(ZK8(ISOUR),TG,SOUR,DSDT)
          COEF = W*DSDT*COEFOP

C        CONTRIBUTION A LA MATRICE
          OSM = 0
          DO 110 N = 0,NNO-1
            DO 115 M = 0,N
              ZR(IMATR+OSM)=ZR(IMATR+OSM)+COEF*ZR(IVF+OS+N)*ZR(IVF+OS+M)
              OSM = OSM + 1
 115        CONTINUE
 110      CONTINUE
        END IF
 100  CONTINUE

 9999 CONTINUE
      END
