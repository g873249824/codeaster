      SUBROUTINE MMTRPR(NDIM  ,LPENAF,DJEUT ,DLAGRF,COEFAF,
     &                  TAU1  ,TAU2  ,LADHE ,RESE  ,NRESE )
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 17/10/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INTEGER      NDIM
      REAL*8       DLAGRF(2)
      REAL*8       COEFAF
      REAL*8       DJEUT(3)
      REAL*8       TAU1(3),TAU2(3)
      REAL*8       RESE(3),NRESE    
      LOGICAL      LPENAF,LADHE
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - UTILITAIRE)
C
C ETAT D'ADHERENCE DU POINT DE CONTACT 
C
C ----------------------------------------------------------------------
C
C
C  CALCUL DE P_B(0,1)(LAMDBA+ RHO C [[DELTA X]])
C
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  LPENAF : .TRUE. SI FROTTEMENT PENALISE
C IN  DLAGRF : INCREMENT DEPDEL DES LAGRANGIENS DE FROTTEMENT
C IN  DJEUT  : INCREMENT DEPDEL DU JEU TANGENT
C IN  COEFAF : COEF_AUGM_FROT
C IN  TAU1   : PREMIER VECTEUR TANGENT
C IN  TAU2   : SECOND VECTEUR TANGENT
C OUT LADHE  : .TRUE. SI ADHERENCE
C OUT RESE   : SEMI-MULTIPLICATEUR GTK DE FROTTEMENT 
C               GTK = LAMBDAF + COEFAF*VITESSE
C OUT NRESE  : NORME DU SEMI-MULTIPLICATEUR GTK DE FROTTEMENT
C
C ----------------------------------------------------------------------
C
      INTEGER IDIM
C
C ----------------------------------------------------------------------
C
      NRESE = 0.D0
      DO 10 IDIM = 1,3
        RESE(IDIM) = 0.D0
   10 CONTINUE
C
C --- SEMI-MULTIPLICATEUR DE FROTTEMENT RESE
C
      IF (LPENAF) THEN
        DO 32 IDIM = 1,3
          RESE(IDIM) = COEFAF*DJEUT(IDIM)
   32   CONTINUE        
      ELSE
        IF (NDIM.EQ.2) THEN
          DO 30 IDIM = 1,2
            RESE(IDIM) = DLAGRF(1)*TAU1(IDIM)+COEFAF*DJEUT(IDIM)
   30     CONTINUE
        ELSE IF (NDIM.EQ.3) THEN
          DO 31 IDIM = 1,3
            RESE(IDIM) = DLAGRF(1)*TAU1(IDIM)+
     &                   DLAGRF(2)*TAU2(IDIM)+
     &                   COEFAF*DJEUT(IDIM)
   31     CONTINUE
        ELSE
          CALL ASSERT(.FALSE.)
        END IF
      ENDIF
C
C --- CALCUL DU COEF D'ADHERENCE
C
      DO 40 IDIM = 1,3
        NRESE = RESE(IDIM)*RESE(IDIM) + NRESE
 40   CONTINUE
      NRESE = SQRT(NRESE)
C
C --- ADHERENCE OU GLISSEMENT ?
C
      IF (NRESE.LE.1.D0) THEN
        LADHE = .TRUE.
      ELSE
        LADHE = .FALSE.
      END IF
C
      END
