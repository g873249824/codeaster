      SUBROUTINE INIOB1(NDIM,NNO,NNOS,NBFPG,ELREFE,X,NBPG)
      IMPLICIT NONE
      INTEGER NDIM,NNO,NNOS,NBFPG,NBPG(*)
      REAL*8 X(*),X2(3*27)
      CHARACTER*8 ELREFE
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 03/07/2002   AUTEUR CIBHHPD D.NUNEZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C                                                                       
C                                                                       
C ======================================================================

C BUT: ROUTINE D'INITIALISATION DES OBJETS &INEL.ELREFE.XXXX

C ----------------------------------------------------------------------
C   IN   ELREFE : NOM DE L'ELEMENT DE REFERENCE
C        NDIM   : DIMENSION DE L'ESPACE DE REFERENCE
C        NNO    : NOMBRE DE NOEUDS
C        NNOS   : NOMBRE DE NOEUDS SOMMETS
C        NBFPG  : NOMBRE DE FAMILLE DE POINTS DE GAUSS
C        NBPG(*): NOMBRE DE POINTS DE GAUSS POUR CHAQUE FAMILLE
C        X(*)   : COORDONNEES DES NOEUDS DE L'ELEMENT DE REFERENCE
C ----------------------------------------------------------------------
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------

      INTEGER NCAR,JIN,IFAPG,NVAL,NBF,JVAL,II,INO,I,J,K,JDEC,JD,IPG
      INTEGER NDERIV,NPG,IBID1,IBID2,IBID3,IBID4,JBID(10)
      REAL*8 POIPG(27)
      REAL*8 FF(27),DFF(3,27),COOPG(3*27)
      REAL*8 XG(3),XI(3)
      CHARACTER*24 CHCTE,CHVAL

C DEB ------------------------------------------------------------------

C     CREATION ET REMPLISSAGE DE : .CARACTE

      LOGICAL THLU
      INTEGER FAMLUM

      CALL JEMARQ()
C
C --- THLU EST VRAI SI ON TRAITE UN ELEMENT THM LUMPE
C     FAMLUM EST ALORS LE NUMERO DE LA FAMILLE C A D DES GAUSS
C     IDENTIQUES AU SOMMET
C
      CALL ALTHLU ( ELREFE, THLU , FAMLUM )

      NCAR = NBFPG + 4
      CHCTE = '&INEL.'//ELREFE//'.CARACTE'
      CALL WKVECT(CHCTE,'G V I',NCAR,JIN)

      ZI(JIN+1-1) = NDIM
      ZI(JIN+2-1) = NNO
      ZI(JIN+3-1) = NBFPG
      DO 10 IFAPG = 1,NBFPG
        ZI(JIN+3-1+IFAPG) = NBPG(IFAPG)
   10 CONTINUE
      ZI(JIN+3-1+NBFPG+1) = NNOS

C --- CREATION ET REMPLISSAGE DE : .FFORMES

      NVAL = NNO*NNO* (NDIM+1)
      NBF = NBFPG
      IF (ELREFE.EQ.'PENTA15 ' .OR. ELREFE.EQ.'PENTA15D') NBF = NBFPG -
     &    1
      DO 20 I = 1,NBF
        NVAL = NVAL + NBPG(I)* (1+NNO+NDIM*NNO)
   20 CONTINUE
      IF (ELREFE.EQ.'PENTA15 ' .OR. ELREFE.EQ.'PENTA15D') THEN
        NVAL = NVAL + NBPG(NBFPG)* (1+NNOS+NDIM*NNOS)
      END IF
      CHVAL = '&INEL.'//ELREFE//'.FFORMES'
      CALL WKVECT(CHVAL,'G V R',NVAL,JVAL)

      JDEC = NNO*NNO

      DO 50 II = 0,NNO - 1
        XI(1) = X(NDIM*II+1)
        XI(2) = X(NDIM*II+2)
        IF (NDIM.EQ.3) XI(3) = X(NDIM*II+3)
        CALL ELRFVF(ELREFE,XI,27,FF,NNO)
        CALL ELRFDF(ELREFE,XI,27*NDIM,DFF,NNO,NDERIV)
        DO 40 INO = 1,NNO
          K = NNO*II + INO
          ZR(JVAL+K-1) = FF(INO)

          J = JVAL + JDEC + NDIM* (K-1) - 1
          DO 30 I = 1,NDIM
            ZR(J+I) = DFF(I,INO)
   30     CONTINUE
   40   CONTINUE
   50 CONTINUE

      JDEC = NNO*NNO* (NDIM+1)

      DO 100 IFAPG = 1,NBF

        NPG = NBPG(IFAPG)
C
C ------ STOCKAGE DES POIDS DES POINTS DE GAUSS
C
         IF ( THLU.AND.(IFAPG.EQ.FAMLUM)) THEN
           IF ( ELREFE .EQ.'HEXD20') THEN
             CALL CARREF ('HEXA8   ',IBID1,IBID2,IBID3,IBID4,JBID,X2)
             DO 55 I=1,8
               POIPG(I)=1.0D0
 55          CONTINUE
           ELSE IF ( ELREFE.EQ.'PENTA15D') THEN
             CALL CARREF ('PENTA6  ',IBID1,IBID2,IBID3,IBID4,JBID,X2)
             DO 56 I=1,6
               POIPG(I)= 1.D0/6.D0
 56          CONTINUE
           ELSE IF ( ELREFE.EQ.'PYRAM13D') THEN
             CALL CARREF ('PYRAM5  ',IBID1,IBID2,IBID3,IBID4,JBID,X2)
             DO 57 I=1,5
               POIPG(I)= 1.D0/5.D0
 57          CONTINUE
           ELSE IF ( ELREFE.EQ.'TETRA10D') THEN
             CALL CARREF ('TETRA4  ',IBID1,IBID2,IBID3,IBID4,JBID,X2)
             DO 58 I=1,4
               POIPG(I)= 0.25D0
 58          CONTINUE
           ELSE
             CALL UTMESS('F','INIOB1','ELEM THM LUMPE INCONNU ')
           ENDIF
         ELSE
           CALL ELRFGF(ELREFE,IFAPG,NBPG,3,COOPG,27,POIPG)
         ENDIF

        DO 60 IPG = 1,NPG
          ZR(JVAL+JDEC+IPG-1) = POIPG(IPG)
   60   CONTINUE


C ------ STOCKAGE DES FONCTIONS DE FORMES ET DE LEURS DERIVEES
C        AUX POINTS DE GAUSS

        JDEC = JDEC + NPG
        JD = NPG*NNO

        DO 90 IPG = 0,NPG - 1
         IF ( THLU.AND.(IFAPG.EQ.FAMLUM)) THEN
            XG(1) = X2(NDIM*IPG+1)
            XG(2) = X2(NDIM*IPG+2)
            IF (NDIM.EQ.3)  XG(3) = X2(NDIM*IPG+3)
         ELSE
            XG(1) = COOPG(NDIM*IPG+1)
            XG(2) = COOPG(NDIM*IPG+2)
          IF (NDIM.EQ.3) XG(3) = COOPG(NDIM*IPG+3)
         ENDIF

          CALL ELRFVF(ELREFE,XG,27,FF,NNO)
          CALL ELRFDF(ELREFE,XG,NDIM*27,DFF,NNO,NDERIV)
          DO 80 INO = 1,NNO
            K = NNO*IPG + INO
            ZR(JVAL+JDEC+K-1) = FF(INO)

            J = JVAL + JDEC + JD + NDIM* (K-1) - 1
            DO 70 I = 1,NDIM
              ZR(J+I) = DFF(I,INO)
   70       CONTINUE
   80     CONTINUE
   90   CONTINUE

        JDEC = JDEC + NPG*NNO + NPG*NNO*NDIM

  100 CONTINUE


C  STOCKAGE DE LA DERNIERE FAMILLE DE POINTS DE GAUSS CORRESPONDANT A :

C    VALEURS DES FCTS DE FORME ASSOCIEES A L'ELEMENT DE DEGRE 1 AUX
C    POINTS DE GAUSS DE L'ELEMENT DE DEGRE 2 (POUR SIGM_ELNO)
C    (UNIQUEMENT POUR LE PENTA15)

      IF (ELREFE.EQ.'PENTA15 ' .OR. ELREFE.EQ.'PENTA15D') THEN

        NPG = NBPG(NBFPG)

C ------ STOCKAGE DES POIDS DES POINTS DE GAUSS

        CALL ELRFGF('PENTA6  ',NBFPG,NBPG,3,COOPG,27,POIPG)
        DO 110 IPG = 1,NPG
          ZR(JVAL+JDEC+IPG-1) = POIPG(IPG)
  110   CONTINUE

C ------ STOCKAGE DES FONCTIONS DE FORMES AUX POINTS DE GAUSS

        JDEC = JDEC + NPG
        JD = NPG*NNOS

        DO 140 IPG = 0,NPG - 1
          XG(1) = COOPG(NDIM*IPG+1)
          XG(2) = COOPG(NDIM*IPG+2)
          XG(3) = COOPG(NDIM*IPG+3)
          CALL ELRFVF('PENTA6  ',XG,27,FF,NNO)
          CALL ELRFDF('PENTA6  ',XG,NDIM*27,DFF,NNO,NDERIV)

          DO 130 INO = 1,NNOS
            K = NNOS*IPG + INO
            ZR(JVAL+JDEC+K-1) = FF(INO)

            J = JVAL + JDEC + JD + NDIM* (K-1) - 1
            DO 120 I = 1,NDIM
              ZR(J+I) = DFF(I,INO)
  120       CONTINUE
  130     CONTINUE

  140   CONTINUE

      END IF

      CALL JEDEMA()

      END
