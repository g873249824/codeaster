      SUBROUTINE CRGDM (IMATE,COMPOR,LAMBDA,DEUXMU,LAMF,DEUMUF,
     &GT,GC,GF,SEUIL,ALPHA,ALFMC,EP,LRGM,IPG,THER,TREF,DTMOY,DTGRA,
     &TMOYM,TGRAM,ALPH)
      IMPLICIT NONE
C RESPONSABLE SFAYOLLE S.FAYOLLE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 26/02/2013   AUTEUR DESROCHE X.DESROCHES 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE  CRP_21

C     CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR


      INCLUDE 'jeveux.h'

      LOGICAL LRGM, THER
      CHARACTER*16 COMPOR, PHENOM
      INTEGER      IMATE, IPG
      REAL*8       LAMBDA, DEUXMU, DEUMUF, LAMF
      REAL*8       GT, GC, GF, SEUIL, ALPHA, ALFMC
      REAL*8       TREF, DTMOY, DTGRA, TMOYP, TMOYM, TGRAP, TGRAM, ALPH
C ----------------------------------------------------------------------
C
C BUT : LECTURE DES PARAMETRES MATERIAU POUR LE MODELE GLRC_DM
C
C
C IN:
C       IMATE   : ADRESSE DU MATERIAU
C       COMPOR  : COMPORTMENT
C       EP      : EPAISSEUR DE LA PLAQUE
C OUT:
C       LAMBDA  : PARAMETRE D ELASTICITE - MEMBRANE
C       DEUXMU  : PARAMETRE D ELASTICITE - MEMBRANE
C       LAMF    : PARAMETRE D ELASTICITE - FLEXION
C       DEUMUF  : PARAMETRE D ELASTICITE - FLEXION
C       GT      : PARAMETRE GAMMA POUR LA MEMBRANE EN TRACTION 
C       GC      : PARAMETRE GAMMA POUR LA MEMBRANE EN COMPRESSION
C       GF      : PARAMETRE GAMMA POUR LA FLEXION
C       SEUIL   : INITIAL MEMBRANE
C       ALPHA   : PARAMETRE DE SEUIL FLEXION
C       ALFMC   : PARAMETRE DE DECOUPLAGE SEUILS TRACTION-COMPRESSION
C ----------------------------------------------------------------------

      REAL*8      VALRES(7), E, NU, EP, EF, NYCMAX, RMESG(2)
      REAL*8      NYT, NYC, MYF, NUF, DELAS(6,6), T1P, T2P, T3P
      REAL*8      T1M, T2M, T3M

      INTEGER ICODRE(7), IRET, IRET1, IRETM
      CHARACTER*4 FAMI
      CHARACTER*8 NOMRES(7)

      THER=.FALSE.
      IF ((.NOT.( COMPOR(1:7) .EQ. 'GLRC_DM'))) THEN
        CALL U2MESK('F','ELEMENTS4_65',1,COMPOR)
      ENDIF

      CALL R8INIR(6*6,0.0D0,DELAS,1)
      CALL R8INIR(7,0.0D0,VALRES,1)
      PHENOM = 'GLRC_DM         '
      IF(PHENOM . NE . 'GLRC_DM         ' ) THEN
        CALL U2MESK('F','ELEMENTS2_32',1,PHENOM)
      ENDIF

C     EPAISSEUR
C      CALL JEVECH('PCACOQU','L',JCOQU)
C      EPAIS = ZR(JCOQU)
C      NOMRES(1)  = 'EPAIS'
C      CALL RCVALA(IMATE,' ',PHENOM,0,' ',R8B,1,NOMRES,
C     &              VALRES,CODRES,'FM')
C      IF(VALRES(1) .NE. EPAIS) THEN
C        VALRES(2) = EPAIS
C        CALL U2MESG('F','ELEMENTS5_42',0,' ',0,0,2,VALRES)
C      ENDIF

       FAMI='RIGI' 
 
       CALL RCVARC(' ','TEMP_INF','+',FAMI,IPG,1,T2P,IRET)
       CALL RCVARC(' ','TEMP_SUP','+',FAMI,IPG,1,T3P,IRET)
       IF(IRET.EQ.0) THEN
         CALL RCVARC(' ','TEMP','+',FAMI,IPG,1,T1P,IRETM)
C SI TEMP N'EST PAS FOURNI ON MET LA MOYENNE DE TSUP ET TINF
         IF(IRETM.NE.0) THEN
           T1P=(T2P+T3P)/2.D0
         ENDIF
         TMOYP=(4.D0*T1P+T2P+T3P)/6.D0 
         TGRAP=(T3P-T2P)/EP 
         CALL RCVARC(' ','TEMP','REF',FAMI,1,1,TREF,IRET1)
         IF(IRET1.EQ.0) THEN
           THER=.TRUE.
         ENDIF 
       ENDIF 
       IF(THER) THEN
         CALL RCVARC(' ','TEMP_INF','-',FAMI,IPG,1,T2M,IRET)
         CALL RCVARC(' ','TEMP_SUP','-',FAMI,IPG,1,T3M,IRET)
         IF(IRET.EQ.0) THEN
           CALL RCVARC(' ','TEMP','-',FAMI,IPG,1,T1M,IRETM)
C SI TEMP N'EST PAS FOURNI ON MET LA MOYENNE DE TSUP ET TINF
           IF(IRETM.NE.0) THEN
             T1M=(T2M+T3M)/2.D0
           ENDIF
           TMOYM=(4.D0*T1M+T2M+T3M)/6.D0 
           TGRAM=(T3M-T2M)/EP 
           DTMOY=TMOYP-TMOYM       
           DTGRA=TGRAP-TGRAM       
         ELSE
           CALL U2MESS('F','ALGORITH17_30')           
         ENDIF 
       ENDIF 

C    LECTURE DES CARACTERISTIQUES DU MATERIAU
      NOMRES(1) = 'E'
      NOMRES(2) = 'NU'

      IF(IRET.EQ.0) THEN
        NOMRES(3) = 'ALPHA'
        CALL RCVALA(IMATE,' ','ELAS',1,'TEMP',TMOYP,3,
     &              NOMRES,VALRES,ICODRE,1)
        ALPH = VALRES(3)

      ELSE
        CALL RCVALA(IMATE,' ','ELAS',0,' ',0.D0,2,
     &              NOMRES,VALRES,ICODRE,1)

      ENDIF
      E    = VALRES(1)
      NU   = VALRES(2)
      LAMBDA = E * NU / (1.D0+NU) / (1.D0 - 2.D0*NU)*EP
      DEUXMU = E/(1.D0+NU)*EP

      NOMRES(1) = 'EF'
      NOMRES(2) = 'NUF'

      CALL RCVALA(IMATE,' ','GLRC_DM   ',0,' ',0.D0,2,
     &              NOMRES,VALRES,ICODRE, 0)

      IF(ICODRE(1).EQ.0) THEN
        EF = VALRES(1)
      ELSE
        EF = E
      ENDIF

      IF(ICODRE(2).EQ.0) THEN
        NUF = VALRES(2)
      ELSE
        NUF = NU
      ENDIF

      LAMF   = EF*NUF/(1.D0-NUF*NUF) *EP**3/12.0D0
      DEUMUF = EF/(1.D0+NUF) *EP**3/12.0D0

C    LECTURE DES CARACTERISTIQUES D'ENDOMMAGEMENT
      NOMRES(1) = 'GAMMA_T'
      NOMRES(2) = 'GAMMA_C'
      NOMRES(3) = 'GAMMA_F'
      NOMRES(4) = 'NYT'
      NOMRES(5) = 'NYC'
      NOMRES(6) = 'MYF'
      NOMRES(7) = 'ALPHA_C'
      CALL RCVALA(IMATE,' ','GLRC_DM',0,' ',0.D0,7,
     &            NOMRES,VALRES,ICODRE,0)

      GT    = VALRES(1)
      GF    = VALRES(3)
      NYT   = VALRES(4)
      MYF   = VALRES(6)
      ALFMC = VALRES(7)

      IF (ICODRE(2).EQ.0 .AND. ICODRE(5).EQ.0) THEN
C - ON EST DANS LE CAS DE DEFI_GLRC
        GC  = VALRES(2)
        NYC = VALRES(5)
      ELSEIF (ICODRE(2).EQ.0) THEN
C - ON EST DANS LE CAS DE DEFI_MATERIAU
        GC  = VALRES(2)

        IF (GC .EQ. 1.D0 .AND. GT .EQ. 1.D0) THEN
          CALL U2MESS('F','ALGORITH6_1')
        END IF

        NYC = (1.D0-NU)*(1.D0+2.D0*NU)*(1.D0-GT)+NU**2*(1.D0-GC)
        NYC = NYC/((1.D0-NU)*(1.D0+2.D0*NU)*(1.D0-GC)+NU**2*(1.D0-GT))
        NYC = -SQRT(NYC*NYT**2)
      ELSEIF (ICODRE(5).EQ.0) THEN
C - ON EST DANS LE CAS DE DEFI_MATERIAU
        NYC = VALRES(5)
        NYCMAX = NYT*SQRT((1.D0-NU)*(1.D0+2.D0*NU))/NU

        IF (VALRES(5) .GT. NYCMAX) THEN
          RMESG(1) = NYC
          RMESG(2) = NYCMAX
          CALL U2MESR('F','ALGORITH6_2',2,RMESG)
        END IF

        GC  = (1.D0-GT)*(NYT**2*(1.D0-NU)*(1.D0+2.D0*NU)-NYC**2*NU**2)
        GC  = GC/(NYC**2*(1.D0-NU)*(1.D0+2.D0*NU)-NYT**2*NU**2)
        GC  = 1.D0 - GC
      ELSE
        GC = 1.D0
      END IF

      IF (GC .EQ. 1.D0 .AND. GT .EQ. 1.D0) THEN
        CALL U2MESS('F','ALGORITH6_1')
      ENDIF

      IF (ICODRE(7).EQ.0 .AND. GC .NE. 1.D0)THEN
        ALFMC = VALRES(7)
      ELSE
        IF (GC .EQ. 1.D0) THEN
          ALFMC = 1.D0
        ELSE
          ALFMC=(1.D0-GC)*(NYC**2*(1.D0-NU)*(1.D0+2.D0*NU)/NYT**2-NU**2)
     &         /((1.D0-GT)*((1.D0-NU)*(1.D0+2.D0*NU)-(NU*NYC/NYT)**2))
        ENDIF
      ENDIF

C    CALCUL DU SEUIL (k0 DANS R7.01.32) ET DE ALPHA
      IF (LRGM) THEN
        ALPHA = 1.D0
        ALFMC = 1.D0
        SEUIL = 0.D0
      ELSE
        SEUIL = LAMBDA*(1.0D0 - GT)*(1.0D0-2.0D0*NU)**2  +
     &          DEUXMU*(1.0D0 - GT + (1.0D0 - GC)*NU**2/ALFMC)

        SEUIL = SEUIL/(2.0D0*(LAMBDA*(1.0D0-2.0D0*NU) + DEUXMU))**2
        SEUIL = SEUIL*NYT**2

        IF (SEUIL .NE. 0.D0) THEN
          ALPHA = LAMF*(1.0D0-NUF)**2 + DEUMUF
          ALPHA = ALPHA/(2.0D0*(LAMF*(1.0D0-NUF) + DEUMUF)**2)
          ALPHA = ALPHA*(1.0D0 - GF)*MYF**2/SEUIL
        ELSE
          CALL U2MESS('F','ALGORITH6_3')
        END IF
      END IF

      END
