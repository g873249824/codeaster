      SUBROUTINE ERHMS2(INO   , NBS   , NBNA  , HF   ,
     &                  JAC   , NX    , NY    ,
     &                  SIELNP, ADSIP ,
     &                  NBCMP , TYPMAV, TBREF1, IVOIS,
     &                  TERSAM, TERSAH)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 23/04/2007   AUTEUR GNICOLAS G.NICOLAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C =====================================================================
C  ERREUR EN HYDRO-MECANIQUE - TERME DE SAUT - DIMENSION 2
C  **        *     *                    *                *
C =====================================================================
C  - FONCTION REALISEE :  CALCUL DE L'ERREUR DUE AUX TERMES DE SAUT 
C                         DE LA MECANIQUE ET DE L'HYDRAULIQUE DANS
C                         L'INDICATEUR HM PERMANENT.
C
C     ARGUMENTS:
C     ----------
C
C      ENTREE :
C-------------
C IN   INO    : NUMERO DE L'ARETE
C IN   NBS    : NOMBRE DE SOMMETS SUR L'ARETE = NOMBRE D'ARETES
C IN   NBNA   : NOMBRE DE NOEUDS PAR ARETE
C IN   HF     : LONGUEUR DE L'ARETE
C IN   JAC    : VECTEUR DES JACOBIENS DE LA TRANSFORMATION AUX NOEUDS
C IN   NX     : VECTEUR DES ABSCISSES DES NORMALES AUX NOEUDS
C IN   NY     : VECTEUR DES ORDONNEES DES NORMALES AUX NOEUDS
C IN   SIELNP : CONTRAINTES AUX NOEUDS PAR ELEMENT A L'INSTANT ACTUEL
C IN   ADSIP  : ADRESSE DANS ZR DU TABLEAU DES CONTRAINTES DE PRESSION
C               DE LA MECANIQUE
C IN   NBCMP  : NOMBRE DE COMPOSANTES DU VECTEUR DES CONTRAINTES 
C               GENERALISEES 
C IN   TYPMAV : TYPE DE LA MAILLE VOISINE
C IN   TBREF1 : TABLEAU AUXILIAIRE CONTENANT DES ADRESSES/VALEURS
C IN   IVOIS  : ADRESSE DANS ZI DES VOISINS
C
C      SORTIE :
C-------------
C OUT   TERSAM   : TERME DE SAUT DE LA MECANIQUE
C OUT   TERSAH   : TERME DE SAUT DE L'HYDRAULIQUE
C
C ......................................................................
C
      IMPLICIT NONE
C
C DECLARATION PARAMETRES D'APPELS
C
      INTEGER     INO,NBS,NBNA
      INTEGER     IVOIS,NBCMP,ADSIP
      INTEGER     TBREF1(12)
      CHARACTER*8 TYPMAV
      REAL*8      HF,JAC(3),NX(3),NY(3)
      REAL*8      SIELNP(81)
      REAL*8      TERSAM,TERSAH
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------

      INTEGER        ZI
      COMMON /IVARJE/ZI(1)
      REAL*8         ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16     ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL        ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8    ZK8
      CHARACTER*16          ZK16
      CHARACTER*24                  ZK24
      CHARACTER*32                          ZK32
      CHARACTER*80                                  ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

C DECLARATION VARIABLES LOCALES
C
      REAL*8      SIXXP(3) ,SIYYP(3) ,SIXYP(3)
      REAL*8      SIXXPV(3),SIYYPV(3),SIXYPV(3)
      REAL*8      DSGXX(3) ,DSGYY(3) ,DSGXY(3)
      REAL*8      FLUXP(3) ,FLUYP(3) ,FLUXPV(3),FLUYPV(3)
      REAL*8      DMX(3)   ,DMY(3)
C
      REAL*8      INTEM1,INTEM2,INTEM3,INTEH1,INTEH2,INTEH3
C
      INTEGER     IAREPE,JCELDP,JCELVP,IMAV,IGREL,IEL,
     &            IAVALP,ICONX1,ICONX2,ADMAVP,
     &            ADINOV,ADJNOV,ADMNOV
      INTEGER     JAD,JADV,NCHER,INDIIS
      INTEGER     NBNV,I,JNO,MNO,INOV,JNOV,MNOV
      INTEGER     IAUX
      CHARACTER*2 FORMV,NOEUV
C
C =====================================================================
C 1.  RECUPERATION SUR LA MAILLE COURANTE AUX NOEUDS INO ET JNO DE :
C     . CONTRAINTES EFFECTIVES (SIGMA MECANIQUE : SIXX, SIYY, SIXY)
C     . FLUX HYDRAULIQUE M
C
C              X1          X2          X3
C               O-----------O-----------O
C              INO         MNO         JNO
C
C         POINTS  1 --> INO PREMIER POINT DE L'ARETE COURANTE
C                 2 --> JNO DEUXIEME POINT  DE L'ARETE COURANTE
C                 3 --> MNO NOEUD MILIEU S'IL EXISTE
C =====================================================================
C
      IF (INO.EQ.NBS) THEN
        JNO=1
      ELSE
        JNO=INO+1
      ENDIF
C
      IAUX = NBCMP*(INO-1)
      SIXXP(1) = SIELNP(IAUX+1)
      SIYYP(1) = SIELNP(IAUX+2)
      SIXYP(1) = SIELNP(IAUX+4)
      FLUXP(1) = SIELNP(IAUX+ADSIP+1)
      FLUYP(1) = SIELNP(IAUX+ADSIP+2)
C
      IAUX = NBCMP*(JNO-1)
      SIXXP(2) = SIELNP(IAUX+1)
      SIYYP(2) = SIELNP(IAUX+2)
      SIXYP(2) = SIELNP(IAUX+4)
      FLUXP(2) = SIELNP(IAUX+ADSIP+1)
      FLUYP(2) = SIELNP(IAUX+ADSIP+2)
C
      IF (NBNA.EQ.3) THEN
C
C BIEN QUE LA PRESSION NE SOIT CALCULEE QU'AUX NOEUDS SOMMETS, LE
C GRADIENT L'EST EGALEMENT AU NOEUD MILIEU, VIA EPSTHM
C
        MNO=NBS+INO
C
        IAUX = NBCMP*(MNO-1)
        SIXXP(3) = SIELNP(IAUX+1)
        SIYYP(3) = SIELNP(IAUX+2)
        SIXYP(3) = SIELNP(IAUX+4)
        FLUXP(3) = SIELNP(IAUX+ADSIP+1)
       FLUYP(3) = SIELNP(IAUX+ADSIP+2)
C
      ENDIF
C
C =====================================================================
C 2. CARACTERISATION DE LA MAILLE VOISINE
C =====================================================================
C
C 2.1. ---- RECHERCHE DES ADRESSES POUR OBTENIR SIGMA SUR LE VOISIN ---
C
      IAREPE = TBREF1(1)
      JCELDP = TBREF1(2)
      JCELVP = TBREF1(3)
C
C ADRESSE DE LA MAILLE VOISINE PARTAGEANT L'ARETE INO
      IMAV=ZI(IVOIS+INO)
C NUMERO DU LIGREL DE LA MAILLE VOISINE DE NUMERO GLOBAL IMAV
      IGREL=ZI(IAREPE+2*(IMAV-1))
C INDICE DE LA MAILLE VOISINE DANS LE IGREL
      IEL=ZI(IAREPE+2*(IMAV-1)+1)
C ADRESSE (DANS .CELV DE SIELNP) DU DEBUT DU GREL IGREL
      IAVALP=JCELVP-1+ZI(JCELDP-1+ZI(JCELDP-1+4+IGREL)+8)
C
C 2.2. ----- TESTS SUR LA MAILLE VOISINE ------------------------------
C
      FORMV=TYPMAV(1:2)
      NOEUV=TYPMAV(5:5)
C
      IF (FORMV.EQ.'TR') THEN
        IF (NOEUV.EQ.'3') THEN
          NBNV=3
        ELSE
          NBNV=6
        ENDIF
      ELSE IF (FORMV.EQ.'QU') THEN
        IF (NOEUV.EQ.'4') THEN
          NBNV=4
        ELSE IF (NOEUV.EQ.'8') THEN
          NBNV=8
        ELSE
          NBNV=9
        ENDIF
      ENDIF
C
C 2.3 ----- ADRESSE DANS LE VECTEUR DES CONTRAINTES DE LA PREMIERE
C           COMPOSANTE DU 1ER NOEUD DE L'ELEMENT IEL DE NUMERO IGREL 
C
      ADMAVP = IAVALP-1+NBCMP*NBNV*(IEL-1)
C
C ADRESSE DE LA COLLECTION CONNECTIVITE
C
      ICONX1=TBREF1(11)
C ADRESSE DU POINTEUR DE LONGUEUR DE LA CONNECTIVITE
      ICONX2=TBREF1(12)
C 
      JAD =ICONX1-1+ZI(ICONX2+ZI(IVOIS    )-1)
      JADV=ICONX1-1+ZI(ICONX2+ZI(IVOIS+INO)-1)
C
C     NCHER : NUMERO GLOBAL DU NOEUD LOCAL INO DANS LA MAILLE COURANTE
C
      NCHER=ZI(JAD-1+INO)
C
C     ON CHERCHE A QUEL NUMERO LOCAL (INOV) DANS LA MAILLE VOISINE
C     CORRESPOND LE NOEUD DE NUMERO GLOBAL NCHER
C
      INOV=INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C     IDEM POUR JNO LOCAL DE LA MAILLE COURANTE
C
      NCHER=ZI(JAD-1+JNO)
      JNOV=INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C ADRESSE DES NOEUDS INOV ET JNOV DE LA MAILLE VOISINE 
C DE L'ELEMENT COURANT
C 
      ADINOV = NBCMP*(INOV-1)
      ADJNOV = NBCMP*(JNOV-1)
C
C CONTRAINTES AUX NOEUDS SUR L'ELEMENT VOISIN, POUR LES 2 NOEUDS DE 
C L'ARETE
C
      IAUX = ADMAVP+ADINOV
      SIXXPV(1) = ZR(IAUX+1)
      SIYYPV(1) = ZR(IAUX+2)
      SIXYPV(1) = ZR(IAUX+4)
      FLUXPV(1) = ZR(IAUX+ADSIP+1)
      FLUYPV(1) = ZR(IAUX+ADSIP+2)
C
      IAUX = ADMAVP+ADJNOV
      SIXXPV(2) = ZR(IAUX+1)
      SIYYPV(2) = ZR(IAUX+2)
      SIXYPV(2) = ZR(IAUX+4)
      FLUXPV(2) = ZR(IAUX+ADSIP+1)
      FLUYPV(2) = ZR(IAUX+ADSIP+2)
C
C 2.4. NOEUD MILIEU
C
      IF (NBNA.EQ.3) THEN
C
        MNO=NBS+INO
C
C MEME OPERATION QUE POUR LES EXTREMITES : ON PASSE PAR LE NUMERO GLOBAL
C
        NCHER = ZI(JAD-1+MNO)
        MNOV = INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C ADRESSE DU NOEUD MNOV DE LA MAILLE VOISINE DE L'ELEMENT COURANT
C 
        ADMNOV = NBCMP*(MNOV-1)
C
        IAUX = ADMAVP+ADMNOV
        SIXXPV(3) = ZR(IAUX+1)
        SIYYPV(3) = ZR(IAUX+2)
        SIXYPV(3) = ZR(IAUX+4)
        FLUXPV(3) = ZR(IAUX+ADSIP+1)
        FLUYPV(3) = ZR(IAUX+ADSIP+2)
C
      ENDIF
C
C =====================================================================
C 3. --- TERME DE SAUT
C =====================================================================
C 3.1. CALCUL DES SAUTS EN CHAQUE NOEUD DE L'ARETE
C
      DO 10 , I=1 , NBNA
C
        DSGXX(I) = SIXXP(I) - SIXXPV(I)
        DSGYY(I) = SIYYP(I) - SIYYPV(I)
        DSGXY(I) = SIXYP(I) - SIXYPV(I)
C
        DMX(I) = FLUXP(I) - FLUXPV(I)
        DMY(I) = FLUYP(I) - FLUYPV(I)
C
 10   CONTINUE
C        
C 3.2. POUR L'INTEGRATION NUMERIQUE, ON UTILISE :
C      . SI 3 NOEUDS SOMMETS, UNE FORMULATION DE SIMPSON
C      . SI 2 NOEUDS SOMMETS, UNE METHODE DES TRAPEZES
C PARTIE MECANIQUE
C
      INTEM1=JAC(1)*
     &       ( (DSGXX(1)*NX(1)+DSGXY(1)*NY(1))**2
     &        +(DSGXY(1)*NX(1)+DSGYY(1)*NY(1))**2 )
C
      INTEM2=JAC(2)*
     &      ( (DSGXX(2)*NX(2)+DSGXY(2)*NY(2))**2
     &       +(DSGXY(2)*NX(2)+DSGYY(2)*NY(2))**2 )
C
      IF (NBNA.EQ.3) THEN
        INTEM3=JAC(3)*
     &        ( (DSGXX(3)*NX(3)+DSGXY(3)*NY(3))**2
     &         +(DSGXY(3)*NX(3)+DSGYY(3)*NY(3))**2 )
      ENDIF
C        
C PARTIE HYDRAULIQUE
C
      INTEH1=JAC(1)*((DMX(1)*NX(1)+DMY(1)*NY(1))**2)
C     
      INTEH2=JAC(2)*((DMX(2)*NX(2)+DMY(2)*NY(2))**2)
C
      IF (NBNA.EQ.3) THEN
        INTEH3=JAC(3)*((DMX(3)*NX(3)+DMY(3)*NY(3))**2)
      ENDIF
C
C ON ASSEMBLE LES TERMES DE MECANIQUE (TERSAM) ET D'HYDRAULIQUE
C (TERSAH)
C
      IF (NBNA.EQ.2) THEN
        TERSAM = TERSAM+0.5D0*SQRT(HF)*SQRT(INTEM1+INTEM2)
      ELSE
        TERSAM = TERSAM+0.5D0*SQRT(HF)*
     &       SQRT((INTEM1+4.D0*INTEM3+INTEM2)/3.D0)
C
      ENDIF
C
      IF (NBNA.EQ.2) THEN
        TERSAH = TERSAH+0.5D0*SQRT(HF)*SQRT(INTEH1+INTEH2)
      ELSE
        TERSAH = TERSAH+0.5D0*SQRT(HF)*
     &       SQRT((INTEH1+4.D0*INTEH3+INTEH2)/3.D0)
      ENDIF
C
      END
