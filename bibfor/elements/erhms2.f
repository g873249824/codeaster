      SUBROUTINE ERHMS2(INO,NBS,NBNA,HF,JAC,NX,NY,
     &                  SIELNO,ADSIP,
     &                  NBCMP,TYPMAV,TBREF1,IVOIS,
     &                  TERSAM,TERSAH)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 03/07/2006   AUTEUR MEUNIER S.MEUNIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C =====================================================================
C  ERREUR EN HYDRO-MECANIQUE - TERME DE SAUT - DIMENSION 2
C  **        *     *                    *                *
C =====================================================================
C  - FONCTION REALISEE :  CALCUL DE L'ERREUR DUE AUX TERMES DE SAUT 
C                         DE LA MECANIQUE ET DE L'HYDRAULIQUE DANS 
C                         L'INDICATEUR HM PERMANENT.
C
C     ARGUMENTS:
C     ----------
C
C      ENTREE :
C-------------
C IN   INO    : NUMERO DE L'ARETE
C IN   NBS    : NOMBRE DE SOMMETS SUR L'ARETE = NOMBRE D'ARETES
C IN   NBNA   : NOMBRE DE NOEUDS PAR ARETE
C IN   HF     : LONGUEUR DE L'ARETE
C IN   JAC    : VECTEUR DES JACOBIENS DE LA TRANSFORMATION AUX NOEUDS
C IN   NX     : VECTEUR DES ABSCISSES DES NORMALES AUX NOEUDS
C IN   NY     : VECTEUR DES ORDONNEES DES NORMALES AUX NOEUDS
C IN   SIELNO : CONTRAINTES AUX NOEUDS PAR ELEMENT
C IN   ADSIP  : ADRESSE DANS ZR DU TABLEAU DES CONTRAINTES DE PRESSION
C               DE LA MECANIQUE
C IN   NBCMP  : NOMBRE DE COMPOSANTES DU VECTEUR DES CONTRAINTES 
C               GENERALISEES 
C IN   TYPMAV : TYPE DE LA MAILLE VOISINE
C IN   TBREF1 : TABLEAU AUXILIAIRE CONTENANT DES ADRESSES/VALEURS
C IN   IVOIS  : ADRESSE DANS ZI DES VOISINS
C
C      SORTIE :
C-------------
C OUT   TERSAM   : TERME DE SAUT DE LA MECANIQUE
C OUT   TERSAH   : TERME DE SAUT DE L'HYDRAULIQUE
C
C ......................................................................
C
      IMPLICIT NONE
C
C DECLARATION PARAMETRES D'APPELS
C
      INTEGER     INO,NBS,NBNA
      INTEGER     IVOIS,NBCMP,ADSIP
      INTEGER     TBREF1(12)
      CHARACTER*8 TYPMAV
      REAL*8      HF,JAC(3),NX(3),NY(3)
      REAL*8      SIELNO(81)
      REAL*8      TERSAM,TERSAH
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------

      INTEGER        ZI
      COMMON /IVARJE/ZI(1)
      REAL*8         ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16     ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL        ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8    ZK8
      CHARACTER*16          ZK16
      CHARACTER*24                  ZK24
      CHARACTER*32                          ZK32
      CHARACTER*80                                  ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

C DECLARATION VARIABLES LOCALES
C
      REAL*8      SIGXX(3) ,SIGYY(3) ,SIGXY(3 ),SIP(3)
      REAL*8      SIGXXV(3),SIGYYV(3),SIGXYV(3),SIPV(3)
      REAL*8      DSGXX(3) ,DSGYY(3 ),DSGXY(3) ,DSIP(3)
      REAL*8      FLUHX(3),FLUHY(3)
      REAL*8      FLUHXV(3),FLUHYV(3)
      REAL*8      DMX(3),DMY(3)
      REAL*8      INTE1,INTE2,INTE3,INTEH1,INTEH2,INTEH3
C
      INTEGER     IAREPE,JCELD,JCELV,IMAV,IGREL,IEL,
     &            IAVAL,ICONX1,ICONX2,ADMAV,ADINOV,ADJNOV,ADMNOV
      INTEGER     JAD,JADV,NCHER,INDIIS
      INTEGER     NBNV,I,JNO,MNO,INOV,JNOV,MNOV
      INTEGER     IAUX
      CHARACTER*2 FORMV,NOEUV
C
C =====================================================================
C 1.  RECUPERATION SUR LA MAILLE COURANTE AUX NOEUDS INO ET JNO DE :
C     . CONTRAINTES EFFECTIVES (SIGMA MECANIQUE : SIXX, SIYY, SIXY)
C     . CONTRAINTES DE PRESSION (BIOT*PRESSION : SIP)
C     . FLUX HYDRAULIQUE M
C
C              X1          X2          X3
C               O-----------O-----------O
C              INO         MNO         JNO
C
C         POINTS  1 --> INO PREMIER POINT DE L'ARETE COURANTE
C                 2 --> JNO DEUXIEME POINT  DE L'ARETE COURANTE
C                 3 --> MNO NOEUD MILIEU S'IL EXISTE
C =====================================================================
C
      IF (INO.EQ.NBS) THEN
        JNO=1
      ELSE
        JNO=INO+1
      ENDIF
C
      IAUX = NBCMP*(INO-1)
      SIGXX(1) = SIELNO(IAUX+1)
      SIGYY(1) = SIELNO(IAUX+2)
      SIGXY(1) = SIELNO(IAUX+4)
      SIP(1)   = SIELNO(IAUX+ADSIP)  
      FLUHX(1) = SIELNO(IAUX+ADSIP+1)  
      FLUHY(1) = SIELNO(IAUX+ADSIP+2)  
C
      IAUX = NBCMP*(JNO-1)
      SIGXX(2) = SIELNO(IAUX+1)
      SIGYY(2) = SIELNO(IAUX+2)
      SIGXY(2) = SIELNO(IAUX+4)
      SIP(2)   = SIELNO(IAUX+ADSIP)
      FLUHX(2) = SIELNO(IAUX+ADSIP+1)  
      FLUHY(2) = SIELNO(IAUX+ADSIP+2)  
C
      IF (NBNA.EQ.3) THEN
C
C BIEN QUE LA PRESSION NE SOIT CALCULEE QU'AUX NOEUDS SOMMETS, LE
C GRADIENT L'EST EGALEMENT AU NOEUD MILIEU, VIA EPSTHM
C
        MNO=NBS+INO
C
        IAUX = NBCMP*(MNO-1)
        SIGXX(3) = SIELNO(IAUX+1)
        SIGYY(3) = SIELNO(IAUX+2)
        SIGXY(3) = SIELNO(IAUX+4)
        SIP(3)   = SIELNO(IAUX+ADSIP)
        FLUHX(3) = SIELNO(IAUX+ADSIP+1)  
        FLUHY(3) = SIELNO(IAUX+ADSIP+2)  
C
      ENDIF
C
C =====================================================================
C 2. CARACTERISATION DE LA MAILLE VOISINE
C =====================================================================
C
C 2.1. ---- RECHERCHE DES ADRESSES POUR OBTENIR SIGMA SUR LE VOISIN ---
C
      IAREPE = TBREF1(1)
      JCELD = TBREF1(2)
      JCELV = TBREF1(3)
C
C ADRESSE DE LA MAILLE VOISINE PARTAGEANT L'ARETE INO     
      IMAV=ZI(IVOIS+INO)
C NUMERO DU LIGREL DE LA MAILLE VOISINE DE NUMERO GLOBAL IMAV
      IGREL=ZI(IAREPE+2*(IMAV-1))
C INDICE DE LA MAILLE VOISINE DANS LE IGREL
      IEL=ZI(IAREPE+2*(IMAV-1)+1)
C ADRESSE (DANS .CELV DE SIELNO) DU DEBUT DU GREL IGREL
      IAVAL=JCELV-1+ZI(JCELD-1+ZI(JCELD-1+4+IGREL)+8)
C
C 2.2. ----- TESTS SUR LA MAILLE VOISINE ------------------------------
C
      FORMV=TYPMAV(1:2)
      NOEUV=TYPMAV(5:5)
C
      IF (FORMV.EQ.'TR') THEN
        IF (NOEUV.EQ.'3') THEN
          NBNV=3
        ELSE
          NBNV=6
        ENDIF
      ELSE IF (FORMV.EQ.'QU') THEN
        IF (NOEUV.EQ.'4') THEN
          NBNV=4
        ELSE IF (NOEUV.EQ.'8') THEN
          NBNV=8
        ELSE
          NBNV=9
        ENDIF
      ENDIF
C
C 2.3 ----- ADRESSE DANS LE VECTEUR DES CONTRAINTES DE LA PREMIERE
C           COMPOSANTE DU 1ER NOEUD DE L'ELEMENT IEL DE NUMERO IGREL 
C
      ADMAV = IAVAL-1+NBCMP*NBNV*(IEL-1)      
C
C ADRESSE DE LA COLLECTION CONNECTIVITE
C
      ICONX1=TBREF1(11)
C ADRESSE DU POINTEUR DE LONGUEUR DE LA CONNECTIVITE
      ICONX2=TBREF1(12)
C 
      JAD =ICONX1-1+ZI(ICONX2+ZI(IVOIS    )-1)
      JADV=ICONX1-1+ZI(ICONX2+ZI(IVOIS+INO)-1)
C
C     NCHER : NUMERO GLOBAL DU NOEUD LOCAL INO DANS LA MAILLE COURANTE
C
      NCHER=ZI(JAD-1+INO)
C
C     ON CHERCHE A QUEL NUMERO LOCAL (INOV) DANS LA MAILLE VOISINE
C     CORRESPOND LE NOEUD DE NUMERO GLOBAL NCHER
C
      INOV=INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C     IDEM POUR JNO LOCAL DE LA MAILLE COURANTE
C
      NCHER=ZI(JAD-1+JNO)
      JNOV=INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C ADRESSE DES NOEUDS INOV ET JNOV DE LA MAILLE VOISINE 
C DE L'ELEMENT COURANT
C 
      ADINOV = NBCMP*(INOV-1)
      ADJNOV = NBCMP*(JNOV-1)
C
C CONTRAINTES ET PRESSION  AUX NOEUDS SUR L'ELEMENT VOISIN, POUR LES
C 2 NOEUDS DE L'ARETE
C
      IAUX = ADMAV+ADINOV
      SIGXXV(1) = ZR(IAUX+1)
      SIGYYV(1) = ZR(IAUX+2)
      SIGXYV(1) = ZR(IAUX+4)
      SIPV(1)   = ZR(IAUX+ADSIP)
      FLUHXV(1) = ZR(IAUX+ADSIP+1)
      FLUHYV(1) = ZR(IAUX+ADSIP+2)
C
      IAUX = ADMAV+ADJNOV
      SIGXXV(2) = ZR(IAUX+1)
      SIGYYV(2) = ZR(IAUX+2)
      SIGXYV(2) = ZR(IAUX+4)
      SIPV(2)   = ZR(IAUX+ADSIP)
      FLUHXV(2) = ZR(IAUX+ADSIP+1)
      FLUHYV(2) = ZR(IAUX+ADSIP+2)
C
C 2.4. NOEUD MILIEU
C
      IF (NBNA.EQ.3) THEN
C
        MNO=NBS+INO
C
C MEME OPERATION QUE POUR LES EXTREMITES : ON PASSE PAR LE NUMERO GLOBAL
C
        NCHER = ZI(JAD-1+MNO)
        MNOV = INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C ADRESSE DU NOEUD MNOV DE LA MAILLE VOISINE DE L'ELEMENT COURANT
C 
        ADMNOV = NBCMP*(MNOV-1)
C
        IAUX = ADMAV+ADMNOV
        SIGXXV(3) = ZR(IAUX+1)
        SIGYYV(3) = ZR(IAUX+2)
        SIGXYV(3) = ZR(IAUX+4)
        SIPV(3)   = ZR(IAUX+ADSIP)
        FLUHXV(3) = ZR(IAUX+ADSIP+1)
        FLUHYV(3) = ZR(IAUX+ADSIP+2)
C
      ENDIF
C
C =====================================================================
C 3. --- TERME DE SAUT
C =====================================================================
C 3.1. CALCUL DES SAUTS EN CHAQUE NOEUD DE L'ARETE
C
      DO 41 , I=1 , NBNA
C
        DSGXX(I) = SIGXX(I) - SIGXXV(I)
        DSGYY(I) = SIGYY(I) - SIGYYV(I)
        DSGXY(I) = SIGXY(I) - SIGXYV(I)
C
        DSIP(I) = SIP(I) - SIPV(I)
C
        DMX(I) = FLUHX(I) - FLUHXV(I)
        DMY(I) = FLUHY(I) - FLUHYV(I)
C
 41   CONTINUE
C        
C 3.2. ON REALISE UNE INTEGRATION DE NEWTON-COTES AVEC 2 OU 3 POINTS
C D'INTEGRATION SELON LE NOMBRE DE POINTS DE L'ARETE CONSIDEREE
C SOIT NBNA = 2 OU 3
C ON REALISE LA MULTIPLICATION MATRICIELLE DE (SIGMA+SIP*ID)*NORMALE =
C PARTIE MECANIQUE
C
      INTE1=JAC(1)*
     &      ( ((DSIP(1)+DSGXX(1))*NX(1)+DSGXY(1)*NY(1))**2
     &       +(DSGXY(1)*NX(1)+(DSGYY(1)+DSIP(1))*NY(1))**2 )
C     
      INTE2=JAC(2)*
     &      ( ((DSIP(2)+DSGXX(2))*NX(2)+DSGXY(2)*NY(2))**2
     &       +(DSGXY(2)*NX(2)+(DSGYY(2)+DSIP(2))*NY(2))**2 )
C
      IF (NBNA.EQ.3) THEN
      INTE3=JAC(3)*
     &      ( ((DSIP(3)+DSGXX(3))*NX(3)+DSGXY(3)*NY(3))**2
     &       +(DSGXY(3)*NX(3)+(DSGYY(3)+DSIP(3))*NY(3))**2 )
      ENDIF
C        
C PARTIE HYDRAULIQUE
C
      INTEH1=JAC(1)*((DMX(1)*NX(1)+DMY(1)*NY(1))**2)
C     
      INTEH2=JAC(2)*((DMX(2)*NX(2)+DMY(2)*NY(2))**2)
C
      IF (NBNA.EQ.3) THEN
       INTEH3=JAC(3)*((DMX(3)*NX(3)+DMY(3)*NY(3))**2)
      ENDIF
C
C ON ASSEMBLE LES TERMES DE MECANIQUE (TERSAM) ET D'HYDRAULIQUE
C (TERSAH)
C
      IF (NBNA.EQ.2) THEN
        TERSAM = TERSAM+0.5D0*SQRT(HF)*SQRT(INTE1+INTE2)
      ELSE
        TERSAM = TERSAM+0.5D0*SQRT(HF)*
     &       SQRT((INTE1+4.D0*INTE3+INTE2)/3.D0)
      ENDIF
C
      IF (NBNA.EQ.2) THEN
        TERSAH = TERSAH+0.5D0*SQRT(HF)*SQRT(INTEH1+INTEH2)
      ELSE
        TERSAH = TERSAH+0.5D0*SQRT(HF)*
     &       SQRT((INTEH1+4.D0*INTEH3+INTEH2)/3.D0)
      ENDIF
C
      END
