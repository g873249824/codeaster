      SUBROUTINE BRBACK(ZERO,NBACKN,DCURVP,DDISIP,MP1,MP2,
     &                  ZEROF,ZEROG,DC1,DC2,DTG,BBOK)
      IMPLICIT NONE
      COMMON /TDIM/ N, ND
      REAL*8 NBACKN(6),DCURVP(3),DDISIP,MP1(3),MP2(3),ZERO
      REAL*8 ZEROF,ZEROG,DC1(3,3),DC2(3,3),DTG(6,6),TMP(3)
      REAL*8 DEPSLO(6),DCURBB(3),F1,F2,DDISBB,FPLAS,GPLAS
      INTEGER NCRIT,NCRNEW,NBB,I,J,CRINUM, N, ND , NBITER
      LOGICAL BBOK
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 18/10/2004   AUTEUR LEBOUVIE F.LEBOUVIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C     BUT    :  - REVENIR PAR METHODE DIRECTE SUR LA SURFACE ELASTIQUE 
C                 QUAND LA DIFFERENCE DES MOMENTS ENTRE 2 ITERATIONS 
C                 SUCCESSIVES EST A L'EXTERIEUR DU VOLUME ELASTIQUE
C  
C IN    R  DC1    : MATRICE ELSTIQUE + CONSTANTE DE PRAGER (FLEXION +)
C       R  DC2    : MATRICE ELSTIQUE + CONSTANTE DE PRAGER (FLEXION -)
C       R  DTG    : MATRICE TANGENTE
C
C INOUT R  NBACKN : FORCES DE RAPPEL =(N-BACKN,M-BACKM)=(N,M-BACKM)
C       R  DCURVP : INCREMENT DE COURBURE PLASTIQUE
C       R  DDISIP : INCREMENT DE DISSIPATION PLASTIQUE
C       R  MP1    : MOMENT LIMITE ELASTIQUE EN FLEXION POSITIVE
C       R  MP2    : MOMENT LIMITE ELASTIQUE EN FLEXION NEGATIVE
C       I  NCRIT  : NUMERO DU CRITERE DE PLASTICITE
C        
C OUT   L  BBOK   : VRAI SI L'ALGORITHME A CONVERGER 
C-----------------------------------------------------------------------
C
      BBOK=.FALSE.
      NBB = 0
      NBITER = 1000
C
      CALL INITVE(6,DEPSLO)
      NCRIT=CRINUM(NBACKN(4),DEPSLO,MP1,MP2,DTG)
C
C     ATTENTION, NCRIT DOIT ETRE PLUS GRAND QUE ZERO CE QUI
C     SIGNIFIE QUE (M-BACKM) EST A L'EXTERIEUR DU VOLUME D'ELASTICITE
C
10    CONTINUE
      NBB = NBB+1
      IF (NBB .GT. NBITER) GOTO 20
C        CALCUL DU NOUVEAU MOMENT, DE L'INCREMENT DE COURBURE PLASTIQUE
C        ET DE LA DISSIPATION
         CALL DNCUDI(NBACKN,DEPSLO,NCRIT,NBACKN,DCURBB,
     &        DDISBB,MP1,MP2,DC1,DC2,DTG,ZEROG,ZERO)
         N = 3
         CALL LCSOVE(DCURBB,DCURVP,DCURVP)
         DDISIP=DDISIP+DDISBB
C        ATTENTION: NBACKN(1:3)=N(1:3) CAR BACKN(1:3)=0
         CALL MPUPDA(ZERO,NBACKN,MP1,MP2,ZEROF,ZEROG)
         F1=FPLAS(NBACKN(4),MP1)
         F2=FPLAS(NBACKN(4),MP2)
         IF(F1.LT.ZEROF .AND. 
     &      F2.LT.ZEROF .AND.
     &      GPLAS(NBACKN(4),MP1).LT.ZEROG .AND.
     &      GPLAS(NBACKN(4),MP2).LT.ZEROG .AND.
     &      (F1.GT.(-ZEROF) .OR. F2.GT.(-ZEROF)))THEN
            BBOK=.TRUE.
            GOTO 20
         ENDIF
         NCRNEW=CRINUM(NBACKN(4),DEPSLO,MP1,MP2,DTG)
         IF(NCRNEW.GT.0)THEN
            NCRIT=NCRNEW
         ENDIF
C
C     SI NCRNEW =0 CELA SIGNIFIE QUE MBACKM EST DANS LE VOLUME 
C     ELASTIQUE, NOUS UTILISONS DONC LE CRITERE DE LA DERNIERE
C     ITERATION POUR CALCULER LA COURBURE PLASTIQUE, C'EST POURQUOI
C     NCRIT N'EST PAS REACTUALISEE.
C
      GOTO 10
 20   CONTINUE
      END
