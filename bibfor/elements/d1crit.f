      SUBROUTINE D1CRIT (ZIMAT,NMNBN,NMPLAS,NMDPLA,NMPROX,CNBN
     &              ,CPLAS,RPARA,CIEF,CDEPS,CDTG,CIER,CDEPSP,DC,BEND)
        IMPLICIT  NONE
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 19/05/2009   AUTEUR SFAYOLLE S.FAYOLLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C     CALCUL DU MULTIPLICATEUR PLASTIQUE
C     ET DE L INCREMENT DE COURBURE PLASTIQUE
C     DANS LE CAS OU 1 CRITERE PLASTIQUE EST ACTIVE
C     METHODE EXPLICITE (PREMIER ORDRE)
C
C IN  ZIMAT : ADRESSE DE LA LISTE DE MATERIAU CODE
C IN  NMNBN : FORCE - BACKFORCE
C IN  NMPLAS : MOMENTS LIMITES DE PLASTICITE
C IN  NMDPLA : DERIVEES DES MOMENTS LIMITES DE PLASTICITE
C IN  NMPROX : NMPROX > 0 : NBN DANS ZONE DE CRITIQUE
C IN  CDTG : MATRICE TANGENTE
C IN  DC : MATRICE ELASTIQUE + CONSTANTES DE PRAGER
C IN  BEND : FLEXION POSITIVE (1) OU NEGATIVE (-1)
C
C IN/OUT RPARA : LISTES DE PARAMETRES DE TYPE ENTIER
C
C OUT CNBN : NOUVELLE FORCE - BACKFORCE
C OUT CPLAS : NOUVEAUX MOMENTS LIMITES DE PLASTICITE
C OUT CIEF : NOUVEAU CIEF > 0 : NBN HORS DE LA ZONE DE DEFINITION DE MP
C OUT CDEPS : NOUVEL INCREMENT DE DEFORMATION DANS LE REPERE ORTHO
C OUT CIER : NOUVEAU CODE ERREUR
C OUT CDEPSP : NOUVEL INCREMENT DE DEF PLASTIQUE DANS LE REPERE ORTHO

      INTEGER BEND, J, ZIMAT, NMPROX(2), CIEF, CIER

      REAL*8 NMNBN(6), NMPLAS(2,3), NMDPLA(2,2)
      REAL*8 CNBN(6), CPLAS(2,3), CZEF, CZEG, CDEPS(6)
      REAL*8 CDTG(6,6), CDEPSP(6), DC(6,6), CP(6), NORMM
      REAL*8 LAMBDA, DF(6), TDF(1,6), A(1), B(1), DFU(6)
      REAL*8 FPLASS,RPARA(3)

      CZEF  = RPARA(1)
      CZEG  = RPARA(2)
      NORMM = RPARA(3)

C     CALCUL LE GRADIENT DU CRITERE DE PLASICITE
      CALL DFPLGL(NMNBN,NMPLAS,NMDPLA,BEND,DF)

      DO 10, J = 1,6
        TDF(1,J) = DF(J)
 10   CONTINUE

C     CALUL DES DIRECTIONS DE L ECOULEMENT DES DEFORMATIONS PLASTIQUES
      CALL DFUUSS(NMNBN,NMPLAS,NMDPLA,NMPROX,BEND,DFU)

      CALL MATMUL(CDTG,CDEPS,6,6,1,CP)
      CALL MATMUL(TDF,CP,1,6,1,A)
      CALL MATMUL(DC,DFU,6,6,1,CP)
      CALL MATMUL(TDF,CP,1,6,1,B)

C     CALCUL DU MULTIPLICATEUR PLASTIQUE
      LAMBDA = (FPLASS(NMNBN,NMPLAS,BEND) + A(1)) / B(1)

      DO 20, J = 1,6
        CDEPSP(J) = LAMBDA * DFU(J)
 20   CONTINUE

C     CALCUL DE CNBN ET CDEPSP QUAND UN CRITERE PLASTIQUE EST ACTIVE
      CALL NMNET1(ZIMAT,NMNBN,CNBN,CPLAS,CZEF,CZEG
     &         ,CIEF,CDEPS,CDTG,CIER,CDEPSP,DC,NORMM)

      RPARA(1) = CZEF
      RPARA(2) = CZEG
      RPARA(3) = NORMM

      END
