      SUBROUTINE NMGRIB(NNO,GEOM,DFF,DIR11,LEXC,VECN,B,JAC,P)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 22/11/2010   AUTEUR FLEJOU J-L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C CALCUL DE LA MATRICE B ET JACOBIEN POUR LES GRILLES SECONDE GENERATION
C ----------------------------------------------------------------------
      IMPLICIT NONE
      LOGICAL LEXC
      INTEGER NNO
      REAL*8  GEOM(3,NNO),DFF(2,NNO),DIR11(3)
      REAL*8  B(6,NNO),VECN(3),P(3,6)
      INTEGER I,J,N,ALPHA,BETA,GAMMA
      REAL*8 COVA(3,3),METR(2,2),JAC,CNVA(3,2),A(2,2),R1(3),PROJN
      REAL*8 MTEMP(3,NNO),DENOMI,R8PREM

      CALL SUBACO(NNO,DFF,GEOM,COVA)
      CALL SUMETR(COVA,METR,JAC)
      CALL SUBACV(COVA,METR,JAC,CNVA,A)

      CALL R8INIR(3,0.D0,R1,1)
      CALL R8INIR(6*NNO,0.D0,B,1)

      PROJN = 0.D0

      DO 5 J=1,3
         DO 6 I=1,2
            R1(I) = R1(I)+COVA(J,I)*DIR11(J)
6        CONTINUE
         PROJN = PROJN + COVA(J,3) * DIR11(J)
5     CONTINUE

      DENOMI = (1.D0 - PROJN*PROJN)
      IF ( ABS( DENOMI ).LE.R8PREM() ) THEN
         CALL U2MESS('F','ELEMENTS_3')
      ENDIF

      DO 10 I=1,3
         DO 10 N=1,NNO
            DO 10 ALPHA=1,2
               DO 10 BETA=1,2
                  DO 10 GAMMA=1,2
                     B(I,N) = B(I,N)+R1(ALPHA)*R1(GAMMA)*A(BETA,GAMMA)*
     &                        DFF(BETA,N)*CNVA(I,ALPHA)/DENOMI
10    CONTINUE

      IF (LEXC) THEN
         DO 20 N=1,NNO
            DO 20 I=1,3
               MTEMP(I,N)=B(I,N)
20       CONTINUE

         CALL R8INIR(18,0.D0,P,1)
         CALL R8INIR(6*NNO,0.D0,B,1)

         DO 40 I=1,3
            P(I,I)=1.D0
40       CONTINUE
         P(1,5)=VECN(3)
         P(1,6)=-VECN(2)
         P(2,4)=-VECN(3)
         P(2,6)=VECN(1)
         P(3,4)=VECN(2)
         P(3,5)=-VECN(1)

         DO 50 N=1,NNO
            DO 50 I=1,6
               DO 50 J=1,3
                  B(I,N)=B(I,N)+MTEMP(J,N)*P(J,I)
50       CONTINUE

      ENDIF
      END
