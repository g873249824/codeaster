      SUBROUTINE TE0019(OPTION,NOMTE)
      IMPLICIT NONE
C.......................................................................
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C     BUT: CALCUL DES VECTEURS ELEMENTAIRES EN MECANIQUE
C          CORRESPONDANT A UN CHARGEMENT EN PRESSION
C          SUR DES FACES D'ELEMENTS ISOPARAMETRIQUES 3D
C          (LA PRESSION EST DONNEE SOUS FORME D'UNE FONCTION)
C
C          OPTION : 'CHAR_MECA_PRES_F '
C
C     ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................
C
      INCLUDE 'jeveux.h'
      CHARACTER*8   NOMPAR(4)
      CHARACTER*16  NOMTE,OPTION
      INTEGER       IER,NDIM,NNO,NPG,NNOS,JGANO,KPG,KDEC,N
      INTEGER       IPOIDS,IVF,IDF,IGEOM,IPRES,ITEMPS,IRES
      REAL*8        VALPAR(4),X,Y,Z
C                                   9*27*27
      REAL*8        PR, P(27), MATR(6561)
C
C
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDF,JGANO)
C
C    AUTRES VARIABLES DE L'OPTION
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PPRESSF','L',IPRES)
      CALL JEVECH('PTEMPSR','L',ITEMPS)
      CALL JEVECH('PVECTUR','E',IRES)
C
C    CALCUL DE LA PRESSION AUX PTS DE GAUSS (FONCTION DE T ET/OU X,Y,Z)
      VALPAR(4) = ZR(ITEMPS)
      NOMPAR(4) = 'INST'
      NOMPAR(1) = 'X'
      NOMPAR(2) = 'Y'
      NOMPAR(3) = 'Z'
C
      DO 100 KPG=0,NPG-1
        KDEC = KPG*NNO
C
C      COORDONNEES DU POINT DE GAUSS
        X = 0.D0
        Y = 0.D0
        Z = 0.D0
        DO 105 N = 0,NNO-1
          X = X + ZR(IGEOM+3*N  ) * ZR(IVF+KDEC+N)
          Y = Y + ZR(IGEOM+3*N+1) * ZR(IVF+KDEC+N)
          Z = Z + ZR(IGEOM+3*N+2) * ZR(IVF+KDEC+N)
 105    CONTINUE
C
C      VALEUR DE LA PRESSION
        VALPAR(1) = X
        VALPAR(2) = Y
        VALPAR(3) = Z
        CALL FOINTE('FM',ZK8(IPRES),4,NOMPAR,VALPAR,PR,IER)
        P(KPG+1) = PR
100   CONTINUE
C
C    CALCUL EFFECTIF DU SECOND MEMBRE
      CALL NMPR3D(1,NNO,NPG,ZR(IPOIDS),ZR(IVF),ZR(IDF),ZR(IGEOM),P,
     &            ZR(IRES),MATR)
C
      END
