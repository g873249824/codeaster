      SUBROUTINE MBCINE(NNO,GEOM,DFF,ALPHA,BETA,B,JAC)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 04/09/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C ----------------------------------------------------------------------
C      CALCUL DE LA MATRICE B ET DU JACOBIEN POUR LES MEMBRANES
C ----------------------------------------------------------------------
C IN  NNO          NOMBRE DE NOEUDS
C IN  GEOM         COORDONNEES DES NOEUDS
C IN  DFF          DERIVEE DES F. DE FORME
C IN  ALPHA,BETA   ANGLES NAUTIQUES ORIENTANT LE COMPORTEMENT
C                             ORTHOTROPE DE LA MEMBRANE (EN RADIAN)
C OUT B            MATRICE DE PASSAGE DEPL. NODAL --> DEF. MEMBRANAIRES
C OUT JAC          JACOBIEN DE LA TRANSFORMATION
C ----------------------------------------------------------------------
C     LES TROIS INDICES DE B CORRESPONDENT RESPECTIVEMENT :
C               - A LA COMPOSANTE DE EPSILON MEMBRANAIRE PARMI
C                     (EPS11, EPS22, SQRT(2)EPS12)
C                     CALCULEES DANS LA BASE LOCALE
C               - A LA COMPOSANTE DU VECTEUR DEPLACEMENT
C               - AU NUMERO DU NOEUD
C
C     LA COMPOSANTE VIDE FACILITE L'UTILISATION DE NMCOMP
C ----------------------------------------------------------------------

      IMPLICIT NONE
      INTEGER  NNO,I,N,GAMMA
      REAL*8   GEOM(3,NNO),DFF(2,NNO),VDIREC(3),VORTHO(3)
      REAL*8   COVA(3,3),METR(2,2),JAC,CNVA(3,2),A(2,2)
      REAL*8   ALPHA,BETA,PROJN,B(3,3,NNO),DENOMI,R8PREM
      REAL*8   FACTOR,DICNVA(2),ORCNVA(2)

C - CALCUL DES COORDONNEES COVARIANTES ET CONTRAVARIANTES DE SURFACE

      CALL SUBACO(NNO,DFF,GEOM,COVA)
      CALL SUMETR(COVA,METR,JAC)
      CALL SUBACV(COVA,METR,JAC,CNVA,A)

C - CALCUL ET PROJECTION DU VECTEUR DIRECTION SUR LA SURFACE

      VDIREC(1) = COS(BETA)*COS(ALPHA)
      VDIREC(2) = COS(BETA)*SIN(ALPHA)
      VDIREC(3) = SIN(BETA)

      PROJN = 0.D0
      DO 10 I=1,3
        PROJN = PROJN + VDIREC(I)*COVA(I,3)
10    CONTINUE

      IF ( ABS( 1.D0 - PROJN*PROJN ).LE.R8PREM() ) THEN
         CALL U2MESS('F','ELEMENTS_3')
      ENDIF

      DENOMI = SQRT(1.D0 - PROJN*PROJN)
      DO 20 I=1,3
         VDIREC(I) = (VDIREC(I) - PROJN*COVA(I,3))/DENOMI
20    CONTINUE

C - CALCUL DU VECTEUR TANGENT ORTHOGONAL AU VECTEUR DIRECTION

      VORTHO(1) = COVA(2,3)*VDIREC(3) - COVA(3,3)*VDIREC(2)
      VORTHO(2) = COVA(3,3)*VDIREC(1) - COVA(1,3)*VDIREC(3)
      VORTHO(3) = COVA(1,3)*VDIREC(2) - COVA(2,3)*VDIREC(1)

C - CALCUL DE LA MATRICE B

      CALL R8INIR(3*NNO*3, 0.D0, B, 1)

C - LE TERME DE CISAILLEMENT EST SYMETRISE ET MULTIPLIE PAR SQRT(2)
      FACTOR = 1.D0/SQRT(2.D0)

C - ON PRECALCULE CERTAINS PRODUITS SCALAIRES
      CALL R8INIR(2, 0.D0, DICNVA, 1)
      CALL R8INIR(2, 0.D0, ORCNVA, 1)
      DO 30 GAMMA=1,2
        DO 30 I=1,3
          DICNVA(GAMMA) = DICNVA(GAMMA) + VDIREC(I)*CNVA(I,GAMMA)
          ORCNVA(GAMMA) = ORCNVA(GAMMA) + VORTHO(I)*CNVA(I,GAMMA)
30    CONTINUE


C - ON BOUCLE SUR LES DEGRES DE LIBERTE
      DO 40 N=1,NNO
        DO 40 I=1,3
          DO 40 GAMMA=1,2
            B(1,I,N) = B(1,I,N) + DFF(GAMMA,N)*DICNVA(GAMMA)*VDIREC(I)
            B(2,I,N) = B(2,I,N) + DFF(GAMMA,N)*ORCNVA(GAMMA)*VORTHO(I)
            B(3,I,N) = B(3,I,N) + FACTOR*DFF(GAMMA,N)
     &               *(DICNVA(GAMMA)*VORTHO(I)+ORCNVA(GAMMA)*VDIREC(I))
40    CONTINUE

      END
