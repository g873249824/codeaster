      SUBROUTINE TE0033(OPTION,NOMTE)
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*16 OPTION,NOMTE
C     ------------------------------------------------------------------
C MODIF ELEMENTS  DATE 04/04/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C     CALCUL DE CONTRAINTES, DEFORMATIONS, EFFORTS ET DEFORMATIONS
C     GENERALISES POUR LES ELEMENTS DKT, DST, DKQ, DSQ ET Q4G
C     POUR UN MATERIAU ISOTROPE OU MULTICOUCHE
C         OPTIONS TRAITEES  ==>  SIEF_ELGA_DEPL
C                                EFGE_ELNO_DEPL
C                                EFGE_ELNO_DEPL_C
C                                SIGM_ELNO_DEPL
C                                SIGM_ELNO_DEPL_C
C                                EPSI_ELNO_DEPL
C                                DEGE_ELNO_DEPL
C     IN   K16   OPTION : NOM DE L'OPTION A CALCULER
C     IN   K16   NOMTE  : NOM DU TYPE_ELEMENT
C     ------------------------------------------------------------------
C     ----- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      INTEGER MULTIC ,JTAB(8)
      LOGICAL GRILLE
      REAL*8 PGL(3,3),XYZL(3,4),EFFOV(32),SIGV(32),VALPU(2)
      REAL*8 DEPL(24),DEPLR(24),DEPLI(24),SIGMR(32),SIGMI(32)
      REAL*8 DEPGR(24),DEPGI(24),SIGMRL(32),SIGMIL(32),EPAIS
      REAL*8 VALRES(1),ROT(2,2),XAB(2,2),SIGC(4),EFFIN2(32)
      REAL*8 TMOY(4),TSUP(4),TINF(4),EFFGT(32),SIGTOT(24)
      CHARACTER*2 CODRET(1),VAL
      CHARACTER*3 NUM
      CHARACTER*8 NOMRES(1),NOMPU(2),ELREFE
      CHARACTER*24 DESI,DESR
C     ------------------------------------------------------------------

      CALL ELREF1(ELREFE)

      IF (OPTION.NE.'SIEF_ELGA_DEPL' .AND.
     +    OPTION.NE.'EFGE_ELNO_DEPL' .AND.
     +    OPTION.NE.'EFGE_ELNO_DEPL_C' .AND.
     +    OPTION.NE.'SIGM_ELNO_DEPL' .AND.
     +    OPTION.NE.'SIGM_ELNO_DEPL_C' .AND.
     +    OPTION.NE.'EPSI_ELNO_DEPL' .AND.
     +    OPTION.NE.'DEGE_ELNO_DEPL   ') THEN
        CALL UTMESS('F','TE0033','OPTION NON TRAITEE')
      END IF
      ICOMPX = 0

      IF (NOMTE(1:8).EQ.'MEGRDKT ') THEN
        GRILLE = .TRUE.
      ELSE
        GRILLE = .FALSE.
      END IF


      ZERO   = 0.0D0

      DO 10 I = 1,32
        EFFGT(I) = ZERO
   10 CONTINUE

      DO 20 I = 1,24
        SIGTOT(I) = ZERO
   20 CONTINUE

      DO 30 I = 1,4
        TINF(I) = ZERO
        TMOY(I) = ZERO
        TSUP(I) = ZERO
   30 CONTINUE

      IF (OPTION(15:16).EQ.'_C') ICOMPX = 1

      CALL JEVECH('PGEOMER','L',JGEOM)
      CALL JEVECH('PMATERC','L',JMATE)
      CALL JEVECH('PCACOQU','L',JCARA)
      EPAIS  = ZR(JCARA)

      DESI = '&INEL.'//ELREFE//'.DESI'
      CALL JEVETE(DESI,'L',LZI)
      DESR = '&INEL.'//ELREFE//'.DESR'
      CALL JEVETE(DESR,'L',LZR)
      NNO = ZI(LZI)
      NPG = ZI(LZI+2)

      IF (OPTION(8:9).EQ.'GA') THEN
        NP = NPG
      ELSE IF (OPTION(8:9).EQ.'NO') THEN
        NP = NNO
      END IF

      IF (NNO.EQ.3) THEN
        CALL DXTPGL(ZR(JGEOM),PGL)
        LT2EV = 51
      ELSE IF (NNO.EQ.4) THEN
        CALL DXQPGL(ZR(JGEOM),PGL)
        LT2EV = 81
      END IF

      CALL UTPVGL(NNO,3,PGL,ZR(JGEOM),XYZL)
      CALL DXREPE(NNO,PGL,ZR(LZR))

      IF (ICOMPX.EQ.0) THEN
        CALL JEVECH('PDEPLAR','L',JDEPG)
        CALL UTPVGL(NNO,6,PGL,ZR(JDEPG),DEPL)
      ELSE
        CALL JEVECH('PDEPLAC','L',JDEPG)
        ICOMPX = 1
        NDIM = NNO*6
        DO 40 I = 1,NDIM
          DEPGR(I) = DBLE(ZC(JDEPG+I-1))
          DEPGI(I) = DIMAG(ZC(JDEPG+I-1))
   40   CONTINUE
        CALL UTPVGL(NNO,6,PGL,DEPGR,DEPLR)
        CALL UTPVGL(NNO,6,PGL,DEPGI,DEPLI)
      END IF

      IF (OPTION(1:9).EQ.'SIEF_ELGA' .OR.
     +    OPTION(1:9).EQ.'EFGE_ELNO' .OR.
     +    OPTION(1:9).EQ.'SIGM_ELNO') THEN
C===============================================================
C          -- RECUPERATION DE LA TEMPERATURE :
C          -- SI LA TEMPERATURE EST CONNUE AUX NOEUDS :
      CALL TECAC2 ('NNN','PTEMPER',8,JTAB,IRET)
      ITEMP=JTAB(1)
        IF (ITEMP.GT.0) THEN
          DO 50 I = 1,NNO
            CALL DXTPIF(ZR(ITEMP+3*(I-1)),ZL(JTAB(8)+3*(I-1)))
            TMOY(I) = ZR(ITEMP+3* (I-1))
            TINF(I) = ZR(ITEMP+3* (I-1)+1)
            TSUP(I) = ZR(ITEMP+3* (I-1)+2)
   50     CONTINUE
        END IF
C          -- SI LA TEMPERATURE EST UNE FONCTION DE 'INST' ET 'EPAIS'
      CALL TECACH(.FALSE.,.FALSE.,'PTEMPEF',1,ITEMP)
        IF (ITEMP.GT.0) THEN
          NOMPU(1) = 'INST'
          NOMPU(2) = 'EPAIS'
          CALL JEVECH('PTEMPSR','L',IBID)
          VALPU(1) = ZR(IBID)
          VALPU(2) = 0.D0
          CALL FOINTE('FM',ZK8(ITEMP),2,NOMPU,VALPU,TMOY1,IER)
          VALPU(2) = -EPAIS/2.D0
          CALL FOINTE('FM',ZK8(ITEMP),2,NOMPU,VALPU,TINF1,IER)
          VALPU(2) = +EPAIS/2.D0
          CALL FOINTE('FM',ZK8(ITEMP),2,NOMPU,VALPU,TSUP1,IER)
          DO 60,I = 1,NNO
            TMOY(I) = TMOY1
            TINF(I) = TINF1
            TSUP(I) = TSUP1
   60     CONTINUE
        END IF
C===============================================================
      END IF

C     ---------- CONTRAINTES ET DEFORMATIONS --------------------------

      IF (OPTION(1:9).EQ.'SIGM_ELNO') THEN
        CALL JEVECH('PNUMCOR','L',JNUMCO)
        IC = ZI(JNUMCO)
        INIV = ZI(JNUMCO+1)
        IF (ICOMPX.EQ.0) THEN
          CALL JEVECH('PCONTRR','E',JSIGM)

C ---     CALCUL DES CONTRAINTES VRAIES (I.E. SIG_MECA - SIG_THERM)
C ---     AUX POINTS DE CALCUL
C         --------------------
          CALL DXSIGV(NOMTE,OPTION,XYZL,PGL,IC,INIV,DEPL,TSUP,TINF,TMOY,
     +                SIGTOT)

C ---     PASSAGE DES CONTRAINTES DU REPERE INTRINSEQUE
C ---     A L'ELEMENT AU REPERE LOCAL DE LA COQUE
C         ---------------------------------------
          CALL DXSIRO(NP,ZR(LZR-1+LT2EV),SIGTOT,ZR(JSIGM))
        ELSE

          IF (NOMTE(1:8).EQ.'MEDKTR3 ' .OR.
     +        NOMTE(1:8).EQ.'MEGRDKT ') THEN
            CALL DKTCOD(XYZL,OPTION,PGL,IC,INIV,DEPLR,SIGMRL,MULTIC,
     +                  GRILLE)
            CALL DKTCOD(XYZL,OPTION,PGL,IC,INIV,DEPLI,SIGMIL,MULTIC,
     +                  GRILLE)

          ELSE IF (NOMTE(1:8).EQ.'MEDSTR3 ') THEN
            CALL DSTCOD(XYZL,OPTION,PGL,IC,INIV,DEPLR,SIGMRL)
            CALL DSTCOD(XYZL,OPTION,PGL,IC,INIV,DEPLI,SIGMIL)
          ELSE IF (NOMTE(1:8).EQ.'MEDKQU4 ') THEN
            CALL DKQCOD(XYZL,OPTION,PGL,IC,INIV,DEPLR,SIGMRL)
            CALL DKQCOD(XYZL,OPTION,PGL,IC,INIV,DEPLI,SIGMIL)
          ELSE IF (NOMTE(1:8).EQ.'MEDSQU4 ') THEN
            CALL DSQCOD(XYZL,OPTION,PGL,IC,INIV,DEPLR,SIGMRL)
            CALL DSQCOD(XYZL,OPTION,PGL,IC,INIV,DEPLI,SIGMIL)
          ELSE IF (NOMTE(1:8).EQ.'MEQ4QU4 ') THEN
            CALL Q4GCOD(XYZL,OPTION,PGL,IC,INIV,DEPLR,SIGMRL)
            CALL Q4GCOD(XYZL,OPTION,PGL,IC,INIV,DEPLI,SIGMIL)
          END IF

          CALL DXSIRO(NP,ZR(LZR-1+LT2EV),SIGMRL,SIGMR)
          CALL DXSIRO(NP,ZR(LZR-1+LT2EV),SIGMIL,SIGMI)

          CALL JEVECH('PCONTRC','E',JSIGM)
          DO 70 I = 1,6*NNO
            ZC(JSIGM+I-1) = DCMPLX(SIGMR(I),SIGMI(I))
   70     CONTINUE
        END IF

      ELSE IF (OPTION(1:9).EQ.'EPSI_ELNO') THEN
        CALL JEVECH('PDEFORR','E',JSIGM)
        CALL JEVECH('PNUMCOR','L',JNUMCO)
        IC = ZI(JNUMCO)
        INIV = ZI(JNUMCO+1)


        IF (NOMTE(1:8).EQ.'MEDKTR3 ' .OR. NOMTE(1:8).EQ.'MEGRDKT ') THEN
          CALL DKTCOD(XYZL,OPTION,PGL,IC,INIV,DEPL,SIGTOT,MULTIC,GRILLE)

        ELSE IF (NOMTE(1:8).EQ.'MEDSTR3 ') THEN
          CALL DSTCOD(XYZL,OPTION,PGL,IC,INIV,DEPL,SIGTOT)
        ELSE IF (NOMTE(1:8).EQ.'MEDKQU4 ') THEN
          CALL DKQCOD(XYZL,OPTION,PGL,IC,INIV,DEPL,SIGTOT)
        ELSE IF (NOMTE(1:8).EQ.'MEDSQU4 ') THEN
          CALL DSQCOD(XYZL,OPTION,PGL,IC,INIV,DEPL,SIGTOT)
        ELSE IF (NOMTE(1:8).EQ.'MEQ4QU4 ') THEN
          CALL Q4GCOD(XYZL,OPTION,PGL,IC,INIV,DEPL,SIGTOT)
        END IF

C ---     PASSAGE DES DEFORMATIONS  DU REPERE INTRINSEQUE
C ---     A L'ELEMENT AU REPERE LOCAL DE LA COQUE
C         ---------------------------------------
        CALL DXSIRO(NP,ZR(LZR-1+LT2EV),SIGTOT,ZR(JSIGM))

C     --------  EFFORTS GENERALISES  SIEF_ELGA  ET  EFGE_ELNO ---------
C     --------  DEFORMATIONS GENERALISEES  DEGE_ELNO ------------------

      ELSE IF (OPTION(1:9).EQ.'SIEF_ELGA' .OR.
     +         OPTION(1:9).EQ.'EFGE_ELNO') THEN
        IF (ICOMPX.EQ.0) THEN

C ---     CALCUL DES EFFORTS GENERALISES VRAIS
C ---     AUX POINTS DE CALCUL
C         --------------------
          CALL DXEFGV(NOMTE,OPTION,XYZL,PGL,DEPL,TSUP,TINF,TMOY,EFFGT)
C
C ---    PRISE EN COMPTE DE L'EXCENTREMENT SI ON CALCULE LES
C ---    EFFORTS GENERALISES SUR UN FEUILLET DE REFERENCE DIFFERENT
C ---    DU FEUILLET DU MAILLAGE (I.E. EN PEAU SUP, INF OU MOY)
C        ------------------------------------------------------
C
          CALL EXCENT(OPTION,NOMTE,NNO,EFFGT,ICOMPX)

C ---     PASSAGE DES EFFORTS GENERALISES DU REPERE INTRINSEQUE
C ---     A L'ELEMENT AU REPERE LOCAL DE LA COQUE
C         ---------------------------------------

          IF (OPTION(1:9).EQ.'SIEF_ELGA') THEN
            CALL DXEFRO(NP,ZR(LZR-1+LT2EV),EFFGT,EFFIN2)
            CALL TECACH(.TRUE.,.TRUE.,'PCONTRR',7,JTAB)
            NPG=JTAB(3)
            NBSP=JTAB(7)
            IF (NP.NE.NPG) CALL UTMESS('F','TE0033','STOP')
            DO 777,IPG=1,NPG
              ICONTP=JTAB(1)+(6*NBSP)*(IPG-1)+6*(NBSP-2)
              CALL R8COPY(8,EFFIN2(8*(IPG-1)+1),1,ZR(ICONTP),1)
777         CONTINUE

          ELSE IF (OPTION(1:9).EQ.'EFGE_ELNO') THEN
            CALL JEVECH('PEFFORR','E',JEFFG)
            CALL DXEFRO(NP,ZR(LZR-1+LT2EV),EFFGT,ZR(JEFFG))
          END IF

        ELSE

          IF (NOMTE(1:8).EQ.'MEDKTR3 ' .OR.
     +        NOMTE(1:8).EQ.'MEGRDKT ') THEN
            CALL DKTEDG(XYZL,OPTION,PGL,DEPLR,SIGMRL,MULTIC,GRILLE)
            CALL DKTEDG(XYZL,OPTION,PGL,DEPLI,SIGMIL,MULTIC,GRILLE)

          ELSE IF (NOMTE(1:8).EQ.'MEDSTR3 ') THEN
            CALL DSTEDG(XYZL,OPTION,PGL,DEPLR,SIGMRL)
            CALL DSTEDG(XYZL,OPTION,PGL,DEPLI,SIGMIL)
          ELSE IF (NOMTE(1:8).EQ.'MEDKQU4 ') THEN
            CALL DKQEDG(XYZL,OPTION,PGL,DEPLR,SIGMRL)
            CALL DKQEDG(XYZL,OPTION,PGL,DEPLI,SIGMIL)
          ELSE IF (NOMTE(1:8).EQ.'MEDSQU4 ') THEN
            CALL DSQEDG(XYZL,OPTION,PGL,DEPLR,SIGMRL)
            CALL DSQEDG(XYZL,OPTION,PGL,DEPLI,SIGMIL)
          ELSE IF (NOMTE(1:8).EQ.'MEQ4QU4 ') THEN
            CALL Q4GEDG(XYZL,OPTION,PGL,DEPLR,SIGMRL)
            CALL Q4GEDG(XYZL,OPTION,PGL,DEPLI,SIGMIL)
          END IF

C ---    PRISE EN COMPTE DE L'EXCENTREMENT SI ON CALCULE LES
C ---    EFFORTS GENERALISES SUR UN FEUILLET DE REFERENCE DIFFERENT
C ---    DU FEUILLET DU MAILLAGE (I.E. EN PEAU SUP, INF OU MOY)
C        ------------------------------------------------------
          CALL EXCENT(OPTION,NOMTE,NNO,SIGMRL,ICOMPX)
          CALL EXCENT(OPTION,NOMTE,NNO,SIGMIL,ICOMPX)
C
          CALL DXEFRO(NP,ZR(LZR-1+LT2EV),SIGMRL,SIGMR)
          CALL DXEFRO(NP,ZR(LZR-1+LT2EV),SIGMIL,SIGMI)

          CALL JEVECH('PEFFORC','E',JEFFG)
          DO 90 I = 1,8*NNO
            ZC(JEFFG+I-1) = DCMPLX(SIGMR(I),SIGMI(I))
   90     CONTINUE
        END IF
      ELSE IF (OPTION(1:9).EQ.'DEGE_ELNO') THEN

        CALL JEVECH('PDEFOGR','E',JEFFG)


        IF (NOMTE(1:8).EQ.'MEDKTR3 ' .OR. NOMTE(1:8).EQ.'MEGRDKT ') THEN
          CALL DKTEDG(XYZL,OPTION,PGL,DEPL,EFFGT,MULTIC,GRILLE)

        ELSE IF (NOMTE(1:8).EQ.'MEDSTR3 ') THEN
          CALL DSTEDG(XYZL,OPTION,PGL,DEPL,EFFGT)
        ELSE IF (NOMTE(1:8).EQ.'MEDKQU4 ') THEN
          CALL DKQEDG(XYZL,OPTION,PGL,DEPL,EFFGT)
        ELSE IF (NOMTE(1:8).EQ.'MEDSQU4 ') THEN
          CALL DSQEDG(XYZL,OPTION,PGL,DEPL,EFFGT)
        ELSE IF (NOMTE(1:8).EQ.'MEQ4QU4 ') THEN
          CALL Q4GEDG(XYZL,OPTION,PGL,DEPL,EFFGT)
        END IF
C
C ---    PRISE EN COMPTE DE L'EXCENTREMENT SI ON CALCULE LES
C ---    EFFORTS GENERALISES SUR UN FEUILLET DE REFERENCE DIFFERENT
C ---    DU FEUILLET DU MAILLAGE (I.E. EN PEAU SUP, INF OU MOY)
C        ------------------------------------------------------
          CALL EXCENT(OPTION,NOMTE,NNO,EFFGT,ICOMPX)
C
C ---     PASSAGE DES DEFORMATIONS GENERALISEES DU REPERE INTRINSEQUE
C ---     A L'ELEMENT AU REPERE LOCAL DE LA COQUE
C         ---------------------------------------
        CALL DXEFRO(NP,ZR(LZR-1+LT2EV),EFFGT,ZR(JEFFG))

      END IF

      END
