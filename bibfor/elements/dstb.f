      SUBROUTINE DSTB ( CARAT3, PGL, IGAU, JACGAU, BMAT )
      IMPLICIT  NONE
      INTEGER   IGAU
      REAL*8    PGL(3,3), BMAT(8,1), CARAT3(*), JACGAU
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 04/04/2011   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     ------------------------------------------------------------------
C --- CALCUL DE LA MATRICE (B) RELIANT LES DEFORMATIONS DU PREMIER
C --- ORDRE AUX DEPLACEMENTS AU POINT D'INTEGRATION D'INDICE IGAU
C --- POUR UN ELEMENT DE TYPE DST
C --- (I.E. (EPS_1) = (B)*(UN))
C --- D'AUTRE_PART, ON CALCULE LE PRODUIT NOTE JACGAU = JACOBIEN*POIDS
C     ------------------------------------------------------------------
C     IN  PGL(3,3)      : MATRICE DE PASSAGE DU REPERE GLOBAL AU REPERE
C                         LOCAL
C     IN  IGAU          : INDICE DU POINT D'INTEGRATION
C     OUT JACGAU        : PRODUIT JACOBIEN*POIDS AU POINT D'INTEGRATION
C                         COURANT
C     OUT BMAT(8,1)     : MATRICE (B) AU POINT D'INTEGRATION COURANT
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      INTEGER  NDIM,NNO,NNOS,NPG,IPOIDS,ICOOPG,IVF,IDFDX,IDFD2,JGANO
      INTEGER  MULTIC, I, J, K
      REAL*8 DF(3,3),DM(3,3),DMF(3,3),DC(2,2),DCI(2,2),DMC(3,2),DFC(3,2)
      REAL*8 BFB(3,9),BFA(3,3),BFN(3,9),BM(3,6),BF(3,9)
      REAL*8 BCA(2,3),BCN(2,9),BC(2,9)
      REAL*8 HFT2(2,6),AN(3,9)
      REAL*8 QSI, ETA, T2EV(4), T2VE(4), T1VE(9)
      LOGICAL COUPMF
C     ------------------------------------------------------------------
C
      CALL ELREF5(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,ICOOPG,
     +                                         IVF,IDFDX,IDFD2,JGANO)
C
C --- COORDONNEES DU POINT D'INTEGRATION COURANT :
C     ------------------------------------------
      QSI = ZR(ICOOPG-1+NDIM*(IGAU-1)+1)
      ETA = ZR(ICOOPG-1+NDIM*(IGAU-1)+2)
C
C --- PRODUIT JACOBIEN*POIDS
C     ----------------------
      JACGAU = ZR(IPOIDS+IGAU-1)*CARAT3(7)

C --- CALCUL DES MATRICES DE HOOKE DE FLEXION, MEMBRANE,
C --- MEMBRANE-FLEXION, CISAILLEMENT, CISAILLEMENT INVERSE
C     ----------------------------------------------------
      CALL DXMATE('RIGI',DF,DM,DMF,DC,DCI,DMC,DFC,NNO,PGL,MULTIC,
     +                                   COUPMF,T2EV,T2VE,T1VE)

C --- CALCUL DE LA MATRICE NOTEE (HF.T2) PAR BATOZ RELIANT LES
C --- EFFORTS TRANCHANTS (T) AU VECTEUR DES DERIVEES DES COURBURES
C --- PAR RAPPORT AUX COORDONNEES PARAMETRIQUES ,
C --- SOIT (T) = (HF.T2)*(D (D_BETA/D_QSI)/D_QSI)
C     -----------------------------------------------------------
      CALL DSXHFT( DF, CARAT3(9), HFT2 )

C --- CALCUL DE LA MATRICE (AN) RELIANT LES INCONNUES NOTEES (ALFA)
C --- PAR BATOZ AUX INCONNUES (UN) = (...,W_I,BETAX_I,BETAY_I,...)
C --- SOIT (ALFA) = (AN)*(UN)
C --- ON RAPPELLE QUE CES INCONNUES SONT DEFINIES AU MILIEU DES COTES
C --- DE TELLE MANIERE QUE LA COMPOSANTE DES ROTATIONS LE LONG DES
C --- COTES EST UNE FONCTION QUADRATIQUE DE L'ABSCISSE CURVILIGNE
C     -----------------------------------------------------------
      CALL DSTCIS( DCI, CARAT3, HFT2, BCA, AN )

C --- CALCUL DE LA MATRICE B_MEMBRANE NOTEE, ICI, (BM)
C     ------------------------------------------------
      CALL DXTBM( CARAT3(9), BM )

C --- CALCUL DE LA MATRICE B_FLEXION RELATIVE AUX INCONNUES W, BETAX
C --- ET BETAY ET NE TENANT PAS COMPTE DE L'INTERPOLATION QUADRATIQUE
C --- DES ROTATIONS EN FONCTION DES INCONNUES ALFA.
C --- CETTE MATRICE EST NOTEE (BF_BETA) PAR BATOZ ET ICI (BFB)
C     --------------------------------------------------------
      CALL DSTBFB( CARAT3(9), BFB )

C --- CALCUL DE LA PARTIE DE LA MATRICE B_FLEXION RELATIVE
C --- AUX INCONNUES ALFA
C --- CETTE MATRICE EST NOTEE (BF_ALFA) PAR BATOZ ET ICI (BFA)
C     --------------------------------------------------------
      CALL DSTBFA( QSI, ETA, CARAT3, BFA )

C --- CALCUL DE LA MATRICE B_FLEXION COMPLETE, NOTEE (BF) ET RELATIVE
C --- AUX SEULES INCONNUES (UN) :
C --- (BF) = (BFB) + (BFA)*(AN)
C     -------------------------
      DO 20 I = 1,3
        DO 10 J = 1,9
          BFN(I,J) = 0.D0
   10   CONTINUE
   20 CONTINUE

      DO 50 I = 1,3
        DO 40 J = 1,9
          DO 30 K = 1,3
            BFN(I,J) = BFN(I,J) + BFA(I,K)*AN(K,J)
   30     CONTINUE
          BF(I,J) = BFB(I,J) + BFN(I,J)
   40   CONTINUE
   50 CONTINUE

C --- CALCUL DE LA MATRICE B_CISAILLEMENT, NOTEE (BC) ET RELATIVE
C --- AUX  INCONNUES (UN) , AVEC LES NOTATIONS DE BATOZ
C --- (T) = (BCA)*(ALFA) OU T EST LE VECTEUR DES EFFORTS TRANCHANTS.
C --- (T) = (BCA)*(AN)*(UN)
C --- D'AUTRE-PART (GAMMA) = (DCI)*(T) OU GAMMA EST LE VECTEUR
C --- DES DEFORMATIONS DE CISAILLEMENT TRANSVERSE , D'OU :
C --- (BC) = (DCI)*(BCA)*(AN)
C     -----------------------
      DO 70 I = 1,2
        DO 60 J = 1,9
          BC(I,J) = 0.D0
          BCN(I,J) = 0.D0
   60   CONTINUE
   70 CONTINUE

      DO 100 I = 1,2
        DO 90 J = 1,9
          DO 80 K = 1,3
            BCN(I,J) = BCN(I,J) + BCA(I,K)*AN(K,J)
   80     CONTINUE
   90   CONTINUE
  100 CONTINUE

      DO 130 I = 1,2
        DO 120 J = 1,9
          DO 110 K = 1,2
            BC(I,J) = BC(I,J) + DCI(I,K)*BCN(K,J)
  110     CONTINUE
  120   CONTINUE
  130 CONTINUE

C --- AFFECTATION DE LA MATRICE B COMPLETE, NOTEE (BMAT)
C --- AVEC LES MATRICES (BM), (BF) ET (BC)
C     ------------------------------------
      CALL BCOQAF(BM,BF,BC,NNO,BMAT)

      END
