      SUBROUTINE XTELGA(NDIM,ELREFP,NNOP,IGEOM,TEMPNO,LONCH,CNSET,
     &                  JPINTT,LSN,LST,BASLOC,HEAVT,NFH,NFE,TEMPPG)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 11/03/2013   AUTEUR CUVILLIE M.CUVILLIEZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRS_1404
C RESPONSABLE CUVILLIEZ
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8 ELREFP
      INTEGER     NDIM,NNOP,IGEOM,NFH,NFE,JPINTT
      INTEGER     LONCH(10),CNSET(4*32),HEAVT(36)
      REAL*8      TEMPNO(NNOP*(1+NFH+NFE)),LSN(NNOP),LST(NNOP)
      REAL*8      BASLOC(*),TEMPPG(*)
C
C-----------------------------------------------------------------------
C
C     BUT: THERMIQUE LINEAIRE / ELEMENTS X-FEM LINEAIRES
C          CALCUL DE L'OPTION : 'TEMP_ELGA'
C
C IN :
C ---
C NDIM   --> DIMENSION DE L'ESPACE (2 OU 3)
C ELREFP --> NOM DE L'ELT PARENT DE REFERENCE
C NNOP   --> NBRE DE NOEUDS DE L'ELT PARENT DE REFERENCE
C IGEOM  --> ADRESSE DES COORDONEES DES NOEUDS DE L'ELT PARENT
C TEMPNO --> TEMPERATURE AUX NOEUDS
C LONCH  --> LONGUEURS DES CHAMPS UTILISES
C CNSET  --> CONNECTIVITE DES SOUS-ELEMENTS
C JPINTT --> ADRESSE DES COORDONEES DES POINTS D'INTERSECTION
C LSN    --> VALEUR DE LA LEVEL SET NORMALE AUX NOEUDS PARENTS
C LST    --> VALEUR DE LA LEVEL SET TANGENTIELLE AUX NOEUDS PARENTS
C BASLOC --> BASE LOCALE AU FOND DE FISSURE
C HEAVT  --> VALEURS DE L'HEAVISIDE SUR LES SS-ELTS
C NFH    --> NBRE DE FONCTION D'ENRICHISSEMENT HEAVISIDE (0 OU 1)
C NFE    --> NBRE DE FONCTION D'ENRICHISSEMENT CRACKTIP  (0 OU 1)
C
C OUT :
C ----
C TEMPPG --> TEMPERATURE AUX POINTS DE GAUSS
C
C-----------------------------------------------------------------------
C
      CHARACTER*8 ELRESE(3),FAMI(3)
      REAL*8  BASLOG(3*NDIM),TEM,LSNG,LSTG,COORSE(81),XG(NDIM),XE(NDIM)
      REAL*8  FEMEC(4),DGDMEC(4,NDIM),FETH,FF(NNOP),DFDI(NNOP,NDIM),HE
      REAL*8  FFENR(NNOP,1+NFH+NFE)
      INTEGER IVF,KPG,IBID,NNO,NPG,J,IRET,NSE,ISE,INP,IN,INO,KDDL,NBDDL
      INTEGER MXSTAC
C
      PARAMETER   (MXSTAC=1000)
C
      DATA    ELRESE /'SE2','TR3','TE4'/
      DATA    FAMI   /'BID','XINT','XINT'/
C
C-----------------------------------------------------------------------
C
C     VERIF QUE LES TABLEAUX LOCAUX DYNAMIQUES NE SONT PAS TROP GRANDS
C     (VOIR CRS 1404)
      CALL ASSERT(NNOP.LE.MXSTAC)
C
C     NBRE DE DDLS PAR NOEUD
      NBDDL = 1+NFH+NFE
C
C     SOUS-ELEMENT DE REFERENCE : RECUP DE NNO,NPG,IVF
      CALL ELREF5(ELRESE(NDIM),FAMI(NDIM),IBID,NNO,IBID,NPG,IBID,IBID,
     &            IVF,IBID,IBID,IBID)
C
C     RECUPERATION DE LA SUBDIVISION DE L'ELEMENT EN NSE SOUS ELEMENT
      NSE=LONCH(1)
C
C ----------------------------------------------------------------------
C --- BOUCLE SUR LES NSE SOUS-ELEMENTS
C ----------------------------------------------------------------------
C
      DO 1000 ISE=1,NSE
C
C       VALEUR (CSTE) DE LA FONCTION HEAVISIDE SUR LE SS-ELT
        HE = 1.D0*HEAVT(ISE)
C
C       BOUCLE SUR LES SOMMETS DU SOUS-TETRA/TRIA -> COORDS NOEUDS
        DO 1100 IN=1,NNO
          INO=CNSET(NNO*(ISE-1)+IN)
          DO 1110 J=1,NDIM
            IF (INO.LT.1000) THEN
              COORSE(NDIM*(IN-1)+J)=ZR(IGEOM-1+NDIM*(INO-1)+J)
            ELSEIF (INO.GT.1000 .AND. INO.LT.2000) THEN
              COORSE(NDIM*(IN-1)+J)=ZR(JPINTT-1+NDIM*(INO-1000-1)+J)
            ELSE 
              CALL ASSERT(.FALSE.)
            ENDIF
 1110     CONTINUE
 1100   CONTINUE
C
C ----------------------------------------------------------------------
C ----- BOUCLE SUR LES POINTS DE GAUSS
C ----------------------------------------------------------------------
C       
        DO 1200 KPG=1,NPG
C
C         COORDONNÉES DU PT DE GAUSS DANS LE REPÈRE RÉEL : XG
          CALL VECINI(NDIM,0.D0,XG)
          DO 1210 J=1,NDIM
            DO 1211 IN=1,NNO
              XG(J)=XG(J)+ZR(IVF-1+NNO*(KPG-1)+IN)*COORSE(NDIM*(IN-1)+J)
 1211       CONTINUE
 1210     CONTINUE
C
C         XG -> XE (DANS LE REPERE DE l'ELREFP) ET VALEURS DES FF EN XE
          CALL VECINI(NDIM,0.D0,XE)
          CALL REERET(ELREFP,NNOP,ZR(IGEOM),XG,NDIM,'OUI',XE,FF,DFDI)
C
C ------- SI ENRICHISSEMENT SINGULIER
          IF (NFE.GT.0) THEN
C           BASE LOCALE ET LEVEL SETS AU POINT DE GAUSS
            CALL VECINI(3*NDIM,0.D0,BASLOG)
            LSNG = 0.D0
            LSTG = 0.D0
            DO 1220 INP=1,NNOP
              LSNG = LSNG + LSN(INP) * FF(INP)
              LSTG = LSTG + LST(INP) * FF(INP)
              DO 1221 J=1,3*NDIM
                BASLOG(J) = BASLOG(J)
     &                    + BASLOC(3*NDIM*(INP-1)+J) * FF(INP)
 1221         CONTINUE
 1220       CONTINUE
C           FONCTION D'ENRICHISSEMENT (MECA) AU PG ET DÉRIVÉES
            IF (NDIM.EQ.2) THEN
              CALL XCALF2(HE,LSNG,LSTG,BASLOG,FEMEC,DGDMEC,IRET)
            ELSEIF (NDIM.EQ.3) THEN
              CALL XCALFE(HE,LSNG,LSTG,BASLOG,FEMEC,DGDMEC,IRET)
            ENDIF
C           PB DE CALCUL DES DERIVEES DES FONCTIONS SINGULIERES
C           CAR ON SE TROUVE SUR LE FOND DE FISSURE
            CALL ASSERT(IRET.NE.0)
C           ON NE GARDE QUE LES ENRICHISSEMENTS UTILES EN THERMIQUE
            FETH = FEMEC(1)
          ENDIF
C ------- FIN SI ENRICHISSEMENT SINGULIER
C
C         FFENR : TABLEAU DES FF ENRICHIES
          DO 1250 INP=1,NNOP
C           DDL CLASSIQUE (TEMP)
            FFENR(INP,1) = FF(INP)
C           DDL HEAVISIDE (H1)
            IF (NFH.EQ.1) THEN
              FFENR(INP,1+NFH) = HE*FF(INP)
            ENDIF
C           DDL CRACK-TIP (E1)
            IF (NFE.EQ.1) THEN
              FFENR(INP,1+NFH+NFE) = FETH*FF(INP)
            ENDIF
 1250     CONTINUE
C
C         CALCUL DE TEMP AU PG
          TEM = 0.D0
          DO 1270 INP=1,NNOP
            DO 1271 KDDL=1,NBDDL
              TEM = TEM + TEMPNO(NBDDL*(INP-1)+KDDL)*FFENR(INP,KDDL)
 1271       CONTINUE
 1270     CONTINUE
C
C         ECRITURE DE TEMP AU PG
          TEMPPG(NPG*(ISE-1)+KPG) = TEM
C
 1200   CONTINUE
C
C ----------------------------------------------------------------------
C ----- FIN BOUCLE SUR LES POINTS DE GAUSS
C ----------------------------------------------------------------------
C       
 1000   CONTINUE
C
C ----------------------------------------------------------------------
C --- FIN BOUCLE SUR LES SOUS-ELEMENTS
C ----------------------------------------------------------------------
C
      END
