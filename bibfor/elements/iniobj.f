      SUBROUTINE INIOBJ(NDIM,NNO,NNOS,NBFPG,ALIAS,XIN,YIN,ZIN,XSO,YSO,
     &ZSO,NBPG)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 20/03/2002   AUTEUR MJBHHPE J.L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C.......................................................................
C DERNIERE MAJ : 02/04/90
      IMPLICIT REAL*8 (A-H,O-Z)
C
C BUT: ROUTINE D'INITIALISATION DES OBJETS &INEL
C
C ENTREE ---> ALIAS       : NOM D'ALIAS DE L'ELEMENT
C        ---> NDIM        : DIMENSION DE L'ESPACE DE REFERENCE
C        ---> NNO         : NOMBRE DE NOEUDS DE L'ELEMENT
C        ---> NBPG        : TABLEAU DES NOMBRES DE PTS DE GAUSS
C        ---> NBFPG       : NOMBRE DE FAMILLE DE POINTS DE GAUSS
C        ---> XIN,YIN,ZIN : COORDONNEES DES NOEUDS DE L'ELT DE REFERENCE
C        ---> XSO,YSO,ZSO : COORDONNEES DES SOMMETS
C
      CHARACTER*8  ALIAS,ALIAS2
      CHARACTER*24 CHCTE,CHVAL,CHMAT
C
      REAL*8       XIN(1),YIN(1),ZIN(1)
      REAL*8       XSO(1),YSO(1),ZSO(1)
      REAL*8       XPG(27),YPG(27),ZPG(27),HPG(27)
      REAL*8       TN(27),AJ(3,27),AA,BB,CC,DD
      REAL*8       M(64),N(216),PP(6,6)
      INTEGER      NBPG(1)
C
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
C
      CHARACTER*32       JEXNUM , JEXNOM , JEXR8 , JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
C
C     CREATION ET REMPLISSAGE DE : &INEL.00I.CARACTE_MAILLE
C
      LOGICAL THLU
      INTEGER FAMLUM
C
C  THLU EST VRAI SI ON TRAITE UN ELEMENT THM LUMPE
C  FAMLUM EST ALORS LE NUMERO DE LA FAMILLE C A D DES GAUSS
C  IDENTIQUES AU SOMMET
C
      CALL  ALTHLU(ALIAS,THLU ,FAMLUM ) 
C
      CALL JEMARQ()
      NCAR  = NBFPG + 4
      CHCTE = '&INEL.'//ALIAS//'.CARACTE'
      CALL JECREO(CHCTE,'G V I')
      CALL JEECRA(CHCTE,'LONMAX',NCAR,' ')
      CALL JEVEUO(CHCTE,'E',JIN)
C
      ZI(JIN+1-1) = NDIM
      ZI(JIN+2-1) = NNO
      ZI(JIN+3-1) = NBFPG
      DO 99 IFAPG=1,NBFPG
         LL=JIN+3-1+IFAPG
         ZI(LL) = NBPG(IFAPG)
 99   CONTINUE
      ZI(JIN+3-1+NBFPG+1) = NNOS
C
C     CREATION ET REMPLISSAGE DE : &INEL.00I.VALEURS_MAILLE
C
      NVAL  = NNO*NNO*(NDIM+1)
      NBF = NBFPG
      IF((ALIAS(1:7).EQ.'PENTA15').OR.
     >    (ALIAS(1:8).EQ.'PENTA15D')) THEN
       NBF = NBFPG-1
      ENDIF
      DO 111 I=1,NBF
         NVAL = NVAL + NBPG(I)*(1+NNO+NDIM*NNO)
 111  CONTINUE
      IF(ALIAS(1:7).EQ.'PENTA15'.OR.
     >    ALIAS(1:8).EQ.'PENTA15D') THEN
         NVAL = NVAL + NBPG(NBFPG)*(1+NNOS+NDIM*NNOS)
      ENDIF
      CHVAL = '&INEL.'//ALIAS//'.FFORMES'
      CALL JECREO(CHVAL,'G V R')
      CALL JEECRA(CHVAL,'LONMAX',NVAL,' ')
      CALL JEVEUO(CHVAL,'E',JVAL)
C
      JDEC=NNO*NNO
C
      DO 11 II=1,NNO
         XN = XIN(II)
         YN = YIN(II)
         ZN = ZIN(II)
         CALL CALCFF(ALIAS,NNO,XN,YN,ZN,XIN,YIN,ZIN,TN,AJ)
         DO 21 INO=1,NNO
            K = NNO*(II-1)+INO
            J = JVAL+K-1
            ZR(J) = TN(INO)
C
            L = NDIM*(K-1)
            J = JVAL+JDEC+L-1
            DO 31 I=1,NDIM
               ZR(J+I) = AJ(I,INO)
 31         CONTINUE
 21      CONTINUE
 11   CONTINUE
C
      JDEC = NNO*NNO*(NDIM+1)
C
      DO 100 IFAPG=1,NBF
C
         NPG = NBPG(IFAPG)
C
C        STOCKAGE DES POIDS DES POINTS DE GAUSS
C
         IF ( THLU.AND.(IFAPG.EQ.FAMLUM)) THEN
          CALL GAUSNO(ALIAS,NPG,XPG,YPG,ZPG,HPG)
         ELSE
          CALL GAUSS(ALIAS,NPG,XPG,YPG,ZPG,HPG)
         ENDIF
         DO 22 IPG=1,NPG
            ZR(JVAL+JDEC+IPG-1) = HPG(IPG)
 22      CONTINUE
C
C        STOCKAGE DES FONCTIONS DE FORMES ET DE LEURS DERIVEES
C        AUX POINTS DE GAUSS
C
         JDEC = JDEC + NPG
         JD   = NPG*NNO
C
         DO 33 IPG=1,NPG
            XG = XPG(IPG)
            YG = YPG(IPG)
            ZG = ZPG(IPG)
            CALL CALCFF(ALIAS,NNO,XG,YG,ZG,XIN,YIN,ZIN,TN,AJ)
            DO 34 INO=1,NNO
               K = NNO*(IPG-1)+INO
               J = JVAL+JDEC+K-1
               ZR(J) = TN(INO)
C
               L = NDIM*(K-1)
               J = JVAL+JDEC+JD+L-1
               DO 35 I=1,NDIM
                  ZR(J+I) = AJ(I,INO)
 35            CONTINUE
 34         CONTINUE
 33      CONTINUE
C
         JDEC = JDEC + NPG*NNO + NPG*NNO*NDIM
C
 100  CONTINUE
C
C
C  STOCKAGE DE LA DERNIERE FAMILLE DE POINTS DE GAUSS CORRESPONDANT A :
C
C    VALEURS DES FCTS DE FORME ASSOCIEES A L'ELEMENT DE DEGRE 1 AUX
C    POINTS DE GAUSS DE L'ELEMENT DE DEGRE 2 (POUR SIGM_ELNO)
C    (UNIQUEMENT POUR LE PENTA15)
C
      IF(ALIAS.EQ.'PENTA15'.OR.ALIAS.EQ.'PENTA15D') THEN
         ALIAS2 = 'PENTA6'
C
         NPG = NBPG(NBFPG)
C
C        STOCKAGE DES POIDS DES POINTS DE GAUSS
C
         CALL GAUSS(ALIAS,NPG,XPG,YPG,ZPG,HPG)
         DO 23 IPG=1,NPG
            ZR(JVAL+JDEC+IPG-1) = HPG(IPG)
 23      CONTINUE
C
C        STOCKAGE DES FONCTIONS DE FORMES  AUX POINTS DE GAUSS
C
         JDEC = JDEC + NPG
         JD   = NPG*NNOS
C
         DO 36 IPG=1,NPG
            XG = XPG(IPG)
            YG = YPG(IPG)
            ZG = ZPG(IPG)
            CALL CALCFF(ALIAS2,NNOS,XG,YG,ZG,XSO,YSO,ZSO,TN,AJ)
C
            DO 37 INO=1,NNOS
               K = NNOS*(IPG-1)+INO
               J = JVAL+JDEC+K-1
               ZR(J) = TN(INO)
C
               L = NDIM*(K-1)
               J = JVAL+JDEC+JD+L-1
               DO 38 I=1,NDIM
                  ZR(J+I) = AJ(I,INO)
 38            CONTINUE
 37         CONTINUE
C
 36      CONTINUE
C
         ENDIF
C
      CALL JEDEMA()
C
      END
