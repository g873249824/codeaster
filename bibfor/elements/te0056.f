      SUBROUTINE TE0056(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 11/04/2002   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C.......................................................................
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*16      OPTION,NOMTE
C
C     BUT: CALCUL DU SECOND MEMBRE ELEMENTAIRE EN THERMIQUE CORRESPON-
C          DANT A UNE SOURCE VOLUMIQUE FONCTION
C          ELEMENTS ISOPARAMETRIQUES 3D
C
C          OPTION : 'CHAR_THER_SOUR_F'
C
C     ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      CHARACTER*8        ELREFE,NOMPAR(4)
      CHARACTER*24       CHVAL,CHCTE
      REAL*8             DFDX(27),DFDY(27),DFDZ(27),POIDS,SOURC,THETA
      REAL*8             VALPAR(4),XX,YY,ZZ
      INTEGER            IPOIDS,IVF,IDFDE,IDFDN,IDFDK,IGEOM
      INTEGER            NNO,KP,NPG1,I,IVECTT,ISOUR,ITEMPS,NBPG(10)
C
      CALL ELREF1(ELREFE)
C
      CHCTE = '&INEL.'//ELREFE//'.CARACTE'
      CALL JEVETE(CHCTE,'L',JIN)
      NDIM = ZI(JIN+1-1)
      NNO = ZI(JIN+2-1)
      NBFPG = ZI(JIN+3-1)
      DO 111 I = 1,NBFPG
        NBPG(I) = ZI(JIN+3-1+I)
  111 CONTINUE
      NPG1 = NBPG(1)
C
      CHVAL = '&INEL.'//ELREFE//'.FFORMES'
      CALL JEVETE(CHVAL,'L',JVAL)
C
      IPOIDS = JVAL + (NDIM+1)*NNO*NNO
      IVF    = IPOIDS + NPG1
      IDFDE  = IVF    + NPG1*NNO
      IDFDN  = IDFDE  + 1
      IDFDK  = IDFDN  + 1
C
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PSOURCF','L',ISOUR)
      CALL JEVECH('PTEMPSR','L',ITEMPS)
      CALL JEVECH('PVECTTR','E',IVECTT)
C
      THETA     = ZR(ITEMPS+2)
      NOMPAR(1) = 'X'
      NOMPAR(2) = 'Y'
      NOMPAR(3) = 'Z'
      NOMPAR(4) = 'INST'
C
      DO 10 I=1,NNO
        ZR(IVECTT-1+I) = 0.0D0
 10   CONTINUE
C
C    BOUCLE SUR LES POINTS DE GAUSS
C
      DO 101 KP=1,NPG1
        L=(KP-1)*NNO
        K=(KP-1)*NNO*3
        CALL DFDM3D ( NNO,ZR(IPOIDS+KP-1),ZR(IDFDE+K),ZR(IDFDN+K),
     &   ZR(IDFDK+K),ZR(IGEOM),DFDX,DFDY,DFDZ,POIDS )
C
C    CALCUL DE SOURC
C
         XX = 0.D0
         YY = 0.D0
         ZZ = 0.D0
         DO 102 I = 1,NNO
           XX = XX + ZR(IGEOM+3*I-3) * ZR(IVF+L+I-1)
           YY = YY + ZR(IGEOM+3*I-2) * ZR(IVF+L+I-1)
           ZZ = ZZ + ZR(IGEOM+3*I-1) * ZR(IVF+L+I-1)
 102     CONTINUE
         VALPAR(1) = XX
         VALPAR(2) = YY
         VALPAR(3) = ZZ
         VALPAR(4) = ZR(ITEMPS)
         CALL FOINTE('FM',ZK8(ISOUR),4,NOMPAR,VALPAR,SOUNP1,IER)
         IF ( THETA .NE. 1.0D0 ) THEN
           VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
           CALL FOINTE('FM',ZK8(ISOUR),4,NOMPAR,VALPAR,SOUN,IER)
         ELSE
           SOUN = 0.D0
         ENDIF
         SOURC = THETA*SOUNP1+(1.0D0-THETA)*SOUN
C
CCDIR$ IVDEP
         DO 106 I=1,NNO
           ZR(IVECTT+I-1) = ZR(IVECTT+I-1)
     &                      + POIDS * SOURC * ZR(IVF+L+I-1)
 106     CONTINUE
C
 101  CONTINUE
C
      END
