      SUBROUTINE D2CRIT (ZIMAT,NMNBN,NMPLAS,NMDPLA,NMPROX,CNBN,
     &         CPLAS,RPARA,CIEF,CDEPS,CDTG,CIER,CDEPSP,DC1,DC2)
      IMPLICIT NONE
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 19/05/2009   AUTEUR SFAYOLLE S.FAYOLLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C     CALCUL DES MULTIPLICATEURS PLASTIQUES
C     ET DE L INCREMENT DE COURBURE PLASTIQUE
C     DANS LE CAS OU 2 CRITERE PLASTIQUE SONT ACTIVES
C     METHODE EXPLICITE (PREMIER ORDRE)
C
C IN  ZIMAT : ADRESSE DE LA LISTE DE MATERIAU CODE
C IN  NMNBN : FORCE - BACKFORCE
C IN  NMPLAS : MOMENTS LIMITES DE PLASTICITE
C IN  NMDPLA : DERIVEES DES MOMENTS LIMITES DE PLASTICITE
C IN  NMDDPL : DERIVEES SECONDES DES MOMENTS LIMITES DE PLASTICITE
C IN  NMPROX : NMPROX > 0 : NBN DANS ZONE DE CRITIQUE
C IN  CDTG : MATRICE TANGENTE
C IN  DC1 : MATRICE ELASTIQUE + CONSTANTES DE PRAGER
C IN  DC2 : MATRICE ELASTIQUE + CONSTANTES DE PRAGER
C
C IN/OUT RPARA : LISTES DE PARAMETRES DE TYPE ENTIER
C
C OUT CNBN : NOUVELLE FORCE - BACKFORCE
C OUT CPLAS : NOUVEAUX MOMENTS LIMITES DE PLASTICITE
C OUT CIEF : NOUVEAU CIEF > 0 : NBN HORS DE LA ZONE DE DEFINITION DE MP
C OUT CDEPS : NOUVEL INCREMENT DE DEFORMATION DANS LE REPERE ORTHO
C OUT CIER : NOUVEAU CODE ERREUR
C OUT CDEPSP : NOUVEL INCREMENT DE DEF PLASTIQUE DANS LE REPERE ORTHO

      INTEGER J, ZIMAT, NMPROX(2), CIEF, CIER

      REAL*8 NMNBN(6), NMPLAS(2,3), NMDPLA(2,2)
      REAL*8 CNBN(6), CPLAS(2,3), CZEF, CZEG, CDEPS(6)
      REAL*8 CDTG(6,6), CDEPSP(6), DC1(6,6), DC2(6,6), NORMM
      REAL*8 FPLASS, RPARA(3), CP(6), DEPSP2(6,2)
      REAL*8 LAMBDA(2,2),F1,F2,DF(6,2),TDF(2,6),A(2),B1(2),B2(2)
      REAL*8 DENOM,DF1(6),DF2(6),AUX,DFU1(6),DFU2(6),DFU(6,2)

      CZEF  = RPARA(1)
      CZEG  = RPARA(2)
      NORMM = RPARA(3)

C     CALCUL LE GRADIENT DES CRITERES DE PLASICITE
      CALL DFPLGL(NMNBN,NMPLAS,NMDPLA,1,DF1)
      CALL DFPLGL(NMNBN,NMPLAS,NMDPLA,2,DF2)

      DO 10, J = 1,6
        DF(J,1) = DF1(J)
        DF(J,2) = DF2(J)
        TDF(1,J) = DF(J,1)
        TDF(2,J) = DF(J,2)
 10   CONTINUE

C     CALUL DES DIRECTIONS DE L ECOULEMENT DES DEFORMATIONS PLASTIQUES
      CALL DFUUSS(NMNBN,NMPLAS,NMDPLA,NMPROX,1,DFU1)
      CALL DFUUSS(NMNBN,NMPLAS,NMDPLA,NMPROX,2,DFU2)

      DO 20, J = 1,6
        DFU(J,1) = DFU1(J)
        DFU(J,2) = DFU2(J)
 20   CONTINUE

      CALL MATMUL(CDTG,CDEPS,6,6,1,CP)
      CALL MATMUL(TDF,CP,2,6,1,A)
      CALL MATMUL(DC1,DFU1,6,6,1,CP)
      CALL MATMUL(TDF,CP,2,6,1,B1)
      CALL MATMUL(DC2,DFU2,6,6,1,CP)
      CALL MATMUL(TDF,CP,2,6,1,B2)

      DENOM = B1(1)*B2(2)-B1(2)*B2(1)
      AUX = B1(1)**2 + B2(1)**2 + B1(2)**2 + B2(2)**2

C     CALUL DES MULTIPLICATEURS PLASTIQUES
      CALL R8INIR(2*2,0.0D0,LAMBDA,1)

      F1 = FPLASS(NMNBN,NMPLAS,1) + A(1)
      F2 = FPLASS(NMNBN,NMPLAS,2) + A(2)

      IF ( ABS(DENOM) .LT. 1.0D-3 * AUX ) THEN
        DENOM = B1(1)+B2(2)+B1(2)+B2(1)

        IF (ABS(DENOM) .LT. 1.0D-3 * SQRT(AUX)) THEN
          CIER=3
          CALL R8INIR(6,0.0D0,CDEPSP,1)
          GOTO 40
        ENDIF

        LAMBDA(1,1) = (F1 + F2) / DENOM
        LAMBDA(2,2) = LAMBDA(1,1)
      ELSE
        LAMBDA(1,1) = (F1*B2(2)-F2*B2(1))/DENOM
        LAMBDA(2,2) = (F2*B1(1)-F1*B1(2))/DENOM
      ENDIF

      CALL MATMUL(DFU,LAMBDA,6,2,2,DEPSP2)

      DO 30, J = 1,6
        CDEPSP(J) = DEPSP2(J,1)+DEPSP2(J,2)
 30   CONTINUE

C     CALCUL DE CNBN ET DEPSP2 QUAND DEUX CRITERES PLAST SONT ACTIVES
      CALL NMNET2(ZIMAT,NMNBN,CNBN,CPLAS,CZEF,CZEG
     &           ,CIEF,CDEPS,CDTG,CIER
     &           ,DC1,DC2,DEPSP2,NORMM)

 40   CONTINUE

      RPARA(1) = CZEF
      RPARA(2) = CZEG
      RPARA(3) = NORMM

      END
