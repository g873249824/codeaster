      SUBROUTINE TE0021(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C.......................................................................
      IMPLICIT NONE

C     BUT: CALCUL DES MATRICES DE RAIDEUR CENTRIFUGE ELEMENTAIRES
C          ELEMENTS ISOPARAMETRIQUES 3D

C          OPTION : 'RIGI_MECA_RO '

C     ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................

      INCLUDE 'jeveux.h'

      INTEGER ICODRE
      CHARACTER*8   FAMI,POUM
      CHARACTER*16  NOMTE,OPTION
      REAL*8        A(3,3,27,27)
      REAL*8        DFDX(27),DFDY(27),DFDZ(27),POIDS
      INTEGER       IPOIDS,IVF,IDFDE,IGEOM,IMATE
      INTEGER       JGANO,NNO,KP,I,J,IMATUU,KPG,SPT


C-----------------------------------------------------------------------
      INTEGER IJKL ,IK ,IROTA ,K ,L ,NDIM ,NNOS
      INTEGER NPG2
      REAL*8 OMEGA1 ,OMEGA2 ,OMEGA3 ,R8B ,RHO ,WIJ
C-----------------------------------------------------------------------
      CALL ELREF4(' ','MASS',NDIM,NNO,NNOS,NPG2,IPOIDS,IVF,IDFDE,JGANO)

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PMATERC','L',IMATE)
      CALL JEVECH('PMATUUR','E',IMATUU)
      CALL JEVECH('PROTATR','E',IROTA)
      FAMI='FPG1'
      KPG=1
      SPT=1
      POUM='+'
      CALL RCVALB(FAMI,KPG,SPT,POUM,ZI(IMATE),' ','ELAS',0,' ',
     &            R8B,1,'RHO',RHO,ICODRE,1)
      OMEGA1 = ZR(IROTA+1)*ZR(IROTA)
      OMEGA2 = ZR(IROTA+2)*ZR(IROTA)
      OMEGA3 = ZR(IROTA+3)*ZR(IROTA)

      DO 50 K = 1,3
        DO 40 L = 1,3
          DO 30 I = 1,NNO
            DO 20 J = 1,I
              A(K,L,I,J) = 0.D0
   20       CONTINUE
   30     CONTINUE
   40   CONTINUE
   50 CONTINUE

C    BOUCLE SUR LES POINTS DE GAUSS

      DO 80 KP = 1,NPG2

        L = (KP-1)*NNO
        CALL DFDM3D ( NNO, KP, IPOIDS, IDFDE,
     &                ZR(IGEOM), DFDX, DFDY, DFDZ, POIDS )

        DO 70 I = 1,NNO
          DO 60 J = 1,I
            WIJ = RHO*POIDS*ZR(IVF+L+I-1)*ZR(IVF+L+J-1)
            A(1,1,I,J) = A(1,1,I,J) - (OMEGA2**2+OMEGA3**2)*WIJ
            A(2,2,I,J) = A(2,2,I,J) - (OMEGA1**2+OMEGA3**2)*WIJ
            A(3,3,I,J) = A(3,3,I,J) - (OMEGA1**2+OMEGA2**2)*WIJ
            A(2,1,I,J) = A(2,1,I,J) + OMEGA1*OMEGA2*WIJ
            A(3,1,I,J) = A(3,1,I,J) + OMEGA1*OMEGA3*WIJ
            A(3,2,I,J) = A(3,2,I,J) + OMEGA2*OMEGA3*WIJ
   60     CONTINUE
   70   CONTINUE
   80 CONTINUE

      DO 100 I = 1,NNO
        DO 90 J = 1,I
          A(1,2,I,J) = A(2,1,I,J)
          A(1,3,I,J) = A(3,1,I,J)
          A(2,3,I,J) = A(3,2,I,J)
   90   CONTINUE
  100 CONTINUE

C PASSAGE DU STOCKAGE RECTANGULAIRE (A) AU STOCKAGE TRIANGULAIRE (ZR)

      DO 140 K = 1,3
        DO 130 L = 1,3
          DO 120 I = 1,NNO
            IK = ((3*I+K-4)* (3*I+K-3))/2
            DO 110 J = 1,I
              IJKL = IK + 3* (J-1) + L
              ZR(IMATUU+IJKL-1) = A(K,L,I,J)
  110       CONTINUE
  120     CONTINUE
  130   CONTINUE
  140 CONTINUE

      END
