      SUBROUTINE POREA2(NNO,NC,GEOM,GAMMA,PGL,XL)
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'

      INTEGER NNO,NC
      REAL*8  GEOM(3,NNO),GAMMA

      REAL*8 PGL(3,3),XL

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 05/03/2013   AUTEUR CHEIGNON E.CHEIGNON 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

C     ------------------------------------------------------------------
C     CALCUL DE LA MATRICE DE PASSAGE GLOBALE/LOCALE EN TENANT COMPTE
C     DE LA GEOMETRIE REACTUALISEE AINSI QUE LA LONGUEUR DE LA POUTRE
C     POUR L'OPTION FORC_NODA
C     ------------------------------------------------------------------
C IN  NNO    : NOMBRE DE NOEUDS
C IN  NC     : NOMBRE DE COMPOSANTE DU CHAMP DE DEPLACEMENTS
C IN  GEOM   : COORDONNEES DES NOEUDS
C IN  GAMMA  : ANGLE DE VRILLE AU TEMPS -
C OUT PGL    : MATRICE DE PASSAGE GLOBAL/LOCAL
C     ------------------------------------------------------------------
C
C     VARIABLES LOCALES
      INTEGER I,IDEPLM,IDEPLP,IRET
      REAL*8 UTG(14),XUG(6),XD(3),XL2,ALFA1,BETA1
      REAL*8 TET1,TET2,GAMMA1,DDOT,ANG1(3)

      CALL ASSERT(NNO.EQ.2)

      CALL TECACH('ONN','PDEPLMR','L',1,IDEPLM,IRET)
      IF ( IRET .NE. 0 ) THEN
        DO 100 I = 1,2*NC
          UTG(I) = 0.D0
100     CONTINUE
      ELSE
        DO 102 I = 1,2*NC
          UTG(I) = ZR(IDEPLM-1+I)
102     CONTINUE
      ENDIF

      CALL TECACH('ONN','PDEPLPR','L',1,IDEPLP,IRET)
      IF ( IRET .EQ. 0 ) THEN
        DO 104 I = 1,2*NC
          UTG(I) = UTG(I) + ZR(IDEPLP-1+I)
104     CONTINUE
      ENDIF

      DO 110 I = 1,3
        XUG(I) = UTG(I) + GEOM(I,1)
        XUG(I+3) = UTG(I+NC) + GEOM(I,2)
110   CONTINUE

      CALL VDIFF(3,XUG(4),XUG(1),XD)
      XL2=DDOT(3,XD,1,XD,1)
      XL = SQRT(XL2)
      TET1=DDOT(3,UTG(4),1,XD,1)
      TET2=DDOT(3,UTG(NC+4),1,XD,1)
      TET1 = TET1/XL
      TET2 = TET2/XL
      CALL ANGVX(XD,ALFA1,BETA1)

      GAMMA1 = GAMMA + (TET1+TET2)/2.D0
      ANG1(1) = ALFA1
      ANG1(2) = BETA1
      ANG1(3) = GAMMA1

      CALL MATROT ( ANG1 , PGL )
      END
