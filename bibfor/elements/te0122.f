      SUBROUTINE TE0122(OPTION,NOMTE)
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16 OPTION,NOMTE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 08/04/2013   AUTEUR CUVILLIE M.CUVILLIEZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE LAVERNE J.LAVERNE
C......................................................................
C     FONCTION REALISEE:  CALCUL DES CHAMELEM AUX NOEUDS A PARTIR DES
C     VALEURS AUX POINTS DE GAUSS ( SIEF_ELNO ET VARI_ELNO )
C     ELEMENTS DE JOINT, JOINT_HYME, INTERFACE ET INTERFACE_S

C IN  OPTION : OPTION DE CALCUL
C IN  NOMTE  : NOM DU TYPE ELEMENT
C ......................................................................

      INTEGER JGANO,NPG,NNOS,NNO,NDIM,NDIME,IPOIDS,IVF,IDFDE
      INTEGER ICHG,ICHN,JTAB(7),NCMP,IBID,I,J,IN,IG,NTROU
      LOGICAL QUADRA,JHM,INTERF,JLIN2D,JLIN3D,JQUAD
      CHARACTER*8 LIELRF(10)
C     ------------------------------------------------------------------
      
      JLIN2D =  ((NOMTE(1:7) .EQ.'MFPLQU4' ).OR.
     &           (NOMTE(1:7) .EQ.'MFAXQU4' )    )
     
      JLIN3D =  ((NOMTE(1:10).EQ.'MEFI_HEXA8' ).OR.
     &           (NOMTE(1:11).EQ.'MEFI_PENTA6' )    )
      
      JQUAD  = ((NOMTE(1:7) .EQ.'MFPLQU8'     ).OR.
     &          (NOMTE(1:11).EQ.'MEFI_HEXA20' ).OR.
     &          (NOMTE(1:12).EQ.'MEFI_PENTA15')    )
     
      JHM    =   NOMTE(1:6).EQ.'EJHYME'
      
      INTERF = ((NOMTE(1:4).EQ.'EIPL').OR.
     &          (NOMTE(1:4).EQ.'EIAX').OR.
     &          (NOMTE(1:5).EQ.'MEEI_') )
     
      QUADRA = (JQUAD.OR.JHM.OR.INTERF)

C RAPPEL DES INTERPOLATIONS POUR L'ESPACE
C JOINT 2D (QUAD4)  EST BASE SUR LE QUAD4 A 2PG DONC NPG=2, NNO=4
C JOINT 3D (HEXA8)  EST BASE SUR LE QUAD4 A 4PG DONC NPG=4, NNO=4
C JOINT 3D (PENTA6) EST BASE SUR LE TRIA3 A 1PG DONC NPG=1, NNO=3
C JOINT_HYME 2D (QUAD8) EST BASE SUR LE SEG2 A 2PG DONC NPG=2, NNO=2
C JOINT_HYME 3D (HEXA20) EST BASE SUR LE QUAD4 A 4PG DONC NPG=4, NNO=4
C JOINT_HYME 3D (PENTA15) EST BASE SUR LE TRIA3 A 3PG DONC NPG=3, NNO=3
C INTERFACE_S 2D (QUAD8)   EST BASE SUR LE SEG2  A 2PG DONC NPG=2, NNO=2
C INTERFACE_S 3D (HEXA20)  EST BASE SUR LE QUAD4 A 4PG DONC NPG=4, NNO=4
C INTERFACE_S 3D (PENTA15) EST BASE SUR LE TRIA3 A 3PG DONC NPG=3, NNO=3
C INTERFACE   2D (QUAD8)   EST BASE SUR LE SEG2  A 3PG DONC NPG=3, NNO=2
C INTERFACE   3D (HEXA20)  EST BASE SUR LE QUAD4 A 9PG DONC NPG=9, NNO=4
C INTERFACE   3D (PENTA15) EST BASE SUR LE TRIA3 A 6PG DONC NPG=6, NNO=3

C REMARQUE POUR LES MODELISATIONS INTERFACE ON EFFECTUE EXACTEMENT LE
C MEME TRAITEMENT QUE POUR LES MODELISATIONS INTERFACE_S CE QUI VEUT 
C DIRE QUE L'ON NE PREND EN COMPTE RESPECTIVEMENT QUE LES VALEURS DES 
C 2, 4 ET 3 PREMIERS PG.

C     INFORMATIONS SUR L'ELEMENT DE REFERENCE
      IF (QUADRA) THEN
        CALL ELREF2(NOMTE,2,LIELRF,NTROU)
        CALL ELREF4
     &       (LIELRF(2),'RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
C       DIMENSION ESPACE POUR LES JOINTS QUADRA, HYME OU INTERFACE    
        NDIME = NDIM + 1
      ELSE
        CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
C       DIMENSION DE L'ESPACE POUR LES JOINTS LINEAIRES  
        IF (JLIN2D) NDIME = NDIM 
        IF (JLIN3D) NDIME = NDIM + 1
      ENDIF

C     INFORMATIONS SUR LES CHAMPS
      IF (OPTION.EQ.'SIEF_ELNO') THEN

        CALL JEVECH('PCONTRR' ,'L',ICHG)
        CALL JEVECH('PSIEFNOR','E',ICHN)

        IF (JHM.OR.JQUAD)  NCMP = 2*NDIME-1
        IF (INTERF)        NCMP = 2*NDIME
        IF (.NOT.(QUADRA)) NCMP = NDIME  
                  
      ELSEIF (OPTION.EQ.'VARI_ELNO') THEN

        CALL JEVECH('PVARIGR','L',ICHG)
        CALL TECACH('OOO','PVARINR','E',7,JTAB,IBID)
        NCMP = MAX(JTAB(6),1)*JTAB(7)
        ICHN = JTAB(1)
       
      ENDIF

C ############################################################
C     ELEMENTS QUADRATIQUE 2D ET 3D
C ############################################################

      IF (QUADRA) THEN

        IF (NDIME.EQ.2) THEN

          DO 10 I = 1,NCMP
          
            IG = ICHG-1+I
            IN = ICHN-1+I
C           NOEUDS 1,4,8
            ZR(IN)        = ZR(IG+NCMP) +
     &                     (ZR(IG)-ZR(IG+NCMP))*(1.D0-SQRT(3.D0))/2.D0
            ZR(IN+3*NCMP) = ZR(IG+NCMP) +
     &                     (ZR(IG)-ZR(IG+NCMP))*(1.D0-SQRT(3.D0))/2.D0
            ZR(IN+7*NCMP) = ZR(IG+NCMP) +
     &                     (ZR(IG)-ZR(IG+NCMP))*(1.D0-SQRT(3.D0))/2.D0
C           NOEUDS 2,3,6
            ZR(IN+  NCMP) = ZR(IG+NCMP) +
     &                     (ZR(IG)-ZR(IG+NCMP))*(1.D0+SQRT(3.D0))/2.D0
            ZR(IN+2*NCMP) = ZR(IG+NCMP) +
     &                     (ZR(IG)-ZR(IG+NCMP))*(1.D0+SQRT(3.D0))/2.D0
            ZR(IN+5*NCMP) = ZR(IG+NCMP) +
     &                     (ZR(IG)-ZR(IG+NCMP))*(1.D0+SQRT(3.D0))/2.D0
C           NOEUDS 5,7
            ZR(IN+4*NCMP) = (ZR(IG) + ZR(IG+NCMP))/2.D0
            ZR(IN+6*NCMP) = (ZR(IG) + ZR(IG+NCMP))/2.D0
   10     CONTINUE

        ELSE

C         ON REMPLIT LES NOEUDS SOMMETS DE LA 1ERE FACE
C         (POUR L'HEXA20 : 1 2 3 4 , POUR LE PENTA15 : 1 2 3 )
          CALL PPGAN2(JGANO,1,NCMP,ZR(ICHG),ZR(ICHN))

          DO 18 I = 1,NCMP

            IN = ICHN-1+I

C           ON REMPLIT LES NOEUDS MILIEU DE LA 1ERE FACE
C           EN MOYENNANT LES VALEURS DES NOEUDS SOMMETS DE LA 1ERE FACE

            IF (NNO.EQ.4) THEN 
C             (POUR L'HEXA20 : 9 10 11 12)
              ZR(IN +  8*NCMP) = (ZR(IN       ) + ZR(IN+  NCMP) )/2.D0
              ZR(IN +  9*NCMP) = (ZR(IN+  NCMP) + ZR(IN+2*NCMP) )/2.D0
              ZR(IN + 10*NCMP) = (ZR(IN+2*NCMP) + ZR(IN+3*NCMP) )/2.D0
              ZR(IN + 11*NCMP) = (ZR(IN+3*NCMP) + ZR(IN       ) )/2.D0
            ELSEIF (NNO.EQ.3) THEN
C             (POUR LE PENTA15 : 7 8 9)
              ZR(IN +  6*NCMP) = (ZR(IN       ) + ZR(IN+  NCMP) )/2.D0
              ZR(IN +  7*NCMP) = (ZR(IN+  NCMP) + ZR(IN+2*NCMP) )/2.D0
              ZR(IN +  8*NCMP) = (ZR(IN+2*NCMP) + ZR(IN       ) )/2.D0
            ENDIF 

            DO 19 J=1,NNO
C             ON REMPLIT LES NOEUDS SOMMETS DE LA 2ND FACE
C             (POUR L'HEXA20 : 5 6 7 8, POUR LE PENTA15 : 4 5 6)
C             AINSI QUE LES NOEUDS MILIEU INTERMEDIAIRES
C             (POUR L'HEXA20 : 13 14 15 16, POUR LE PENTA15 : 10 11 12)
C             A L'IDENTIQUE DES NOEUDS SOMMETS DE LA 1ERE FACE
              ZR(IN+(J+  NNO-1)*NCMP) = ZR(IN+(J-1)*NCMP)
              ZR(IN+(J+3*NNO-1)*NCMP) = ZR(IN+(J-1)*NCMP)
C             ON REMPLIT LES NOEUDS MILIEU DE LA DEUXIEME FACE
C             (POUR L'HEXA20 : 17 18 19 20, POUR LE PENTA15 : 13 14 15)
C             A L'IDENTIQUE DES NOEUDS MILIEU DE LA PREMIERE FACE
              ZR(IN+(J+4*NNO-1)*NCMP) = ZR(IN+(J+2*NNO-1)*NCMP)
   19       CONTINUE

   18     CONTINUE

        ENDIF
        
C#########################################
C     ELEMENTS DE JOINT LINEAIRES 2D ET 3D
C#########################################
      ELSE

        IF (NDIME.EQ.2) THEN
        
C         REMARQUE : ICI LA POSITION DES 2 PG DE l'EJ SONT INVERSEES PAR
C         RAPPORT A LA POS HABITUELLE DES FAM D'EF 1D (SEG2 SEG3) A 2 PG
          DO 11 I = 1,NCMP
            IG = ICHG-1+I
            IN = ICHN-1+I
            ZR(IN)        = ZR(IG)+
     &                     (ZR(IG+NCMP)-ZR(IG))*(1.D0-SQRT(3.D0))/2.D0
            ZR(IN+3*NCMP) = ZR(IG)+
     &                     (ZR(IG+NCMP)-ZR(IG))*(1.D0-SQRT(3.D0))/2.D0
            ZR(IN+NCMP)   = ZR(IG)+
     &                     (ZR(IG+NCMP)-ZR(IG))*(1.D0+SQRT(3.D0))/2.D0
            ZR(IN+2*NCMP) = ZR(IG)+
     &                     (ZR(IG+NCMP)-ZR(IG))*(1.D0+SQRT(3.D0))/2.D0
   11     CONTINUE

        ELSE
          
C         ON REMPLIT LES NOEUDS DE LA PREMIERE FACE
          CALL PPGAN2(JGANO,1,NCMP,ZR(ICHG),ZR(ICHN))

C         ON REMPLIT LES NOEUD DE LA DEUXIEME FACE A L'IDENTIQUE
          DO 20 I = 1,NCMP
            IN = ICHN-1+I
            DO 30 J=1,NNO
              ZR(IN+(J+NNO-1)*NCMP) = ZR(IN+(J-1)*NCMP)
   30       CONTINUE
   20     CONTINUE

        ENDIF

      ENDIF

      END
