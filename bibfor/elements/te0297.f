      SUBROUTINE TE0297(OPTION,NOMTE)
      IMPLICIT NONE
      CHARACTER*16 OPTION,NOMTE
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 05/09/2005   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE VABHHTS J.PELLET
C
C    - FONCTION REALISEE:  CALCUL DES OPTIONS DE POST-TRAITEMENT
C                          EN MÉCANIQUE DE LA RUPTURE 
C                          POUR LES ÉLÉMENTS X-FEM
C
C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................

      INTEGER NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO
      INTEGER DDLH,NFE,DDLC,NIT,CPT,IT,NSE,ISE,IN,INO,NSEMAX
      INTEGER JPINTT,JCNSET,JHEAVT,JLONCH,JBASLO,JCOORS,IGEOM,IDEPL
      INTEGER ITHET,I,J,COMPT,IGTHET
      REAL*8  HE,THET,R8PREM
      CHARACTER*8   ELREFP
      CHARACTER*24  COORSE
      PARAMETER    (NSEMAX=6)      
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      CALL ELREF1(ELREFP)
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)

C     DDLS PAR NOEUD SOMMET : HEAVYSIDE, ENRICHIS (FOND), CONTACT
      IF (NOMTE(1:8).EQ.'MECA_XH_') THEN
        DDLH=NDIM
        NFE=0
        DDLC=NDIM
      ELSEIF (NOMTE(1:8).EQ.'MECA_XT_') THEN
        DDLH=0
        NFE=4
        DDLC=0
      ELSEIF (NOMTE(1:8).EQ.'MECA_XHT') THEN
        DDLH=NDIM
        NFE=4
        DDLC=NDIM
      ELSE 
        CALL UTMESS('F','TE539','NOM D''ELEMENT FINI INCOMPATIBLE')
      ENDIF
C
C     PARAMÈTRES PROPRES À X-FEM
      CALL JEVECH('PPINTTO','L',JPINTT)
      CALL JEVECH('PCNSETO','L',JCNSET)
      CALL JEVECH('PHEAVTO','L',JHEAVT)
      CALL JEVECH('PLONCHA','L',JLONCH)
      CALL JEVECH('PBASLOR','L',JBASLO)
 
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PDEPLAR','L',IDEPL)
      CALL JEVECH('PTHETAR','L',ITHET)
      CALL JEVECH('PGTHETA','E',IGTHET)

C     SI LA VALEUR DE THETA EST NULLE SUR L'ÉLÉMENT, ON SORT
      COMPT = 0
      DO 10 I = 1,NNO
        THET = 0.D0
        DO 11 J = 1,NDIM
          THET = THET + ABS(ZR(ITHET+NDIM*(I-1)+J-1))
 11     CONTINUE
        IF (THET.LT.R8PREM()) COMPT = COMPT + 1
 10   CONTINUE
      IF (COMPT.EQ.NNO) GOTO 9999


C     RÉCUPÉRATION DE LA SUBDIVISION L'ÉLÉMENT PARENT EN NIT TETRAS 
      NIT=ZI(JLONCH-1+1)

      CPT=0
C     BOUCLE SUR LES NIT TETRAS
      DO 100 IT=1,NIT

C       RÉCUPÉRATION DU DÉCOUPAGE EN NSE SOUS-ÉLÉMENTS 
        NSE=ZI(JLONCH-1+1+IT)

C       BOUCLE D'INTÉGRATION SUR LES NSE SOUS-ÉLÉMENTS
        DO 110 ISE=1,NSE

          CPT=CPT+1

C         COORD DU SOUS-ÉLT EN QUESTION
          COORSE='&&TE0297.COORSE'
          CALL WKVECT(COORSE,'V V R',3*4,JCOORS)

C         BOUCLE SUR LES 4 SOMMETS DU SOUS-TÉTRA
          DO 112 IN=1,4
            INO=ZI(JCNSET-1+4*(CPT-1)+IN)
            IF (INO.LT.1000) THEN
              ZR(JCOORS-1+3*(IN-1)+1)=ZR(IGEOM-1+3*(INO-1)+1)
              ZR(JCOORS-1+3*(IN-1)+2)=ZR(IGEOM-1+3*(INO-1)+2)
              ZR(JCOORS-1+3*(IN-1)+3)=ZR(IGEOM-1+3*(INO-1)+3)
            ELSE
              ZR(JCOORS-1+3*(IN-1)+1)=ZR(JPINTT-1+3*(INO-1000-1)+1)
              ZR(JCOORS-1+3*(IN-1)+2)=ZR(JPINTT-1+3*(INO-1000-1)+2)
              ZR(JCOORS-1+3*(IN-1)+3)=ZR(JPINTT-1+3*(INO-1000-1)+3)
            ENDIF
 112      CONTINUE

C         FONCTION HEAVYSIDE CSTE SUR LE SS-ÉLT
C         REMARQUE : REAL*8 = INTEGER EST POSSIBLE EN FORTRAN
          HE=ZI(JHEAVT-1+NSEMAX*(IT-1)+ISE)

          NPG=15
          CALL XSIF3D(ELREFP,NDIM,COORSE,IGEOM,HE,DDLH,DDLC,NFE,
     &         ZR(JBASLO),NNO,NPG,ZR(IDEPL),IGTHET)

          CALL JEDETR(COORSE)

 110    CONTINUE

 100  CONTINUE


9999  CONTINUE
      END
