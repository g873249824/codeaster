      SUBROUTINE FONNO7 (NOMA,CNXINV,NDIM,NA,VECDIR,HMAX)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXATR
      INTEGER             NA,NDIM
      REAL*8              VECDIR(3),HMAX
      CHARACTER*8         NOMA
      CHARACTER*19        CNXINV

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 07/05/2013   AUTEUR LADIER A.LADIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C       ----------------------------------------------------------------
C       DETERMINATION DE LA TAILLE MAXIMALE DES MAILLES CONNECTEES AU
C       NOEUD DU FOND
C       ----------------------------------------------------------------
C    ENTREES
C       NOMA   : NOM DU MAILLAGE
C       CNXINV : CONNECTIVITE INVERSE
C       NDIM   : DIMENSION DU MODELE
C       NA     : NUMERO DU NOEUD SOMMET COURANT
C       VECDIR : VECTEUR TANGENT
C    SORTIE
C       HMAX  : TAILLE MAXIMALE DES MAILLES
C
C
      INTEGER       ADRA, AR(12,3)
      INTEGER       IATYMA, IAR, IMA, INO1, INO2, IRET, ITYP
      INTEGER       JCNCIN, JCONX1, JCONX2, JCOOR, JDRVLC, K
      INTEGER       NBAR, NBMACA, NDIME, NNO, NNO1, NNO2, NUMAC
      REAL*8        COOR(3), DDOT, VECT(3), P, COS70, COSINU, NORMV
      REAL*8        R8PREM,R8PI
      CHARACTER*8   K8B, TYPE
C     -----------------------------------------------------------------

      CALL JEMARQ()
C
C     RECUPERATION DES DONNEES SUR LE MAILLAGE
      CALL JEVEUO( NOMA//'.CONNEX','L', JCONX1 )
      CALL JEVEUO( JEXATR(NOMA//'.CONNEX','LONCUM'),'L', JCONX2 )
      CALL JEVEUO( NOMA//'.TYPMAIL','L', IATYMA )
      CALL JEVEUO( NOMA//'.COORDO    .VALE','L', JCOOR )

C     RECUPERATION DES COORDONNNES DE NA
      COOR(1) = ZR(JCOOR-1 + (NA-1)*3 + 1)
      COOR(2) = ZR(JCOOR-1 + (NA-1)*3 + 2)
      COOR(3) = ZR(JCOOR-1 + (NA-1)*3 + 3)

C     RECUPERATION DE LA CONNECTIVITE INVERSE
      CALL JEVEUO ( JEXATR(CNXINV,'LONCUM'), 'L', JDRVLC )
      CALL JEVEUO ( JEXNUM(CNXINV,1)       , 'L', JCNCIN )

C     MAILLES CONNECTEES A NA
      ADRA   = ZI(JDRVLC-1 + NA)
      NBMACA = ZI(JDRVLC-1 + NA+1) - ZI(JDRVLC-1 + NA)
C
      HMAX=R8PREM()

      DO 10 IMA=1,NBMACA
C       NUMERO DE LA MAILLE
        NUMAC = ZI(JCNCIN-1 + ADRA+IMA-1)
        ITYP = IATYMA-1+NUMAC
        CALL JENUNO( JEXNUM('&CATA.TM.NOMTM',ZI(ITYP)), TYPE )
        CALL DISMOI('F','DIM_TOPO',TYPE,'TYPE_MAILLE',NDIME,K8B,IRET)

C       ON ZAPPE LES MAILLES DE BORDS
        IF (NDIME.NE.NDIM) GOTO 10

        CALL CONARE(TYPE,AR,NBAR)

C       BOUCLE SUR LE NOMBRE D'ARETES DE LA MAILLE NUMAC
        DO 100 IAR=1,NBAR

           INO1 = AR(IAR,1)
           NNO1 = ZI(JCONX1-1 + ZI(JCONX2+NUMAC-1) +INO1-1)
           INO2 = AR(IAR,2)
           NNO2 = ZI(JCONX1-1 + ZI(JCONX2+NUMAC-1) +INO2-1)

           IF (NA.EQ.NNO1) THEN
              NNO = NNO2
           ELSE IF (NA.EQ.NNO2) THEN
              NNO = NNO1
           ELSE
              GOTO 100
           ENDIF

C          VECTEUR REPRESENTANT L'ARETE NA-NNO
           DO 110 K=1,NDIM
              VECT(K) = ZR(JCOOR-1+ (NNO-1)*3+K) - COOR(K)
 110       CONTINUE

C          PROJECTION DE L'ARETE SUR LE VECTEUR TANGENT
           P = DDOT(NDIM,VECT,1,VECDIR,1)

C          FILTRAGE DES ARETES A PRENDRE EN COMPTE:
C          L'ANGLE ENTRE VECT ET VECDIR DOIT ETRE <60
           NORMV = SQRT(DDOT(NDIM,VECT,1,VECT,1))

           COSINU = P/NORMV
           COS70 = COS(70*R8PI()/180.D0)
           IF(ABS(COSINU).LT.COS70)GOTO 100

C          ON PREND LE MAX DES PROJECTIONS
           P = ABS(P)
           IF (P.GE.HMAX) HMAX = P

 100    CONTINUE

 10   CONTINUE
C
      IF (HMAX.LE.R8PREM()) CALL U2MESS('A','RUPTURE0_49')

      CALL JEDEMA()
      END
