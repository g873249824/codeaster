      SUBROUTINE XCNINV(NDIM,NNOTOT,NSETOT,NNOP,NNO,JCNSET,JLONCH,CNINV)
      IMPLICIT NONE
      INTEGER   NDIM,NNOTOT,NSETOT,NNOP,NNO,JCNSET,JLONCH
      INTEGER   CNINV(NNOTOT,NSETOT+1)
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 14/10/2008   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C     
C     BUT:
C         CALCUL DE LA CONNECTIVITE INVERSE DES SOUS ELEMENTS
C         DE L'ELEMENT XFEM PARENT (EN 2D).
C
C
C     ARGUMENTS:
C     ----------
C
C      ENTREE :
C-------------
C IN   NDIM   : DIMENSION
C IN   NNOTOT : NOMBRE TOTAL DE NOEUDS (POINTS D'INTERSECTION INCLUS)
C IN   NSETOT : OMBRE TOTAL DE SOUS ELEMENT DE L'ELEMENT PARENT
C IN   NNOP   : NOMBRE DE NOEUDS DE L'ELEMENT PARENT (POINTS)
C                D'INTERSECTION EXCLUS
C IN   NNO    : NOMBRE DE NOEUDS DU SOUS-ELEMENT DE REFERENCE
C IN   JCNSET : ADRESSE DANS ZI DE LA CONNECTIVITE DES SOUS-ELEMENTS
C IN   JLONCH : ADRESSE DANS ZI DES DONNEES CONCERNANT LE DECOUPAGE
C                EN SOUS ELEMENTS
C
C      SORTIE :
C-------------
C OUT  CNINV  : TABLEAU DE LA CONNECTIVITE INVERSE
C
C ......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      INTEGER NSIMPL,NSE,NCMPSE,CPT
      INTEGER ISIMPL,ISE,IN,INO,JNO,J,K
C
C ----------------------------------------------------------------------
C
C --- INITIALISATION DU TABLEAU DE CONNECTIVITE INVERSE
      NCMPSE=NSETOT+1
      DO 200 J=1,NNOTOT
        DO 210 K=1,NCMPSE
          CNINV(J,K)=0
 210    CONTINUE
 200  CONTINUE
C
C --- RÉCUPÉRATION DE LA SUBDIVISION DE L'ÉLÉMENT PARENT 
C --- EN NSIMPL SIMPLEXES 
      NSIMPL=ZI(JLONCH-1+1)
C            
C ------------------- BOUCLE SUR LES NSIMPL SIMPLEXES ------------------
C
      CPT=0
      DO 100 ISIMPL=1,NSIMPL
C                
C ----- RÉCUPÉRATION DU DÉCOUPAGE EN NSE SOUS-ÉLÉMENTS 
        NSE=ZI(JLONCH-1+1+ISIMPL)
C        
C ------------------- BOUCLE SUR LES NSE SOUS-ÉLÉMENTS  ----------------
C
        DO 110 ISE=1,NSE
          
          CPT=CPT+1
C
C ------------- BOUCLE SUR LES SOMMETS DU SOUS-ÉLÉMENTS  ---------------
C                    
          DO 111 IN=1,NNO

            INO=ZI(JCNSET-1+(NDIM+1)*(CPT-1)+IN)          
C
C --------- NUMÉROTATION PROPRE A LA CONNECTIVITÉ INVERSE
            IF (INO.LT.1000) THEN
              JNO=INO
            ELSE
              JNO=INO-1000+NNOP              
            END IF
C --------- STOCKAGE
            CNINV(JNO,1)=CNINV(JNO,1)+1
            CALL ASSERT(CNINV(JNO,1).LE.NSETOT)
            CNINV(JNO,CNINV(JNO,1)+1)=CPT
C            
 111      CONTINUE
C
 110    CONTINUE
C
 100  CONTINUE
C
      END
