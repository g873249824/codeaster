      SUBROUTINE DINON4(NEQ,UL,DUL,UTL,NNO,
     &                  NBCOMP,VARIMO,RAIDE,NBPAR,PARAM,OKDIRE,
     &                  VARIPL)
C ----------------------------------------------------------------------
      IMPLICIT NONE
      INTEGER  NEQ,NBCOMP,NNO,NBPAR
      REAL*8   UL(NEQ),DUL(NEQ),UTL(NEQ)
      REAL*8   VARIMO(NBCOMP*1),VARIPL(NBCOMP*1)
      REAL*8   RAIDE(NBCOMP),PARAM(6,NBPAR)
      LOGICAL  OKDIRE(6)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 05/02/2008   AUTEUR FLEJOU J-L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C ======================================================================
C
C     RELATION DE COMPORTEMENT "BI-LINEAIRE" (DISCRET NON LINEAIRE).
C
C    f < FPRE
C       df = KDEB*dU
C    f > FPRE
C       df = KFIN*dU
C
C        KDEB  : raideur au début
C        KFIN  : raideur à la fin
C        FPREC : effort de pre-tension
C
C======================================================================
C
C IN  :
C       NEQ    : NOMBRE DE DDL DE L'ELEMENT
C       UL     : DEPLACEMENT PRECEDENT REPERE LOCAL (DIM NEQ)
C       DUL    : INCREMENT DE DEPLACEMENT REPERE LOCAL (DIM NEQ)
C       UTL    : DEPLACEMENT COURANT REPERE LOCAL (DIM NEQ)
C       NNO    : NOMBRE DE NOEUDS
C       NBCOMP : NOMBRE DE COMPOSANTES
C       VARIMO : VARIABLES INTERNES A T- (1 PAR COMPOSANTES)
C       RAIDE  : RAIDEUR ELASTIQUE DES DISCRETS
C       NBPAR  : NOMBRE MAXIMAL DE PARAMETRE DE LA LOI
C       PARAM  : PARAMETRES DE LA LOI
C       OKDIRE : VRAI SI LE COMPORTEMENT AFFECTE CETTE DIRECTION
C
C OUT :
C       RAIDE  : RAIDEUR QUASI-TANGENTE AU COMPORTEMENT DES DISCRETS
C       VARIPL : VARIABLES INTERNES INTERNES A T+ (1 PAR COMPOSANTES)
C
C***************** DECLARATION DES VARIABLES LOCALES *******************
C
      INTEGER II
      REAL*8  R8MIEM,ULEL,DULEL,UTLEL,R8MIN

      INTEGER IVARI
      REAL*8  KDEB,KFIN,FPRE,USEUIL,FPLUS,FMOINS,DEPL

      REAL*8     ZERO
      PARAMETER (ZERO=0.0D0)
C
C************ FIN DES DECLARATIONS DES VARIABLES LOCALES ***************
      R8MIN  = R8MIEM()

      DO 20, II=1,NBCOMP
C        INDEX DES VARIABLES INTERNES
         IVARI = II
C        PAR DEFAUT LES VARIABLES N'EVOLUENT PAS
         VARIPL(IVARI) = VARIMO(IVARI)
C        SI LE COMPORTEMENT EST BI-LINEAIRE DANS CETTE DIRECTION
         IF ( .NOT. OKDIRE(II) ) GOTO 20
         KDEB = PARAM(II,1)
         KFIN = PARAM(II,2)
         FPRE = PARAM(II,3)

C          write(*,'(I2,3(2X,E12.5))') II,KDEB,KFIN,FPRE
         IF ( ABS(KDEB) .LE. R8MIN ) GOTO 20
C
         IF ( NNO .EQ. 1 ) THEN
            DULEL = DUL(II)
            ULEL  = UL(II)
            UTLEL = UTL(II)
         ELSE
            DULEL = DUL(II+NBCOMP) - DUL(II)
            ULEL  = UL(II+NBCOMP)  - UL(II)
            UTLEL = UTL(II+NBCOMP) - UTL(II)
         ENDIF
C        SEUIL EN DEPLACEMENT
         USEUIL = ABS(FPRE/KDEB)
         IF ( ABS(DULEL) .GT. R8MIN ) THEN
C           A L'INSTANT MOINS
            DEPL = ABS(ULEL)
            IF      ( DEPL .LE. USEUIL ) THEN
               FMOINS = KDEB*DEPL
            ELSE
               FMOINS = FPRE + KFIN*(DEPL-USEUIL)
            ENDIF
            IF ( ULEL .LT. ZERO ) FMOINS = -FMOINS
C           A L'INSTANT PLUS
            DEPL = ABS(UTLEL)
            IF      ( DEPL .LE. USEUIL ) THEN
               FPLUS = KDEB*DEPL
               VARIPL(IVARI) = 1.0D0
            ELSE
               FPLUS = FPRE + KFIN*(DEPL-USEUIL)
               VARIPL(IVARI) = 2.0D0
            ENDIF
            IF ( UTLEL .LT. ZERO ) FPLUS = -FPLUS
            RAIDE(II) = ABS(FPLUS - FMOINS)/ABS(DULEL)
         ELSE
C           CAS OU DUEL=0 ==> UTLEL=ULEL
            DEPL = ABS(UTLEL)
            IF      ( DEPL .LE. USEUIL ) THEN
               RAIDE(II) = KDEB
            ELSE
               RAIDE(II) = KFIN
            ENDIF
         ENDIF
20    CONTINUE

      END
