      SUBROUTINE NMGEOB(NDIM, NNOB, GEOM, KPG, POIDSG, VFFB,
     &                  DFDE, DFDN, DFDK, EPSB, POIDS, DFDIB,
     &                  EPSR, GEPSR)
C
      IMPLICIT REAL*8 (A-H,O-Z)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 10/04/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      INTEGER NDIM, NDIMSI, NNOB, KPG
      REAL*8  POIDSG, VFFB(NNOB), DFDE(*), DFDN(*), DFDK(*)
      REAL*8  GEOM(NDIM,NNOB), DFDIB(NNOB,NDIM), EPSB(NDIM*2,NNOB)
      REAL*8  POIDS, EPSR(2*NDIM)
      REAL*8  GEPSR(2*NDIM,NDIM)
C.......................................................................
C
C     BUT:  CALCUL DES DEFORMATIONS GENERALISEES ET DE LEURS GRADIENTS
C           EN UN POINT DE GAUSS
C           POUR ELEMENTS NON LOCAUX A GRAD DE DEF
C
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  NNOB    : NOMBRE DE NOEUDS DE L'ELEMENT
C IN  GEOM    : COORDONEES DES NOEUDS
C IN  KPG     : NUMERO DU POINT DE GAUSS (POUR L'ACCES AUX FCT. FORMES)
C IN  POIDSG  : POIDS DU POINT DE GAUSS DE L'ELEMENT DE REFERENCE
C IN  VFFB    : VALEUR DES FONCTIONS DE FORME (EN AXISYMETRIQUE)
C IN  DFDE    : DERIVEE DES FONCTIONS DE FORME DE REFERENCE (X)
C IN  DFDN    : DERIVEE DES FONCTIONS DE FORME DE REFERENCE (Y) (3D)
C IN  DFDK    : DERIVEE DES FONCTIONS DE FORME DE REFERENCE (Z)
C IN  EPSB    : DEFORMATIONS GENERALISEES AUX NOEUDS
C OUT POIDS   : POIDS D'INTEGRATION (*R EN AXI)
C OUT DFDIB   : DERIVEE DES FONCTIONS DE FORME
C OUT EPSR    : DEFORMATIONS GENERALISEES AUS POINTS DE GAUSS
C OUT GEPSR   : GRADIENT DE DEF GENERALISEES AUX POINTS DE GAUSS
C......................................................................
C
      LOGICAL TRIDIM
      INTEGER I,J,K,N,KK
      
      NDIMSI = 2*NDIM
C
C - CALCUL DES DERIVEES DES FONCTIONS DE FORME ET JACOBIEN
      TRIDIM = (NDIM.EQ.3)
      IF (TRIDIM) THEN
        KK = (KPG-1)*NNOB*3 +1

        CALL DFDM3D (NNOB,POIDSG,DFDE(KK),DFDN(KK),DFDK(KK),
     &               GEOM,DFDIB(1,1),DFDIB(1,2),DFDIB(1,3),POIDS)
      ELSE
        KK=(KPG-1)*NNOB + 1
        CALL DFDM2D ( NNOB,POIDSG,DFDE(KK),DFDK(KK),GEOM,DFDIB(1,1),
     &                DFDIB(1,2),POIDS )
      ENDIF
C
C
C - CALCUL DE EPSR

      DO 110 K=1,NDIMSI
        EPSR(K)=0.D0
        DO 100 N=1,NNOB
          EPSR(K)=EPSR(K)+VFFB(N)*EPSB(K,N)
100     CONTINUE           
110   CONTINUE
C
C - CALCUL DU GRADIENT DE EPSR: GEPSR
C
      DO 22 I=1,NDIMSI
        DO 24 J=1,NDIM
          GEPSR(I,J)=0.D0
          DO 20 N=1,NNOB
            GEPSR(I,J) = GEPSR(I,J) + DFDIB(N,J)*EPSB(I,N)
 20       CONTINUE
 24     CONTINUE
 22   CONTINUE

      END
