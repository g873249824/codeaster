      SUBROUTINE NMGEOB(NDIM, NNOB, GEOM, KPG, IPOIDS, IVF, IDFDE,
     &                  EPSB, POIDS, DFDIB, EPSR, GEPSR)
C
      IMPLICIT REAL*8 (A-H,O-Z)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 30/03/2004   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      INTEGER NDIM, NDIMSI, NNOB, KPG, IPOIDS, IVF, IDFDE
      REAL*8  POIDSG
      REAL*8  GEOM(NDIM,NNOB), DFDIB(NNOB,NDIM), EPSB(NDIM*2,NNOB)
      REAL*8  POIDS, EPSR(2*NDIM)
      REAL*8  GEPSR(2*NDIM,NDIM)
C.......................................................................
C
C     BUT:  CALCUL DES DEFORMATIONS GENERALISEES ET DE LEURS GRADIENTS
C           EN UN POINT DE GAUSS
C           POUR ELEMENTS NON LOCAUX A GRAD DE DEF
C
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  NNOB    : NOMBRE DE NOEUDS DE L'ELEMENT
C IN  GEOM    : COORDONEES DES NOEUDS
C IN  KPG     : NUMERO DU POINT DE GAUSS (POUR L'ACCES AUX FCT. FORMES)
C IN  IPOIDS  : POIDS DU POINT DE GAUSS DE L'ELEMENT DE REFERENCE
C IN  IVF     : VALEUR DES FONCTIONS DE FORME (EN AXISYMETRIQUE)
C IN  IDFDE   : DERIVEE DES FONCTIONS DE FORME DE REFERENCE 
C IN  EPSB    : DEFORMATIONS GENERALISEES AUX NOEUDS
C OUT POIDS   : POIDS D'INTEGRATION (*R EN AXI)
C OUT DFDIB   : DERIVEE DES FONCTIONS DE FORME
C OUT EPSR    : DEFORMATIONS GENERALISEES AUS POINTS DE GAUSS
C OUT GEPSR   : GRADIENT DE DEF GENERALISEES AUX POINTS DE GAUSS
C......................................................................
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      LOGICAL TRIDIM
      INTEGER I,J,K,N
      
      NDIMSI = 2*NDIM
      TRIDIM = (NDIM.EQ.3)
C
C - CALCUL DES DERIVEES DES FONCTIONS DE FORME ET JACOBIEN
      IF (TRIDIM) THEN
        CALL DFDM3D ( NNOB, KPG, IPOIDS, IDFDE,
     &                GEOM, DFDIB(1,1), DFDIB(1,2), DFDIB(1,3), POIDS)
      ELSE
        CALL DFDM2D ( NNOB, KPG, IPOIDS, IDFDE, 
     &                GEOM, DFDIB(1,1), DFDIB(1,2), POIDS )
      ENDIF
C
C
C - CALCUL DE EPSR

      DO 110 K=1,NDIMSI
        EPSR(K)=0.D0
        DO 100 N=1,NNOB
          EPSR(K)=EPSR(K)+ZR(IVF-1+N+(KPG-1)*NNOB)*EPSB(K,N)
100     CONTINUE           
110   CONTINUE
C
C - CALCUL DU GRADIENT DE EPSR: GEPSR
C
      DO 22 I=1,NDIMSI
        DO 24 J=1,NDIM
          GEPSR(I,J)=0.D0
          DO 20 N=1,NNOB
            GEPSR(I,J) = GEPSR(I,J) + DFDIB(N,J)*EPSB(I,N)
 20       CONTINUE
 24     CONTINUE
 22   CONTINUE

      END
