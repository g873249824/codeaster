      SUBROUTINE GLRCST(OPT,DELAS,EPS,DEPS,KHI,DKHI,T1VE,T2VE,T2EV,
     &                  SIGM,VARIM,
     &                  SIGP,VARIP,
     &                  DSIDEP)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 01/12/2003   AUTEUR KOECHLIN P.KOECHLIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C ----------------------------------------------------------------------
C     REALISE L'INTEGRATION DE LA LOI DE COMPORTEMENT GLRC STANDARD 
C     POUR LES ELEMENTS DKTG <=> DKT EN PETITS DEPLAGEMENTS
C
C IN  OPT    : OPTION DEMANDEE : RIGI_MECA_TANG , FULL_MECA , RAPH_MECA
C IN  DELAS  : MATRICE ELASTIQUE EN MEMBRANE, FLEXION ET COUPLAGE
C IN  EPS    : DEFORMATION DE MEMBRANE A L'INSTANT PRECEDENT
C IN  DEPS   : INCREMENT DE DEF. DE MEMBRANE A L'INSTANT DE CALCUL
C IN  KHI    : COURBURE A L'INSTANT PRECEDENT
C IN  DKHI   : INCREMENT DE COURBURE A L'INSTANT DE CALCUL
C IN  T1VE   : MATRICE DE PASSAGE D'UNE MATRICE (3,3) 
C              DU REPERE DE LA VARIETE AU REPERE ELEMENT
C IN  T2VE   : MATRICE DE PASSAGE D'UNE MATRICE (2,2) 
C              DU REPERE DE LA VARIETE AU REPERE ELEMENT
C IN  T2EV   : INVERSE DE T2VE
C IN  SIGM   : EFFORT NORMAL ET MOMENT FLECHISSANT A L'INSTANT PRECEDENT
C IN  VARIM  : VARIABLES INTERNES A L'INSTANT PRECEDENT
C OUT SIGP   : EFFORT NORMAL ET MOMENT FLECHISSANT A L'INSTANT DE CALCUL
C OUT VARIP  : VARIABLES INTERNES A L'INSTANT DE CALCUL
C OUT DSIDEP : MATRICE TANGENTE
C ----------------------------------------------------------------------
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C     CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL,VECTEU,MATRIC,TEMPNO
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C 
      COMMON /TDIM/  N,ND
      CHARACTER*16   OPT
      CHARACTER*16   PHENOM
      CHARACTER*8    NOMRES(4), NOMPAR(4)
      CHARACTER*2    CODRES(4)
C
      REAL*8  VALPAR(4),  VALRES(4)
      REAL*8  NBACKN(6),  NBCKN(6)
      REAL*8  SIGM(6),    SIGP(6),   VARIM(7),   VARIP(7)
      REAL*8  DNORM(3),   DMOME(3)
      REAL*8  EPS(3),     KHI(3),    DEPS(3),    DKHI(3)
      REAL*8  EPSOR(3),   KHIOR(3),  DEPSOR(3),  DKHIOR(3)
      REAL*8  DEPSEO(6),  DEPSTO(6)
      REAL*8  T1VE(*),    T2VE(*),   T2EV(*)
C      
      REAL*8  DELAS(6,6)
      REAL*8  MP1(3),    MP2(3),   C1(3,3), C2(3,3)
      REAL*8  DC1(3,3),  DC2(3,3)
      REAL*8  MATMP(3,3)
      REAL*8  CURVP(3),  DSIDEP(6,6)
      REAL*8  REPS(6),   DEPSTE(6)
      REAL*8  NBNNEW(6), MP1NEW(3),MP2NEW(3), DDISIP, DISSIP,  DCURVP(3)
      REAL*8  ZERO,      ZEROF,    ZEFNEW,    ZEROG,  ZEGNEW,  ZEDEPS
      REAL*8  NORM6,     GPLAS,    FPLAS
      REAL*8  R8B
      INTEGER IT,     NCRIT,  I,   J,    NBITER,   NCRIT0
      INTEGER CRINUM, N,      ND,  NBB,  IMATE
      LOGICAL BRBAOK
C-----------------------------------------------------------------------
C
      CALL R8INIR(9,0.D0,C1,1)
      CALL R8INIR(9,0.D0,C2,1)
C
      CALL JEVECH('PMATERC','L',IMATE)
C
      DO 90 I=1,3
        NBACKN(I)   =  SIGM(I)
        NBACKN(I+3) =  VARIM(I)
        CURVP(I)    =  0.D0
        MP1(I)      =  0.D0 
        MP2(I)      =  0.D0
 90   CONTINUE 
      DISSIP = 0.D0
      DDISIP = 0.D0
C
C EFFORT NORMAL EXPRIME DANS LE REPERE D'ORTHOTROPIE
C
      CALL MULSYM(T2VE,NBACKN,T2EV,NBACKN)
C
C---- CALCUL DES DEFORMATIONS DANS LE REPERE D'ORTHOTROPIE
C          
      EPS(3)   = EPS(3)  / 2.D0 
      DEPS(3)  = DEPS(3) / 2.D0 
      KHI(3)   = KHI(3)  / 2.D0 
      DKHI(3)  = DKHI(3) / 2.D0 
C
      CALL MULSYM(T2VE, EPS,   T2EV, EPSOR)
      CALL MULSYM(T2VE, DEPS,  T2EV, DEPSOR)
      CALL MULSYM(T2VE, KHI,   T2EV, KHIOR)
      CALL MULSYM(T2VE, DKHI,  T2EV, DKHIOR)
C
      EPS(3)   = EPS(3)  *2.D0
      DEPS(3)  = DEPS(3) *2.D0
      KHI(3)   = KHI(3)  *2.D0
      DKHI(3)  = DKHI(3) *2.D0
C
      EPSOR(3)   = EPSOR(3)  * 2.D0
      DEPSOR(3)  = DEPSOR(3) * 2.D0
      KHIOR(3)   = KHIOR(3)  * 2.D0
      DKHIOR(3)  = DKHIOR(3) * 2.D0
C
      DO 50 I=1,3
        DEPSTO(I)   = DEPSOR(I)
        DEPSTO(I+3) = DKHIOR(I)
 50   CONTINUE
C
C- RECUPERATION DES PARAMETRES DE LA LOI DE COMPORTEMENT GLRC
C
      CALL RCCOMA ( ZI(IMATE), 'GLRC' , PHENOM, CODRES)
C
      IF(PHENOM . NE . 'GLRC' ) THEN
        CALL UTMESS('F','DXGLRC','COMPORTEMENT NON TROUVE:'//PHENOM)
      ENDIF
C
      NOMRES(1)  = 'MEX1'
      NOMRES(2)  = 'MEX2'
      NOMRES(3)  = 'CX1'
      NOMRES(4)  = 'CX2'     
      CALL RCVALA( ZI(IMATE),PHENOM,1,'NORM',NBACKN(1),4,NOMRES,
     +                 VALRES,CODRES,'FM')
      MP1(1)   = VALRES(1)
      MP2(1)   = VALRES(2)
      C1(1,1)  = VALRES(3)
      C2(1,1)  = VALRES(4)
C                       
      NOMRES(1)  = 'MEY1'
      NOMRES(2)  = 'MEY2'
      NOMRES(3)  = 'CY1'
      NOMRES(4)  = 'CY2'
      CALL RCVALA( ZI(IMATE),PHENOM,1,'NORM',NBACKN(2),4,NOMRES,
     +                 VALRES,CODRES,'FM')      
      MP1(2)  = VALRES(1)
      MP2(2)  = VALRES(2)
      C1(2,2) = VALRES(3)
      C2(2,2) = VALRES(4)      
C             
      NOMRES(1)  = 'CXY1'
      NOMRES(2)  = 'CXY2'
      CALL RCVALA( ZI(IMATE),PHENOM,1,'NORM',NBACKN(3),2,NOMRES,
     +                 VALRES,CODRES,'FM')    
      C1(3,3) = VALRES(1)
      C2(3,3) = VALRES(2)
C
C -- INTEGRATION DE LA LOI DE COMPORTEMENT GLRC
C
      ZERO   = 0.001D0
      NBITER = 10000
      ZEDEPS = ZERO*NORM6(DEPSTO)
      NBB = 0
C
      DO 10 I =1,6
        NBCKN(I) = NBACKN(I)
  10  CONTINUE
C
      CALL MPUPDA(ZERO,NBACKN,MP1,MP2,ZEROF,ZEROG)
C
      N=3
      CALL LCEQVE( MP1,MP1NEW)
      CALL LCEQVE( MP2,MP2NEW)
C
      DO 100 I=1,3
        DO 110 J=1,3
          DC1(I,J) = DELAS(I+3,J+3) + C1(I,J)
          DC2(I,J) = DELAS(I+3,J+3) + C2(I,J)
  110   CONTINUE
  100 CONTINUE
C
      CALL INITVE(3, DCURVP)
      DDISIP=0.D0
C
      N=6    
      CALL LCEQVE(DEPSTO,REPS)
      NCRIT0=CRINUM(NBACKN(4),REPS,MP1,MP2,DELAS)
C
C   METHODE MIXTE POUR ETRE SUR D'AVOIR F(M,BACKM)<=0 A CHAQUE ITERATION
C   --------------------------------------------------------------------
120   CONTINUE
C
C     INITIALISATION DE DEPSTE A REPS
        N=6
        CALL LCEQVE(REPS,DEPSTE)
C   
 20     CONTINUE
C       CALCUL DES NOUVELLES FORCES, DE L'INCREMENT DE COURBURE 
C       PLASTIQUE ET DE L'INCREMENT DE DISSIPATION
        NCRIT=CRINUM(NBACKN(4),DEPSTE,MP1,MP2,DELAS)
C
        CALL DNCUDI(NBACKN,DEPSTE,NCRIT,NBNNEW,DCURVP,
     &              DDISIP,MP1,MP2,DC1,DC2,DELAS,ZEROG,ZERO)
C       REACTUALISE LES LIMITES ELASTIQUES
        CALL MPUPDA(ZERO,NBNNEW,MP1NEW,MP2NEW,ZEFNEW,ZEGNEW)
C
        NBB = NBB +1
        IF(NBB.GT.NBITER)THEN
          CALL UTMESS('F','GLRCST',
     &                'INTEGRATION DE LA LOI DE COMPORTEMENT : 
     &                 LE NOMBRE DE PAS DE CALCUL > 10000')
        ENDIF
C
C       TESTE SI LE NOUVEAU MOMENT EST ELOIGNE DE LA ZONE LIMITE 
C
        IF(GPLAS(NBNNEW(4),MP1NEW).GT.ZEGNEW  .OR.
     &     GPLAS(NBNNEW(4),MP2NEW).GT.ZEGNEW) THEN
C
          CALL LCPRSV(0.5D0,DEPSTE,DEPSTE)
C 
C       TESTE SI LE NOUVEAU MOMENT EST A L'EXTERIEUR DU VOLUME ELASTIQUE
C
        ELSEIF(FPLAS(NBNNEW(4),MP1NEW).GT.ZEFNEW .OR.
     &    FPLAS(NBNNEW(4),MP2NEW).GT.ZEFNEW)THEN 
C
C         ON ESSAIE DE RAMENER LE MOMENT SUR LA SURFACE LIMITE
C
          CALL BRBACK(ZERO,NBNNEW,DCURVP,DDISIP,MP1NEW,MP2NEW,
     &                ZEFNEW,ZEGNEW,DC1,DC2,DELAS,BRBAOK)
          IF(BRBAOK) GOTO 30
C
          CALL LCPRSV(0.5D0,DEPSTE,DEPSTE) 
        ELSE
          GO TO 30 
        ENDIF
          GOTO 20
 30     CONTINUE
C
        N=6
        CALL LCEQVE(NBNNEW,NBACKN)
C
        CALL LCDIVE(REPS,DEPSTE,REPS)
C
        N=3
        CALL LCSOVE(CURVP,DCURVP,CURVP)
        CALL LCEQVE(MP1NEW,MP1)
        CALL LCEQVE(MP2NEW,MP2)
        DISSIP=DISSIP+DDISIP
        ZEROF=ZEFNEW
        ZEROG=ZEGNEW
        IF(NORM6(REPS).GT.ZEDEPS) GOTO 120
C    
C - CALCUL DE LA MATRICE TANGENTE DSIDEP
C
      IF ( OPT .EQ. 'RIGI_MECA_TANG') THEN
         CALL DXKTAN(DELAS,MP1,MP2,NBCKN,NCRIT0,DC1,DC2,DSIDEP)
      ELSE IF ( OPT .EQ. 'FULL_MECA') THEN
         CALL DXKTAN(DELAS,MP1,MP2,NBACKN,NCRIT,DC1,DC2,DSIDEP)
      ENDIF
C
C ----- PASSAGE DE LA MATRICE TANGENTE DU REPERE D'ORTHOTROPIE
C       AU REPERE DE L'ELEMENT
C      
      IF(OPT .EQ. 'RIGI_MECA_TANG'.OR. OPT .EQ. 'FULL_MECA') THEN
C 
         CALL ORTLOC(DSIDEP,0,0,T1VE)
         CALL ORTLOC(DSIDEP,3,3,T1VE)
         CALL ORTLOC(DSIDEP,0,3,T1VE)
C
         DO 60 I =1,3
           DO 80 J = 4,6
              DSIDEP(J,I)=DSIDEP(I,J)
 80       CONTINUE
 60      CONTINUE      
      ENDIF      
C
        DO 200 I=1,3
           DEPSEO(I)   = DEPSTO(I)
           DEPSEO(I+3) = DEPSTO(I+3)-CURVP(I)
  200   CONTINUE
        DO 210 I=1,3
          DNORM(I) = 0.D0
          DMOME(I) = 0.D0
          DO 220 J = 1,6
             DNORM(I) = DNORM(I) + DELAS(I,J)  * DEPSEO(J)
             DMOME(I) = DMOME(I) + DELAS(I+3,J)* DEPSEO(J)
220       CONTINUE
210     CONTINUE
C
C - PASSAGE DES VARIATIONS D'EFFORT DANS LE REPERE DE L'ELEMENT
C     
          CALL MULSYM(T2EV,DNORM,T2VE,DNORM)
          CALL MULSYM(T2EV,DMOME,T2VE,DMOME)
C
C - STOCKAGE DES EFFORTS GENERALISES ET DES VARIABLES INTERNES
C
      DO 125 I=1,3
        SIGP(I)    = SIGM(I)    + DNORM(I)
        SIGP(I+3)  = SIGM(I+3)  + DMOME(I)
        VARIP(I)   = NBACKN(I+3)
        VARIP(I+3) = VARIM(I+3) + CURVP(I)
  125 CONTINUE
       VARIP(7)    = VARIP(7) + DISSIP 
C
      END
