      SUBROUTINE  XDEPLA(NDIM,ELREFP,INO,NNOP,IGEOM,PINTT,DEPL,HEI,
     &                   DDLH,NFE,DDLT,BASLOC,LSN,LST,DEPLA)
      IMPLICIT NONE
      INTEGER     NDIM,DDLT,INO, NNOP,IGEOM, HEI, DDLH, NFE
      REAL*8      DEPL(DDLT,NNOP),BASLOC(3*NDIM*NNOP),LSN(NNOP)
      REAL*8      LST(NNOP)
      CHARACTER*8 ELREFP

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 04/04/2006   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C.......................................................................
C
C     BUT:  CALCUL DES DEPLACEMENTs AUX NOEUDS DES SOUS-ELEMENTS 
C           (TRIANGLES OU TETRAEDRES)
C.......................................................................
C IN  NDIM    : DIMENSION GEOMETRIQUE
C IN  ELREFP  : ÉLÉMENT DE RÉFÉRENCE PARENT
C IN  INO     : NUMERO DU NOEUD
C IN  NNOP    : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C IN  IGEOM   : COORDONNÉES DES NOEUDS DE L'ÉLÉMENT PARENT
C IN  PINTT   : COORDONNÉES DES POINTS D'INTERSECTION
C IN  DEPL    : DEPLACEMENTS 
C IN  HEI     : VALEUR (ENTIER) DE LA FONCTION HEAVYSIDE CSTE LE SS-ÉLT
C IN  DDLH    : NOMBRE DE DDL HEAVYSIDE (PAR NOEUD)
C IN  NFE     : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT
C IN  DDLT    : NOMBRE DE DDLS TOTAL PAR NOEUD
C IN  BASLOC  : BASE LOCALE AU FOND DE FISSURE AUX NOEUDS
C IN  LSN     : VALEUR DE LA LEVEL SET NORMALE AUX NOEUDS PARENTS
C IN  LST     : VALEUR DE LA LEVEL SET TANGENTE AUX NOEUDS PARENTS

C OUT DEPLA   : VALEUR DES DEPLACEMENTS AU NOEUD DU SOUS-ELEMENTS
C..............................................................
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM
C     ----- FIN COMMUNS NORMALISES  JEVEUX  --------------------------
C
      INTEGER IN, ID,IRET,NBLOC
      INTEGER IBI,I,CPT,IG
      REAL*8  PINTT(NDIM*11),GRAD(3,3),BASLOG(3*NDIM)
      REAL*8  DEPR(NDIM),DEPLA(NDIM),FE(4),HE
      REAL*8 F(3,3),EPS(6),XN(NDIM),FF(NNOP),LSNG,LSTG
      REAL*8 DFDI(NNOP,NDIM),XE(NDIM),R0(9,NNOP),R1,R2(4),R3(4,3)
C
      CALL JEMARQ()
C
      HE  = 1.D0 * HEI
      CALL LCINVN(NDIM,0.D0,DEPR)
C
      IF (INO.LT.1000) THEN

C       NOEUD SOMMET
        XN(1) = ZR(IGEOM-1+NDIM*(INO-1)+1)
        XN(2) = ZR(IGEOM-1+NDIM*(INO-1)+2)
        IF(NDIM.EQ.3)XN(3) = ZR(IGEOM-1+3*(INO-1)+3)   

      ELSE

C       NOEUD D'INTERSECTION
        XN(1) = PINTT(NDIM*(INO-1000-1)+1)
        XN(2) = PINTT(NDIM*(INO-1000-1)+2)
        IF(NDIM.EQ.3)XN(3) = PINTT(3*(INO-1000-1)+3)   

      ENDIF



C  REMARQUE : SI LE NOEUD EST UN NOEUD SOMMET DE L'ELEMENT PARENT, ON
C             POURRAIT SE PASSER DE FAIRE LA BOUCLE 100 (SUR LES NOEUDS)
C
C             MAIS ON PREFERE UNE ECRITURE GENERALE, QUI PERMET DE NE 
C             PAS FAIRE LA DISTINCTION ENTRE NOEUD SOMMET ET NOEUD 
C             D'INTERSECTION


C     FF : FONCTIONS DE FORMES AU NOEUD SOMMET OU D'INTERSECTION
      CALL REEREF(ELREFP,NNOP,IGEOM,XN,R0,.FALSE.,NDIM,R1,IBI,IBI,
     &               IBI,R2,R3,'NON',XE,FF,DFDI,F,EPS,GRAD)

      IF (NFE.GT.0) THEN
        NBLOC=3*NDIM
C       BASE LOCALE AU NOEUD SOMMET OU D'INTERSECTION)
        CALL LCINVN(NBLOC,0.D0,BASLOG)
        LSNG = 0.D0
        LSTG = 0.D0
        DO 10 IN=1,NNOP
          LSNG = LSNG + LSN(IN) * FF(IN)
          LSTG = LSTG + LST(IN) * FF(IN)
          DO 11 I=1,NBLOC
            BASLOG(I) = BASLOG(I) + BASLOC(NBLOC*(IN-1)+I) * FF(IN)
 11       CONTINUE 
 10    CONTINUE
C
C       FE : FONCTIONS D'ENRICHISSEMENT AU NOEUD SOMMET OU D'INTERSECT.
        IF(NDIM.EQ.3)THEN
           CALL XCALFE(XN,HE,LSNG,LSTG,BASLOG,FE,R3,IRET)
        ELSE
           CALL XCALF2(XN,HE,LSNG,LSTG,BASLOG,FE,R3,IRET)
        ENDIF
      ENDIF 

C     CALCUL DE L'APPROXIMATION DU DEPLACEMENT
      DO 100 IN=1,NNOP

        CPT=0

C       DDLS CLASSIQUES
        DO 101 I=1,NDIM
          CPT=CPT+1
          DEPR(I) = DEPR(I) +  FF(IN) * DEPL(CPT,IN)
 101    CONTINUE
              
C       DDLS HEAVISIDE
        DO 102 I=1,DDLH
          CPT=CPT+1
          DEPR(I) = DEPR(I) + HE * FF(IN) * DEPL(CPT,IN)
 102    CONTINUE    

C       DDL ENRICHIS EN FOND DE FISSURE
        DO 103 IG=1,NFE
          DO 104 I=1,NDIM
            CPT=CPT+1         
            DEPR(I) = DEPR(I) + FE(IG) * FF(IN) * DEPL(CPT,IN)
 104      CONTINUE    
 103    CONTINUE

 100  CONTINUE

C
      DEPLA(1)= DEPR(1)
      DEPLA(2)= DEPR(2)
      IF(NDIM.EQ.3)DEPLA(3)= DEPR(3)
C
      CALL JEDEMA()

      END
