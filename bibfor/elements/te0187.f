      SUBROUTINE TE0187(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C.......................................................................

C     BUT: CALCUL DU VECTEUR INTENSITE ACTIVE AUX NOEUDS
C          ELEMENTS ISOPARAMETRIQUES 3D

C          OPTION : 'INTE_ELNO'

C     ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................

      IMPLICIT REAL*8(A-H,O-Z)

      INCLUDE 'jeveux.h'
      INTEGER ICODRE
      CHARACTER*16 NOMTE,OPTION
      CHARACTER*8 FAMI,POUM
      REAL*8 OMEGA
      REAL*8 OMERHO,PI
      COMPLEX*16 VITX(27),VITY(27),VITZ(27)

      REAL*8 DFDX(27),DFDY(27),DFDZ(27),JAC
      INTEGER IDFDE,IGEOM,IDINO,IPINO

      INTEGER IINTE,IPRES,IMATE,IFREQ
      INTEGER JGANO,NNO,INO,I,KPG,SPT


      CALL ELREF4(' ','NOEU',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PPRESSC','L',IPRES)
      CALL JEVECH('PMATERC','L',IMATE)
      CALL JEVECH('PINTER','E',IINTE)

      FAMI='FPG1'
      KPG=1
      SPT=1
      POUM='+'
      MATER=ZI(IMATE)
      CALL RCVALB(FAMI,KPG,SPT,POUM,MATER,' ','FLUIDE',0,' ',R8B,
     &            1,'RHO',RHO,ICODRE,1)

      PI=R8PI()
      CALL JEVECH('PFREQR','L',IFREQ)
      OMEGA=2.D0*PI*ZR(IFREQ)
      OMERHO=OMEGA*RHO


C    BOUCLE SUR LES NOEUDS
      DO 30 INO=1,NNO

        IDINO=IINTE+(INO-1)*6-1
        IPINO=IPRES+INO-1
        CALL DFDM3D(NNO,INO,IPOIDS,IDFDE,ZR(IGEOM),DFDX,DFDY,DFDZ,JAC)

        VITX(INO)=(0.0D0,0.0D0)
        VITY(INO)=(0.0D0,0.0D0)
        VITZ(INO)=(0.0D0,0.0D0)

        DO 20 I=1,NNO

          VITX(INO)=VITX(INO)+DFDX(I)*ZC(IPRES+I-1)
          VITY(INO)=VITY(INO)+DFDY(I)*ZC(IPRES+I-1)
          VITZ(INO)=VITZ(INO)+DFDZ(I)*ZC(IPRES+I-1)
   20   CONTINUE

        VITX(INO)=VITX(INO)*(0.D0,1.D0)/OMERHO
        VITY(INO)=VITY(INO)*(0.D0,1.D0)/OMERHO
        VITZ(INO)=VITZ(INO)*(0.D0,1.D0)/OMERHO

        ZR(IDINO+1)=0.5D0*DBLE(ZC(IPINO)*DCONJG(VITX(INO)))
        ZR(IDINO+2)=0.5D0*DBLE(ZC(IPINO)*DCONJG(VITY(INO)))
        ZR(IDINO+3)=0.5D0*DBLE(ZC(IPINO)*DCONJG(VITZ(INO)))
        ZR(IDINO+4)=0.5D0*DIMAG(ZC(IPINO)*DCONJG(VITX(INO)))
        ZR(IDINO+5)=0.5D0*DIMAG(ZC(IPINO)*DCONJG(VITY(INO)))
        ZR(IDINO+6)=0.5D0*DIMAG(ZC(IPINO)*DCONJG(VITZ(INO)))
   30 CONTINUE

      END
