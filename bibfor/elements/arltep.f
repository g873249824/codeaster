      SUBROUTINE ARLTEP(NDIM  ,NPGS  ,KPGS  ,
     &                  NNS   ,COORS ,FS   ,
     &                  ELREFC,NNC   ,COORC ,
     &                  FC    ,DFDXC ,DFDYC ,DFDZC)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 22/06/2009   AUTEUR MEUNIER S.MEUNIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT    NONE
      INTEGER     NNS,NPGS,KPGS
      CHARACTER*8 ELREFC
      INTEGER     NNC,NDIM
      REAL*8      COORS(NDIM*NNS),COORC(NDIM*NNC)
      REAL*8      FS(NNS*NPGS)
      REAL*8      FC(NNC)
      REAL*8      DFDXC(NNC),DFDYC(NNC),DFDZC(NNC)
C
C ----------------------------------------------------------------------
C
C CALCUL DES MATRICES DE COUPLAGE ARLEQUIN
C OPTION ARLQ_MATR
C
C
C EXTENSION DES FF ET DFF D'UNE MAILLE C DANS UNE MAILLE S PLUS GRANDE
C
C ----------------------------------------------------------------------
C
C ON AGIT EN 3 TEMPS:
C 1/ CALCUL DES COORD. DS ESPACE REEL DU PT DE GAUSS CONSIDERE
C 2/ CALCUL DES COORD. ESPACE PARA. DANS LA MAILLE C DE CE POINT
C    PAR METHODE DE NEWTON (REEREG)
C 3/ CALCUL DES FF ET DFF DE LA MAILLE C EN CE POINT
C
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  NPGS   : NOMBRE DE PT DE GAUSS DE LA MAILLE S
C IN  KPGS   : NUMERO PT DE GAUSS DANS MAILLE S
C IN  NNS    : NOMBRE DE NOEUDS DE LA MAILLE S
C IN  COORS  : COORD. NOEUDS DE MAILLE S
C IN  FS     : FCT. FORME DE MAILLE S
C IN  ELREFC : TYPE DE LA MAILLE C
C IN  NNC    : NOMBRE DE NOEUDS DE LA MAILLE C
C IN  COORC  : COORD. NOEUDS DE MAILLE C
C OUT FC     : FCT. FORME DE MAILLE C AU POINT DE GAUSS KPGS
C               DE LA MAILLE S
C OUT DFDXC  : DERIVEE FCT. FORME/X DE MAILLE C AU POINT DE GAUSS KPGS
C              DE LA MAILLE S (SI LCALDF = .TRUE.)
C OUT DFDYC  : DERIVEE FCT. FORME/Y DE MAILLE C AU POINT DE GAUSS KPGS
C              DE LA MAILLE S (SI LCALDF = .TRUE.)
C OUT DFDZC  : DERIVEE FCT. FORME/Z DE MAILLE C AU POINT DE GAUSS KPGS
C              DE LA MAILLE S (SI LCALDF = .TRUE.)
C
C ----------------------------------------------------------------------
C
      REAL*8   COORR(3),COORPC(3)
      INTEGER  IDIM,JDIM,INO,IBID,JBID,JDECAL
      REAL*8   DFR(3,NNC)
      REAL*8   INVJAC(NDIM,NDIM)
      INTEGER  IRET
C
C ----------------------------------------------------------------------
C
      IF (KPGS.GT.NPGS) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- COORDONNEES DS ESPACE REEL DU POINT DE GAUSS IPG DE LA MAILLE S
C
      JDECAL = (KPGS-1)*NNS
      CALL LCINVN(3,0.D0,COORR)
      CALL LCINVN(3,0.D0,COORPC)

      DO 10 INO = 1,NNS
        DO 20 IDIM = 1,NDIM
          COORR(IDIM) = COORR(IDIM) +
     &                  FS(JDECAL+INO)*
     &                  COORS(NDIM*(INO-1)+IDIM)
   20   CONTINUE
   10 CONTINUE
C
C --- COORDONNEES DU PT DE GAUSS DS ESPACE PARA DE MAILLE C
C
      CALL REEREG('S',ELREFC,NNC   ,COORC ,COORR ,NDIM  ,
     &            COORPC, IRET)
C
C --- VALEURS DES FONCTIONS DE FORME DE ELT C EN COORPC
C
      CALL ELRFVF(ELREFC,COORPC,NNC   ,FC   ,IBID)
C
C --- DERIVEES DES FONCTIONS DE FORME DE REFERENCE EN COORPC
C
      CALL ELRFDF(ELREFC,COORPC,NDIM*NNC,DFR ,IBID,JBID)
C
C --- INVERSE DE LA JACOBIENNE
C
      CALL INVJAX(NNC, NDIM, DFR, COORC, INVJAC)
C
C --- DERIVEES DES FONCTIONS DE FORMES CLASSIQUES EN COORPC
C
      DO 30 INO = 1,NNC

         DFDXC(INO)= INVJAC(1,1)*DFR(1,INO)
         DFDYC(INO)= INVJAC(1,2)*DFR(1,INO)
         DFDZC(INO)= INVJAC(1,3)*DFR(1,INO)

        DO 40 JDIM = 2,NDIM

          DFDXC(INO)= DFDXC(INO) +
     &                INVJAC(JDIM,1)*DFR(JDIM,INO)
          DFDYC(INO)= DFDYC(INO) +
     &                INVJAC(JDIM,2)*DFR(JDIM,INO)
          DFDZC(INO)= DFDZC(INO) +
     &                INVJAC(JDIM,3)*DFR(JDIM,INO)

   40   CONTINUE
   30 CONTINUE
C
      END
