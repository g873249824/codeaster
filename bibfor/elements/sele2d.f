      SUBROUTINE SELE2D(NNO,DFDE,DFDK,GEOM,THETA,NCOMP,CHAMP,DLAG,DEUL)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 02/06/99   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      IMPLICIT NONE

      INTEGER  NNO, NCOMP
      REAL*8   DFDE(*), DFDK(*), GEOM(2,NNO), THETA(2,NNO)
      REAL*8   CHAMP(NCOMP,NNO), DLAG(NCOMP,NNO), DEUL(NCOMP,NNO)
C ----------------------------------------------------------------------
C       PASSAGE DERIVEE LAGRANGIENNE -> DERIVEE EULERIENNE   (2D)
C ----------------------------------------------------------------------
C  IN  NNO    NOMBRE DE NOEUDS
C  IN  DFDE   GRADIENTS DES FONCTIONS DE FORME DE REF
C  IN  DFDK   GRADIENTS DES FONCTIONS DE FORME DE REF
C  IN  GEOM   COORDONNEES DES NOEUDS
C  IN  THETA  CHAMP THETA
C  IN  NCOMP  NOMBRE DE COMPOSANTES DU CHAMP A DERIVER
C  IN  CHAMP  CHAMP A DERIVER
C  IN  DLAG   DERIVEE LAGRANGIENNE DU CHAMP
C OUT  DEUL   DERIVEE EULERIENNE DU CHAMP
C ----------------------------------------------------------------------


      INTEGER N, K, I, NC
      REAL*8  POIDSN
      REAL*8  GRAD(2)

      REAL*8  DFDX(9), DFDY(9), POIDS(9)



C    INITIALISATION DES POIDS DES PTS D'INTEGRATION (INUTILISES)
      CALL R8INIR(NNO,0.D0,POIDS,1)


C    CALCUL EN CHAQUE NOEUDS (= POINTS D'INTEGRATION)

      DO 20 N = 1 , NNO

        K = (N-1)*NNO + 1
        CALL DFDM2D ( NNO,POIDS,DFDE(K),DFDK(K),
     &                  GEOM,DFDX,DFDY,POIDSN )

        DO 30 NC = 1, NCOMP

C        GRADIENT DU CHAMP
          GRAD(1) = 0.D0
          GRAD(2) = 0.D0
          DO 100 I = 1 , NNO
            GRAD(1) = GRAD(1) + CHAMP(NC,I)*DFDX(I)
            GRAD(2) = GRAD(2) + CHAMP(NC,I)*DFDY(I)
  100     CONTINUE

C        DERIVEE EULERIENNE : DT/DE  =  DT/DL  - GRAD(T).THETA
          DEUL(NC,N) = DLAG(NC,N) - THETA(1,N)*GRAD(1)
     &                            - THETA(2,N)*GRAD(2)

   30   CONTINUE
   20 CONTINUE

      END
