      SUBROUTINE TE0537(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 05/09/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      CHARACTER*16 OPTION,NOMTE
C     ------------------------------------------------------------------
C     POU_D_EM : POUTRE MULTIFIBRE EULER BERNOULLI
C     CALCUL DE L'OPTION EPSI_ELGA
C       - DEFORMATIONS DANS LES FIBRES (SOUS PTS DE GAUSS) A PARTIR DES
C         DEPLACEMENTS
C     CALCUL DE L'OPTION SIEF_ELGA
C       - CONTRAINTE DANS LES FIBRES (SOUS PTS DE GAUSS) COMPO LINEAIRE
C     ------------------------------------------------------------------
C IN  OPTION : K16 : NOM DE L'OPTION A CALCULER
C IN  NOMTE  : K16 : NOM DU TYPE ELEMENT
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER JCONT,LORIEN,JDEPL,IMATE,LX,NNO,NC,I,IRET
      PARAMETER (NNO=2,NC=6)
      CHARACTER*16 CH16
      REAL*8 UL(12),PGL(3,3),DEGE(6),XL,E
      INTEGER NBFIB,NCARFI,JACF,JTAB(7)
      REAL*8 CASECT(6),CASEC1(6)
      REAL*8 ZERO
      PARAMETER (ZERO=0.0D+0)
      REAL*8 B(4),GG,XI,WI,VALRES(2)
      INTEGER IP,IPOS,IADZI,IAZK24
      INTEGER IPOS1,IPOS2,NBFIG,NBGF,IG,NUGF,IFB,ICP,ISDCOM,ICOMPO
      CHARACTER*8 MATERI,NOMRES(2),NOMAIL
      INTEGER CODRES(2)
C     ------------------------------------------------------------------
C ----------------------------------------------------------------------

C     --- RECUPERATION DES CARACTERISTIQUES DES FIBRES :
      CALL JEVECH('PNBSP_I','L',IFB)
      NBFIB = ZI(IFB)
      NBGF=ZI(IFB+1)
      CALL JEVECH('PFIBRES','L',JACF)
      NCARFI = 3

      IF (OPTION.EQ.'EPSI_ELGA') THEN
         CALL TECACH('OON','PDEFORR',7,JTAB,IRET)
         JCONT = JTAB(1)
      ELSE IF (OPTION.EQ.'SIEF_ELGA') THEN
         CALL JEVECH('PMATERC','L',IMATE)
         CALL TECACH('OON','PCONTRR',7,JTAB,IRET)
         JCONT = JTAB(1)
      ELSE
         CH16 = OPTION
         CALL U2MESK('F','ELEMENTS2_47',1,CH16)
      END IF
      CALL JEVECH('PGEOMER','L',LX)
      CALL JEVECH('PCAORIE','L',LORIEN)
      CALL JEVECH('PDEPLAR','L',JDEPL)


C     --- RECUPERATION DES COORDONNEES DES NOEUDS ---
      LX = LX - 1
      XL = SQRT( (ZR(LX+4)-ZR(LX+1))**2 +
     &           (ZR(LX+5)-ZR(LX+2))**2 +
     &           (ZR(LX+6)-ZR(LX+3))**2 )
      IF (XL.EQ.ZERO) THEN
        CALL TECAEL(IADZI,IAZK24)
        NOMAIL = ZK24(IAZK24-1+3)(1:8)
        CALL U2MESK('F','ELEMENTS2_43',1,NOMAIL)
      END IF

C     --- RECUPERATION DES ORIENTATIONS ---
      CALL MATROT(ZR(LORIEN),PGL)

C     --- PASSAGE DES DEPLACEMENTS DANS LE REPERE LOCAL ---
      CALL UTPVGL(NNO,NC,PGL,ZR(JDEPL),UL)

C --- SI OPTION EPSI_ELGA OU SIEF_ELGA

C --- BOUCLE SUR LES POINTS DE GAUSS
      DO 20 IP=1,2
C        ---  MATRICE B PUIS DEGE PUIS DEFORMATIONS SUR LES FIBRES
         CALL PMFPTI(IP,XL,XI,WI,B,GG)
C        ZERO POUR LA VARIABLE ALPHA DES MODES INCOMPATIBLES CAR
C        NON ACTIF SI CALCUL ELASTIQUE (RIGI_MECA et X_X_DEPL)
         CALL PMFDGE(B,GG,UL,ZERO,DEGE)
         IPOS=JCONT+NBFIB*(IP-1)
         CALL PMFDEF(NBFIB,NCARFI,ZR(JACF),DEGE,ZR(IPOS))
 20   CONTINUE

C --- SI EPSI_ELGA JCONT EST L'ADRESSE PDEFORR, ON SORT
      IF(OPTION .EQ. 'EPSI_ELGA') GOTO 50

C --- SI OPTION SIEF_ELGA ON CONTINUE

C --- RECUPERATION DES DIFFERENTS MATERIAUX DANS SDCOMP DANS COMPOR
      CALL JEVECH('PCOMPOR','L',ICOMPO)
      CALL JEVEUO(ZK16(ICOMPO-1+7),'L',ISDCOM)

C --- CALCUL DES CARACTERISTIQUES INTEGREES DE LA SECTION ---
      DO 16 I = 1,6
         CASECT(I) = ZERO
16    CONTINUE
C --- BOUCLE SUR LES GROUPES DE FIBRE
      IPOS=JACF
      IPOS1=JCONT-1
      IPOS2=IPOS1+NBFIB
      DO 100 IG=1,NBGF
         NUGF=ZI(IFB+1+IG)
         ICP=ISDCOM-1+(NUGF-1)*6
         READ(ZK16(ICP+6),'(I16)')NBFIG
         MATERI=ZK16(ICP+2)(1:8)
C ---    CALCUL DES CARACTERISTIQUES DE LA SECTION ---
         CALL PMFITG(NBFIG,NCARFI,ZR(IPOS),CASEC1)
C ---    ON MULTIPLIE PAR E (CONSTANT SUR LE GROUPE)
         NOMRES(1) = 'E'
         NOMRES(2) = 'NU'
         CALL RCVALB('RIGI',1,1,'+',ZI(IMATE),MATERI,'ELAS',
     &              0,' ',0.D0,2,
     &              NOMRES, VALRES, CODRES, 1)
         E = VALRES(1)
         DO 26 I = 1,6
            CASECT(I) = CASECT(I) + E*CASEC1(I)
26       CONTINUE
         IPOS=IPOS+NBFIG*NCARFI

C ---    ON MULTIPLIE LES ZR(JCONT) (DEFORMATIONS) PAR E
C        POUR AVOIR DES CONTRAINTES
         DO 32 I=1,NBFIG
            ZR(IPOS1+I)=ZR(IPOS1+I) * E
            ZR(IPOS2+I)=ZR(IPOS2+I) * E
32       CONTINUE
         IPOS1=IPOS1+NBFIG
         IPOS2=IPOS2+NBFIG
100   CONTINUE


50    CONTINUE
      END
