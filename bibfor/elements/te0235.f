      SUBROUTINE TE0235(OPTION,NOMTE)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*(*) OPTION,NOMTE
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 26/03/2013   AUTEUR CHEIGNON E.CHEIGNON 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ======================================================================
C     CALCULE LA MATRICE DE RAIDEUR CENTRIFUGE DES ELEMENTS DE POUTRE
C     AVEC GAUCHISSEMENT (MULTIFIBRE ON NON)
C     ------------------------------------------------------------------
C IN  OPTION : K16 : NOM DE L'OPTION A CALCULER
C       'RIGI_MECA_RO'      : CALCUL DE LA MATRICE DE RAIDEUR CENTRIFUGE
C IN  NOMTE  : K16 : NOM DU TYPE ELEMENT
C       'MECA_POU_D_TG' : POUTRE DROITE DE TIMOSHENKO (GAUCHISSEMENT)
C       'MECA_POU_D_TGM': POUTRE DROITE DE TIMOSHENKO (GAUCHISSEMENT)
C                         MULTI-FIBRES SECTION CONSTANTE
C     ------------------------------------------------------------------

      INTEGER NBRES
      PARAMETER (NBRES=6)
      REAL*8 VALRES(NBRES),R8B
      INTEGER CODRES(NBRES)
      CHARACTER*8 NOMRES(NBRES),NOMAIL,FAMI,POUM
      CHARACTER*24 MATOR
      INTEGER I, LMATER,J,IRET
      INTEGER LORIEN, LMAT
      INTEGER NNO, NC,KPG,SPT,NBGFMX
      INTEGER ITYPE, LSECT, LX,IADZI,IAZK24,IROTA
      INTEGER INBFIB,NBFIB,JACF,ICOMPO,ISICOM,NPG,ISDCOM
      REAL*8  OMEGA(3),OMEGL(3),S
      REAL*8  XL
      REAL*8  ZERO, UN, DEUX
      REAL*8  E, G, XNU, RHO
      REAL*8  A, XIY, XIZ, ALFAY, ALFAZ
      REAL*8  PGL(3,3),MLV(105),MATP1(105)
      REAL*8  CARSEC(6),TEMP,RBID,CASRHO(6),CASECE(6)
C     ------------------------------------------------------------------
      DATA NOMRES/'E','NU','RHO','RHO_F_IN','RHO_F_EX','CM'/
C     ------------------------------------------------------------------
      ZERO = 0.D0
      UN   = 1.D0
      DEUX = 2.D0
C     ------------------------------------------------------------------

C     --- CARACTERISTIQUES DES ELEMENTS
C
      NNO    = 2
      NC     = 7
      ITYPE  = 0

C     --- CARACTERISTIQUES GENERALES DES SECTIONS ---
      CALL JEVECH('PCAGNPO','L',LSECT)
      LSECT = LSECT - 1
C
C     --- RECUPERATION DES CARACTERISTIQUES MATERIAUX ---
      CALL JEVECH('PMATERC','L',LMATER)

      IF (NOMTE.EQ.'MECA_POU_D_TG')THEN
        DO 10 I = 1,NBRES
           VALRES(I) = ZERO
10      CONTINUE


        FAMI='FPG1'
        KPG=1
        SPT=1
        POUM='+'
        CALL RCVALB(FAMI,KPG,SPT,POUM,ZI(LMATER),' ','ELAS',0,' ',R8B,
     &            3,NOMRES,VALRES,CODRES,1)
        E = VALRES(1)
        XNU = VALRES(2)
        RHO = VALRES(3)
        G   = E / ( DEUX * ( UN + XNU ) )

        A = ZR(LSECT+1)
        XIY = ZR(LSECT+2)
        XIZ = ZR(LSECT+3)

      ELSEIF (NOMTE.EQ.'MECA_POU_D_TGM') THEN
C       CALCUL DE E ET G
        CALL PMFITX(ZI(LMATER),1,CASECE,G)

C       CALCUL DE RHO MOYEN
        CALL PMFITX(ZI(LMATER),2,CASRHO,RBID)


        CALL JEVECH('PNBSP_I','L',INBFIB)
        NBFIB = ZI(INBFIB)
        CALL JEVECH('PFIBRES','L',JACF)
        CALL PMFITG(NBFIB,3,ZR(JACF),CARSEC)
        A     = CARSEC(1)
        XIY   = CARSEC(5)
        XIZ   = CARSEC(4)

        RHO = CASRHO(1)/A
        E   = CASECE(1)/A

      ENDIF

      ALFAY = ZR(LSECT+4)
      ALFAZ = ZR(LSECT+5)
C     --- COORDONNEES DES NOEUDS ---
      CALL JEVECH('PGEOMER','L',LX)
      LX = LX - 1
      XL = SQRT( (ZR(LX+4)-ZR(LX+1))**2 +
     &           (ZR(LX+5)-ZR(LX+2))**2 +
     &           (ZR(LX+6)-ZR(LX+3))**2)
      IF (XL.EQ.ZERO) THEN
        CALL TECAEL(IADZI,IAZK24)
        NOMAIL = ZK24(IAZK24-1+3)(1:8)
        CALL U2MESK('F','ELEMENTS2_43',1,NOMAIL)
      ENDIF

C     --- RECUPERATION DES ORIENTATIONS ---
      CALL JEVECH('PCAORIE','L',LORIEN)
C     --- RECUPERATION DU VECTEUR ROTATION ---
      CALL JEVECH('PROTATR','L',IROTA)
      OMEGA(1) = ZR(IROTA+1)*ZR(IROTA)
      OMEGA(2) = ZR(IROTA+2)*ZR(IROTA)
      OMEGA(3) = ZR(IROTA+3)*ZR(IROTA)
      CALL MATROT(ZR(LORIEN),PGL)
      DO 1 I=1,3
      S=0.D0
        DO 2 J=1,3
          S=S+PGL(I,J)*OMEGA(J)
2       CONTINUE
      OMEGL(I)=S
1     CONTINUE

CCC     --- CALCUL DE LA MATRICE DE MASSE LOCALE ---
         DO 20 I = 1 , 105
            MATP1(I) = 0.0D0
20       CONTINUE
C        --- POUTRE DROITE SECTION CONSTANTE OU VARIABLE (1 OU 2)
        CALL PORIRO(ITYPE,MATP1,RHO,OMEGL,E,A,A,XL,XIY,XIY,XIZ,
     &              XIZ,G,ALFAY,ALFAY,ALFAZ,ALFAZ)
        CALL MASSTG(MATP1,MLV)


      CALL JEVECH('PMATUUR','E',LMAT)

      CALL UTPSLG(NNO,NC,PGL,MLV,ZR(LMAT))

      END
