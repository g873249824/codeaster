      SUBROUTINE TE0291(OPTION,NOMTE)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16 OPTION,NOMTE
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 20/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE DELMAS J.DELMAS
C
C     BUT:
C         CALCUL DE L'INDICATEUR D'ERREUR EN ENERGIE
C         SUR UN ELEMENT AVEC LA METHODE DE ZHU-ZIENKIEWICZ.
C         OPTION : 'CALC_ESTI_ERRE'
C
C ......................................................................
C
C
C
C
      INTEGER NNO,KP,NPG1,I,K,NNOS,JGANO,NDIM
      INTEGER IPOIDS,IVF,IDFDE,IGEOM,NIV,NBCMP
      INTEGER IBID,IERR,IMATE,ISIGM,ISIGNO,MATER

      REAL*8 DFDX(27),DFDY(27),DFDZ(27),POIDS,VALRES(2)
      REAL*8 SIGL11,SIGL22,SIGL33,SIGL12,SIGL13,SIGL23
      REAL*8 SIGC11,SIGC22,SIGC33,SIGC12,SIGC13,SIGC23
      REAL*8 ESIG11,ESIG22,ESIG33,ESIG12,ESIG13,ESIG23
      REAL*8 E,NU,EEST,NOR,NORSIG,NU0,HE,R

      INTEGER ICODRE(2)
      CHARACTER*4 FAMI
      CHARACTER*8 NOMRES(2)

      LOGICAL LTEATT,LAXI
C
C ----------------------------------------------------------------------
C
      FAMI = 'RIGI'
      CALL ELREF4(' ',FAMI,NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDE,JGANO)
      CALL JEMARQ()
C
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PMATERC','L',IMATE)
      MATER = ZI(IMATE)
      NOMRES(1) = 'E'
      NOMRES(2) = 'NU'

C     CHAMP DE CONTRAINTE CALCULE
      CALL JEVECH('PSIEF_R','L',ISIGM)
C     CHAMP DE CONTRAINTE LISSE
      CALL JEVECH('PSIGMA','L',ISIGNO)
C
      CALL JEVECH('PERREUR','E',IERR)
C
      NORSIG = 0.D0
      ZR(IERR) = 0.D0
C
C    BOUCLE SUR LES POINTS DE GAUSS
C
      LAXI = .FALSE.
      IF (LTEATT(' ','AXIS','OUI')) LAXI = .TRUE.

      DO 101 KP=1,NPG1
        K=(KP-1)*NNO
        IF (NDIM.EQ.2) THEN
          CALL DFDM2D(NNO,KP,IPOIDS,IDFDE,ZR(IGEOM),DFDX,DFDY,POIDS)
          NBCMP=4
        ELSE IF (NDIM.EQ.3) THEN
          CALL DFDM3D(NNO,KP,IPOIDS,IDFDE,ZR(IGEOM),DFDX,DFDY,
     &                DFDZ,POIDS)
          NBCMP=6
        ENDIF

        IF (LAXI) THEN
           R = 0.D0
           DO 103 I=1,NNO
             R = R + ZR(IGEOM+2*(I-1)) * ZR(IVF+K+I-1)
103        CONTINUE
           POIDS = POIDS*R
        ENDIF

        SIGL11 = 0.D0
        SIGL22 = 0.D0
        SIGL33 = 0.D0
        SIGL12 = 0.D0
        SIGL13 = 0.D0
        SIGL23 = 0.D0
        DO 102 I=1,NNO
          SIGL11 = SIGL11 + ZR(ISIGNO-1+NBCMP*(I-1)+1) * ZR(IVF+K+I-1)
          SIGL22 = SIGL22 + ZR(ISIGNO-1+NBCMP*(I-1)+2) * ZR(IVF+K+I-1)
          SIGL33 = SIGL33 + ZR(ISIGNO-1+NBCMP*(I-1)+3) * ZR(IVF+K+I-1)
          SIGL12 = SIGL12 + ZR(ISIGNO-1+NBCMP*(I-1)+4) * ZR(IVF+K+I-1)
          IF (NDIM.EQ.3) THEN
            SIGL13 = SIGL13 + ZR(ISIGNO-1+NBCMP*(I-1)+5) * ZR(IVF+K+I-1)
            SIGL23 = SIGL23 + ZR(ISIGNO-1+NBCMP*(I-1)+6) * ZR(IVF+K+I-1)
          ENDIF
C
102     CONTINUE

        CALL RCVALB(FAMI,KP,1,'+',MATER,' ','ELAS',0,' ',0.D0,
     &              2,NOMRES,VALRES, ICODRE, 1)
        E  = VALRES(1)
        NU = VALRES(2)
C
C    ESTIMATION DE L'ERREUR EN NORME DE L' ENERGIE
C
        SIGC11 = ZR(ISIGM-1+NBCMP*(KP-1)+1)
        SIGC22 = ZR(ISIGM-1+NBCMP*(KP-1)+2)
        SIGC33 = ZR(ISIGM-1+NBCMP*(KP-1)+3)
        SIGC12 = ZR(ISIGM-1+NBCMP*(KP-1)+4)

        ESIG11 = SIGL11-SIGC11
        ESIG22 = SIGL22-SIGC22
        ESIG33 = SIGL33-SIGC33
        ESIG12 = SIGL12-SIGC12
        
        IF (NDIM.EQ.2) THEN
          EEST = ESIG11**2+ESIG22**2+ESIG33**2
     &         +2*(1.D0+NU)*(ESIG12)**2
     &         -2*NU*ESIG11*ESIG22-2*NU*ESIG11*ESIG33-2*NU*ESIG22*ESIG33
          ZR(IERR) = ZR(IERR) + EEST * POIDS / E
C
C    NORME DE L' ENERGIE DE LA SOLUTION CALCULEE
C
        NOR    = SIGC11**2+SIGC22**2+SIGC33**2
     &         +2*(1.D0+NU)*(SIGC12)**2
     &         -2*NU*SIGC11*SIGC22-2*NU*SIGC11*SIGC33-2*NU*SIGC22*SIGC33
        NORSIG = NORSIG + NOR * POIDS / E
C
        ELSE IF (NDIM.EQ.3) THEN
 
          SIGC13 = ZR(ISIGM-1+NBCMP*(KP-1)+5)
          SIGC23 = ZR(ISIGM-1+NBCMP*(KP-1)+6)

          ESIG13 = SIGL13-SIGC13
          ESIG23 = SIGL23-SIGC23

          EEST = ESIG11**2+ESIG22**2+ESIG33**2
     &         +2*(1.D0+NU)*(ESIG12)**2
     &         +2*(1.D0+NU)*(ESIG13)**2
     &         +2*(1.D0+NU)*(ESIG23)**2
     &         -2*NU*ESIG11*ESIG22-2*NU*ESIG11*ESIG33-2*NU*ESIG22*ESIG33
          ZR(IERR) = ZR(IERR) + EEST * POIDS / E
C
C    NORME DE L' ENERGIE DE LA SOLUTION CALCULEE
C
          NOR    = SIGC11**2+SIGC22**2+SIGC33**2
     &         +2*(1.D0+NU)*(SIGC12)**2
     &         +2*(1.D0+NU)*(SIGC13)**2
     &         +2*(1.D0+NU)*(SIGC23)**2
     &         -2*NU*SIGC11*SIGC22-2*NU*SIGC11*SIGC33-2*NU*SIGC22*SIGC33
          NORSIG = NORSIG + NOR * POIDS / E
        ELSE
           CALL ASSERT(.FALSE.)
        ENDIF
C
101   CONTINUE
C
      NIV=1
      CALL UTHK(NOMTE,ZR(IGEOM),HE,NDIM,IBID,IBID,IBID,IBID,NIV,IBID)
C
      IF ((ZR(IERR)+NORSIG).NE.0.D0) THEN
        NU0 = 100.D0*SQRT(ZR(IERR)/(ZR(IERR)+NORSIG))
      ELSE
        NU0 = 0.D0
      ENDIF

      ZR(IERR  ) = SQRT(ZR(IERR))
      ZR(IERR+1) = NU0
      ZR(IERR+2) = SQRT(NORSIG)
      ZR(IERR-1+10)=HE
C
      CALL JEDEMA()
C
      END
