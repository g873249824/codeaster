      SUBROUTINE D1CRO2 (ZIMAT,NMNBN,NMPLAS,NMDPLA,NMDDPL,NMPROX,CNBN,
     &           CPLAS,RPARA,CIEF,CDEPS,CDTG,CIER,CDEPSP,DC,BEND)
        IMPLICIT  NONE
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 19/05/2009   AUTEUR SFAYOLLE S.FAYOLLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C     CALCUL DU MULTIPLICATEUR PLASTIQUE
C     ET DE L INCREMENT DE COURBURE PLASTIQUE
C     DANS LE CAS OU 1 CRITERE PLASTIQUE EST ACTIVE
C     METHODE EXPLICITE AVEC UNE CONDITION DE SECOND ORDRE
C
C IN  ZIMAT : ADRESSE DE LA LISTE DE MATERIAU CODE
C IN  NMNBN : FORCE - BACKFORCE
C IN  NMPLAS : MOMENTS LIMITES DE PLASTICITE
C IN  NMDPLA : DERIVEES DES MOMENTS LIMITES DE PLASTICITE
C IN  NMDDPL : DERIVEES SECONDES DES MOMENTS LIMITES DE PLASTICITE
C IN  NMPROX : NMPROX > 0 : NBN DANS ZONE DE CRITIQUE
C IN  CDTG : MATRICE TANGENTE
C IN  DC : MATRICE ELASTIQUE + CONSTANTES DE PRAGER
C IN  BEND : FLEXION POSITIVE (1) OU NEGATIVE (-1)
C
C IN/OUT RPARA : LISTES DE PARAMETRES DE TYPE ENTIER
C
C OUT CNBN : NOUVELLE FORCE - BACKFORCE
C OUT CPLAS : NOUVEAUX MOMENTS LIMITES DE PLASTICITE
C OUT CIEF : NOUVEAU CIEF > 0 : NBN HORS DE LA ZONE DE DEFINITION DE MP
C OUT CDEPS : NOUVEL INCREMENT DE DEFORMATION DANS LE REPERE ORTHO
C OUT CIER : NOUVEAU CODE ERREUR
C OUT CDEPSP : NOUVEL INCREMENT DE DEF PLASTIQUE DANS LE REPERE ORTHO

      INTEGER BEND, NBXX, I, J
      INTEGER NMPROX(2), CIEF, CIER, ZIMAT

      REAL*8 NMNBN(6), NMPLAS(2,3), NMDPLA(2,2), NMDDPL(2,2)
      REAL*8 CNBN(6), CPLAS(2,3), CZEF, CZEG
      REAL*8 CDEPS(6), CDTG(6,6),CDEPSP(6)
      REAL*8 DC(6,6),NORMM,CP1(1,6),CP0(1),TDCU(1,6),H(6,6),DF(6)
      REAL*8 LAMBDA,U(6),A0(1),A1(1), A2(1),XX(2),DCU(6),HDCU(6)
      REAL*8 FPLASS,TDF(1,6),DDEPS(6),TDDEPS(1,6),RPARA(3)

      CZEF  = RPARA(1)
      CZEG  = RPARA(2)
      NORMM = RPARA(3)

C     CALCUL LA MATRICE HESSIENNE DU CRITERE DE PLASTICITE
      CALL HPLASS(NMNBN,NMPLAS,NMDPLA,NMDDPL,BEND,H)
C     CALUL DES DIRECTIONS DE L ECOULEMENT DES DEFORMATIONS PLASTIQUES
      CALL DFUUSS(NMNBN,NMPLAS,NMDPLA,NMPROX,BEND,U)
C     CALCUL LE GRADIENT DU CRITERE DE PLASICITE
      CALL DFPLGL(NMNBN,NMPLAS,NMDPLA,BEND,DF)

      DO 10, J = 1,6
        TDF(1,J) = DF(J)
 10   CONTINUE

      CALL MATMUL(CDTG,CDEPS,6,6,1,DDEPS)

      DO 20, J = 1,6
        TDDEPS(1,J) = DDEPS(J)
 20   CONTINUE

      CALL MATMUL(DC,U,6,6,1,DCU)

      DO 30, J = 1,6
        TDCU(1,J) = DCU(J)
 30   CONTINUE

      CALL MATMUL(H,DCU,6,6,1,HDCU)
      CALL MATMUL(TDDEPS,H,1,6,6,CP1)

      DO 40, J = 1,6
        CP1(1,J) = TDF(1,J) + 0.5D0*CP1(1,J)
 40   CONTINUE

      CALL MATMUL(CP1,DDEPS,1,6,1,CP0)

      A0(1) = FPLASS(NMNBN,NMPLAS,BEND) + CP0(1)

      CALL MATMUL(TDF,DCU,1,6,1,A1)
      CALL MATMUL(TDDEPS,HDCU,1,6,1,CP0)
      A1(1) = -A1(1) - CP0(1)
      CALL MATMUL(TDCU,HDCU,1,6,1,CP0)
      A2(1) = 0.5D0 * CP0(1)

C     RESOLUTION DE L EQUATION DU SECOND DEGRE
      CALL DRAAC2(A2,A1,A0,XX(1),XX(2),NBXX)

      DO 54, I = 1, NBXX
        IF(XX(I) .GE. 0.D0) THEN
          LAMBDA=XX(I)

          DO 50, J = 1,6
            CDEPSP(J) = LAMBDA * U(J)
 50       CONTINUE

C     CALCUL DE CNBN ET CDEPSP QUAND UN CRITERE PLASTIQUE EST ACTIVE
          CALL NMNET1(ZIMAT,NMNBN,CNBN,CPLAS,CZEF,CZEG
     &         ,CIEF,CDEPS,CDTG,CIER,CDEPSP,DC,NORMM)

          IF(CIER .EQ. 0) GOTO 60
        ENDIF
 54   CONTINUE

      CIER=3

      CALL R8INIR(6,0.0D0,CDEPSP,1)

 60   CONTINUE

      RPARA(1) = CZEF
      RPARA(2) = CZEG
      RPARA(3) = NORMM
      END
