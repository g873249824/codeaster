      SUBROUTINE TE0239(OPTION,NOMTE)
C MODIF ELEMENTS  DATE 21/07/2009   AUTEUR LEBOUVIER F.LEBOUVIER 
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ......................................................................
C    - FONCTION REALISEE:  CALCUL DES OPTIONS NON LINEAIRES
C                          COQUE 1D
C                          OPTION : 'RIGI_MECA_TANG ',
C                                   'FULL_MECA      ','RAPH_MECA      '
C                          ELEMENT: MECXSE3,METCSE3,METDSE3
C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................
      IMPLICIT NONE
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR,R8VIDE
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      INTEGER ICOMPO,NBCOU,NPGE,ICONTM,IDEPLM,IVECTU,ICOU,INTE,ICONTP,
     &        KPKI,K1,K2,KOMPT,IVARIM,IVARIP,IINSTM,
     &        IINSTP,LGPG,IDEPLP,ICARCR,NBVARI,JCRET,CODRET
      REAL*8 CISAIL,ZIC,COEF,RHOS,RHOT,EPSX3,GSX3,SGMSX3
      REAL*8 TPC,TMC
      REAL*8 ZMIN,HIC,EPAIS,DEPSX3
      INTEGER NBRES,ITAB(8),JNBSPI,ITABM(8),ITABP(8)
      CHARACTER*16 OPTION,NOMTE
      PARAMETER (NBRES=2)
      CHARACTER*8 NOMRES(NBRES),TYPMOD(2),NOMPAR,NOMPU(NBRES),ELREFE
      CHARACTER*2 VALRET(NBRES)
      REAL*8 VALRES(NBRES),VALPU(NBRES),VALPAR,TEMPM,TEMPP,TREF
      REAL*8 DFDX(3),ZERO,UN,DEUX
      REAL*8 TEST,TEST2,EPS,NU,H,COSA,SINA,COUR,R
      REAL*8 JACP,KAPPA,CORREC
      REAL*8 EPS2D(4),DEPS2D(4),SIGTDI(5),SIGMTD(5)
      REAL*8 X3,T,TINF,TSUP
      REAL*8 DTILD(5,5),DTILDI(5,5),DSIDEP(6,6)
      REAL*8 RTANGI(9,9),RTANGE(9,9),SIGM2D(4),SIGP2D(4)
      REAL*8 XI,XS,XM,SIGMA,ANGMAS(3),R8NNEM
      REAL*8 LC
      INTEGER NNO,NNOS,JGANO,NDIM,KP,NPG,I,J,K,IMATUU,ICACO,
     &        NDIMV,IVARIX,MOD
      INTEGER IPOIDS,IVF,IDFDK,IGEOM,IMATE,ITER
      INTEGER NBPAR,IER,COD,IRET,KSP,IRET1,IRET2,IRET3
      LOGICAL VECTEU,MATRIC,TESTL1,TESTL2,TEMPNO

      PARAMETER (NPGE=3)

      DATA ZERO,UN,DEUX/0.D0,1.D0,2.D0/

      EPS = 1.D-3
      CODRET = 0
      
C     ANGLE DU MOT_CLEF MASSIF (AFFE_CARA_ELEM)      
C     INITIALISE A R8NNEM (ON NE S'EN SERT PAS)      
      ANGMAS(1) = R8NNEM()                           
      ANGMAS(2) = R8NNEM()                           
      ANGMAS(3) = R8NNEM()                           
            

      VECTEU = ((OPTION.EQ.'FULL_MECA') .OR. (OPTION.EQ.'RAPH_MECA'))
      MATRIC = ((OPTION.EQ.'FULL_MECA') .OR.
     &         (OPTION.EQ.'RIGI_MECA_TANG'))

      CALL ELREF1(ELREFE)
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDK,JGANO)

C       TYPMOD(1) = 'C_PLAN  '
C       TYPMOD(2) = '        '

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PCACOQU','L',ICACO)
      H = ZR(ICACO)
      KAPPA = ZR(ICACO+1)
      CORREC = ZR(ICACO+2)

C---- COTE MINIMALE SUR L'EPAISSEUR
      ZMIN = -H/2.D0

      CALL JEVECH('PMATERC','L',IMATE)
      NOMRES(1) = 'E'
      NOMRES(2) = 'NU'

      CALL JEVECH('PVARIMR','L',IVARIM)
      CALL JEVECH('PINSTMR','L',IINSTM)
      CALL JEVECH('PDEPLMR','L',IDEPLM)
      CALL JEVECH('PCONTMR','L',ICONTM)
      CALL JEVECH('PINSTPR','L',IINSTP)
      CALL JEVECH('PDEPLPR','L',IDEPLP)
      CALL JEVECH('PCOMPOR','L',ICOMPO)
      CALL JEVECH('PCARCRI','L',ICARCR)

      IF (ZK16(ICOMPO+3).EQ.'COMP_ELAS') THEN
        CALL U2MESS('F','ELEMENTS2_90')
      END IF

      IF (ZK16(ICOMPO+2) (6:10).EQ.'_REAC') THEN
        CALL U2MESS('A','ELEMENTS3_54')
      END IF

C--- LECTURE DU NBRE DE VAR. INTERNES, DE COUCHES ET LONG. MAX DU
C--- POINT D'INTEGRATION
      READ (ZK16(ICOMPO-1+2),'(I16)') NBVARI
      CALL TECACH('OON','PVARIMR',7,ITAB,IRET)
C      LGPG = MAX(ITAB(6),1)*ITAB(7) resultats faux sur Bull avec ifort
      IF (ITAB(6).LE.1) THEN
         LGPG=ITAB(7)
      ELSE
         LGPG = ITAB(6)*ITAB(7)
      ENDIF
      CALL JEVECH('PNBSP_I','L',JNBSPI)
      NBCOU = ZI(JNBSPI-1+1)
C---- MESSAGES LIMITATION NBRE DE COUCHES
      IF (NBCOU.LE.0) CALL U2MESS('F','ELEMENTS_12')
      IF (NBCOU.GT.10) CALL U2MESS('F','ELEMENTS3_55')
C---- EPAISSEUR DE CHAQUE COUCHE
      HIC = H/NBCOU
      IF (VECTEU) THEN
        CALL JEVECH('PVECTUR','E',IVECTU)
        CALL JEVECH('PCONTPR','E',ICONTP)
        CALL JEVECH('PVARIPR','E',IVARIP)
      END IF
      IF (VECTEU) THEN
        NDIMV = NPG*NPGE*NBCOU*NBVARI
        CALL JEVECH('PVARIMP','L',IVARIX)
        CALL DCOPY(NDIMV,ZR(IVARIX),1,ZR(IVARIP),1)
      END IF
      IF (MATRIC) THEN
        CALL JEVECH('PMATUUR','E',IMATUU)
C        IVARIP=IVARIM
      END IF

      CALL R8INIR(81,0.D0,RTANGE,1)
      KPKI = 0
C-- DEBUT DE BOUCLE D'INTEGRATION SUR LA SURFACE NEUTRE
      DO 140 KP = 1,NPG
        K = (KP-1)*NNO
        CALL DFDM1D(NNO,ZR(IPOIDS+KP-1),ZR(IDFDK+K),ZR(IGEOM),DFDX,COUR,
     &              JACP,COSA,SINA)
        R = ZERO

        CALL R8INIR(5,0.D0,SIGMTD,1)
        CALL R8INIR(25,0.D0,DTILD,1)

C-- BOUCLE SUR LES POINTS D'INTEGRATION SUR LA SURFACE

        DO 30 I = 1,NNO
          R = R + ZR(IGEOM+2*I-2)*ZR(IVF+K+I-1)
   30   CONTINUE

C===============================================================
C     -- RECUPERATION DE LA TEMPERATURE POUR LE MATERIAU:
C     -- SI LA TEMPERATURE EST CONNUE AUX NOEUDS :
        CALL MOYTPG('RIGI',KP,3,'-',TEMPM,IRET)
        NBPAR = 1
        NOMPAR = 'TEMP'
        CALL RCVALB('RIGI',KP,1,'-',ZI(IMATE),' ','ELAS',NBPAR,NOMPAR,
     &              TEMPM,2,NOMRES,VALRES,VALRET,'FM')

        NU = VALRES(2)
        CISAIL = VALRES(1)/ (UN+NU)

        IF (NOMTE.EQ.'MECXSE3') JACP = JACP*R

        TEST = ABS(H*COUR/DEUX)
        IF (TEST.GE.UN) CORREC = ZERO
        TEST2 = ABS(H*COSA/ (DEUX*R))
        IF (TEST2.GE.UN) CORREC = ZERO

        TESTL1 = (TEST.LE.EPS .OR. CORREC.EQ.ZERO)
        TESTL2 = (TEST2.LE.EPS .OR. CORREC.EQ.ZERO .OR.
     &           ABS(COSA).LE.EPS .OR. ABS(COUR*R).LE.EPS .OR.
     &           ABS(COSA-COUR*R).LE.EPS)

C-- DEBUT DE BOUCLE D'INTEGRATION DANS L'EPAISSEUR

        DO 110 ICOU = 1,NBCOU
          DO 100 INTE = 1,NPGE
            IF (INTE.EQ.1) THEN
              ZIC = ZMIN + (ICOU-1)*HIC
              COEF = 1.D0/3.D0
            ELSE IF (INTE.EQ.2) THEN
              ZIC = ZMIN + HIC/2.D0 + (ICOU-1)*HIC
              COEF = 4.D0/3.D0
            ELSE
              ZIC = ZMIN + HIC + (ICOU-1)*HIC
              COEF = 1.D0/3.D0
            END IF

            X3 = ZIC

            IF (TESTL1) THEN
              RHOS = 1.D0
            ELSE
              RHOS = 1.D0 + X3*COUR
            END IF
            IF (TESTL2) THEN
              RHOT = 1.D0
            ELSE
              RHOT = 1.D0 + X3*COSA/R
            END IF

C           CALCULS DES COMPOSANTES DE DEFORMATIONS TRIDIMENSIONNELLES :
C           EPSSS, EPSTT, EPSSX3 (EN FONCTION DES DEFORMATIONS
C           GENERALISEES :ESS,KSS,ETT,KTT,GS)
C           DE L'INSTANT PRECEDANT ET DES DEFORMATIONS INCREMENTALES
C           DE L'INSTANT PRESENT

            CALL DEFGEN(TESTL1,TESTL2,NNO,R,X3,SINA,COSA,COUR,ZR(IVF+K),
     &                  DFDX,ZR(IDEPLM),EPS2D,EPSX3)
            CALL DEFGEN(TESTL1,TESTL2,NNO,R,X3,SINA,COSA,COUR,ZR(IVF+K),
     &                  DFDX,ZR(IDEPLP),DEPS2D,DEPSX3)

C           COQUE_D_PLAN
            IF (NOMTE.EQ.'METDSE3') THEN
              EPS2D(2) = 0.D0
              DEPS2D(2) = 0.D0
              MOD=2
C           COQUE_C_PLAN
            ELSE IF (NOMTE.EQ.'METCSE3') THEN
              EPS2D(2) = 0.D0
              DEPS2D(2) = 0.D0
              MOD=-1
C           COQUE_AXIS
            ELSEIF (NOMTE.EQ.'MECXSE3') THEN
              MOD=1
            END IF
            
C           CONSTRUCTION DE LA DEFORMATION GSX3 
C           ET DE LA CONTRAINTE SGMSX3
            GSX3 = 2.D0* (EPSX3+DEPSX3)
            SGMSX3 = CISAIL*KAPPA*GSX3/2.D0

C           CALCUL DU NUMERO DU POINT D'INTEGRATION COURANT
            KPKI = KPKI + 1
            K1 = 4* (KPKI-1)
            K2 = LGPG* (KP-1) + (NPGE* (ICOU-1)+INTE-1)*NBVARI
            KSP=(ICOU-1)*NPGE + INTE
            
            DO 55 I=1,4
                SIGM2D(I)=ZR(ICONTM+K1+I-1)
   55       CONTINUE
            
C  APPEL AU COMPORTEMENT
            CALL COMCQ1('RIGI',KP,KSP,MOD,ZI(IMATE),ZK16(ICOMPO),
     &                  ZR(ICARCR),ZR(IINSTM),ZR(IINSTP),
     &                  EPS2D,DEPS2D,TEMPM,TEMPP,
     &                  SIGM2D,ZR(IVARIM+K2),OPTION,ANGMAS,
     &                  SIGP2D,ZR(IVARIP+K2),DSIDEP,COD)
     
            IF (VECTEU) THEN
               DO 56 I=1,4
                   ZR(ICONTP+K1+I-1)=SIGP2D(I)
   56          CONTINUE
            ENDIF
            
C           COD=1 : ECHEC INTEGRATION LOI DE COMPORTEMENT
C           COD=3 : C_PLAN DEBORST SIGZZ NON NUL
            IF (COD.NE.0) THEN
              IF (CODRET.NE.1) THEN
                CODRET = COD
              END IF
            END IF


            IF (MATRIC) THEN
C-- CALCULS DE LA MATRICE TANGENTE : BOUCLE SUR L'EPAISSEUR
C-- CONSTRUCTION DE LA MATRICE DTD (DTILD)
              CALL MATDTD(NOMTE,TESTL1,TESTL2,DSIDEP,CISAIL,X3,COUR,R,
     &                    COSA,KAPPA,DTILDI)
              DO 80 I = 1,5
                DO 70 J = 1,5
                  DTILD(I,J) = DTILD(I,J) + DTILDI(I,J)*0.5D0*HIC*COEF
   70           CONTINUE
   80         CONTINUE
            END IF

            IF (VECTEU) THEN
C-- CALCULS DES EFFORTS INTERIEURS : BOUCLE SUR L'EPAISSEUR
              IF (NOMTE.EQ.'MECXSE3') THEN
                SIGTDI(1) = ZR(ICONTP-1+K1+1)/RHOS
                SIGTDI(2) = X3*ZR(ICONTP-1+K1+1)/RHOS
                SIGTDI(3) = ZR(ICONTP-1+K1+2)/RHOT
                SIGTDI(4) = X3*ZR(ICONTP-1+K1+2)/RHOT
                SIGTDI(5) = SGMSX3/RHOS
              ELSE
                SIGTDI(1) = ZR(ICONTP-1+K1+1)/RHOS
                SIGTDI(2) = X3*ZR(ICONTP-1+K1+1)/RHOS
                SIGTDI(3) = SGMSX3/RHOS
                SIGTDI(4) = 0.D0
                SIGTDI(5) = 0.D0
              END IF

              DO 90 I = 1,5
                SIGMTD(I) = SIGMTD(I) + SIGTDI(I)*0.5D0*HIC*COEF
   90         CONTINUE
            END IF
C-- FIN DE BOUCLE SUR LES POINTS D'INTEGRATION DANS L'EPAISSEUR
  100     CONTINUE
  110   CONTINUE

        IF (VECTEU) THEN
C-- CALCUL DES EFFORTS INTERIEURS
          CALL EFFI(NOMTE,SIGMTD,ZR(IVF+K),DFDX,JACP,SINA,COSA,R,
     &              ZR(IVECTU))
        END IF
        IF (MATRIC) THEN
C-- CONSTRUCTION DE LA MATRICE TANGENTE
          CALL MATTGE(NOMTE,DTILD,SINA,COSA,R,JACP,ZR(IVF+K),DFDX,
     &                RTANGI)
          DO 130 I = 1,9
            DO 120 J = 1,9
              RTANGE(I,J) = RTANGE(I,J) + RTANGI(I,J)
  120       CONTINUE
  130     CONTINUE
        END IF
C-- FIN DE BOUCLE SUR LES POINTS D'INTEGRATION DE LA SURFACE NEUTRE
  140 CONTINUE
      IF (MATRIC) THEN
C-- STOCKAGE DE LA MATRICE TANGENTE
        KOMPT = 0
        DO 160 J = 1,9
          DO 150 I = 1,J
            KOMPT = KOMPT + 1
            ZR(IMATUU-1+KOMPT) = RTANGE(I,J)
  150     CONTINUE
  160   CONTINUE
      END IF
      IF (OPTION(1:9).EQ.'FULL_MECA' .OR.
     &    OPTION(1:9).EQ.'RAPH_MECA') THEN
        CALL JEVECH('PCODRET','E',JCRET)
        ZI(JCRET) = CODRET
      END IF
      END
