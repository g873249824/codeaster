      SUBROUTINE NMCB1D(E0,Y01,Y02,A1,A2,B1,B2,BETA1,BETA2,SIGF1,
     &            SIG0,VAR0,EPSM,DEPS,EF,SIGF,VARF,CRIT)
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 05/09/2011   AUTEUR GREFFET N.GREFFET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C  IN :
C       E0 : MODULE D'YOUNG INITIAL
C       Y01,Y02,A1,A2,B1,B2,BETA1,BETA2,SIGF1 = PARAMETRES DE LA LOI

C       TR  : TEMPERATURE DE REFERENCE
C       TP  : TEMPERATURE A L'INSTANT PLUS
C       SIG0 : CONTRAINTE A L'INSTANT MOINS
C       VAR0 : VARIABLES INTERNES A L'INSTANT MOINS
C       EPSM : DEFORMATION TOTALE A L'INSTANT MOINS
C       DEPS : INCREMENT DE DEFORMATION TOTALE
C              - INCREMENT DEFORMATION THERMIQUE
C
C  OUT :
C       EF : MODULE D'YOUNG TANGENT (ACTUELLEMENT (SIGP - SIGM)/DEPS )
C       SIGF : CONTRAINTE A L'INSTANT PLUS
C       VARF : VARIABLES INTERNES A L'INSTANT PLUS

      IMPLICIT NONE
      REAL*8 E0,EF,EPSM,DEPS,SIG0,VAR0(5),SIGF,VARF(5)
      REAL*8 ZERO,UN
      PARAMETER (ZERO=0.0D+0,UN=1.D+0)
      REAL*8 ESEC,ETAN,D2,D1,DD1,DD2,EPS,YLIM1,YLIM2
      REAL*8 Y1,Y2,SIG
      REAL*8 A1,A2,BETA1,BETA2,Y01,Y02,B1,B2,SIGF1
      REAL*8 X1,X2,EPSF,Z1,Z2,CRIT(*)
      INTEGER CAS,CCAS
C

      Y1 = YLIM1 - UN
      Y2 = YLIM2 - UN

C ----VARIABLES HISTORIQUES
      DD1 = VAR0(1)
      DD2 = VAR0(2)
      YLIM1 = VAR0(3)
      YLIM2 = VAR0(4)
C ---- ON PASSE EN MECANIQUE
C     (ON ENLEVE LA PARTIE THERMIQUE EN CONSIDERANT ALPHA CONSTANTE)
C  ON A : EPSM = EPSM - ALPHA (TM - TR)
C     ET  DEPS = DEPS - ALPHA (TP - TR)
C     ET EPS = EPSM + DEPS
      EPS = EPSM + DEPS


C --- ON PREND LE MAX POUR EVITER QUE Z1 ET Z2 SOIT "NAN"
C --- AU DEBUT DU PAS DE TEMPS, ON SOUSTRAIT 1 POUR NE PAS QUE
C --- L'ENDOMMAGEMENT N'EVOLUE PLUS A CAUSE DU TEST DE DEPASSEMENT DE
C --- SEUIL
      Z1=MAX(YLIM1-1,Y01)
      Z2=MAX(YLIM2-1,Y02)
C     ON DUPLIQUE L'ENDOMMAGEMENT :
C         DD -> VALEURS DU PAS DE TEMPS PRECEDENT
C         D  -> VALEURS MISES A JOUR
      D1=DD1
      D2=DD2
C --- BOUCLE TANT QUE L'ON CHANGE DE CAS
   10 CONTINUE
      X1 = (BETA1*D1/ (UN-D1)+BETA2*D2/ (UN-D2))/E0
      X2 = (BETA2*D2-SIGF1)/ (UN-D2)/E0
C     CAS 1 : TRACTION DOC R7.01.07 ยง3.2.1
      IF (X1.LE.EPS) THEN
        EPSF = EPS - BETA2*D2/ (UN-D2)/E0
        CALL NMCB13(EPSF,SIG,ESEC,E0,DD1,D1,BETA1,A1,B1,Y1,Y01,Z1,CRIT)
        CAS = 1
C     CAS 3 : COMPRESSION AU DELA DE LA FERMETURE DES FISSURES (3.2.3)
      ELSE IF (EPS.LE.X2) THEN
        CALL NMCB13(EPS,SIG,ESEC,E0,DD2,D2,BETA2,A2,B2,Y2,Y02,Z2,CRIT)
        CAS = 3
C     CAS 2 : FAIBLE COMPRESSION     ยง3.2.3
      ELSE
        CALL NMCB2(EPS,BETA1,E0,D1,SIGF1,BETA2,D2,SIG,ESEC)
        CAS = 2
      END IF
      X1 = (BETA1*D1/ (UN-D1)+BETA2*D2/ (UN-D2))/E0
      X2 = (BETA2*D2-SIGF1)/ (UN-D2)/E0
      IF (X1.LE.EPS) THEN
        CCAS = 1
      ELSE IF (EPS.LE.X2) THEN
        CCAS = 3
      ELSE
        CCAS = 2
      END IF
      IF (CCAS.NE.CAS) GO TO 10
C --- FIN BOUCLE

C --- MODULE TANGENT
C        IF(CAS.EQ.1)THEN
C          CALL NMCBMT(CAS,E0,D1,D2,A1,B1,Y1,Y01,BETA1,BETA2,
C     +            SIGF1,EPS,ETAN)
C        ELSE
C          CALL NMCBMT(CAS,E0,D1,D2,A2,B2,Y2,Y02,BETA1,BETA2,
C     +            SIGF1,EPS,ETAN)
C        ENDIF
C --- MODULE TANGENT NUMERIQUE + 10% D'INITIAL
       IF (DEPS.NE.ZERO) THEN
         ETAN=(SIG-SIG0)/DEPS + 0.1D0 * E0
         IF(ETAN.GT.E0)ETAN=E0
       ELSE
         IF(VAR0(5).NE.ZERO)THEN
           ETAN=VAR0(5)
         ELSE
           ETAN=E0
         ENDIF
       ENDIF

C --- ACTUALISATION DES SEUILS
      YLIM1 = MAX(Y1,YLIM1)
      YLIM2 = MAX(Y2,YLIM2)
C --- STOCKAGE DES VARIABLES HISTORIQUES
      VARF(1) = D1
      VARF(2) = D2
      VARF(3) = YLIM1
      VARF(4) = YLIM2
      VARF(5) = ETAN
C --- CONTRAINTE
      SIGF = SIG
C --- MODULE
      EF = ETAN

      END
