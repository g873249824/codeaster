      SUBROUTINE MMMJAC(ALIAS ,JGEOM ,FF    ,DFF   ,LAXIS ,
     &                  NNE   ,NDIM  ,JACOBI)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 11/02/2013   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8  ALIAS
      INTEGER      JGEOM
      REAL*8       FF(9) ,DFF(2,9)
      LOGICAL      LAXIS
      INTEGER      NNE   ,NDIM
      REAL*8       JACOBI
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - UTILITAIRE)
C
C CALCUL DU JACOBIEN D'UN ELEMENT
C
C ----------------------------------------------------------------------
C
C
C IN  ALIAS  : NOM DE L'ELEMENT
C IN  JGEOM  : ADRESSE DES COORDONNEES INITIALES DES NOEUDS DE L'ELEMENT
C IN  FF     : FONCTIONS DE FORMES EN XI,YI
C IN  DFF    : DERIVEES PREMIERES DES FONCTIONS DE FORME EN XI YI
C IN  LAXIS  : SI PROBLEME AXISYMETRIQUE
C IN  NNE    : NOMBRE DE NOEUDS ESCLAVES
C IN  NDIM   : DIMENSION DU PROBLEME
C OUT JACOBI : VALEUR DU JACOBIEN
C
C
C ----------------------------------------------------------------------
C
      INTEGER INOE  ,IDIM
      REAL*8  GEOM(3,9)
      REAL*8  DXDK,DYDK,DZDK
      REAL*8  R
      REAL*8  COVA(3,3),METR(2,2)
C
C ----------------------------------------------------------------------
C
      DXDK = 0.D0
      DYDK = 0.D0
      DZDK = 0.D0
C
C --- COORDONNEES DES NOEUDS DANS LA CONFIGURATION DE REFERENCE
C
      DO 100 INOE = 1,NNE
        DO 110 IDIM = 1,NDIM
          GEOM(IDIM,INOE) = ZR(JGEOM-1+(INOE-1)*NDIM+IDIM)
 110    CONTINUE
 100  CONTINUE
C
C --- ELEMENTS DE TYPE SEGMENT
C --- DISTINCTION 2D/3D/AXIS
C
      IF (ALIAS(1:2).EQ.'SE') THEN
C       COMPOSANTES DU VECTEUR TANGENT
        DO 90 INOE = 1,NNE
          DXDK = DXDK + GEOM(1,INOE)*DFF(1,INOE)
          DYDK = DYDK + GEOM(2,INOE)*DFF(1,INOE)
          IF (NDIM.EQ.3) DZDK = DZDK + GEOM(3,INOE)*DFF(1,INOE)
90      CONTINUE
C       JACOBIEN 1D == DERIVEE DE L'ABSCISSE CURVILIGNE
        JACOBI = SQRT(DXDK**2+DYDK**2+DZDK**2)
C       TRAITEMENT AXISYMETRIE
        IF (LAXIS) THEN
          R = 0.D0
          DO 80 INOE=1,NNE
            R = R + GEOM(1,INOE)*FF(INOE)
80        CONTINUE
          IF (R.EQ.0.D0) THEN
            R=1.D-7
            CALL U2MESS('A','CONTACT2_14')
          ENDIF
          JACOBI = JACOBI*ABS(R)
        END IF
C
C --- AUTRES ELEMENTS : DIMENSION 3
C
      ELSE
        CALL ASSERT(NDIM.EQ.3)
        CALL SUBACO(NNE,DFF,GEOM,COVA)
        CALL SUMETR(COVA,METR,JACOBI)
      END IF

      END
