      SUBROUTINE CLCPLQ ( HT,ENROBG,TYPCMB,
     &                    PIVA,PIVB,CEQUI,
     &                    SIGACI,SIGBET,EFFRTS,
     &                    DNSITS,SIGMBE,EPSIBE,
     &                    IERR)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 11/10/2010   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C_____________________________________________________________________
C
C     CLCPLQ
C
C      ARMATURES DANS LES ELEMENTS DE PLAQUE
C
C      I HT      EPAISSEUR DE LA COQUE
C      I ENROBG  ENROBAGE
C      I TYPCMB  TYPE DE COMBINAISON
C                  0 = ELU, 1 = ELS
C      I PIVA    VALEUR DU PIVOT A
C      I PIVB    VALEUR DU PIVOT B
C      I CEQUI   COEFFICIENT D'EQUIVALENCE ACIER/BETON
C      I SIGACI  CONTRAINTE ADMISSIBLE DANS L'ACIER
C      I SIGBET  CONTRAINTE ADMISSIBLE DANS LE BETON
C      I EFFRTS  (DIM 8) VALEURS DES EFFORTS, MOMENTS, ...
C      O DNSITS  (DIM 5) DENSITES
C          1..4 : SURFACES D'ACIER LONGITUDINAL ,
C          5 TRANSVERSAL
C      O SIGMBE  VALEUR DE CONTRAINTE BETON (ELS)
C      O EPSIBE  VALEUR DE DEFORMATION BETON (ELU)
C      O IERR    CODE RETOUR (0 = OK)
C
C_____________________________________________________________________


        IMPLICIT NONE


        REAL*8 HT
        REAL*8 ENROBG
        INTEGER TYPCMB
        REAL*8 PIVA,PIVB,CEQUI
        REAL*8 SIGACI
        REAL*8 SIGBET
        REAL*8 EFFRTS(8)
        REAL*8 DNSITS(5)
        REAL*8 SIGMBE,EPSIBE
        INTEGER IERR
C
C     ! FACETTES POUR METHODE DE CAPRA ET MAURY
C
C       36 FACETTES
C       NOMBRE DE DIVISIONS ENTRE -PI/2 ET +PI/2
        REAL*8 FCTTAB(36,3)

C       EFFORT NORMAL DANS CETTE DIRECTION
        REAL*8  EFFN
C       MOMENT DE FLEXION DANS CETTE DIRECTION
        REAL*8  EFFM
C       DEFORMATION (ELU)
        REAL*8  EPSIL
C       DEFORMATION MAXIMUM (ELU)
        REAL*8  EPSIMX
C       CONTRAINTE (ELS) DU BETON
        REAL*8  SIGMA
C       CONTRAINTE MAXIMUM (ELS)
        REAL*8  SIGMMX
C       BRAS DE LEVIER
        REAL*8 LEVIER

C       SECTIONS DES ACIERS INFERIEURS SUIVANT LES 36 FCTTAB
        REAL*8 AI(36)
C       SECTIONS DES ACIERS SUPERIEURS SUIVANT LES 36 FCTTAB
        REAL*8 AS(36)

        INTEGER I

        IERR = 0

C       INITIALISATION DES FACETTES
        CALL TRGFCT ( FCTTAB )

        DO 5 I=1,5
          DNSITS(I) = -1D0
    5   CONTINUE

        SIGMMX = 0D0
        EPSIMX = 0D0
C        ! BOUCLE SUR LES FACETTES
C        ! DETERMINATION DU FERRAILLAGE POUR CHACUNE DES FACETTES
        DO 10 I = 1, 36
          EFFN = FCTTAB(I,1) * EFFRTS(1)
     &         + FCTTAB(I,2) * EFFRTS(2)
     &         + FCTTAB(I,3) * EFFRTS(3)
          EFFM = FCTTAB(I,1) * EFFRTS(4)
     &         + FCTTAB(I,2) * EFFRTS(5)
     &         + FCTTAB(I,3) * EFFRTS(6)

          IF( TYPCMB .EQ. 0 ) THEN
            CALL CLCELU (PIVA, PIVB,
     &                   EFFM,EFFN,HT,ENROBG,
     &                   SIGACI,SIGBET,AI(I),AS(I),EPSIL,IERR)
            IF (IERR .NE. 0) GOTO 9999
            IF( EPSIL .GT. EPSIMX ) EPSIMX = EPSIL
          ELSE
            CALL CLCELS (CEQUI,
     &                   EFFM,EFFN,HT,ENROBG,
     &                   SIGACI,SIGBET,AI(I),AS(I),SIGMA,IERR)
            IF (IERR .NE. 0) GOTO 9999
            IF( SIGMA .GT. SIGMMX ) SIGMMX = SIGMA
          END IF
   10   CONTINUE

        IF (TYPCMB .EQ. 0) THEN
          EPSIBE = EPSIMX
         ELSE
          SIGMBE = SIGMMX
        ENDIF

C       OPTIMISATION DES FERRAILLAGES
        CALL CLCOPT ( FCTTAB, AI, DNSITS(1), DNSITS(2) )
        CALL CLCOPT ( FCTTAB, AS, DNSITS(3), DNSITS(4) )
C
C CALCUL DU FERRAILLAGE TRANSVERSAL
C
        LEVIER = HT - 2D0 * ENROBG
        IF (LEVIER .LE. 0D0) THEN
          DO 20 I = 1,5
            DNSITS(I) = -1D0
   20     CONTINUE
          IERR = 1000
          GOTO 9999
        ENDIF
        SIGMA = SQRT(EFFRTS(7)*EFFRTS(7)+EFFRTS(8)*EFFRTS(8))/LEVIER
        DNSITS(5) = SIGMA / SIGACI

9999  CONTINUE
      END
