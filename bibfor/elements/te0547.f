      SUBROUTINE TE0547(OPTION,NOMTE)
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'

      CHARACTER*16 OPTION,NOMTE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 15/01/2013   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ======================================================================
C RESPONSABLE MASSIN P.MASSIN
C
C ......................................................................
C
C       CALCUL DES COEFFICIENTS A0 ET A1 POUR LE PILOTAGE PAR
C      PREDICTION ELASTIQUE POUR LES ELEMENTS X_FEM EN CONTACT
C               SOUMIS A LA LOI COHESIVE CZM_EXP_REG
C
C  OPTION : 'PILO_PRED_XLAS'
C
C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C
C......................................................................
C


      INTEGER      I,J,IBID,NFE,IB,IRET
      INTEGER      NDIM,DDLC,DDLS,NDDL
      INTEGER      NNO,NNOS,NNOM,NNOF
      INTEGER      IPOIDS,IVF,IDFDE,JGANO,DDLM
      INTEGER      IPOIDF,IVFF,IDFDEF,IADZI,IAZK24
      INTEGER      NPG,NPGF,NPTF,INTEG,SINGU
      INTEGER      NINTER,NFACE,CFACE(5,3)
      INTEGER      LACT(8),NLACT,NCOMPV
      INTEGER      JDONCO,JLSN,JLST,IGEOM,JPTINT
      INTEGER      JAINT,JCFACE,JLONCH,JBASEC,ICOPIL,ICTAU
      INTEGER      IDEPL0,IDEPL1,IDEPLM,IDDEPL,JCOHES,IMATE
      INTEGER      NFH,NFISS,CONTAC,JTAB(2)
      CHARACTER*8  ELREF,TYPMA,FPG,ELC,ELREFC
      CHARACTER*16 ENR
      REAL*8       RELA
      
      REAL*8       COHES,COPILO(5),DTAU,FFC(8),FFP(27)
      REAL*8       LAMB(3),MAT3BD(3,3),MAT6BD(6,6)
      REAL*8       JAC,MUD(3),MUP(3),R8VIDE,R3BD(3),MA3BD(3,3)
      REAL*8       ND(3),R8BID,R6BID(6),RB,R3BID(3)
      REAL*8       RR,RBID,SUD(3),SUD2D(2),SUDD(3),SUP(3)
      REAL*8       SUP2D(2),SUPP(3),TAU1(3),TAU2(3),VIM(9)
      INTEGER      IFA,IPGF,ISSPG,MATE,NNOL,NVEC,PLA(27)
      CHARACTER*8  JOB
      LOGICAL      LBID
C......................................................................
      CALL JEMARQ()
C
C-----------------------------------------------------------------------
C     INITIALISATIONS
C-----------------------------------------------------------------------
C
      CALL ELREF1(ELREF)
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
C
C     INITIALISATION DES DIMENSIONS DES DDLS X-FEM
C
      CALL XTEINI(NOMTE,NFH,NFE,SINGU,DDLC,NNOM,DDLS,NDDL,
     &            DDLM,NFISS,CONTAC)
      CALL TECAEL(IADZI,IAZK24)
      TYPMA=ZK24(IAZK24-1+3+ZI(IADZI-1+2)+3)
C
C --- ROUTINE SPECIFIQUE P2P1
C.
      CALL ELELIN(CONTAC,ELREF,ELREFC,IBID,IBID)
C
C-----------------------------------------------------------------------
C     RECUPERATION DES ENTREES / SORTIE
C-----------------------------------------------------------------------
C
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PDEPLMR','L',IDEPLM)
      CALL JEVECH('PDDEPLR','L',IDDEPL)
      CALL JEVECH('PDEPL0R','L',IDEPL0)
      CALL JEVECH('PDEPL1R','L',IDEPL1)
      CALL JEVECH('PDONCO','L',JDONCO)
      CALL JEVECH('PLSN','L',JLSN)
      CALL JEVECH('PLST','L',JLST)
      CALL JEVECH('PPINTER','L',JPTINT)
      CALL JEVECH('PAINTER','L',JAINT)
      CALL JEVECH('PCFACE','L',JCFACE)
      CALL JEVECH('PLONCHA','L',JLONCH)
      CALL JEVECH('PBASECO','L',JBASEC)
      CALL JEVECH('PCDTAU' ,'L',ICTAU)
      DTAU = ZR(ICTAU)

      CALL TEATTR(NOMTE,'S','XFEM',ENR,IBID)
      IF (ENR.EQ.'XHC') THEN
        RELA =  ZR(JDONCO-1+10)
      ELSE
        RELA=0.0D0
      ENDIF

      IF(RELA.NE.0.D0) THEN
        CALL JEVECH('PMATERC','L',IMATE)
        CALL JEVECH('PCOHES' ,'L',JCOHES)
          CALL TECACH('OOO','PCOHES','L',2,JTAB,IRET)
          NCOMPV = JTAB(2)
      ENDIF
      MATE = ZI(IMATE)
C
C RECUPERATIONS DES DONNEES SUR LA TOPOLOGIE DES FACETTES
C
      NINTER=ZI(JLONCH)
      NFACE=ZI(JLONCH-1+2)
      NPTF=ZI(JLONCH-1+3)
C
      DO 11 I=1,NFACE
        DO 12 J=1,NPTF
          CFACE(I,J)=ZI(JCFACE-1+NPTF*(I-1)+J)
 12     CONTINUE
 11   CONTINUE
C
C SCHEMA D'INTEGRATION NUMERIQUE ET ELEMENT DE REFERENCE DE CONTACT
C DISCUSSION VOIR BOOK IV 18/10/2004 ET BOOK VI 06/07/2005
C
      INTEG = NINT(ZR(JDONCO-1+4))
      CALL XMINTE(NDIM,INTEG,FPG)
      IF (NDIM .EQ. 3) THEN
        ELC='TR3'
      ELSEIF (NDIM.EQ.2) THEN
        IF(CONTAC.LE.2) THEN
          ELC='SE2'
        ELSE
          ELC='SE3'
        ENDIF
      ENDIF

C RECUPERATION DU NOMBRE DE POINTS DE GAUSS NPGF
      CALL ELREF4(ELC,FPG,IBID,NNOF,IBID,NPGF,IPOIDF,IVFF,IDFDEF,IBID)

C
C LISTE DES LAMBDAS ACTIFS
C
      CALL XMULCO(CONTAC,DDLC,DDLM,JAINT,1,
     &            IBID,IB,LACT,.FALSE.,LBID,NDIM,NFE,
     &            NFH,1,NINTER,NLACT,NNO,
     &            NNOL,NNOM,NNOS,PLA,TYPMA)
C
C PARAMETRES EN SORTIE
C
      CALL JEVECH('PCOPILO','E',ICOPIL)
C
C BOUCLE SUR LE NOMBRE DE FACETTES DE CONTACT
C
      DO 100 IFA=1,NFACE
C
         DO 110 IPGF=1,NPGF 
C         INDICE DE CE POINT DE GAUSS DANS INDCO
          ISSPG = NPGF*(IFA-1)+IPGF
          COHES = ZR(JCOHES+NCOMPV*(ISSPG-1))
          CALL XMPREP(CFACE ,CONTAC,ELREF ,ELREFC,ELC   ,
     &                FFC   ,FFP   ,FPG   ,JAINT ,JBASEC,
     &                JPTINT,IFA   ,IGEOM ,IPGF  ,JAC   ,
     &                JLST  ,LACT  ,ND    ,NDIM  ,NINTER,
     &                NLACT ,NNO   ,NNOS  ,NPTF  ,IBID  ,
     &                RR    ,SINGU ,TAU1  ,TAU2)
C
C           INITIALISATION
C
            CALL VECINI(2,0.D0,SUP2D)
            CALL VECINI(2,0.D0,SUD2D)
            CALL VECINI(3,0.D0,MUP)
            CALL VECINI(3,0.D0,MUD)
            CALL VECINI(9,0.D0,VIM)
            CALL VECINI(5,0.D0,COPILO)
C
C CODE ERREUR A UNDEF. UNE VALEUR INDIQUE UN PLANTAGE
C
            COPILO(5) = R8VIDE()
C            
C           SAUT PILOTE AU POINT DE GAUSS : SU(ETA) = SUPP + ETA * SUDD
C           COMPOSANTE PILOTEE DU SAUT SUDD
C           SAUT A T- SAUTM
C           COMPOSANTE FIXE DU SAUT A ITERATION N+1 SUPP
C
            NVEC = 3
            CALL XMMSA4(NDIM,NNO,NNOS,FFP,NDDL,NVEC,
     &                 ZR(IDEPLM),ZR(IDDEPL),ZR(IDEPL0),NFH,
     &                 SINGU,RR,DDLS,DDLM,SUPP)
            NVEC = 1
            CALL XMMSA4(NDIM,NNO,NNOS,FFP,NDDL,NVEC,
     &                 ZR(IDEPL1),RB,RBID,NFH,
     &                 SINGU,RR,DDLS,DDLM,SUDD)
C
            IF(RELA.EQ.1.D0.OR.RELA.EQ.2.D0) THEN
               JOB='SAUT_LOC'
               CALL XMMSA2(NDIM ,IPGF  ,MATE  ,SUDD ,ND    ,
     &                     TAU1 ,TAU2  ,RB    ,JOB  ,R8BID ,
     &                     RBID ,MAT6BD,R6BID ,MAT3BD,R3BID,
     &                     R3BD,MA3BD,SUD)
     
               CALL XMMSA2(NDIM ,IPGF  ,MATE  ,SUPP ,ND    ,
     &                     TAU1 ,TAU2  ,RB    ,JOB  ,R8BID ,
     &                     RBID ,MAT6BD,R6BID ,MAT3BD,R3BID,
     &                     R3BD,MA3BD,SUP)
C           APPEL DU PILOTAGE PRED_ELAS SPECIFIQUE 
C           A LA LOI DE COMPORTEMENT                         
               IF (NDIM.EQ.2)     THEN
                  SUP2D(1)=SUP(1)
                  SUP2D(2)=SUP(2)
                  SUD2D(1)=SUD(1)
                  SUD2D(2)=SUD(2)
                  CALL PIPEBA(NDIM,MATE,SUP2D,SUD2D,COHES,
     &                  DTAU,COPILO)
               ELSEIF (NDIM.EQ.3) THEN
                  CALL PIPEBA(NDIM,MATE,SUP,SUD,COHES,
     &                  DTAU,COPILO)
               ENDIF
            ELSE IF(RELA.EQ.3.D0.OR.RELA.EQ.4.D0) THEN
C
C ON RECUPERE LAMBDA FIXE PUIS PILOTE
C
               VIM(4) = COHES
               NVEC = 3
               CALL XXLAG3(FFC  ,IDEPLM,IDDEPL,IDEPL0,LACT,
     &                     NDIM ,NNOL  ,PLA   ,MUP   ,NVEC)
               NVEC = 1
               CALL XXLAG3(FFC  ,IDEPL1,IBID  ,IB   ,LACT  ,
     &                     NDIM ,NNOL  ,PLA   ,MUD  ,NVEC)
C
C ON RECUPERE [U] FIXE PUIS PILOTE
C
               JOB='SAUT_LOC'
               CALL XMMSA5(NDIM ,IPGF  ,MATE ,SUDD ,R3BID ,
     &                     ND   ,TAU1 ,TAU2  ,RB   ,JOB  ,
     &                     RELA ,R8BID,MAT6BD,R6BID ,MAT3BD,
     &                     SUD  ,RBID)
               CALL XMMSA5(NDIM ,IPGF  ,MATE ,SUPP ,R3BID ,
     &                     ND   ,TAU1 ,TAU2  ,RB    ,JOB  ,
     &                     RELA ,R8BID,MAT6BD,R6BID ,MAT3BD,
     &                     SUP  ,RBID)
C
C APPEL DU PILOTAGE PRED_ELAS SPECIFIQUE LOI DE COMPORTEMENT
C
               IF(RELA.EQ.3.D0) THEN
                  CALL PIPETC(MATE,SUP,SUD,MUP,MUD,
     &                        VIM,DTAU,COPILO)
               ELSE IF(RELA.EQ.4.D0) THEN
                  CALL PIPEOU(MATE,SUP,SUD,MUP,MUD,
     &                        VIM,DTAU,COPILO)

               ENDIF
            ENDIF
C
            DO 120 I=1,5
               ZR(ICOPIL-1+5*(ISSPG-1)+I) = COPILO(I)
120         CONTINUE
C
110      CONTINUE
100   CONTINUE
C
      CALL JEDEMA()
      END
