      SUBROUTINE TE0060(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 21/01/2004   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C----------------------------------------------------------------------
C     BUT: CALCUL DES VECTEURS ELEMENTAIRES EN THERMIQUE
C          CORRESPONDANT AU TERME D'ECHANGE (FONCTION)
C          SUR DES FACES D'ELEMENTS ISOPARAMETRIQUES 3D
C
C          OPTION : 'CHAR_THER_TEXT_F/R'
C                   'CHAR_THER_RAYO_F/R'
C                   'CHAR_SENS_TEXT_F'
C                   'CHAR_SENS_RAYO_F'
C
C     ENTREES  ---> OPTION : OPTION DE CALCUL
C              ---> NOMTE  : NOM DU TYPE ELEMENT
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C       18/01/02 (OB): MODIFICATIONS POUR INSERER LES ARGUMENTS OPTION
C       NELS PERMETTANT D'UTILISER CETTE ROUTINE POUR CALCULER LA
C       SENSIBILITE PAR RAPPORT A H.
C       + MODIFS FORMELLES: IMPLICIT NONE, IDENTATION...
C       08/03/02 (OB): CORRECTION BUG EN STATIONNAIRE ET SENSIBILITE
C       11/03/02 (OB): INTRO. SENSIBILITE PAR RAPPORT AU RAYONNEMENT
C       + MODIFS FORMELLES: CALL FOINTE....
C----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE

      CHARACTER*8    ELREFE,NOMPAR(4)
      CHARACTER*16   NOMTE,OPTION
      CHARACTER*24   CHVAL,CHCTE
      REAL*8         NX,NY,NZ,SX(9,9),SY(9,9),SZ(9,9),JAC,THETA,
     &               VALPAR(4),XX,YY,ZZ,TEM,ECHN,ECHNP1,TEXN,TEXNP1,
     &               SIGM1,SIGMN,EPS1,EPSN,TPF1,TPFN,TZ0,R8T0,
     &               VAPRIN,VAPRMO,PREC,R8PREM,SIGM1S,SIGMNS,EPS1S,
     &               EPSNS,TPF1S,TPFNS
      INTEGER        IPOIDS,IVF,IDFDX,IDFDY,IGEOM,ITEMPS,JIN,NBFPG,I,
     &               NDIM,NNO,IPG,NPG1,IVECTT,ITEXT,IECH,IRAY,JVAL,INO,
     &               IDEC,JDEC,KDEC,LDEC,ITEMP,JNO,J,IER,IRAYS,JGANO,
     &               NBPG(10),IVAPRI,IVAPRM,IFM,NIV,IRET,NNOS
      LOGICAL        LSENS,LSTAT,LTEXT

C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
C -----------FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

C====
C 1.1 PREALABLES: RECUPERATION ADRESSES FONCTIONS DE FORMES...
C====
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDX,JGANO)
      IDFDY  = IDFDX  + 1

      TZ0  = R8T0()
      PREC = R8PREM()

C INITS.
      IF (OPTION(11:14).EQ.'TEXT') THEN
        LTEXT = .TRUE.
      ELSE IF (OPTION(11:14).EQ.'RAYO') THEN
        LTEXT = .FALSE.
      ELSE
        CALL UTMESS('F','TE0060','OPTION DE CALCUL INVALIDE')
      ENDIF
      IF (OPTION(6:9).EQ.'SENS') THEN
        LSENS = .TRUE.
      ELSE IF (OPTION(6:9).EQ.'THER') THEN
        LSENS = .FALSE.
      ELSE
        CALL UTMESS('F','TE0060','OPTION DE CALCUL INVALIDE')
      ENDIF

C====
C 1.2 PREALABLES LIES AUX RECHERCHES DE DONNEES GENERALES
C====

C CALCUL DE SENSIBILITE PART 0
      LSTAT = .FALSE.
      IF (LTEXT) THEN
C CHAR_.._TEXT : 2 TYPES DE CALCUL

        CALL JEVECH('PTEMPER','L',ITEMP)
        CALL JEVECH('PCOEFHF','L',IECH)
        CALL JEVECH('PT_EXTF','L',ITEXT)

C CALCUL DE SENSIBILITE PART I : SENSIBILITE / H ECHANGE EXT
        IF (LSENS) THEN
          CALL JEVECH('PVAPRIN','L',IVAPRI)
          CALL TECACH('ONN','PVAPRMO',1,IVAPRM,IRET)
C L'ABSENCE DE CE CHAMP DETERMINE LE CRITERE STATIONNAIRE OU PAS
          IF (IVAPRM.EQ.0) LSTAT = .TRUE.
        ENDIF

      ELSE
C CHAR_..._RAYO: 4 TYPES DE CALCUL

C CHAMP DE RAYONNEMENT STD OU DERIVE
        CALL JEVECH('PRAYONF','L',IRAY)

C CALCUL DE SENSIBILITE PART I BIS
        IF (LSENS) THEN
C RECUPERATION DE (DT/DS)-. LA PRESENCE DE CE PARAMETRE, OPTIONNEL EN
C CALCUL DE SENSIBILITE, SIGNIFIE QU'IL FAUT ASSEMBLER LE TERME COMPLEN
C TAIRE. SINON, IL FAUT ASSEMBLER LE TERME CORRESPONDANT A LA DERIVEE
C PAR RAPPORT A UNE DES CARACTERISTIQUES DU RAYONNEMENT
          CALL TECACH('ONN','PTEMPER',1,ITEMP,IRET)
          IF (ITEMP.EQ.0) THEN
C SENSIBILITE EN RAYONNEMENT: CHAMP DE RAYONNEMENT STD ET T+/T-
            CALL JEVECH('PRAYONS','L',IRAYS)
            CALL JEVECH('PVAPRIN','L',IVAPRI)
            CALL TECACH('ONN','PVAPRMO',1,IVAPRM,IRET)
            IF (IVAPRM.EQ.0) LSTAT = .TRUE.
          ELSE
C TERME COMPLEMENTAIRE EN RAYONNEMENT: CHAMP CORRESPONDANT A T-
            CALL JEVECH('PVAPRMO','L',IVAPRM)
            IVAPRI = 0
            IRAYS = 0
            LSTAT = .FALSE.
          ENDIF
        ELSE
C CALCUL STD: LECTURE DE T-
          CALL JEVECH('PTEMPER','L',ITEMP)
          LSTAT = .FALSE.
        ENDIF
C FIN DU IF LTEXT
      ENDIF

C TRONC COMMUN
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PTEMPSR','L',ITEMPS)
      CALL JEVECH('PVECTTR','E',IVECTT)

C====
C 1.3 PREALABLES LIES AUX CALCULS
C====
      THETA     = ZR(ITEMPS+2)
      NOMPAR(1) = 'X'
      NOMPAR(2) = 'Y'
      NOMPAR(3) = 'Z'
      NOMPAR(4) = 'INST'
      DO 10 I = 1,NNO
        ZR(IVECTT+I-1) = 0.0D0
10    CONTINUE

C    CALCUL DES PRODUITS VECTORIELS OMI X OMJ

      DO 1 INO = 1,NNO
        I = IGEOM + 3*(INO-1) -1
          DO 2 JNO = 1,NNO
            J = IGEOM + 3*(JNO-1) -1
              SX(INO,JNO) = ZR(I+2) * ZR(J+3) - ZR(I+3) * ZR(J+2)
              SY(INO,JNO) = ZR(I+3) * ZR(J+1) - ZR(I+1) * ZR(J+3)
              SZ(INO,JNO) = ZR(I+1) * ZR(J+2) - ZR(I+2) * ZR(J+1)
2         CONTINUE
1     CONTINUE

C====
C 2. CALCULS TERMES DE MASSE (STD ET/OU SENSIBLE)
C====
C    BOUCLE SUR LES POINTS DE GAUSS

      DO 101 IPG=1,NPG1
        KDEC = (IPG-1)*NNO*NDIM
        LDEC = (IPG-1)*NNO
        NX = 0.0D0
        NY = 0.0D0
        NZ = 0.0D0

C    CALCUL DE LA NORMALE AU POINT DE GAUSS IPG
        DO 102 I=1,NNO
          IDEC = (I-1)*NDIM
          DO 102 J=1,NNO
            JDEC = (J-1)*NDIM
            NX=NX+ZR(IDFDX+KDEC+IDEC) * ZR(IDFDY+KDEC+JDEC) * SX(I,J)
            NY=NY+ZR(IDFDX+KDEC+IDEC) * ZR(IDFDY+KDEC+JDEC) * SY(I,J)
            NZ=NZ+ZR(IDFDX+KDEC+IDEC) * ZR(IDFDY+KDEC+JDEC) * SZ(I,J)
102     CONTINUE
        JAC = SQRT(NX*NX + NY*NY + NZ*NZ)
        TEM = 0.D0
        XX  = 0.D0
        YY  = 0.D0
        ZZ  = 0.D0

        IF (ITEMP.NE.0) THEN
          DO 104 I = 1,NNO
C CALCUL DE T- (OU (DT/DS)- EN SENSIBILITE)
            TEM = TEM + ZR(ITEMP+I-1) * ZR(IVF+LDEC+I-1)
104       CONTINUE
        ENDIF

        DO 106 I = 1,NNO
C CALCUL DE LA POSITION GEOMETRIQUE DU PT DE GAUSS
          XX  = XX  + ZR(IGEOM+3*I-3) * ZR(IVF+LDEC+I-1)
          YY  = YY  + ZR(IGEOM+3*I-2) * ZR(IVF+LDEC+I-1)
          ZZ  = ZZ  + ZR(IGEOM+3*I-1) * ZR(IVF+LDEC+I-1)
106     CONTINUE

C CALCUL DE SENSIBILITE PART II
C DETERMINATION DE T+ (VAPRIN) ET DE T- (VAPRMO) SI NECESSAIRE
        IF (LSENS) THEN
          VAPRIN = 0.D0
          VAPRMO = 0.D0
C CALCUL T+ SAUF TERME COMPLEMENTAIRE EN RAYONNEMENT
          IF (IVAPRI.NE.0) THEN
            DO 107 I=1,NNO
              VAPRIN=VAPRIN+ZR(IVAPRI+I-1)*ZR(IVF+LDEC+I-1)
107         CONTINUE
          ENDIF
C CALCUL DE T- EN TRANSITOIRE
          IF (.NOT.LSTAT) THEN
            DO 108 I=1,NNO
              VAPRMO=VAPRMO+ZR(IVAPRM+I-1)*ZR(IVF+LDEC+I-1)
108         CONTINUE
          ENDIF
        ENDIF

        VALPAR(1) = XX
        VALPAR(2) = YY
        VALPAR(3) = ZZ
        VALPAR(4) = ZR(ITEMPS)

C====
C 2.1 OPTION CHAR_THER_TEXT_F/R OU CHAR_SENS_TEXT_F
C====
        IF (LTEXT) THEN

          CALL FOINTE('FM',ZK8(ITEXT),4,NOMPAR,VALPAR,TEXNP1,IER)
          IF (IER.NE.0)
     &      CALL UTMESS('F','TE0060','ERREUR CALCUL DE TEXNP1')
          IF( THETA .NE. 1.0D0 ) THEN
            VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
            CALL FOINTE('FM',ZK8(ITEXT),4,NOMPAR,VALPAR,TEXN,IER)
            IF (IER.NE.0)
     &        CALL UTMESS('F','TE0060','ERREUR CALCUL DE TEXN')
          ELSE
            TEXN = 0.D0
          ENDIF

          VALPAR(4) = ZR(ITEMPS)
          CALL FOINTE('FM',ZK8(IECH),4,NOMPAR,VALPAR,ECHNP1,IER)
          IF (IER.NE.0)
     &      CALL UTMESS('F','TE0060','ERREUR CALCUL DE ECHNP1')
          IF( THETA .NE. 1.0D0 ) THEN
            VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
            CALL FOINTE('FM',ZK8(IECH),4,NOMPAR,VALPAR,ECHN,IER)
            IF (IER.NE.0)
     &        CALL UTMESS('F','TE0060','ERREUR CALCUL DE ECHN')
          ELSE
            ECHN = 0.D0
          ENDIF

          IF (.NOT.LSENS) THEN
C CALCUL STD
            DO 120 I=1,NNO
              ZR(IVECTT+I-1) = ZR(IVECTT+I-1) +
     &        JAC * ZR(IPOIDS+IPG-1) * ZR(IVF+LDEC+I-1) *
     &        ( THETA*ECHNP1*TEXNP1+(1.0D0-THETA)*ECHN*(TEXN-TEM))
120         CONTINUE
          ELSE
C CALCUL DE SENSIBILITE PART III
            DO 122 I=1,NNO
              ZR(IVECTT+I-1) = ZR(IVECTT+I-1) +
     &           JAC * ZR(IPOIDS+IPG-1) * ZR(IVF+LDEC+I-1) *
     &         ( THETA*ECHNP1*(TEXNP1-VAPRIN)+(1.0D0-THETA)*ECHN*
     &           (TEXN-VAPRMO))
122         CONTINUE
          ENDIF

C====
C 2.2 OPTION CHAR_THER_RAYO_F/R OU CHAR_SENS_RAYO_F
C====
        ELSE

C PB STD ET TERME COMPLEMENTAIRE EN SENSIBILITE
C                         ===> DONNEES DU RAYONNEMENT INITIAL
C SENSIBILITE / UNE DES CARACTERISTIQUES DU RAYONNEMENT
C                         ===> DONNEES DU RAYONNEMENT DERIVE
          CALL FOINTE('FM',ZK8(IRAY),4,NOMPAR,VALPAR,SIGM1,IER)
          IF (IER.NE.0)
     &      CALL UTMESS('F','TE0060','ERREUR CALCUL DE SIGM1')
          IF( THETA .NE. 1.0D0 ) THEN
            VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
            CALL FOINTE('FM',ZK8(IRAY),4,NOMPAR,VALPAR,SIGMN,IER)
            IF (IER.NE.0)
     &        CALL UTMESS('F','TE0060','ERREUR CALCUL DE SIGMN')
          ELSE
            SIGMN = 0.D0
          ENDIF

          VALPAR(4) = ZR(ITEMPS)
          CALL FOINTE('FM',ZK8(IRAY+1),4,NOMPAR,VALPAR,EPS1,IER)
          IF (IER.NE.0)
     &      CALL UTMESS('F','TE0060','ERREUR CALCUL DE EPS1')
          IF( THETA .NE. 1.0D0 ) THEN
            VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
            CALL FOINTE('FM',ZK8(IRAY+1),4,NOMPAR,VALPAR,EPSN,IER)
            IF (IER.NE.0)
     &        CALL UTMESS('F','TE0060','ERREUR CALCUL DE EPSN')
          ELSE
            EPSN = 0.D0
          ENDIF

          VALPAR(4) = ZR(ITEMPS)
          CALL FOINTE('FM',ZK8(IRAY+2),4,NOMPAR,VALPAR,TPF1,IER)
          IF (IER.NE.0)
     &      CALL UTMESS('F','TE0060','ERREUR CALCUL DE TPF1')
          IF( THETA .NE. 1.0D0 ) THEN
            VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
            CALL FOINTE('FM',ZK8(IRAY+2),4,NOMPAR,VALPAR,TPFN,IER)
            IF (IER.NE.0)
     &        CALL UTMESS('F','TE0060','ERREUR CALCUL DE TPFN')
          ELSE
            TPFN = 0.D0
          ENDIF

C CALCUL DE SENSIBILITE PART IV
C SENSIBILITE / UNE DES CARACTERISTIQUES DU RAYONNEMENT
C                         ===> DONNEES DU RAYONNEMENT INITIAL
          IF (LSENS.AND.(IRAYS.NE.0)) THEN
            CALL FOINTE('FM',ZK8(IRAYS),4,NOMPAR,VALPAR,SIGM1S,IER)
            IF (IER.NE.0)
     &        CALL UTMESS('F','TE0060','ERREUR CALCUL DE SIGM1S')
            IF( THETA .NE. 1.0D0 ) THEN
              VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
              CALL FOINTE('FM',ZK8(IRAYS),4,NOMPAR,VALPAR,SIGMNS,IER)
              IF (IER.NE.0)
     &          CALL UTMESS('F','TE0060','ERREUR CALCUL DE SIGMNS')
            ELSE
              SIGMNS = 0.D0
            ENDIF

            VALPAR(4) = ZR(ITEMPS)
            CALL FOINTE('FM',ZK8(IRAYS+1),4,NOMPAR,VALPAR,EPS1S,IER)
            IF (IER.NE.0)
     &        CALL UTMESS('F','TE0060','ERREUR CALCUL DE EPS1S')
            IF( THETA .NE. 1.0D0 ) THEN
              VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
              CALL FOINTE('FM',ZK8(IRAYS+1),4,NOMPAR,VALPAR,EPSNS,IER)
              IF (IER.NE.0)
     &          CALL UTMESS('F','TE0060','ERREUR CALCUL DE EPSNS')
            ELSE
              EPSNS = 0.D0
            ENDIF

            VALPAR(4) = ZR(ITEMPS)
            CALL FOINTE('FM',ZK8(IRAYS+2),4,NOMPAR,VALPAR,TPF1S,IER)
            IF (IER.NE.0)
     &        CALL UTMESS('F','TE0060','ERREUR CALCUL DE TPF1S')
            IF( THETA .NE. 1.0D0 ) THEN
              VALPAR(4) = ZR(ITEMPS)-ZR(ITEMPS+1)
              CALL FOINTE('FM',ZK8(IRAYS+2),4,NOMPAR,VALPAR,TPFNS,IER)
              IF (IER.NE.0)
     &          CALL UTMESS('F','TE0060','ERREUR CALCUL DE TPFNS')
            ELSE
              TPFNS = 0.D0
            ENDIF
          ENDIF

          IF (.NOT.LSENS) THEN
C CALCUL STD

            DO 130 I=1,NNO
              ZR(IVECTT+I-1) = ZR(IVECTT+I-1) +
     &          JAC * ZR(IPOIDS+IPG-1) * ZR(IVF+LDEC+I-1) *
     &         (       THETA *SIGM1*EPS1* (TPF1+TZ0)**4+
     &      (1.0D0-THETA)*SIGMN*EPSN*((TPFN+TZ0)**4-(TEM+TZ0)**4))
130         CONTINUE
          ELSE

C CALCUL DE SENSIBILITE PART V
            IF ((ITEMP.NE.0).AND.(.NOT.LSTAT)) THEN
C TERME COMPLEMENTAIRE EN SENSIBILITE

              DO 132 I=1,NNO
                ZR(IVECTT+I-1) = ZR(IVECTT+I-1)+
     &             4*JAC*ZR(IPOIDS+IPG-1)*(THETA-1.D0)*
     &             ZR(IVF+LDEC+I-1)*TEM*SIGMN*EPSN*((VAPRMO+TZ0)**3)
132           CONTINUE
            ELSE

C ON RAJOUTE CES TESTS POUR DISTINGUER ENTRE LA DERIVATION PAR RAPPORT
C A L'EMISSIVITE, LA CONSTANTE DE STEFAN OU LA TEMPERATURE INFINI
              CALL INFNIV(IFM,NIV)
              IF (NIV.EQ.2) THEN
                WRITE(IFM,*)' '
                WRITE(IFM,*)'   SIGM/EPS/TPF :',SIGM1,EPS1,TPF1
              ENDIF
              IF ((ABS(SIGM1).LT.PREC).AND.(ABS(EPS1).LT.PREC).AND.
     &             (ABS(TPF1).LT.PREC)) THEN
C PAS DE TERME DE SENSIBILITE SUPPLEMENTAIRE, CALCUL INSENSIBLE

              ELSE IF ((ABS(SIGM1).LT.PREC).AND.
     &                 (ABS(TPF1).LT.PREC)) THEN
C SENSIBILITE PAR RAPPORT A L'EMISSIVITE
                DO 134 I=1,NNO
                  ZR(IVECTT+I-1) = ZR(IVECTT+I-1)+
     &               JAC*ZR(IPOIDS+IPG-1)*ZR(IVF+LDEC+I-1)*(
     &          EPS1*SIGM1S*THETA*((TPF1S+TZ0)**4-(VAPRIN+TZ0)**4)+
     &   EPSN*SIGMNS*(1.D0-THETA)*((TPFNS+TZ0)**4-(VAPRMO+TZ0)**4))
134             CONTINUE

              ELSE IF ((ABS(EPS1).LT.PREC).AND.
     &                 (ABS(TPF1).LT.PREC)) THEN
C SENSIBILITE PAR RAPPORT A LA CONSTANTE DE STEFAN
                DO 136 I=1,NNO
                  ZR(IVECTT+I-1) = ZR(IVECTT+I-1)+
     &               JAC*ZR(IPOIDS+IPG-1)*ZR(IVF+LDEC+I-1)*(
     &          EPS1S*SIGM1*THETA*((TPF1S+TZ0)**4-(VAPRIN+TZ0)**4)+
     &   EPSNS*SIGMN*(1.D0-THETA)*((TPFNS+TZ0)**4-(VAPRMO+TZ0)**4))
136             CONTINUE

              ELSE IF ((ABS(EPS1).LT.PREC).AND.
     &                 (ABS(SIGM1).LT.PREC)) THEN
C SENSIBILITE PAR RAPPORT A TEMPERATURE A L'INFINI
              DO 138 I=1,NNO
                ZR(IVECTT+I-1) = ZR(IVECTT+I-1)+
     &             4*JAC*ZR(IPOIDS+IPG-1)*ZR(IVF+LDEC+I-1)*(
     &             THETA*EPS1S*SIGM1S*TPF1*(TPF1S+TZ0)**3 +
     &      (1.D0-THETA)*EPSNS*SIGMNS*TPFN*(TPFNS+TZ0)**3)
138           CONTINUE

              ELSE
                CALL UTMESS('F','TE0060','PB DETERMINATION '//
     &                      'SENSIBILITE DE RAYONNEMENT')
              ENDIF
C FIN DU IF ITEMP
            ENDIF
C FIN DU IF LSENS
          ENDIF
C FIN DU IF LTEXT
        ENDIF
C FIN BOUCLE SUR LES PTS DE GAUSS
101   CONTINUE
      END
