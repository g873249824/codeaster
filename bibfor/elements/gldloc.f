      SUBROUTINE GLDLOC (LAMBDA,DEUXMU,DEUMUF,SEUIL,ALF,ALFMC,GMT,
     &                   GMC,GF,COF1,VIM,Q2D,QFF,TR2D,
     &                   EPS33,DE33D1,DE33D2,KSI2D,DKSI1,DKSI2,DA1,
     &                   DA2,KDMAX,TOLD,CODRET,EMP)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE SFAYOLLE S.FAYOLLE
C TOLE CRP_21
      IMPLICIT NONE

      INTEGER KDMAX,CODRET
      REAL*8  VIM(*),GMT,GMC,GF,TR2D,EPS33
      REAL*8  LAMBDA,DEUXMU,DEUMUF,SEUIL,ALF,QFF(2),TOLD
      REAL*8  DE33D1,DE33D2,KSI2D,DKSI1,DKSI2,DA1,DA2,TREPS,TREPS2
      REAL*8  COF1(2),Q2D(2)

C ----------------------------------------------------------------------
C
C      CALCUL D'ENDOMMAGEMENT (DA1 ET DA2) ET DE LA COMPOSANTE DE
C       DEFORMATION EPS33, APPELE PAR "LCGLDM" (LOI GLRC_DM)
C
C IN:
C       LAMBDA  : PARAMETRE D ELASTICITE - MEMBRANE
C       DEUXMU  : PARAMETRE D ELASTICITE - MEMBRANE
C       GMT     : PARAMETRE GAMMA POUR LA MEMBRANE EN TRACTION
C       GMC     : PARAMETRE GAMMA POUR LA MEMBRANE EN COMPRESSION
C       SEUIL   : INITIAL MEMBRANE
C       ALF     : PARAMETRE DE SEUIL FLEXION
C       VIM     : VARIABLES INTERNES EN T-
C       Q2D     : PARTIE CONSTANTE DU RESIDU MEMBRANE
C       QFF(2)  : PARTIE CONSTANTE DU RESIDU FLEXION
C       TR2D    : EPS11 + EPS22
C       COF1    : PARAMETRE
C OUT:
C       DA1     : ENDOMMAGEMENT SUR LA PARTIE 1 DE LA PLAQUE
C       DA2     : ENDOMMAGEMENT SUR LA PARTIE 2 DE LA PLAQUE
C       KSI2D   : VALEUR DE LA FONCTION CARACTERISTIQUE DE L'ENDOM.
C       EPS33   : COMPOSANTE 33 DE LA DEFORMATION
C       DE33D1  : DERIVEE DE EPS33 PAR RAPPORT A DA1
C       DE33D2  : DERIVEE DE EPS33 PAR RAPPORT A DA2
C       ELAS    : .TRUE. SI ELASTIQUE
C       ELAS1   : .TRUE. SI DA1 == VIM(1)
C       ELAS2   : .TRUE. SI DA2 == VIM(2)
C       CODRET  : CODE RETOUR DE L'INTEGRATION INTEGRATION DU
C                 0 => PAS DE PROBLEME
C                 1 => ABSENCE DE CONVERGENCE
C ----------------------------------------------------------------------

      LOGICAL LCONV1,LCONV2

      INTEGER I

      REAL*8  QM1,QM2
      REAL*8  RD1,RD2,DR1D,DR2D,DD1,DD2,SEUILR
      REAL*8  ALFMC,EMP(2),COF2(2),DQ2D(2)

      CALL CEPS33 (LAMBDA,DEUXMU,ALFMC,GMT,GMC,TR2D,
     &             DA1,DA2,EPS33,DE33D1,DE33D2,KSI2D,DKSI1,
     &             DKSI2,COF1,Q2D,EMP,
     &             COF2,DQ2D)

      TREPS = TR2D + EPS33
      TREPS2 = TREPS**2

C----------CONTRIBUTION DE MEMBRANE-----
      QM1 = 0.5D0*COF1(1)*TREPS2+Q2D(1)
      QM2 = 0.5D0*COF1(2)*TREPS2+Q2D(2)
      RD1 = QM1/(1.0D0 + DA1)**2 - SEUIL
      RD2 = QM2/(1.0D0 + DA2)**2 - SEUIL

C----CONTRIBUTION DES COURBURES---------
      RD1 = RD1 + QFF(1)/(ALF + DA1)**2
      RD2 = RD2 + QFF(2)/(ALF + DA2)**2

      SEUILR = MAX(SEUIL,1.0D-6)

C-----VERIFIER SI LE SEUIL EST ATTEINT
      LCONV1 = RD1 .LT. 0.0D0
      LCONV2 = RD2 .LT. 0.0D0

      IF((.NOT. LCONV1 .AND. DA1.GE.VIM(1))
     &    .OR. (.NOT. LCONV2 .AND. DA2.GE.VIM(2))) THEN

        DO 60, I = 1,KDMAX

          DD1 = 0.0D0
          DD2 = 0.0D0

          IF (RD1 .GE. 0.0D0) THEN
            DR1D  = (COF1(1)*TREPS*DE33D1+0.5D0*COF2(1)*TREPS2+DQ2D(1))
     &            /(1.D0 + DA1)**2
     &            - 2.D0*QM1   /(1.D0 + DA1)**3
     &            - 2.D0*QFF(1)/(ALF  + DA1)**3
            IF(ABS(DR1D) .LT. 1.0D-14 ) THEN
              DD1 = 0.0D0
            ELSE
              DD1 = - RD1/DR1D
            ENDIF
          ENDIF

          IF (RD2 .GE. 0.0D0) THEN
            DR2D  = (COF1(2)*TREPS*DE33D2+0.5D0*COF2(2)*TREPS2+DQ2D(2))
     &            /(1.D0 + DA2)**2
     &            - 2.D0*QM2   /(1.D0 + DA2)**3
     &            - 2.D0*QFF(2)/(ALF  + DA2)**3

            IF(ABS(DR2D) .LT. 1.0D-14 ) THEN
              DD2 = 0.0D0
            ELSE
              DD2 = - RD2/DR2D
            ENDIF
          ENDIF

          IF(((ABS(DD1*RD1) .LT. TOLD*SEUILR) .OR.
     &       ((RD1 .LT. 0.0D0 .AND. DA1 .LE. VIM(1))))
     &        .AND. ((ABS(DD2*RD2) .LT. TOLD*SEUILR) .OR.
     &        ((RD2 .LT. 0.0D0 .AND. DA2 .LE. VIM(2)))))
     &        GOTO 61

          DA1 = DA1 + DD1
          DA2 = DA2 + DD2

          IF(DA1.LT.0.0D0 .AND. RD1.LT.0.0D0) DA1 = VIM(1)
          IF(DA2.LT.0.0D0 .AND. RD2.LT.0.0D0) DA2 = VIM(2)

          CALL CEPS33 (LAMBDA,DEUXMU,ALFMC,GMT,GMC,TR2D,DA1,DA2,EPS33,
     &                 DE33D1,DE33D2,KSI2D,DKSI1,DKSI2,COF1,Q2D,EMP,
     &                 COF2,DQ2D)

          TREPS = TR2D + EPS33
          TREPS2 = TREPS**2

C----------CONTRIBUTION DE MEMBRANE-----
          QM1 = 0.5D0*COF1(1)*TREPS2+Q2D(1)
          QM2 = 0.5D0*COF1(2)*TREPS2+Q2D(2)
          RD1 = QM1/(1.0D0 + DA1)**2 - SEUIL
          RD2 = QM2/(1.0D0 + DA2)**2 - SEUIL

C----CONTRIBUTION DES COURBURES---------
          RD1 = RD1 + QFF(1)/(ALF + DA1)**2
          RD2 = RD2 + QFF(2)/(ALF + DA2)**2

60      CONTINUE

C    NON CONVERGENCE POUR LE NOMBRE MAXIMAL D ITERATION PRESCRIT
        CODRET = 1

61      CONTINUE
      ENDIF
      END
