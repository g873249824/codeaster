      SUBROUTINE FONNOR ( RESU, NOMA, CNXINV )
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNOM
      CHARACTER*8         RESU, NOMA
      CHARACTER*19        CNXINV
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 07/05/2013   AUTEUR LADIER A.LADIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C FONCTION REALISEE:
C
C     CALCUL DE LA NORMALE AU FOND DE FISSURE POUR DEFI_FOND_FISS
C     EN 2D ET 3D DE LA BASE LOCALE
C
C     ENTREES:
C        RESU   : NOM DU CONCEPT RESULTAT DE L'OPERATEUR
C        NOMA   : NOM DU MAILLAGE
C        CNXINV : CONNECTIVITE INVERSE
C-----------------------------------------------------------------------
C
      INTEGER       J,JNOE1,JBASNO,JTYP,JBASSE,JTAIL,K
      INTEGER       JBORL,JDIROL,JNVDIR
      INTEGER       I,INA,INB,ISEG,IRET,NBNOSE,NBNOFF,INC,ILEV
      INTEGER       NA,NB,NRET,NDIM,NBNOEL,NSEG,NBMAX,NBMAC,INOEXT
      INTEGER       INDIC(4),NOE(4,4),INDR(2),TABLEV(2),INOSEG
      REAL*8        VDIR(2,3),VNOR(2,3),NORME,VECDIR(3),HMAX,HMAXPR
      REAL*8        VECT(3),SENS
      CHARACTER*6   NOMPRO,TYFOND
      CHARACTER*8   K8B,TYPFON,NOEUA
      CHARACTER*16  CASFON
      CHARACTER*19  BASNOF,BASSEG,MACOFO
      PARAMETER    (NOMPRO='FONNOR')
      INTEGER      IARG
C     -----------------------------------------------------------------
C
      CALL JEMARQ()

C     ------------------------------------------------------------------
C     INITIALISATIONS
C     ------------------------------------------------------------------

C
C     RECUPERATION DES INFORMATIONS RELATIVES AU MAILLAGE
C
C
C     RECUPERATION DU CONCEPT DU MAILLAGE
      CALL GETVID ( ' ', 'MAILLAGE', 1,IARG,1, NOMA , NRET )
C
C     RECUPERATION DU NOMBRE DE NOEUDS DU MAILLAGE
      CALL DISMOI('F','DIM_GEOM',NOMA,'MAILLAGE',NDIM,K8B,IRET)
C
C     RECUPERATION DU TYPE DU FOND DE FISSURE OUVERT OU FERME OU INF/SUP
      CALL GETVTX('FOND_FISS','TYPE_FOND', 1,IARG,1, TYFOND , IRET )
C
C     RECUPERATION DES NOEUDS DU FOND DE FISSURE
C
      CALL JEEXIN(RESU//'.FOND.NOEU',IRET)
      IF (IRET.NE.0) THEN
C       RECUPERATION DE L'ADRESSE DES NOEUDS DE FOND DE FISSURE
        CALL JEVEUO (RESU//'.FOND.NOEU', 'L', JNOE1 )
C       RECUPERATION DU NOMBRE DE NOEUD
        CALL JELIRA (RESU//'.FOND.NOEU' , 'LONUTI', NBNOFF, K8B)
      ELSE
C       RECUPERATION DE L'ADRESSE DES NOEUDS DE FOND DE FISSURE
        CALL JEVEUO (RESU//'.FOND_SUP.NOEU', 'L', JNOE1 )
C       RECUPERATION DU NOMBRE DE NOEUD
        CALL JELIRA (RESU//'.FOND_SUP.NOEU' , 'LONUTI', NBNOFF, K8B)
      ENDIF

C     VERIFICATION DE LA PRESENCE DE LEVRE_SUP
      CALL JEEXIN(RESU//'.LEVRESUP.MAIL',ILEV)

C     INITIALISATION DU SENS DU VECTEUR NORMAL
      SENS=1.D0
C
C     RECUPERATION DU TYPE DE MAILLE EN FOND DE FISSURE EN 3D
      IF (NDIM.EQ.3) THEN
        CALL JEVEUO (RESU//'.FOND.TYPE', 'L', JTYP )
        TYPFON = ZK8(JTYP)
C       SUIVANT LE CAS QUADRATIQUE/LINEAIRE EN 3D DEUX MAILLES SONT
C       CONNECTEES SI ELLES ONT AU MOINS NBMAX NOEUDS EN COMMUN
C       NBNOSE : NOMBRE DE NOEUDS PAR "SEGMENT" DE FOND DE FISSURE
        IF (TYPFON.EQ.'NOE2'.OR.TYPFON.EQ.'SEG2') THEN
          CASFON = 'LINEAIRE'
          NBNOSE = 2
          NBMAX  = 3
        ELSEIF (TYPFON.EQ.'NOE3'.OR.TYPFON.EQ.'SEG3') THEN
          CASFON = 'QUADRATIQUE'
          NBNOSE = 3
          NBMAX  = 6
        ELSE
          NBNOSE = 2
          NBMAX  = 3

        ENDIF
      ELSE
          NBNOSE = 2
          NBMAX  = 3
      ENDIF
      IF (NDIM.EQ.2) CASFON = '2D'
C
C
C     ALLOCATION DU VECTEUR DES BASES LOCALES PAR NOEUD DU FOND  :
C           - VECTEUR DIRECTION DE PROPA
C           - VECTEUR NORMAL (A LA SURFACE)
      BASNOF = RESU//'.BASEFOND'
      CALL WKVECT (BASNOF,'G V R',2*NDIM*NBNOFF,JBASNO)
C
C     ALLOCATION DU VECTEUR DES TAILLES DE MAILLES MAX PAR NOEUD DU FOND
      CALL WKVECT(RESU//'.FOND.TAILLE_R','G V R',NBNOFF,JTAIL)
C
C
C     NSEG : NOMBRE DE "SEGMENTS" DU FOND A TRAITER
      IF (NDIM.EQ.2) THEN
        CALL ASSERT(NBNOFF.EQ.1)
        NSEG = 1
      ELSEIF (NDIM.EQ.3) THEN
        CALL ASSERT(NBNOFF.GT.1)
        IF (CASFON.EQ.'LINEAIRE')    NSEG =  NBNOFF-1
        IF (CASFON.EQ.'QUADRATIQUE') NSEG = (NBNOFF-1)/2
      ENDIF

C     VECTEUR TEMPORAIRE DES BASES LOCALES PAR SEGMENT DU FOND
      BASSEG = '&&'//NOMPRO//'.BASSEG'
      CALL WKVECT(BASSEG,'V V R',2*NDIM*NSEG,JBASSE)

C     VECTEUR PERMETTANT DE SAVOIR SI LE VECTEUR DE DIRECTION DE
C     PROPAGATION (VDIR) A ETE RECALCULE OU NON AUX POINTS
C     EXTREMITES DE FONFIS
      CALL WKVECT('&&FONNOR.LBORD','V V L',NBNOFF,JBORL)

C     VECTEUR CONTENANT LES VDIR INITIAUX (CAD SANS MODIFICATION
C     DES VECTEURS AUX POINTS EXTREMITES DE FONFIS)
      CALL WKVECT('&&FONNOR.VDIROL','V V R',3*NBNOFF,JDIROL)

C     VECTEUR CONTENANT 0 OU 1 AUX POINTS EXTREMITES DE FONFIS:
C     0: LE PRODUIT SCALAIRE ENTRE LA NORMALE A LA FACE DE BORD ET
C        LE VDIR INITIAL ESI INFERIEUR A 0
C     1: LE PRODUIT SCALAIRE EST SUPERIEUR OU EGAL A 0
      CALL WKVECT('&&FONNOR.NVDIR','V V I',NBNOFF,JNVDIR)

      DO 10 I=1,NBNOFF
         ZL(JBORL-1+I)=.FALSE.
 10   CONTINUE
C
C
C     ------------------------------------------------------------------
C     BOUCLE SUR LES "SEGMENTS" DU FOND DE FISSURE
C     ------------------------------------------------------------------
C
      DO 100 ISEG=1,NSEG

C       INDICES DES NOEUDS DU SEGMENT :
C       NOEUDS SOMMETS (INA ET INB), NOEUD MILIEU (INC)
        IF (CASFON.EQ.'2D') THEN
          INA = ISEG
        ELSEIF (CASFON.EQ.'LINEAIRE') THEN
          INA = ISEG
          INB = ISEG+1
        ELSEIF (CASFON.EQ.'QUADRATIQUE') THEN
          INA = 2*ISEG-1
          INB = 2*ISEG+1
          INC = 2*ISEG
        ENDIF

C       NUMEROS (ABSOLUS) DES NOEUDS SOMMETS DU SEGMENT : NA ET NB
        NOEUA = ZK8(JNOE1-1+INA)
        CALL JENONU (JEXNOM(NOMA//'.NOMNOE',NOEUA),NA)
        IF (NDIM.EQ.3) THEN
          CALL JENONU (JEXNOM(NOMA//'.NOMNOE',ZK8(JNOE1-1+INB)),NB)
          IF (ISEG.EQ.1) THEN
            INOEXT=NA
            INOSEG=NB
          ENDIF
          IF (ISEG.EQ.NSEG) THEN
            INOEXT=NB
            INOSEG=NA
          ENDIF
        ENDIF
C
C
C       1) RECUP DES NUMEROS DES MAILLES CONNECTEES AU SEGMENT DU FOND
C          -> REMPLISSAGE DE MACOFO
C       --------------------------------------------------------------
C
C       VECTEUR DES MAILLES CONNECTEES AU SEGMENT DU FOND
        MACOFO = '&&'//'NOMPRO'//'.MACOFOND'
        CALL FONNO1 (NOMA,CNXINV,NDIM,NA,NB,NBMAC,MACOFO)
C
C
C       2) PARMI LES MAILLES CONNECTEES AU SEGMENT DU FOND, FILTRAGE DES
C          MAILLES CONNECTEES A 1 LEVRE (CAD AYANT UNE FACE LIBRE)
C          -> REMPLISSAGE DE TABLEV
C       ----------------------------------------------------------------
C
        CALL FONNO2 (MACOFO,NOMA,NBMAC,NBNOFF,NBNOSE,
     &               NBMAX,NOEUA,TABLEV)

C
C
C       3) RECUP DES FACES / ARETES CONNECTEES AU FOND
C          POUR CHACUNE DES 2 MAILLES
C          -> REMPLISSAGE DE NOE
C       ----------------------------------------------------

        CALL FONNO3 (NOMA,TABLEV,NDIM,NA,NB,NOE)
C
C
C       4) FILTRE DES FACES LIBRES
C          -> REMPLISSAGE DE INDIC
C       ----------------------------------------------------
C
        CALL FONNO4 (NDIM,MACOFO,NOMA,NBMAC,TABLEV,NOE,NBNOFF,INDIC)
        CALL JEDETR (MACOFO)

C       5) CALCUL DES VECTEURS DE LA BASE LOCALE :
C          -> REMPLISSAGE DE VDIR ET VNOR
C            VNOR : VECTEUR NORMAL A LA SURFACE DE LA FISSURE
C            VDIR : VECTEUR DANS LA DIRECTION DE PROPAGATION
C        RQ : CHACUN CONTIENT EN FAIT 2 VECTEURS (UN PAR LEVRE)
C       --------------------------------------------------------
C
        CALL FONNO5 (NOMA,INDIC,NBNOFF,NOE,NA,NB,NDIM,
     &               NBNOEL,INDR, VNOR,VDIR)
C
C
C       6) DETERMINATION DU VRAI VECTEUR ET BASE PAR SEGMENT
C          -> REMPLISSAGE DE BASSEG
C       ----------------------------------------------------
C
        IF (ILEV.NE.0 .AND. ISEG.EQ.1)THEN
          CALL FONNO8 (RESU,NOMA,TABLEV,VNOR,VECT)
        ENDIF

        CALL FONNO6 (RESU,NOMA,NDIM,INA,NBNOSE,ISEG,NSEG,
     &               NOE,INDR,NBNOEL,VNOR,VDIR,BASSEG,
     &               VECT,SENS)

C
C
C       7) EN 3D : BASE LOCALE : PASSAGE SEGMENTS -> NOEUDS
C          -> REMPLISSAGE DE BASNOF
C       ---------------------------------------------------

        IF (NDIM.EQ.3) THEN

C         MOYENNE POUR LES NOEUDS SOMMETS INA ET INB
C         DIRECT POUR LE NOEUD MILIEU INC
          DO 110 J=1,6

            ZR(JBASNO-1+6*(INA-1)+J)=( ZR(JBASNO-1+6*(INA -1)+J)
     &                                +ZR(JBASSE-1+6*(ISEG-1)+J) )/2.D0

            ZR(JBASNO-1+6*(INB-1)+J)=( ZR(JBASNO-1+6*(INB -1)+J)
     &                                +ZR(JBASSE-1+6*(ISEG-1)+J) )/2.D0

            IF (CASFON.EQ.'QUADRATIQUE')
     &        ZR(JBASNO-1+6*(INC-1)+J) = ZR(JBASSE-1+6*(ISEG-1)+J)

 110      CONTINUE

C         NORMALISATIONS
C         ZR(JBASNO-1+6*(INC-1)+J) DEJA NORMALISE
          CALL NORMEV(ZR(JBASNO-1+6*(INA-1)+1),NORME)
          CALL NORMEV(ZR(JBASNO-1+6*(INA-1)+4),NORME)
          CALL NORMEV(ZR(JBASNO-1+6*(INB-1)+1),NORME)
          CALL NORMEV(ZR(JBASNO-1+6*(INB-1)+4),NORME)

C         CORRECTION DU VECTEUR DE PROPAGATION AUX EXTREMITES DU FOND
C         SE TROUVANT SUR UN BORD DE LA STRUCTURE
          IF (((ISEG.EQ.1).OR.(ISEG.EQ.NSEG))
     &        .AND. TYFOND.NE.'FERME')THEN
            CALL FONEXT(NOMA,CNXINV,JBASNO,INOEXT,INOSEG,NBNOFF,JBORL,
     &                  JDIROL,JNVDIR,ISEG)
          ENDIF

        ELSEIF (NDIM.EQ.2) THEN

          DO 120 J=1,4
            ZR(JBASNO-1+4*(INA-1)+J) = ZR(JBASSE-1+4*(ISEG-1)+J)
 120      CONTINUE

        ENDIF

C     DETERMINATION DE LA TAILLE DE MAILLE POUR LE NOEUD NA
C     (ET LE NOEUD MILIEU INC EN QUADRATIQUE)
        DO 130 K=1,NDIM
           VECDIR(K)=ZR(JBASNO-1+2*NDIM*(INA-1)+K+NDIM)
 130    CONTINUE

        CALL FONNO7(NOMA,CNXINV,NDIM,NA,VECDIR,HMAX)

        IF ((CASFON.EQ.'QUADRATIQUE') .AND. (ISEG.EQ.1)) THEN
           ZR(JTAIL) = HMAX
        ELSE IF ((CASFON.EQ.'QUADRATIQUE') .AND. (ISEG.NE.1)) THEN
C          TAILLE POUR LE NOEUD SOMMET NA COURANT
           ZR(JTAIL-1+2*(ISEG-1)+1) = HMAX
C          RECUP TAILLE NOEUD SOMMET NA PRECEDENT
           HMAXPR = ZR(JTAIL-1+2*(ISEG-2)+1)
C          TAILLE POUR LE NOEUD MILIEU DU SEGMENT PRECEDENT
           ZR(JTAIL-1+2*(ISEG-2)+2) = (HMAX+HMAXPR)/2
        ELSE
           ZR(JTAIL-1+ISEG) = HMAX
        ENDIF

 100  CONTINUE

C     DANS LE CAS D'UN FOND FERME: CORRECTION DE BASEFOND
C     AUX NOEUDS EXTREMITES
      IF ((TYFOND.EQ.'FERME') .AND. (NDIM.EQ.3))THEN
         DO 300 J=1,6
           ZR(JBASNO-1+6*(1-1)+J)=(ZR(JBASSE-1+6*(1-1)+J)
     &                            +ZR(JBASSE-1+6*(NSEG-1)+J))/2.D0
           ZR(JBASNO-1+6*(NBNOFF-1)+J)=ZR(JBASNO-1+6*(1-1)+J)
 300     CONTINUE
         CALL NORMEV(ZR(JBASNO-1+6*(1-1)+1),NORME)
         CALL NORMEV(ZR(JBASNO-1+6*(1-1)+4),NORME)
         CALL NORMEV(ZR(JBASNO-1+6*(NBNOFF-1)+1),NORME)
         CALL NORMEV(ZR(JBASNO-1+6*(NBNOFF-1)+4),NORME)
      ENDIF

C     DETERMINATION DE LA TAILLE DE MAILLE POUR LE DERNIER NOEUD
      IF (NDIM.EQ.3) THEN

          DO 200 K=1,NDIM
            VECDIR(K)=ZR(JBASNO-1+2*NDIM*(INB-1)+K+NDIM)
 200      CONTINUE

          CALL FONNO7(NOMA,CNXINV,NDIM,NB,VECDIR,HMAX)
          ZR(JTAIL-1+NBNOFF)=HMAX

C         TAILLE DE MAILLE POUR LE DERNIER NOEUD MILIEU
          IF (CASFON.EQ.'QUADRATIQUE') THEN
             HMAXPR = ZR(JTAIL-1+NBNOFF-2)
             ZR(JTAIL-1+NBNOFF-1)=(HMAX+HMAXPR)/2
          ENDIF

C        DANS LE CAS D'UN FOND FERME: CORRECTION DE LA TAILLE DE
C        MAILLE AU PREMIER NOEUD
         IF (TYFOND.EQ.'FERME') ZR(JTAIL-1+1)=ZR(JTAIL-1+NBNOFF)

      ENDIF

C     MENAGE
      CALL JEDETR(BASSEG)
      CALL JEDETR('&&FONNOR.LBORD')
      CALL JEDETR('&&FONNOR.VDIROL')
      CALL JEDETR('&&FONNOR.NVDIR')

      CALL JEDEMA()
      END
