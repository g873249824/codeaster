      SUBROUTINE ZEROFO(F,X0,XAP,EPSI,NITMAX,SOLU,IRET,N)
      IMPLICIT NONE
C
      REAL*8   F,X0,XAP,EPSI,SOLU
      INTEGER  NITMAX,IRET,N
      EXTERNAL F
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 30/01/2012   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C TOLE CRP_7
C ----------------------------------------------------------------------
C     BUT:
C         TROUVER UNE RACINE DE L'EQUATION F(X)=0
C         ON SUPPOSE QUE LA FONCTION F EST CROISSANTE ET QUE F(X0)<0
C
C     IN:
C         F  : FONCTION DONT ON CHERCHE LE "ZERO"
C         X0 : POINT 0
C         XAP: APPROXIMATION DE LA SOLUTION.
C        EPSI: TOLERANCE ABSOLU SUR LE ZERO CHERCHE : ABS(F(SOLU))<EPSI
C      NITMAX: NOMBRE MAXI D'ITERATIONS AUTORISEES.
C
C     OUT:
C         SOLU: VALEUR DE LA RACINE CHERCHEE.
C     IRET    : CODE RETOUR DE LA RECHERCHE DE ZERO
C               IRET=0 => PAS DE PROBLEME
C               IRET=1 => ECHEC DANS LA RECHERCHE DE ZERO
C     N       : NOMBRE D'ITERATIONS REALISEES
C
C ----------------------------------------------------------------------
      REAL*8  FX,FY,FZ,X,Y,Z,A,B
C DEB-------------------------------------------------------------------
C
C     INITIALISATIONS
C
      IRET = 1
      N = 1
      X = X0
      FX = F(X0)
      IF (ABS(FX).LT.EPSI) THEN
         Z=0.D0
         GOTO 800
      ENDIF
      Y = XAP
      FY = F(Y)
C
C     DEBUT DES ITERATIONS
C
   10 CONTINUE
      IF (FY.GT.0.D0) THEN
        A = X
        B = Y
   20   CONTINUE
        IF (FX.EQ.FY) GOTO 999
        Z = Y - (Y-X)*FY/(FY-FX)
        IF (((Z-A)*(Z-B)).GT.0.D0) THEN
          Z = (A+B)/2.D0
        ENDIF
C
        N = N + 1
        FZ = F(Z)
        IF (ABS(FZ).LT.EPSI) GOTO 800
        IF (N.GT.NITMAX) GOTO 999
        IF (FZ.LT.0.D0) THEN
          A = Z
        ELSE
          B = Z
        ENDIF
        X = Y
        FX = FY
        Y = Z
        FY = FZ
        GOTO 20
      ELSE
        IF (FY.LT.FX) GOTO 999
        IF (FX.EQ.FY) GOTO 999
        Z = Y - (Y-X)*FY/(FY-FX)
        N = N + 1
        X = Y
        FX = FY
        Y = Z
        FY = F(Z)
C
        IF (ABS(FY).LT.EPSI) GOTO 800
        IF (N.GT.NITMAX) GOTO 999
      ENDIF
      GOTO 10
C
C     SUCCES
 800  CONTINUE
      SOLU=Z
      IRET=0

C     SORTIE
 999  CONTINUE
      END
