      SUBROUTINE DGSEUI(EM,NUM,EF,NUF,EB,NUB,SYTB,H,ICISAI,SYT,SYC,DXD,
     &                  SYF,DRD,PELAST,PELASF,ICOMPR)

      IMPLICIT NONE

C PARAMETRES ENTRANTS
      INTEGER ICOMPR, ICISAI
      REAL*8 EM,NUM,EF,NUF,H
      REAL*8 EB,NUB,SYTB,SYC

C PARAMETRES SORTANTS
      REAL*8 SYT,DXD,SYF,DRD,PELAST,PELASF

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 03/01/2011   AUTEUR SFAYOLLE S.FAYOLLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE SFAYOLLE S.FAYOLLE
C ----------------------------------------------------------------------
C
C BUT : DETERMINATION DES SEUIL D ENDOMMAGEMENT
C
C IN:
C       ICOMPR : METHODE DE COMPRESSION
C       EM     : MODULE D YOUNG EN MEMBRANE
C       EF     : MODULE D YOUNG EN FLEXION
C       NUM    : COEFF DE POISSON EN MEMBRANE
C       NUF    : COEFF DE POISSON EN FLEXION
C       H      : EPAISSEUR DE LA PLAQUE
C       EB     : MODULE D YOUNG DU BETON
C       NUB    : COEFF DE POISSON DU BETON
C       SYTB   : LIMITE A LA TRACTION DU BETON
C       SYC    : SEUIL D'ENDOMMAGEMENT EN COMPRESSION
C OUT:
C       SYT    : SEUIL D'ENDOMMAGEMENT EN TRACTION
C       DXD    : DEPLACEMENT A L'APPARITION DE L'ENDOMMAGEMENT
C       SYF    : SEUIL D'ENDOMMAGEMENT EN FLEXION
C       DRD    : ROTATION A L'APPARITION DE L'ENDOMMAGEMENT
C       PELAST : PENTE ELASTIQUE EN TRACTION
C       PELASF : PENTE ELASTIQUE EN FLEXION
C ----------------------------------------------------------------------
      REAL*8 RMESG(2),SYCMAX

C - DETERMINATION DES PARAMETRES EN MEMBRANE
C - SEUILS D'ENDOMMAGEMENT EN TRACTION PURE
      SYT=SYTB*EM*H*(1.D0-NUB**2)/(EB*(1.D0-NUB*NUM))
      SYCMAX=SQRT((1.D0-NUM)*(1.D0+2.D0*NUM))/NUM*SYT

      IF ((ABS(SYC) .GT. SYCMAX) .AND. (ICOMPR .EQ. 1)) THEN
        RMESG(1) = SYC
        RMESG(2) = SYCMAX
        CALL U2MESR('F','ALGORITH6_2',2,RMESG)
      ENDIF

C - DEPLACEMENT A L'APPARITION DE L'ENDOMMAGEMENT
      DXD=SYT/(H*EM)

      IF (ICISAI .EQ. 1) THEN
C - CALCUL DE LA PENTE ELASTIQUE EN CISAILLEMENT PUR DANS LE PLAN
        PELAST=EM/(1.D0+NUM)/2.D0*H
      ELSE
C - PENTE ELASTIQUE EN TRACTION
        PELAST=EM*H
      ENDIF

C - DETERMINATION DES PARAMETRES EN FLEXION
C - SEUIL D'ENDOMMAGEMENT EN FLEXION PURE
      SYF=SYTB*EF*H**2*(1.D0-NUB**2)/(6.D0*EB*(1.D0-NUB*NUF))
C - ROTATION A L'APPARITION DE L'ENDOMMAGEMENT
      DRD=12.D0*SYF/(H**3*EF)
      PELASF=EF*H**3/12.D0

      END
