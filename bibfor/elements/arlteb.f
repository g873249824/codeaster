      SUBROUTINE ARLTEB(NDIM  ,NBSIGM ,NPGS  ,POIJCS,
     &                  NDML1   ,DFDX1 ,DFDY1 ,DFDZ1,
     &                  NDML2   ,DFDX2 ,DFDY2 ,DFDZ2,
     &                  MCPLB)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 20/12/2010   AUTEUR PELLET J.PELLET 
C TOLE CRS_1404
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT     NONE
      INTEGER      NDIM
      INTEGER      NBSIGM
      INTEGER      NPGS
      REAL*8       POIJCS(NPGS)
      INTEGER      NDML1,NDML2
      REAL*8       DFDX1(NPGS*NDML1),DFDY1(NPGS*NDML1),DFDZ1(NPGS*NDML1)
      REAL*8       DFDX2(NPGS*NDML2),DFDY2(NPGS*NDML2),DFDZ2(NPGS*NDML2)
      REAL*8       MCPLB(NDIM*NDML1,NDIM*NDML2)
C
C ----------------------------------------------------------------------
C
C CALCUL DES MATRICES DE COUPLAGE ARLEQUIN
C OPTION ARLQ_MATR
C
C
C CALCUL DES INTEGRALES DE COUPLAGE ENTRE MAILLE 1 ET MAILLE 2
C
C TERMES DERIVES (VLMT1)T.VLMT2 (INTEGRALE SUR S)
C
C
C ----------------------------------------------------------------------
C
C
C IN  NDIM   : DIMENSION DE LA MAILLE
C IN  NBSIGM : NOMBRE DE CONTRAINTES ASSOCIEES
C IN  NPGS   : NOMBRE DE POINTS DE GAUSS DE LA MAILLE S
C IN  POIJCS : PRODUIT POIDS DE GAUSS*JACOBIEN  DE LA MAILLE S
C IN  NDML1  : NOMBRE DE NOEUDS DE LA MAILLE 1
C IN  DFDX1  : DERIVEE FCT. FORME/X EN CHAQUE PT DE GAUSS DE LA MAILLE 1
C IN  DFDY1  : DERIVEE FCT. FORME/Y EN CHAQUE PT DE GAUSS DE LA MAILLE 1
C IN  DFDZ1  : DERIVEE FCT. FORME/Z EN CHAQUE PT DE GAUSS DE LA MAILLE 1
C IN  NDML2  : NOMBRE DE NOEUDS DE LA MAILLE 2
C IN  DFDX2  : DERIVEE FCT. FORME/X EN CHAQUE PT DE GAUSS DE LA MAILLE 2
C IN  DFDY2  : DERIVEE FCT. FORME/Y EN CHAQUE PT DE GAUSS DE LA MAILLE 2
C IN  DFDZ2  : DERIVEE FCT. FORME/Z EN CHAQUE PT DE GAUSS DE LA MAILLE 2
C OUT MCPLB  : MATRICE DES TERMES DE COUPLAGE (VLMT1)T.VLMT2
C              MATRICE RECTANGULAIRE (NDIM*NDML1xNDIM*NDML2)
C
C ----------------------------------------------------------------------
C
      INTEGER     KPGS
      INTEGER     NBINC1,NBINC2
      REAL*8      VLMT1(NBSIGM,NDIM*NDML1),VLMT2(NBSIGM,NDIM*NDML2)
      INTEGER     MTL1,MTL2
      INTEGER     IAUX,JAUX,KAUX
      REAL*8      POIDS
C
C ----------------------------------------------------------------------
C
      NBINC1 = NDML1*NDIM
      NBINC2 = NDML2*NDIM
C
C --- INITIALISATIONS MATRICES VLMT1, VLMT2
C
      POIDS = POIJCS(1)
C
C ----- CALCUL DE LA MATRICE VLMT1
C
      CALL BMATF1(NDIM    ,NBSIGM  ,NDML1     ,
     &            DFDX1(1),DFDY1(1),DFDZ1(1),VLMT1   )
C
C ----- CALCUL DE LA MATRICE VLMT2
C
      CALL BMATF1(NDIM    ,NBSIGM  ,NDML2     ,
     &            DFDX2(1),DFDY2(1),DFDZ2(1),VLMT2   )
C
C ----- CALCUL DU TERME DE COUPLAGE : (VLMT1)T * VLMT2
C
      DO 10 JAUX = 1, NBINC2
         DO 20 IAUX = 1, NBINC1
            MCPLB(IAUX,JAUX) = POIDS*VLMT1(1,IAUX)*VLMT2(1,JAUX)
            DO 30 KAUX = 2, NBSIGM
               MCPLB(IAUX,JAUX) = MCPLB(IAUX,JAUX)
     &                  +POIDS*VLMT1(KAUX,IAUX)*VLMT2(KAUX,JAUX)
 30         CONTINUE
 20      CONTINUE
 10   CONTINUE
C
C --- CALCUL DES TERMES DE COUPLAGE
C
      DO 99 KPGS = 2,NPGS
C
        MTL1    = NDML1*(KPGS-1)+1
        MTL2    = NDML2*(KPGS-1)+1

        POIDS = POIJCS(KPGS)
C
C ----- CALCUL DE LA MATRICE VLMT1
C
        CALL BMATFR(NDIM     ,NBSIGM   ,NDML1      ,
     &              DFDX1(MTL1),DFDY1(MTL1),DFDZ1(MTL1),VLMT1   )
C
C ----- CALCUL DE LA MATRICE VLMT2
C
        CALL BMATFR(NDIM     ,NBSIGM   ,NDML2      ,
     &              DFDX2(MTL2),DFDY2(MTL2),DFDZ2(MTL2),VLMT2   )
C
C ----- CALCUL DU TERME DE COUPLAGE : (VLMT1)T * VLMT2
C
        DO 130 JAUX = 1, NBINC2
           DO 140 IAUX = 1, NBINC1
              DO 150 KAUX = 1, NBSIGM
                 MCPLB(IAUX,JAUX) = MCPLB(IAUX,JAUX)
     &                  + POIDS*VLMT1(KAUX,IAUX)*VLMT2(KAUX,JAUX)
 150          CONTINUE
 140       CONTINUE
 130    CONTINUE
C
 99   CONTINUE
C
      END
