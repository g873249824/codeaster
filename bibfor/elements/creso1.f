      SUBROUTINE CRESO1(SOLVEU,METHOD,PRECO,RENUM,SYME,SDFETI,EPS,
     &                  RESIRE,TBLOC,NPREC,NMAXIT,ISTOP,NIREMP,IFM,
     &                  NUMSD,NBMA,VERIF,TESTCO,NBREOR,TYREOR,SCALIN,
     &                  INUMSD,IMAIL,NOMSD,INFOFE,STOGI,TESTOK,NBREOI,
     &                  ACMA,ACSM,REACRE)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 04/04/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21
C-----------------------------------------------------------------------
C    - FONCTION REALISEE:  CREATION DES OBJETS JEVEUX DE LA STRUCTURE
C      DE DONNEES SOLVEUR. UTILITAIRE DE BAS NIVEAU LANCE PAR CRESOL.
C-----------------------------------------------------------------------
C IN SOLVEU   : NOM DE LA SD SOLVEUR
C IN METHOD....NIREMP : SES PARAMETRES, POUR SIGNIFICATION VOIR CRESOL.
C IN IFM     : PARAMETRE POUR MONITORING.
C IN NUMSD   : NUMERO DE SOUS-DOMAINE
C IN NBMA    : NBRE DE MAILLES
C IN VERIF   : PARAMETRE POUR DEMANDER LA VERIFICATION DE SDFETI
C IN TESTCO  : PARAMETRE POUR TESTER LA CONTINUITE
C IN NBREOR/TYREOR : PARAMETRES POUR LA REOTHOGONALISATION DES
C              DIRECTIONS DE DESCENTE AVEC FETI
C IN SCALIN  : PARAMETRE DE SCALING POUR LE PRECONDITIONNEMENT FETI
C IN INUMSD : ADRESSE OBJET JEVEUX '&FETI.MAILLE.NUMSD' DESIGNANT
C   LE NUMERO DE SOUS-DOMAINE D'APPARTENANCE D'UNE MAILLE PHYSIQUE DU
C   MODELE DONNEE
C IN IMAIL   : ADRESSE JEVEUX DE L'OBJET MODELE//'.MAILLE'
C IN NOMSD   : NOM DU SOUS-DOMAINE
C IN INFOFE  : NIVEAU DE MONITORING POUR FETI
C IN STOGI   : PARAMETRE DE STOCKAGE DE LA MATRICE GI
C IN TESTOK  : FLAG SUR LA MISE EN OEUVRE DE TESTS DE COHERENCE
C IN NBREOI  : NBRE DE PAS DE TEMPS UTLILISES POUR REORTHO SI LIST_INST
C IN ACMA/ACSM : ACCELERATION POUR PB A MULTIPLES MATRICES ET
C               SECONDS MEMBRES
C IN REACRE  : FREQUENCE DE REACTUALISATION DU RESIDU
C----------------------------------------------------------------------
C RESPONSABLE BOITEAU O.BOITEAU
C CORPS DU PROGRAMME
      IMPLICIT NONE

C DECLARATION PARAMETRES D'APPELS
      INTEGER NPREC,NMAXIT,ISTOP,NIREMP,IFM,NUMSD,NBMA,NBREOR,INUMSD,
     &        IMAIL,NBREOI,REACRE
      REAL*8 EPS,RESIRE,TBLOC,TESTCO
      CHARACTER*3 SYME
      CHARACTER*8 METHOD,PRECO,RENUM,VERIF,TYREOR,SCALIN,NOMSD,STOGI,
     &            ACMA,ACSM
      CHARACTER*19 SOLVEU
      CHARACTER*24 SDFETI,INFOFE
      LOGICAL TESTOK

C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------
      INTEGER ZI
      INTEGER VALI(2)
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --- FIN DECLARATIONS NORMALISEES JEVEUX --------------------

C DECLARATION VARIABLES LOCALES
      INTEGER ISLVK,ISLVR,ISLVI,I,NBMASD,IFETA,IBUFF,NBCHA,IFLII,IFLIN,
     &        IFLIM,INUMCH,DECAL,J,IRET,IFREF
      CHARACTER*2 K2BID
      CHARACTER*3 K3BID
      CHARACTER*8 K8BID
      CHARACTER*24 K24B,NOMLOG,SDFETA
      CHARACTER*32 JEXNOM,JEXNUM

C------------------------------------------------------------------
      CALL JEMARQ()

C --------------------------------------------------------------
C CREATION DES DIFFERENTS ATTRIBUTS DE LA S.D. SOLVEUR
C --------------------------------------------------------------
      CALL WKVECT(SOLVEU(1:19)//'.SLVK','V V K24',11,ISLVK)
      CALL WKVECT(SOLVEU(1:19)//'.SLVR','V V R',4,ISLVR)
      CALL WKVECT(SOLVEU(1:19)//'.SLVI','V V I',7,ISLVI)

C --------------------------------------------------------------
C REMPLISSAGE DE LA SD SOLVEUR
C --------------------------------------------------------------
      ZK24(ISLVK)    = METHOD
      ZK24(ISLVK+1)  = PRECO
      ZK24(ISLVK+2)  = VERIF
      ZK24(ISLVK+3)  = RENUM
      ZK24(ISLVK+4)  = SYME
      ZK24(ISLVK+5)  = SDFETI
      ZK24(ISLVK+6)  = TYREOR
      ZK24(ISLVK+7)  = SCALIN
      ZK24(ISLVK+8)  = STOGI
      ZK24(ISLVK+9)  = ACMA
      ZK24(ISLVK+10) = ACSM

      ZR(ISLVR)   = EPS
      ZR(ISLVR+1) = RESIRE
      ZR(ISLVR+2) = TBLOC
      ZR(ISLVR+3) = TESTCO

      ZI(ISLVI)   = NPREC
      ZI(ISLVI+1) = NMAXIT
      ZI(ISLVI+2) = ISTOP
      ZI(ISLVI+3) = NIREMP
      ZI(ISLVI+4) = NBREOR
      ZI(ISLVI+5) = NBREOI
      ZI(ISLVI+6) = REACRE


C --------------------------------------------------------------
C SI ON UTILISE FETI
C --------------------------------------------------------------
C MONITORING
      IF (METHOD(1:4).EQ.'FETI') THEN
        IF (INFOFE(1:1).EQ.'T') THEN
          IF (NUMSD.EQ.0) THEN
            WRITE (IFM,*) '<FETI/CRESO1> DOMAINE GLOBAL'

          ELSE
            WRITE (IFM,*) '<FETI/CRESO1> SD: ',NUMSD
          END IF
        END IF
        IF (INFOFE(2:2).EQ.'T') CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,
     &                               SOLVEU(1:19),1,'V')
      END IF

C REMPLISSAGE DU VECTEUR AUXILIAIRE '&FETI.MAILLE.NUMSD'
      IF (NUMSD.NE.0) THEN
        SDFETA = SDFETI(1:19)//'.FETA'
        CALL JELIRA(JEXNUM(SDFETA,NUMSD),'LONMAX',NBMASD,K8BID)
        CALL JEVEUO(JEXNUM(SDFETA,NUMSD),'L',IFETA)
        DO 10 I = 1,NBMASD
          IBUFF = ZI(IFETA+I-1)
          IF (ZI(INUMSD+IBUFF-1).GT.0) THEN
C MAILLE COMMUNE A PLUSIEURS SOUS-DOMAINES
                VALI (1) = IBUFF
                VALI (2) = 0
            CALL U2MESG('F', 'ELEMENTS4_75',0,' ',2,VALI,0,0.D0)

          ELSE
            ZI(INUMSD+IBUFF-1) = NUMSD
          END IF
   10   CONTINUE

      ELSE IF ((NUMSD.EQ.0) .AND. (METHOD(1:4).EQ.'FETI')) THEN
        IF (INFOFE(2:2).EQ.'T') THEN
          NOMLOG = '&FETI.MAILLE.NUMSD'
          CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,NOMLOG(1:19),1,'V')
        END IF
        IF (TESTOK) THEN
C VERIFICATION QUE TOUTES LES MAILLES PHYSIQUES DU MODELE SONT DANS UN
C SOUS-DOMAINE SI ON EST EN SEQUENTIEL
          DO 20 I = 1,NBMA
            IBUFF = ZI(INUMSD+I-1)
            IF (IBUFF.LT.0) THEN
C MAILLE PAS DANS LA PARTITION FETI
              IF (ZI(IMAIL-1+I).NE.0) THEN
C MAILLE POURTANT DANS LE MODELE
                VALI (1) = I
                VALI (2) = 0
                CALL U2MESG('F', 'ELEMENTS4_76',0,' ',2,VALI,0,0.D0)
              END IF
            END IF
   20     CONTINUE
        END IF
      END IF

C FIN ------------------------------------------------------
      CALL JEDEMA()
      END
