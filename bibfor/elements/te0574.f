      SUBROUTINE TE0574(OPTION,NOMTE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 07/11/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS
C
      IMPLICIT      NONE
      CHARACTER*16  NOMTE,OPTION
C
C ----------------------------------------------------------------------
C
C ROUTINE CALCUL ELEMENTAIRE
C
C CALCUL DES RIGIDITES ELEMENTAIRES EN MECANIQUE CORRESPONDANT A UN
C CHARGEMENT EN PRESSION SUIVEUSE SUR DES ARETES D'ELEMENTS
C ISOPARAMETRIQUES 2D
C
C LA PRESSION EST UNE FONCTION RELLE
C
C ----------------------------------------------------------------------
C
C
C IN  OPTION : OPTION DE CALCUL
C               RIGI_MECA_PRSU_F
C IN  NOMTE  : NOM DU TYPE ELEMENT
C
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER       MXNOEU,MXVECT,MXMATR
      PARAMETER     (MXNOEU=3,MXVECT=2*3,MXMATR=2*2*3*3)
      INTEGER       MXPARA
      PARAMETER     (MXPARA=3)
C
      CHARACTER*8   NOMPAR(MXPARA)
      REAL*8        VALPAR(MXPARA)
      INTEGER       IER
      REAL*8        X,Y
C
      LOGICAL       LTEATT,LAXI
      INTEGER       NDIM,NNO,NPG,NNOS,NDDL
      INTEGER       IDDL,INO,IPG
      INTEGER       JPOIDS,JVF,JDF,JGANO
      INTEGER       JMATR
      INTEGER       JGEOM,JDEPM,JDEPP,JPRES,JTIME
      INTEGER       KDEC,KK,I,J
C
      REAL*8        PR(2,4)
      REAL*8        VECT(MXVECT),MATR(MXMATR)
C
C ----------------------------------------------------------------------
C
C
C --- CARACTERISTIQUES ELEMENT
C
      CALL ELREF4(' '   ,'RIGI',NDIM  ,NNO   ,NNOS  ,
     &            NPG   ,JPOIDS,JVF   ,JDF   ,JGANO )
      NDDL = 2*NNO
      CALL ASSERT(NNO.LE.MXNOEU)
      CALL ASSERT(NDIM.LE.2)
      CALL ASSERT(NPG.LE.4)
      LAXI = LTEATT(' ','AXIS','OUI')
C
C --- ACCES CHAMPS
C
      CALL JEVECH('PGEOMER','L',JGEOM)
      CALL JEVECH('PDEPLMR','L',JDEPM)
      CALL JEVECH('PDEPLPR','L',JDEPP)
      CALL JEVECH('PPRESSF','L',JPRES)
      CALL JEVECH('PTEMPSR','L',JTIME)
      CALL JEVECH('PMATUNS','E',JMATR)
C
C --- REACTUALISATION DE LA GEOMETRIE PAR LE DEPLACEMENT
C
      DO 10 IDDL = 1,NDDL
        ZR(JGEOM+IDDL-1) = ZR(JGEOM+IDDL-1) + 
     &                     ZR(JDEPM+IDDL-1) + 
     &                     ZR(JDEPP+IDDL-1)
 10   CONTINUE
C
C --- CALCUL DE LA PRESSION AUX POINTS DE GAUSS (FONCTION)
C 
      NOMPAR(1) = 'X'
      NOMPAR(2) = 'Y'
      NOMPAR(3) = 'INST'
      VALPAR(3) = ZR(JTIME)
C
      DO 100 IPG = 1,NPG
        KDEC = (IPG-1) * NNO
        X = 0.D0
        Y = 0.D0
        DO 105 INO = 1,NNO
          X = X + ZR(JGEOM+NDIM*(INO-1)+1-1) * ZR(JVF+KDEC+INO-1)
          Y = Y + ZR(JGEOM+NDIM*(INO-1)+2-1) * ZR(JVF+KDEC+INO-1)
 105    CONTINUE
        VALPAR(1) = X
        VALPAR(2) = Y
        CALL FOINTE('FM',ZK8(JPRES)  ,MXPARA,NOMPAR,VALPAR,
     &              PR(1,IPG),IER)
        CALL FOINTE('FM',ZK8(JPRES+1),MXPARA,NOMPAR,VALPAR,
     &              PR(2,IPG),IER)
 100  CONTINUE
C
C --- CALCUL EFFECTIF DE LA RIGIDITE
C
      CALL NMPR2D(NDIM  ,LAXI  ,2     ,NNO      ,NPG   ,
     &            JPOIDS,JVF   ,JDF   ,ZR(JGEOM),PR    ,
     &            VECT  ,MATR  )
C
C --- RECOPIE DE LA MATRICE ELEMENTAIRE (NON-SYMETRIQUE SAUF AXI)
C
      IF (LAXI) THEN
        KK = 0
        DO 50 J = 0,NDDL - 1
          DO 40 I = 0,J
            ZR(JMATR+KK) = (MATR(J*NDDL+I)+MATR(I*NDDL+J))/2
            KK = KK + 1
   40     CONTINUE
   50   CONTINUE      
      ELSE
        DO 110 IDDL=1,NDDL*NDDL
          ZR(JMATR+IDDL-1) = MATR(IDDL)
  110   CONTINUE 
      ENDIF
      END
