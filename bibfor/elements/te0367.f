      SUBROUTINE TE0367(OPTION,NOMTE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 21/10/2008   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT   NONE
      CHARACTER*16 OPTION,NOMTE
C
C ----------------------------------------------------------------------
C  CALCUL DES SECONDS MEMBRES DE CONTACT ET DE FROTTEMENT DE COULOMB STD
C        AVEC LA METHODE CONTINUE DE L'ECP
C  OPTION : 'CHAR_MECA_CONT' (CALCUL DU SECOND MEMBRE DE CONTACT)
C           'CHAR_MECA_FROT' (CALCUL DU SECOND MEMBRE DE
C                              FROTTEMENT STANDARD )
C
C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      INTEGER I,J,IN,PL,NFAES,NFAMA
      INTEGER NNE,NNES,NNM,NNC,NDDL,NDDLC,NDIM,INDCO
      INTEGER IGEOM,IDEPL,JPCPO,IGEOMT,IGEOES,IGEOMA
      INTEGER IDEPM,JPCPI,JPCAI,JPCCF,IAINES,IAINMA
      INTEGER INDNOR,IAXIS,IFROTT,IFACES,IFACMA
      INTEGER IPINES,IPINMA,IVECT,XOULA
      INTEGER IBID,CFACE(5,3)
      REAL*8 COEFCA
      REAL*8 XPR,YPR,XPC,YPC,HPG,JEU,XES(3),XMA(3)
      REAL*8 VTMP(81),TAU1(3),TAU2(3),NORM(3),NOOR
      REAL*8 GEOMM(3),GEOME(3),RBID,R8PREM
      REAL*8 DEPLME(6),DEPLM(4),DEPLE(6),DEPLMM(4)
      REAL*8 FFPC(9),DFFPC(2,9),DDFFPC(3,9),JACOBI
      REAL*8 FFPR(9),DFFPR(2,9),DDFFPR(3,9)
      REAL*8 FFES(9)
      REAL*8 FFMA(9)
      REAL*8 RBID4(4),RBID43(4,3),DFDI(8,3)
      REAL*8 F(3,3),EPS(6),GRAD(3,3)
      CHARACTER*8 ESC,MAIT,ELC,ESQ
      CHARACTER*19 GEOM2,PINTES,PINTMA,COORDE,COORDM
      CHARACTER*19 AINTES,AINTMA,CFACES,CFACMA
      INTEGER INADH
      REAL*8 LAMBDA,COEFFF,COEFFA
      REAL*8 DPLM(6),DPLE(6),MPROJ(3,3),RESE(3),NRESE
C
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INFOS SUR LA MAILLE DE CONTACT
C
      CALL XMELET(NOMTE,
     &            NDIM,NDDL,NDDLC,ESC,ESQ,NNE,MAIT,NNM,ELC,NNC)

      CALL ASSERT (NDDL.LE.81)
C --- INITIALISATIONS

      DO 6 I=1,6
        DPLM(I) = 0.D0
        DPLE(I) = 0.D0
  6     CONTINUE
      DO 7 I=1,5
        DO 8 J=1,3
          CFACE(I,J)=0
  8     CONTINUE
  7   CONTINUE

       DO 9 I = 1,NDIM
        GEOME(I)  = 0.D0
        GEOMM(I)  = 0.D0
        XES(I)    = 0.D0
        XMA(I)    = 0.D0
 9    CONTINUE
C
C --- INITIALISATION DU VECTEUR DE TRAVAIL
C
      DO 10  J = 1,NDDL
        VTMP(J) = 0.D0
  10  CONTINUE

      GEOM2 ='&&TE0367.GEOM2'
      PINTES='&&TE0367.PINES'
      PINTMA='&&TE0367.PINMA'
      COORDE='&&TE0367.GEOME'
      COORDM='&&TE0367.GEOMM'
      AINTES='&&TE0367.AINTE'
      AINTMA='&&TE0367.AINTM'
      CFACES='&&TE0367.CFACE'
      CFACMA='&&TE0367.CFACM'
C
C --RECUPERATION DES DONNEES DE LA CARTE CONTACT 'POINT' (VOIR MMCART)
      CALL JEVECH('PCAR_PT','L',JPCPO)
      XPC      = ZR(JPCPO-1+1)
      YPC      = ZR(JPCPO-1+10)
      XPR      = ZR(JPCPO-1+2)
      YPR      = ZR(JPCPO-1+3)
      TAU1(1)  = ZR(JPCPO-1+4)
      TAU1(2)  = ZR(JPCPO-1+5)
      TAU1(3)  = ZR(JPCPO-1+6)
      TAU2(1)  = ZR(JPCPO-1+7)
      TAU2(2)  = ZR(JPCPO-1+8)
      TAU2(3)  = ZR(JPCPO-1+9)
      INDCO    = NINT(ZR(JPCPO-1+11))
      LAMBDA   = ZR(JPCPO-1+12)
      COEFCA   = ZR(JPCPO-1+13)
      COEFFA   = ZR(JPCPO-1+14)
      COEFFF   = ZR(JPCPO-1+15)
      IFROTT   = NINT(ZR(JPCPO-1+16))
      INDNOR   = NINT(ZR(JPCPO-1+17))
      IAXIS    = NINT(ZR(JPCPO-1+18))
      HPG      = ZR(JPCPO-1+19)

C---LES NUMEROS DES FACETTES DE CONTACT (ESCLAVE ET MAITRE) DONT LE
C---PTC ET SON PROJETE APPARTIENT
      NFAES    = NINT(ZR(JPCPO-1+31))
      NFAMA    = NINT(ZR(JPCPO-1+32))
C---------------------------------------------------------------------
C      IMA      = NINT(ZR(JPCPO-1+34))

C --RECUPERATION DES DONNEES DE LA CARTE CONTACT 'PINTER' (VOIR MMCART)
      CALL JEVECH('PCAR_PI','L',JPCPI)

C --RECUPERATION DES DONNEES DE LA CARTE CONTACT 'AINTER' (VOIR MMCART)
      CALL JEVECH('PCAR_AI','L',JPCAI)

C --RECUPERATION DES DONNEES DE LA CARTE CONTACT 'CFACE' (VOIR MMCART)
      CALL JEVECH('PCAR_CF','L',JPCCF)
C
C --- RECUPERATION DE LA GEOMETRIE ET DES CHAMPS DE DEPLACEMENT
C
      CALL JEVECH('PGEOMER','E',IGEOM)
      CALL JEVECH('PDEPL_P','E',IDEPL)
      CALL JEVECH('PDEPL_M','L',IDEPM)

C----ON CREE UN CHAMP 'LOCAL' DE LA GÉOMETRIE (POUR MANIPULER PAR LA
C----SUITE LES NOEUDS FICTIFS)

      CALL WKVECT(GEOM2,'V V R',NDIM*(NNE+NNM),IGEOMT)
      DO 50 I=1,NDIM*(NNE+NNM)
        ZR(IGEOMT+I-1)=ZR(IGEOM+I-1)
  50  CONTINUE

C-----NOMBRE DE NOEUDS ESCLAVES SOMMETES(EN 3D IL FAUDRA MODIFIER ICI)
      NNES=NNE/2
C
C --- REACTUALISATION DE LA GEOMETRIE
C
      CALL XMREAC(NDIM,NNES,NNM,
     &            IGEOMT,IDEPM)

C --- FONCTIONS DE FORMES ET DERIVEES POUR LE
C --- POINT DE CONTACT
C
      CALL MMFONF(NDIM,NNC,ELC,XPC,YPC,FFPC,DFFPC,DDFFPC)

C --- FONCTIONS DE FORMES ET DERIVEES POUR LA
C --- PROJECTION DU POINT DE CONTACT DANS 'ELC'
C
      CALL MMFONF(NDIM,NNC,ELC,XPR,YPR,FFPR,DFFPR,DDFFPR)
C
C----CONSTRUCTION DU CHAMP DES COORDONNÉES GÉOMETRIQUE POUR LA
C----MAILLE ESCLAVE (SEULEMENT POUR LES NOEUDS SOMMETS)

      CALL WKVECT(COORDE,'V V R',NDIM*NNES,IGEOES)
      DO 60 I=1,NNES
        DO 70 J=1,NDIM
          ZR(IGEOES-1+(I-1)*NDIM+J)=ZR(IGEOMT-1+(I-1)*NDIM+J)
 70     CONTINUE
 60   CONTINUE

C----CONSTRUCTION DU CHAMP DES COORDONNÉES GÉOMETRIQUE POUR LA
C----MAILLE MAITRE

      CALL WKVECT(COORDM,'V V R',NDIM*NNM,IGEOMA)
      DO 80 I=1,NNM
        DO 90 J=1,NDIM
          ZR(IGEOMA-1+(I-1)*NDIM+J)=
     &              ZR(IGEOMT-1+NDIM*NNE+(I-1)*NDIM+J)
 90     CONTINUE
 80   CONTINUE

C----CONSTRUCTION DES CHAMPS AINTES ET AINTMA (INFO SUR LES
C----ARRETES COUPÉS DANS LA MAILLE ESCLAVE, RESPECTIVEMENT MAITRE )

      CALL WKVECT(AINTES,'V V R',24,IAINES)
      CALL WKVECT(AINTMA,'V V R',24,IAINMA)
      DO 81 I=1,24
        ZR(IAINES-1+I)= ZR(JPCAI-1+I)
        ZR(IAINMA-1+I)= ZR(JPCAI-1+24+I)
 81   CONTINUE

C----CONSTRUCTION DES CHAMPS CFACES ET CFACMA (INFO SUR LA
C----CONNECTIVITÉ DES FACETTES DANS LA MAILLE ESCLAVE,
C----RESPECTIVEMENT MAITRE )

      CALL WKVECT(CFACES,'V V R',24,IFACES)
      CALL WKVECT(CFACMA,'V V R',24,IFACMA)
      DO 82 I=1,15
        ZR(IFACES-1+I)= ZR(JPCCF-1+I)
        ZR(IFACMA-1+I)= ZR(JPCCF-1+15+I)
 82   CONTINUE

C----ON CONSTRUIT LA MATRICE DE CONNECTIVITÉ CFACE (MAILLE ESCLAVE)
C--!!!ATTENTION, POUR L'INSTANT CE QUI SUIT EST VALABLE QUE EN 2D!!!!
C      NFACE=1
      CFACE(1,1)=1
      CFACE(1,2)=2
C      DO 83 I=1,NFACE
C        DO 84 J=1,NDIM
C          CFACE(I,J)=NINT(ZR(IFACES-1+NDIM*(I-1)+J))
C 84     CONTINUE
C 83   CONTINUE

C ---CALCUL DES COORDONNÉES RÉELES DES POINTS D'INTERSECTION ESCLAVES
      CALL WKVECT(PINTES,'V V R',NDIM*NDIM,IPINES)
      DO 100 I=1,NDIM*NDIM
        ZR(IPINES-1+I)=0.D0
 100  CONTINUE
      CALL XMPINT(ESC,NDIM,IGEOES,JPCPI,
     &               NFAES,1,NNES,IPINES)

C ---CALCUL DES COORDONNÉES RÉELES DES POINTS D'INTERSECTION MAITRES
      CALL WKVECT(PINTMA,'V V R',NDIM*NDIM,IPINMA)
      DO 110 I=1,NDIM*NDIM
        ZR(IPINMA-1+I)=0.D0
 110  CONTINUE
      CALL XMPINT(MAIT,NDIM,IGEOMA,JPCPI,
     &               NFAMA,2,NNM,IPINMA)
C
C --- JACOBIEN POUR LE POINT DE CONTACT
C
      CALL MMMJAC(ELC   ,IPINES,FFPC  ,DFFPC ,IAXIS ,
     &            NDIM  ,JACOBI)

C----CALCUL DES COORDONNÉES RÉELES DU POINT DE CONTACT
      DO 120 I = 1,NDIM
        DO 130 J = 1,NNC
          GEOME(I) = GEOME(I) +
     &               FFPC(J)*ZR(IPINES-1+(J-1)*NDIM+I)
 130    CONTINUE
 120  CONTINUE

C----CALCUL DES COORDONNÉES RÉELES DU PROJETE DU POINT DE CONTACT
      DO 140 I = 1,NDIM
        DO 150 J = 1,NNC
          GEOMM(I) = GEOMM(I) +
     &               FFPR(J)*ZR(IPINMA-1+(J-1)*NDIM+I)
 150    CONTINUE
 140  CONTINUE

C---- FONCTIONS DE FORME POUR LE PROJETE DU POINT DE CONTACT
C-----DANS LA MAILLE MAITRE

      CALL REEREF(MAIT,NNM,IGEOMA,GEOMM,RBID,.FALSE.,NDIM,RBID,
     &            IBID,IBID,IBID,RBID4,RBID43,'NON',XMA,FFMA,DFDI,
     &            F,EPS,GRAD)

C---- FONCTIONS DE FORME POUR LE POINT DE CONTACT DANS LA MAILLE ESCLAVE

      CALL REEREF(ESC,NNES,IGEOES,GEOME,RBID,.FALSE.,NDIM,RBID,
     &            IBID,IBID,IBID,RBID4,RBID43,'NON',XES,FFES,DFDI,
     &            F,EPS,GRAD)
C
C --- CALCUL DE LA NORMALE (ARRET BRUTAL SI NUL ?)
C
      CALL MMNORM(NDIM,TAU1,TAU2,NORM,NOOR)
      IF (NOOR.LT.R8PREM()) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- CALCUL DES DEPLACEMENTS GEOMETRIQUES DU POINT DE CONTACT ET
C --- DE SON PROJETE
C
      DO 160 I = 1,2*NDIM
        DEPLM(I)  = 0.D0
        DEPLMM(I) = 0.D0
 160  CONTINUE
      DO 170 I = 1,3*NDIM
        DEPLE(I)  = 0.D0
        DEPLME(I) = 0.D0
 170  CONTINUE

      DO 180 I = 1,2*NDIM
        DO 190 J = 1,NNES
          DEPLE(I)  = DEPLE(I)  +
     &                FFES(J)*ZR(IDEPL+(J-1)*(3*NDIM)+I-1)
          DEPLME(I) = DEPLME(I) +
     &                FFES(J)*ZR(IDEPM+(J-1)*(3*NDIM)+I-1)
 190    CONTINUE
 180  CONTINUE

      DO 200 I = 1,2*NDIM
        DO 210 J = 1,NNM
          DEPLM(I)  = DEPLM(I)  +
     &     FFMA(J)*ZR(IDEPL+NNES*(4*NDIM)+(J-1)*2*NDIM+I-1)
          DEPLMM(I) = DEPLMM(I) +
     &     FFMA(J)*ZR(IDEPM+NNES*(4*NDIM)+(J-1)*2*NDIM+I-1)
 210    CONTINUE
 200  CONTINUE

C----CALCUL DES REACTIONS DE CONTACT (PRESSION ET FROTTEMENT)

      DO 220 I=1,NNC
C---!!!TYPE DE MAILLE QUADRATIQUE À INTRODUIRE DANS XOULA!!!!
        IN=XOULA(CFACE,NFAES,I,IAINES,ESQ)
        CALL XPLMA2(NDIM,NNE,NNES,IN,PL)
        DEPLE(2*NDIM+1)=DEPLE(2*NDIM+1)+FFPC(I)*ZR(IDEPL-1+PL)
        DEPLE(2*NDIM+2)=DEPLE(2*NDIM+2)+FFPC(I)*ZR(IDEPL-1+PL+1)
        DEPLME(2*NDIM+1)=DEPLME(2*NDIM+1)+FFPC(I)*ZR(IDEPM-1+PL)
        DEPLME(2*NDIM+2)=DEPLME(2*NDIM+2)+
     &                   FFPC(I)*ZR(IDEPM-1+PL+1)
 220  CONTINUE
 

C --- NOEUDS EXCLUS PAR PROJECTION HORS ZONE
C
      IF (INDNOR .EQ. 1) THEN
        INDCO = 0
      ENDIF
C
C --- RECUPERATION DES VECTEURS 'OUT' (A REMPLIR => MODE ECRITURE)
C
      CALL JEVECH('PVECTUR','E',IVECT)
C
C --- CALCUL DES SECOND MEMBRES DE CONTACT/FROTTEMENT

      IF (OPTION.EQ.'CHAR_MECA_CONT') THEN
C
        IF (INDCO.EQ.1) THEN
C
C --- VECTEUR SECOND MEMBRE SI CONTACT
          CALL XMMJEU(NDIM,NORM,GEOME,GEOMM,DEPLE,DEPLM,
     &                DEPLME,DEPLMM,JEU,RBID)

          CALL XMVEC1(NDIM,NNE,NNES,NNC,NNM,HPG,NFAES,
     &                FFPC,FFES,FFMA,JACOBI,DEPLE,DEPLME,
     &                IAINES,CFACE,COEFCA,JEU,NORM,ESQ,VTMP)

        ELSEIF (INDCO.EQ.0) THEN
C
C --- CALCUL DU VECTEUR - CAS SANS CONTACT
C
          CALL XMVEC0(NDIM,NNE,NNES,NNC,NFAES,DEPLE,DEPLME,
     &                HPG,FFPC,JACOBI,CFACE,IAINES,
     &                COEFCA,ESQ,VTMP)
        ELSE
          CALL U2MESS('F','ELEMENTS3_80')
        ENDIF

      ELSEIF (OPTION.EQ.'CHAR_MECA_FROT') THEN
C              ---------------------
        IF (COEFFF.EQ.0.D0) INDCO = 0
        IF (LAMBDA.EQ.0.D0) INDCO = 0
        IF (IFROTT.NE.3)    INDCO = 0

        IF (INDCO.EQ.0) THEN
C
C --- CALCUL DU VECTEUR - CAS SANS FROTTEMENT
C
          CALL XMVEF0(NDIM,NNC,NNE,NNES,NFAES,IAINES,
     &                HPG,FFPC,JACOBI,DEPLE,CFACE,
     &                ESQ,TAU1,TAU2,VTMP)

        ELSE IF (INDCO.EQ.1) THEN
C
C
C ---  ON CALCULE L'ETAT DE CONTACT ADHERENT OU GLISSANT
C
      DO 230 I = 1,NDIM
          DPLM(I)  = DEPLM(I) + DEPLM(I+NDIM)
          DPLE(I) = DEPLE(I) - DEPLE(I+NDIM)
 230  CONTINUE
      DO 240 I = 1,NDIM
          DPLE(I+NDIM)  = DEPLE(I+2*NDIM)
 240  CONTINUE
          CALL TTPRSM (NDIM,DPLE,DPLM,1,COEFFA,0.D0,0.D0,
     &                 NORM,TAU1,TAU2,INADH,MPROJ,RESE,NRESE)     
C
C --- SI GLISSANT, NORMALISATION RESE
C    
          IF (INADH.EQ.0)  CALL NORMEV(RESE,NRESE)
C
C --- VECTEUR FROTTEMENT
C
          CALL XMVEF1 (NDIM,NNE,NNES,NNC,NNM,NFAES,CFACE,
     &                 HPG,FFPC,FFES,FFMA,JACOBI,IAINES,    
     &                 LAMBDA,COEFFA,COEFFF,TAU1,TAU2,
     &                 RESE,MPROJ,DEPLE,ESQ,VTMP)
        ENDIF

      ELSE
C       SI OPTION NI 'CHAR_MECA_CONT' NI 'CHAR_MECA_FROT'
        CALL ASSERT(OPTION.EQ.'CHAR_MECA_FROT' .OR.
     &              OPTION.EQ.'CHAR_MECA_CONT')

      ENDIF

C
C --- RECOPIE VALEURS FINALES
C
      DO 250 I=1,NDDL
        ZR(IVECT-1+I)=VTMP(I)
 250  CONTINUE
C
      CALL JEDETR(GEOM2)
      CALL JEDETR(PINTES)
      CALL JEDETR(PINTMA)
      CALL JEDETR(COORDE)
      CALL JEDETR(COORDM)
      CALL JEDETR(AINTES)
      CALL JEDETR(AINTMA)
      CALL JEDETR(CFACES)
      CALL JEDETR(CFACMA)
      CALL JEDEMA()
      END
