      SUBROUTINE CHAUXI(NDIM,MU,KA,R,T,INVP,LCOUR,COURB,
     &                  DU1DM,DU2DM,DU3DM,U1L,U2L,U3L)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 21/08/2007   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT

      IMPLICIT NONE
      INTEGER  NDIM
      REAL*8   MU,KA,R,T,INVP(3,3),COURB(3,3,3)
      REAL*8   DU1DM(3,3),DU2DM(3,3),DU3DM(3,3)
      REAL*8   DU1DME(3,4),DU2DME(3,4),DU3DME(3,4)
      REAL*8   U1L(3),U2L(3),U3L(3)
      LOGICAL  LCOUR

C     BUT : CALCUL DES CHAMPS AUXILIAIRES ET DE LEURS DÉRIVÉES EN (R,T)
C       (POUR LE CALCUL ENERGÉTIQUE DES SIFS EN MÉCANIQUEDE LA RUPTURE)

C           
C   IN
C     MU    :  2ÈME COEFFICIENT DE LAMÉ
C     KA    :  KAPPA
C     R,T   :  COORDONNÉES POLAIRES DU POINT  
C     INVP  :  INVERSE DE LA MATRICE DE PASSAGE LOCALE GLOBALE
C     LCOUR :  PRISE EN COMPTE DE LA COURBURE DE LA BASE LOCALE
C     COURB :  COURBURE DE LA BASE LOCALE

C   OUT
C     DU1DM :  DÉRIVÉES DU CHAMP SINGULIER AUXILIAIRE 1
C     DU2DM :  DÉRIVÉES DU CHAMP SINGULIER AUXILIAIRE 2
C     DU3DM :  DÉRIVÉES DU CHAMP SINGULIER AUXILIAIRE 3
C     U1L   :  CHAMP SINGULIER AUXILIAIRE 1 (DANS LA BASE LOCALE)
C     U2L   :  CHAMP SINGULIER AUXILIAIRE 2 (DANS LA BASE LOCALE)
C     U3L   :  CHAMP SINGULIER AUXILIAIRE 3 (DANS LA BASE LOCALE)

      INTEGER  I,J,K,L
      REAL*8   DU1DPO(3,2),DU2DPO(3,2),DU3DPO(3,2)
      REAL*8   DU1DL(3,3),DU2DL(3,3),DU3DL(3,3),CR1,CR2,R8DEPI,R8PREM
  
      
C     COEFFS  DE CALCUL
      CR1=1.D0/(4.D0*MU*SQRT(R8DEPI()*R))
      CR2=SQRT(R/R8DEPI())/(2.D0*MU)

C-----------------------------------------------------------------------
C     DÉFINITION DU CHAMP SINGULIER AUXILIAIRE U1 ET DE SA DÉRIVÉE
C-----------------------------------------------------------------------
C     CHAMP SINGULIER AUXILIAIRE U1 DANS LA BASE LOCALE
      U1L(1)=CR2*COS(T*0.5D0)*(KA-COS(T))
      U1L(2)=CR2*SIN(T*0.5D0)*(KA-COS(T))
      U1L(3)=0.D0     

C     MATRICE DES DÉRIVÉES DE U1 DANS LA BASE POLAIRE (3X2)

C     DERIVÉES PAR RAPPORT À R (RAYON) DE U1 
      DU1DPO(1,1)=CR1*(COS(T*0.5D0)*(KA-COS(T)))
      DU1DPO(2,1)=CR1*(SIN(T*0.5D0)*(KA-COS(T)))
      DU1DPO(3,1)=0.D0
      
C     DERIVÉES PAR RAPPORT À T (THETA) DE U1      
      DU1DPO(1,2)=CR2*(-0.5D0*SIN(T*0.5D0)*(KA-COS(T))
     &                                 + COS(T*0.5D0)*SIN(T))
      DU1DPO(2,2)=CR2*(0.5D0*COS(T*0.5D0)*(KA-COS(T))
     &                                 + SIN(T*0.5D0)*SIN(T))
      DU1DPO(3,2)=0.D0

C     MATRICE DES DÉRIVÉES DE U1 DANS LA BASE LOCALE (3X3)
      DO 140 I=1,3
        DU1DL(I,1)=COS(T)*DU1DPO(I,1)-SIN(T)/R*DU1DPO(I,2)
        DU1DL(I,2)=SIN(T)*DU1DPO(I,1)+COS(T)/R*DU1DPO(I,2)
        DU1DL(I,3)=0.D0
 140  CONTINUE

C     MATRICE DES DÉRIVÉES DE U1 DANS LA BASE GLOBALE (3X3)
      DO 141 I=1,NDIM
        DO 142 J=1,NDIM
          DO 143 K=1,NDIM
            DO 144 L=1,NDIM
              DU1DM(I,J)=DU1DM(I,J)+DU1DL(K,L)*INVP(L,J)*INVP(K,I)
 144        CONTINUE
C           PRISE EN COMPTE DE LA BASE MOBILE
            IF (LCOUR) DU1DM(I,J)=DU1DM(I,J)+U1L(K)*COURB(K,I,J)
 143      CONTINUE
 142    CONTINUE
 141  CONTINUE
 
C-----------------------------------------------------------------------
C     DÉFINITION DU CHAMP SINGULIER AUXILIAIRE U2 ET DE SA DÉRIVÉE
C-----------------------------------------------------------------------
C     CHAMP SINGULIER AUXILIAIRE U2 DANS LA BASE LOCALE
      U2L(1)=CR2*SIN(T*0.5D0)*(KA+2.D0+COS(T))
      U2L(2)=CR2*COS(T*0.5D0)*(2.D0-KA-COS(T))
      U2L(3)=0.D0

C     MATRICE DES DÉRIVÉES DE U2 DANS LA BASE POLAIRE (3X2)
      DU2DPO(1,1)=CR1*(SIN(T*0.5D0)*(KA+2.D0+COS(T)))
      DU2DPO(2,1)=CR1*COS(T*0.5D0)*(2.D0-KA-COS(T))
      DU2DPO(3,1)=0.D0
      DU2DPO(1,2)=CR2*(0.5D0*COS(T*0.5D0)*(KA+2.D0+COS(T))
     &                               - SIN(T*0.5D0)*SIN(T))
      DU2DPO(2,2)=CR2*(-0.5D0*SIN(T*0.5D0)*(2.D0-KA-COS(T))
     &                               + COS(T*0.5D0)*SIN(T))
      DU2DPO(3,2)=0.D0

C     MATRICE DES DÉRIVÉES DE U2 DANS LA BASE LOCALE (3X3)
      DO 150 I=1,3
        DU2DL(I,1)=COS(T)*DU2DPO(I,1)-SIN(T)/R*DU2DPO(I,2)
        DU2DL(I,2)=SIN(T)*DU2DPO(I,1)+COS(T)/R*DU2DPO(I,2)
        DU2DL(I,3)=0.D0
 150  CONTINUE

C     MATRICE DES DÉRIVÉES DE U2 DANS LA BASE GLOBALE (3X3)
      DO 151 I=1,NDIM
        DO 152 J=1,NDIM
          DO 153 K=1,NDIM
            DO 154 L=1,NDIM
              DU2DM(I,J)=DU2DM(I,J)+DU2DL(K,L)*INVP(L,J)*INVP(K,I)
 154        CONTINUE
C           PRISE EN COMPTE DE LA BASE MOBILE
            IF (LCOUR) DU2DM(I,J)=DU2DM(I,J)+U2L(K)*COURB(K,I,J)
 153      CONTINUE
 152    CONTINUE
 151  CONTINUE

C-----------------------------------------------------------------------
C     DÉFINITION DU CHAMP SINGULIER AUXILIAIRE U3 ET DE SA DÉRIVÉE
C-----------------------------------------------------------------------
C     CHAMP SINGULIER AUXILIAIRE U3 DANS LA BASE LOCALE
      U3L(1)=0.D0
      U3L(2)=0.D0
      U3L(3)=4.D0*CR2*SIN(T*0.5D0)

C     MATRICE DES DÉRIVÉES DE U3 DANS LA BASE POLAIRE (3X2)
      DU3DPO(1,1)=0.D0
      DU3DPO(2,1)=0.D0
      DU3DPO(1,2)=0.D0
      DU3DPO(2,2)=0.D0
      DU3DPO(3,1)=4.D0*CR1*SIN(T*0.5D0)
      DU3DPO(3,2)=2.D0*CR2*COS(T*0.5D0)

C     MATRICE DES DÉRIVÉES DE U3 DANS LA BASE LOCALE (3X3)
      DO 160 I=1,3
        DU3DL(I,1)=COS(T)*DU3DPO(I,1)-SIN(T)/R*DU3DPO(I,2)
        DU3DL(I,2)=SIN(T)*DU3DPO(I,1)+COS(T)/R*DU3DPO(I,2)
        DU3DL(I,3)=0.D0
 160  CONTINUE

C     MATRICE DES DÉRIVÉES DE U3 DANS LA BASE GLOBALE (3X3)
      DO 161 I=1,NDIM
        DO 162 J=1,NDIM
          DO 163 K=1,NDIM
            DO 164 L=1,NDIM
              DU3DM(I,J)=DU3DM(I,J)+DU3DL(K,L)*INVP(L,J)*INVP(K,I)
 164        CONTINUE
C           PRISE EN COMPTE DE LA BASE MOBILE
            IF (LCOUR) DU3DM(I,J)=DU3DM(I,J)+U3L(K)*COURB(K,I,J)
 163      CONTINUE
 162    CONTINUE
 161  CONTINUE

      END
