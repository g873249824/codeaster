      SUBROUTINE TE0418(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16 OPTION,NOMTE
C ......................................................................
C    - FONCTION REALISEE:  CALCUL DES VECTEURS ELEMENTAIRES
C                          OPTION : 'CHAR_ME_FR1D3D  '
C                                   'CHAR_ME_FF1D3D  '
C                        ELEMENT  : 'MEBOCQ3'

C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................

      CHARACTER*8 ELREFE
      REAL*8 FX,FY,FZ,MX,MY,MZ,JAC,JACP,EFFGLB(18)
      INTEGER I

      REAL*8 VALPAR(4)
      CHARACTER*8 NOMPAR(4)

C-----------------------------------------------------------------------
      INTEGER I1 ,ICOD1 ,ICOD2 ,ICOD3 ,ICOD4 ,ICOD5 ,ICOD6 
      INTEGER IDFDK ,IFORC ,IGEOM ,INO ,IPOIDS ,ITPSR ,IVECTU 
      INTEGER IVF ,JGANO ,K ,KP ,NDIM ,NNO ,NNOS 
      INTEGER NPG 
      REAL*8 DXDK ,DYDK ,DZDK ,X ,Y ,Z ,ZERO 

C-----------------------------------------------------------------------
      CALL ELREF1(ELREFE)
      ZERO = 0.D0


      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDK,JGANO)


      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PVECTUR','E',IVECTU)

      CALL R8INIR(18,0.D0,EFFGLB,1)

C     -- CALCUL DE LA FORCE MOYENNE :
      IF (OPTION(11:16).EQ.'FR1D3D') THEN
        CALL JEVECH('PFR1D3D','L',IFORC)
        FX = ZR(IFORC-1+1)
        FY = ZR(IFORC-1+2)
        FZ = ZR(IFORC-1+3)
        MX = ZR(IFORC-1+4)
        MY = ZR(IFORC-1+5)
        MZ = ZR(IFORC-1+6)
      ELSE IF (OPTION(11:16).EQ.'FF1D3D') THEN
        CALL JEVECH('PFF1D3D','L',IFORC)
        CALL JEVECH('PTEMPSR','L',ITPSR)

        NOMPAR(1) = 'X'
        NOMPAR(2) = 'Y'
        NOMPAR(3) = 'Z'
        NOMPAR(4) = 'INST'
        VALPAR(4) = ZR(ITPSR)
      ELSE
        CALL U2MESK('F','ELEMENTS2_77',1,OPTION)
      END IF

      DO 40 KP = 1,NPG
        K = (KP-1)*NNO
C       CALL DFDM1D( NNO,ZR(IPOIDS+KP-1),ZR(IDFDK+K),
C    &               ZR(IGEOM),DFDX,COUR,JACP,COSA,SINA)

        DXDK = ZERO
        DYDK = ZERO
        DZDK = ZERO

        DO 10 I = 1,NNO
          DXDK = DXDK + ZR(IGEOM+3* (I-1))*ZR(IDFDK+K+I-1)
          DYDK = DYDK + ZR(IGEOM+3* (I-1)+1)*ZR(IDFDK+K+I-1)
          DZDK = DZDK + ZR(IGEOM+3* (I-1)+2)*ZR(IDFDK+K+I-1)
   10   CONTINUE
        JAC = SQRT(DXDK**2+DYDK**2+DZDK**2)
        JACP = JAC*ZR(IPOIDS+KP-1)
        IF (OPTION(11:16).EQ.'FF1D3D') THEN
          X = ZERO
          Y = ZERO
          Z = ZERO
          DO 20 I = 1,NNO
            X = X + ZR(IGEOM+3* (I-1))*ZR(IVF+K+I-1)
            Y = Y + ZR(IGEOM+3* (I-1)+1)*ZR(IVF+K+I-1)
            Z = Z + ZR(IGEOM+3* (I-1)+2)*ZR(IVF+K+I-1)
   20     CONTINUE
          VALPAR(1) = X
          VALPAR(2) = Y
          VALPAR(3) = Z
          CALL FOINTE('FM',ZK8(IFORC-1+1),4,NOMPAR,VALPAR,FX,ICOD1)
          CALL FOINTE('FM',ZK8(IFORC-1+2),4,NOMPAR,VALPAR,FY,ICOD2)
          CALL FOINTE('FM',ZK8(IFORC-1+3),4,NOMPAR,VALPAR,FZ,ICOD3)
          CALL FOINTE('FM',ZK8(IFORC-1+4),4,NOMPAR,VALPAR,MX,ICOD4)
          CALL FOINTE('FM',ZK8(IFORC-1+5),4,NOMPAR,VALPAR,MY,ICOD5)
          CALL FOINTE('FM',ZK8(IFORC-1+6),4,NOMPAR,VALPAR,MZ,ICOD6)
        END IF

        DO 30 I = 1,NNO
          I1 = 6* (I-1)
          EFFGLB(I1+1) = EFFGLB(I1+1) + JACP*ZR(IVF+K+I-1)*FX
          EFFGLB(I1+2) = EFFGLB(I1+2) + JACP*ZR(IVF+K+I-1)*FY
          EFFGLB(I1+3) = EFFGLB(I1+3) + JACP*ZR(IVF+K+I-1)*FZ
          EFFGLB(I1+4) = EFFGLB(I1+4) + JACP*ZR(IVF+K+I-1)*MX
          EFFGLB(I1+5) = EFFGLB(I1+5) + JACP*ZR(IVF+K+I-1)*MY
          EFFGLB(I1+6) = EFFGLB(I1+6) + JACP*ZR(IVF+K+I-1)*MZ
   30   CONTINUE

   40 CONTINUE

C     -- AFFECTATION DU RESULTAT:

      DO 50 INO = 1,NNO
        ZR(IVECTU-1+ (INO-1)*6+1) = EFFGLB((INO-1)*6+1)
        ZR(IVECTU-1+ (INO-1)*6+2) = EFFGLB((INO-1)*6+2)
        ZR(IVECTU-1+ (INO-1)*6+3) = EFFGLB((INO-1)*6+3)
        ZR(IVECTU-1+ (INO-1)*6+4) = EFFGLB((INO-1)*6+4)
        ZR(IVECTU-1+ (INO-1)*6+5) = EFFGLB((INO-1)*6+5)
        ZR(IVECTU-1+ (INO-1)*6+6) = EFFGLB((INO-1)*6+6)
   50 CONTINUE

      END
