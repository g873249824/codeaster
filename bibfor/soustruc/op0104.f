      SUBROUTINE OP0104()
      IMPLICIT   NONE
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF SOUSTRUC  DATE 18/12/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     OPERATEUR: DEFI_GROUP
C     ------------------------------------------------------------------
C
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXNOM
C
      INTEGER        N1, N2, NBGRMA, NBGMIN, IRET, NBGMA, NBGRMN,
     &              I, J, NBMA, JGG, JVG, NBOCC, NBGRNO, IOCC, NBGNIN,
     &              NBGNO, NBGRNN, NBNO
      CHARACTER*8   K8B, MA, MA2
      CHARACTER*16  NOMCMD, TYPCON
      CHARACTER*24  GRPMAI,GRPNOE,GRPMAV,GRPNOV,GPPTNM,GPPTNN,NOMG
      INTEGER      IARG
C     ------------------------------------------------------------------
      CALL JEMARQ ( )
      CALL INFMAJ()
C
      CALL GETRES ( MA2, TYPCON, NOMCMD )
      CALL GETVID(' ','MAILLAGE',1,IARG,1,MA,N1)
      IF (N1.EQ.0) CALL GETVID(' ','GRILLE',1,IARG,1,MA,N1)
      IF ( MA .NE. MA2 ) THEN
       CALL U2MESS('F','SOUSTRUC_15')
      ENDIF
      GRPMAI  = MA//'.GROUPEMA'
      GRPNOE  = MA//'.GROUPENO'
      GPPTNM  = MA//'.PTRNOMMAI'
      GPPTNN  = MA//'.PTRNOMNOE'
      GRPMAV  = '&&OP0104'//'.GROUPEMA'
      GRPNOV  = '&&OP0104'//'.GROUPENO'


C     1. ON DETRUIT SI DEMANDE (DETR_GROUP_MA ET DETR_GROUP_NO):
C     ------------------------------------------------------------
      CALL GETFAC('DETR_GROUP_MA',N1)
      CALL GETFAC('DETR_GROUP_NO',N2)
      IF(N1.NE.0 .OR. N2.NE.0)THEN
         CALL DETGNM(MA)
      ENDIF



C     2. CREA_GROUP_MA :
C     ------------------------------------------------------------
C     --- ON COMPTE LE NOMBRE DE NOUVEAUX GROUP_MA :
      CALL GETFAC ( 'CREA_GROUP_MA' , NBGRMA )
      IF (NBGRMA.EQ.0) GOTO 107

C     --- ON AGRANDIT LA COLLECTION SI NECESSAIRE :
      NBGMIN = 0
      CALL JEEXIN(GRPMAI,IRET)
      IF ( IRET .EQ. 0  .AND.  NBGRMA .NE. 0 ) THEN
         CALL JEDETR(GPPTNM)
         CALL JECREO(GPPTNM,'G N K24')
         CALL JEECRA(GPPTNM,'NOMMAX',NBGRMA,' ')
         CALL JECREC(GRPMAI,'G V I','NO '//GPPTNM,
     &               'DISPERSE','VARIABLE',NBGRMA)
      ELSEIF ( IRET .EQ. 0  .AND.  NBGRMA .EQ. 0 ) THEN
      ELSE
         CALL JELIRA(GRPMAI,'NOMUTI',NBGMA,K8B)
         NBGMIN = NBGMA
         NBGRMN = NBGMA + NBGRMA
         CALL CPCLMA(MA,'&&OP0104','GROUPEMA','V')
         CALL JEDETR(GRPMAI)
         CALL JEDETR(GPPTNM)
         CALL JECREO(GPPTNM,'G N K24')
         CALL JEECRA(GPPTNM,'NOMMAX',NBGRMN,' ')
         CALL JECREC(GRPMAI,'G V I','NO '//GPPTNM,
     &               'DISPERSE','VARIABLE',NBGRMN)
         DO 100 I = 1 , NBGMA
            CALL JENUNO(JEXNUM(GRPMAV,I),NOMG)
            CALL JECROC(JEXNOM(GRPMAI,NOMG))
            CALL JEVEUO(JEXNUM(GRPMAV,I),'L',JVG)
            CALL JELIRA(JEXNUM(GRPMAV,I),'LONUTI',NBMA,K8B)
            CALL JEECRA(JEXNOM(GRPMAI,NOMG),'LONMAX',MAX(NBMA,1),' ')
            CALL JEECRA(JEXNOM(GRPMAI,NOMG),'LONUTI',NBMA,' ')
            CALL JEVEUO(JEXNOM(GRPMAI,NOMG),'E',JGG)
            DO 102 J = 0 , NBMA-1
               ZI(JGG+J) = ZI(JVG+J)
 102        CONTINUE
 100     CONTINUE
      ENDIF
 107  CONTINUE


C     3. CREA_GROUP_NO :
C     ------------------------------------------------------------
C     --- ON COMPTE LE NOMBRE DE NOUVEAUX GROUP_NO :
      CALL GETFAC ( 'CREA_GROUP_NO' , NBOCC )
      NBGRNO = 0
      DO 10 IOCC = 1 , NBOCC
         CALL GETVTX('CREA_GROUP_NO','TOUT_GROUP_MA',IOCC,IARG,0,K8B,N1)
         IF ( N1 .NE. 0 ) THEN
            CALL JELIRA(GRPMAI,'NMAXOC',NBGMA,K8B)
            NBGRNO = NBGRNO + NBGMA
            GOTO 10
         ENDIF
         CALL GETVEM(MA,'GROUP_MA','CREA_GROUP_NO','GROUP_MA',
     &                   IOCC,IARG,0,K8B,N2)
         IF ( N2 .NE. 0 ) THEN
            NBGRNO = NBGRNO - N2
            GOTO 10
         ENDIF
C        -- ON CREE UN GROUP_NO PAR MOT CLE FACTEUR --
         NBGRNO = NBGRNO + 1
10    CONTINUE
      IF (NBGRNO.EQ.0) GOTO 207


C     --- ON AGRANDIT LA COLLECTION SI NECESSAIRE :
      CALL JEEXIN(GRPNOE,IRET)
      NBGNIN = 0
      IF ( IRET .EQ. 0  .AND.  NBGRNO .NE. 0 ) THEN
         CALL JEDETR(GPPTNN)
         CALL JECREO(GPPTNN,'G N K24')
         CALL JEECRA(GPPTNN,'NOMMAX',NBGRNO,' ')
         CALL JECREC(GRPNOE,'G V I','NO '//GPPTNN,
     &               'DISPERSE','VARIABLE',NBGRNO)
      ELSEIF ( IRET .EQ. 0  .AND.  NBGRNO .EQ. 0 ) THEN
      ELSE
         CALL JELIRA(GRPNOE,'NOMUTI',NBGNO,K8B)
         NBGRNN = NBGNO + NBGRNO
         NBGNIN = NBGNO
         CALL CPCLMA(MA,'&&OP0104','GROUPENO','V')
         CALL JEDETR(GRPNOE)
         CALL JEDETR(GPPTNN)
         CALL JECREO(GPPTNN,'G N K24')
         CALL JEECRA(GPPTNN,'NOMMAX',NBGRNN,' ')
         CALL JECREC(GRPNOE,'G V I','NO '//GPPTNN,
     &               'DISPERSE','VARIABLE',NBGRNN)
         DO 200 I = 1 , NBGNO
            CALL JENUNO(JEXNUM(GRPNOV,I),NOMG)
            CALL JECROC(JEXNOM(GRPNOE,NOMG))
            CALL JEVEUO(JEXNUM(GRPNOV,I),'L',JVG)
            CALL JELIRA(JEXNUM(GRPNOV,I),'LONUTI',NBNO,K8B)
            CALL JEECRA(JEXNOM(GRPNOE,NOMG),'LONMAX',MAX(NBNO,1),' ')
            CALL JEECRA(JEXNOM(GRPNOE,NOMG),'LONUTI',NBNO,' ')
            CALL JEVEUO(JEXNOM(GRPNOE,NOMG),'E',JGG)
            DO 202 J = 0 , NBNO-1
               ZI(JGG+J) = ZI(JVG+J)
 202        CONTINUE
 200     CONTINUE
      ENDIF
 207  CONTINUE

C     --- TRAITEMENT DU MOT CLEF CREA_GROUP_MA :
      IF ( NBGRMA .GT. 0 ) CALL SSCGMA ( MA , NBGRMA , NBGMIN )

C     --- TRAITEMENT DU MOT CLEF CREA_GROUP_NO :
      IF ( NBGRNO .GT. 0 ) CALL SSCGNO ( MA , NBGNIN )
C
C
      CALL JEDEMA ( )
      END
