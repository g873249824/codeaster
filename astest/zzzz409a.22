      SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD,
     1 RPL,DDSDDT,DRPLDE,DRPLDT,
     2 STRAN,DSTRAN,TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,CMNAME,
     3 NDI,NSHR,NTENS,NSTATV,PROPS,NPROPS,COORDS,DROT,PNEWDT,
     4 CELENT,DFGRD0,DFGRD1,NOEL,NPT,LAYER,KSPT,KSTEP,KINC)
C ======================================================================
C COPYRIGHT (C) 1991 - 2017  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C
C     ELASTO-PLASTIC BEHAVIOUR LAW WITH ISOTROPIC LINEAR HARDENING     
C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C            ARGUMENTS EN ENTREE DE LA ROUTINE UMAT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    NPT     NUMERO DU POINT D'INTEGRATION
C    LAYER   NUMERO DE LA COUCHE POUR LES COQUES (1)
C    KSPT    NUMERO DU SOUS-POINT D'INTEGRATION 
C    NTENS   NTENS = NDI+NSHR
C    NDI     NOMBRE DE COMPOSANTES DE CONTRAINTES (HORS CISAILLEMENT) 
C    NSHR    NOMBRE DE COMPOSANTES DE CONTRAINTES DE CISAILLEMENT 
C    PROPS   TABLEAU DES PARAMETRES MATERIAUX
C    CMNAME  NOM DU MATERIAU = COMPORTEMENT ICI
C            DANS UMAT, TOUJOURS FULL_MECA ?
C            DANS UMAT, PAS DE CRITERES LOCAUX ?
C    CELENT  LONGUEUR CARACTERISTIQUE DE L'ELEMENT.
C    TIME    TEMPS DU PAS ET TEMPS TOTAL AU DEBUT DE L'INCREMENT
C    DTIME   INCREMENT DE TEMPS
C    STRAN   DEFORMATIONS MECANIQUES TOTALES AU DEBUT DE L'INCREMENT. 
C            LES EVENTUELLES DEFORMATIONS THERMIQUES SONT RETIREES.
C    DSTRAN  INCREMENTS DE DEFORMATIONS. 
C            LES  INCREMENTS DE DEFORMATIONS THERMIQUES SONT RETIRES.
C    STRESS  En ENTREE, CONTRAINTES DE CAUCHY AU DEBUT DU PAS DE TEMPS.
C    STATEV  EN ENTREE, VARIABLES INTERNES AU DEBUT DU PAS DE TEMPS. 
C    DROT    MATRICE INCREMENT DE ROTATION.
C    KSTEP   NUMERO DU PAS
C    KINC    NUMERO DE L'INCREMENT
C    NSTATV  NOMBRE DE VARIABLES INTERNES ASSOCIEES AU COMPORTEMENT.
C    TEMP    TEMPERATURE AU DEBUT DE L'INCREMENT
C    DTEMP   INCREMENT DE TEMPERATURE.
C    PREDEF  TABLEAU DES VALEURS INTERPOLEES DES CHAMPS EXTERNES IMPOSES
C    DPRED   TABLEAU DES INCREMENTS DES CHAMPS EXTERNES IMPOSES.
C    NPROPS  NOMBRE DE PARAMETRES MATERIAUX
C    COORDS  TABLEAU CONTENANT LES COORDONNEES DE CE POINT.
C    DFGRD0  GRADIENT DE TRANSFORMATION AU DEBUT DE L'INCREMENT.
C    DFGRD1  GRADIENT DE TRANSFORMATION A LA FIN DE L'INCREMENT.
C    NOEL    NUMERO DE L'ELEMENT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C            ARGUMENTS EN SORTIE DE LA ROUTINE UMAT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    STRESS  TENSEUR DES CONTRAINTES DE CAUCHY A LA FIN DU PAS DE TEMPS.
C    STATEV  VARIABLES INTERNES A LA FIN DU PAS DE TEMPS.
C    DDSDDE  MATRICE TANGENTE DU MODELE DE COMPORTEMENT (NTENS,NTENS)
C    PNEWDT  RAPPORT DU NOUVEAU PAS DE TEMPS SUGGERE SUR LE PAS DE TEMPS
C            UTILISE (DTIME). SI PNEWDT < 1, REDECOUPE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C            ARGUMENTS EN SORTIE DE LA ROUTINE UMAT INUTILSES PAR ASTER
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    SSE     ENERGIE ELASTIQUE
C    SPD     ENERGIE DE DISSIPATION PLASTIQUE
C    SCD     ENERGIE DE FLUAGE.
C    EN THERMO-MECANIQUE COUPLEE UNIQUEMENT :
C    RPL     PRODUCTION DE CHALEUR VOLUMETRIQUE
C    DDSDDT  VARIATION DE L'INCREMENT DES CONTRAINTES VERSUS TEMPERATURE
C    DRPLDE  VARIATION DE LA PRODUCTION DE CHALEUR  VERSUS DEFORMATION
C    DRPLDT  VARIATION DE LA PRODUCTION DE CHALEUR  VERSUS TEMPERATURE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC


      
      IMPLICIT NONE
      CHARACTER*80 CMNAME
      INTEGER NDI, NSHR, NTENS, NPROPS, NSTATV, NOEL,NPT,LAYER,KSPT
      INTEGER KSTEP,KINC
      REAL*8 STRESS(NTENS),STATEV(NSTATV),
     1 DDSDDE(NTENS,NTENS),
     2 DDSDDT(NTENS),DRPLDE(NTENS),DRPLDT, 
     3 STRAN(NTENS),DSTRAN(NTENS),TIME(2),DTIME, PREDEF(1),DPRED(1),
     4 PROPS(NPROPS),COORDS(3),DROT(3,3),DFGRD0(3,3),DFGRD1(3,3), 
     5 TEMP, DTEMP, PNEWDT, CELENT
      REAL*8 DSTRES(6),D(3,3), SSE, SPD, SCD, RPL 
      
      INTEGER K1, K2
      REAL*8 SOM2, YEL
      REAL*8 STRANP(6)
      REAL*8 MAT(6,6), STRANP2(6)
      
      IF (NSTATV .NE. 2) STOP 'NSTATV EST DIFFERENT DE 2 !'
      IF (NTENS .GT. 6) STOP 'TROP DE DIMENSIONS SPATIALES !'

C Deformation meca à la fin du pas de temps :
      DO 1000 K1 = 1,NTENS
         STRANP(K1) = STRAN(K1) + DSTRAN(K1)
 1000 CONTINUE
 
C On définit la matrice E*P servant pour le chgt de convention aster <-> abaqus
C pour les termes de cisaillement dans l'écriture de Voigt
      DO 1001 K1 = 1,NTENS
         DO 1002 K2 = 1, NTENS
            MAT(K1,K2) = 0.D0
 1002 CONTINUE
 1001 CONTINUE
      DO 1003 K1 = 1,NDI
        MAT(K1,K1) = PROPS(1)
 1003 CONTINUE
      DO 1004 K1 = NDI+1,NTENS
        MAT(K1,K1) = PROPS(1)*.5D0
 1004 CONTINUE
 
C Réecriture du tenseur de déformation : E*P*{epsi}
      DO 1005 K1=1,NDI
         STRANP2(K1) = PROPS(1)* STRANP(K1)
 1005 CONTINUE
      DO 1006 K1=NDI+1, NTENS
         STRANP2(K1) = PROPS(1)* .5D0 * STRANP(K1)
 1006 CONTINUE
      
C Produit scalaire nécessaire à l'écriture du second membre de la matrice tangente
      SOM2 = 0
      DO 1010 K1 = 1,NTENS
         SOM2 = SOM2 + STRANP(K1)*STRANP2(K1)
 1010 CONTINUE      

C Critère d'endommagement
      YEL = .5D0 /(1+2*STATEV(1))**2*SOM2
      
      IF (KINC.GE.1) THEN
          STATEV(2) = 0
C Variables internes à la fin du pas de temps
          IF (YEL .GT. PROPS(2)) THEN
C En branche élastique, STATEV(1) ne varie pas 
             STATEV(1) = .5D0 *(SQRT(SOM2/(2*PROPS(2)))-1)
C Si on est en train d'endommager, STATEV(2) est mis à 1, sinon reste à 0
             STATEV(2) = 1
          ENDIF
      END IF
      
C Contraintes à l'issue du pas de temps
      DO 1013 K1 = 1,NTENS
         STRESS(K1) = (1+STATEV(1))/(1+2*STATEV(1))*STRANP2(K1)
 1013 CONTINUE
 
C Matrice tangente : membre commun aux deux branches élastique / plastique
      DO 1007 K1 = 1,NTENS
         DO 1008 K2 = 1,NTENS
            DDSDDE(K1,K2) = (1+STATEV(1))/(1+2*STATEV(1))*MAT(K1,K2)
 1008 CONTINUE
 1007 CONTINUE
 
C Matrice tangente : morceau en plus lorsqu'on est sur la branche endo
      IF (NINT(STATEV(2)) .EQ. 1) THEN
         DO 1011 K1 = 1,NTENS
            DO 1012 K2 = 1,NTENS
               DDSDDE(K1,K2) = DDSDDE(K1,K2) - PROPS(1)*SQRT(PROPS(1))
     & /(1+2*STATEV(1))**2 * .5D0 /SQRT(2*PROPS(2))
     & * STRANP2(K1)*STRANP2(K2) / SQRT(SOM2)
 1012 CONTINUE
 1011 CONTINUE
      ENDIF
        
      END
