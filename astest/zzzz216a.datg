* VALIDATION DE STANLEY (MAILLAGE MASSIF LINEAIRE)
*
*       TUYAUTERIE COMPORTANT UN COUDE
*   MODELISATION PAR DES ELEMENTS VOLUMIQUES
*   ----------------------------------------
***************************************************************
*
*                 MAILLAGE 
*
***************************************************************
*
* (EN FIN DE FICHIER : DEPOUILLEMENT DES RESULTATS AVEC GIBI98)
*
***************************************************************
*
OPTI DIME 3 ELEM CUB8 ;

*---------------------------
*    DONNEES DU PROBLEME
*---------------------------

* RE RAYON EXTERIEUR DES ELEMENTS DE TUYAUTERIE (M)
* EP EPAISSEUR DES TUYAUX (M)
* RM RAYON MOYEN DE LA TUYAUTERIE (M)
* RC RAYON DE COURBURE DU COUDE (M)
* THETA ANGLE DU COUDE (DEGRE)
* LG LONGUEUR DU PREMIER ET DU SECOND TUYAU DROIT (M)
RE = 0.2;
EP = 0.02 ;
RI = RE - EP ;
RM = RE - (EP/2) ;
RC = 0.6 ;
THETA = 90. ;
LG = 3. ;

PE1 = 0. 0. RE ;
AE1 = RE  0. 0. ;
AE2 = (-1.*RE) 0. 0. ;
PI1 = 0. 0. RI ;
AI1 = RI  0. 0. ;
AI2 = (-1.*RI) 0. 0. ;

* EXTREMITE DU PREMIER TUYAU
L1 = CER3 20 AE2 PE1 AE1 ;
L2 = CER3 20 AI2 PI1 AI1 ;
D1 = DROIT 1 AI1 AE1 ;
D2 = DROIT 1 AE2 AI2 ;
ENCAST = DALLER L1  D1  L2  D2 PLAN ;

* MAILLAGE DU PREMIER TUYAU DROIT
VECT1 = 0. LG 0. ;
V = ENCAST 15 VOLU TRAN VECT1 ;
*S2 = FACE 2 V1 ;
*list (elem v1 type);

* MAILLAGE DU COUDE
BETA = -1.*THETA ;
PO = RC LG 0. ;
PZ = RC LG 100. ;
V= V VOLU ROTA 10 BETA PO PZ ;
*S3 = FACE 2 V2 ;

* MAILLAGE DU SECOND TUYAU DROIT
VECT2 = (LG*(SIN(THETA))) (LG*(COS(THETA))) 0. ;
V= V 15 VOLU TRAN VECT2 ;

LIST (NBNO V) ;
LIST (NBEL V) ;


* SECTIONS DE CHARGEMENT
EFOND = FACE 2 V ;

S3 = FACE 3 V ;

Z = COOR 3 V;
SYMETRIE = S3 ELEM APPUYE STRICT (Z POIN INFE 0.001);

S3 = S3 DIFF SYMETRIE;

* MAILLAGE DE LA FACE INTERNE DU TUBE
G2 = L2 15  TRAN VECT1 ;
H3 = COTE 3 G2 ;
G3= H3  ROTA 10 BETA PO PZ ;
H4 = COTE 3 G3 ;
G4= H4 15  TRAN VECT2 ;
SURFINT = ELIM 1.E-4 (G2 ET G3 ET G4) ;

ELIM 0.0001 (S3 et SURFINT) ;
SURFEXT = S3 DIFF SURFINT ;
OUBLIER SURFINT ;
SURFINT = S3 DIFF SURFEXT ;
OUBLIER S3 ;
OUBLIER G2 ;
OUBLIER G3 ;
OUBLIER G4 ;
OUBLIER H3 ;
OUBLIER H4 ;
OUBLIER VECT1 ;
OUBLIER VECT2 ;
OUBLIER PO ;
OUBLIER PZ ;
OUBLIER PE1 ;
OUBLIER PI1 ;


TASS V ;


OPTI SAUV FORM 'stanl01a.mgib' ;
SAUV FORM V   ;

FIN;



