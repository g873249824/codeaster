*
*Maillage Modelisation B (vrai 3d ):
*Contact de deux poutres en grands deplacements
*Elements 3D (HEXA8)
*
*
opti dime 3 ;
opti nive 10 ;
opti elem cub8 ;
*
*Constantes
*
* nombre d'éléments dans la longueur
N = 40;
* demi hauteur de la poutre
h = 63.5/2. ;
* demi largeur de la poutre
l = 10.16/2. ;
* longueur de la poutre
lo = 2032. ;  
* distance du point C
dc = -1320.8 ;

A  = 0.  h   (   l);
A1 = lo  h   (   l);
A3 = 0.  h  (-1.*l) ;

* décalage
dd = 914.4 ;

xb = (lo + dd) ;
yb = (508. + h) ;

B  = xb   yb     (l) ;
B1 = xb   yb  (-1*l) ;

*-------------------------------------------
* Creation de la poutre 1 (Poutre Inferieure)
* points (A A1 A2 A3)
*-------------------------------------------
nh = 3 ;
ne = 1 ;

lA3A  = droit ne A3 A  ;

h1 = -2.*h ;
vect2 = (0. h1 0.) ;
SA    =  lA3A trans nh  vect2 ;

vect1 = (lo 0. 0.) ;
SMAI =  lA3A  trans N vect1 ;
AA1 = droit N A A1;
*A1 = A plus vect1 ;

struct0 = SMAI volu nh trans vect2 ;

elim 0.001 struct0;
struct0 = rege struct0;

POUTRE1 = SMAI et SA et struct0 et AA1;
oublier vect1;

elim 0.001 POUTRE1 ;
POUTRE1 = regenerer POUTRE1 ;

*-------------------------------------------
* Creation poutre 2 (poutre superieure)
*-------------------------------------------

N1 = enti ( 13*N/20 ) ;
N2 = enti (  7*N/20 ) ;

lBB1  = droit ne B1  B ;
SB    = lBB1 trans nh vect2 ;
SBextr = SB plus (-2032. 0. 0.);

vect3 = (dc 0. 0.);
lBINF = SB cote 3 ;
surf1 = lBB1 trans N1 vect3 ;
lCC1  = surf1 cote 3 ;
C  = lCC1 point initial;
C1 = lCC1 point final ;
elim 0.001 (surf1 et lcc1);
surf1 =rege surf1;

struct1 = surf1 volu nh trans vect2 ;
surfe1  = struct1 face 2 ;

xe = -1*(dc+lo) ;
vect4 = (xe 0. 0.);
surf2 = lCC1 trans N2 vect4 ; 

struct2 = surf2 volu nh trans vect2 ;

oublier vect2;
oublier vect3;
oublier vect4;

surfe2  = struct2 face 2 ;
SESC = (surfe1 et surfe2 et SBEXTR) ;
elim 0.001 SESC;
SESC = rege SESC;

POUTRE2 = (SESC et SB et struct1 et struct2) ;
elim 0.001 POUTRE2 ;
POUTRE2= regenerer POUTRE2 ;

STRUCT = (poutre1 et poutre2 );
opti sauv form 'ssnv505d.mgib';
sauver form (STRUCT);

*trac poutre2;
*---------------------------------------------
opti elem seg2;
trac cach (STRUCT et (SESC coul vert) et (SMAI coul rouge) et 
          (@repere (prog 500. 500. 500) (100. 100. 100.)  jaune));
*---------------------------------------------
fin;
