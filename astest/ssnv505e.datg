*
*Maillage Modelisation B (2d deformations planes ):
*Contact de deux poutres en grands deplacements
*Elements 2D (QUAD4)
*
*
opti dime 2 ;
opti nive 10 ;
opti elem QUA4 ;
*
*Constantes
*
* nombre d'éléments dans la longueur
N = 40;
* demi hauteur de la poutre
h = 63.5/2. ;
* longueur de la poutre
lo = 2032. ;  
* distance du point C
dc = -1320.8 ;

A  = 0.  h  ;
A3 = 0.  (-1*h);

* décalage
dd = 914.4 ;

xb = (lo + dd) ;
B  = xb   (508. + h)  ;
B3 = xb   (508. - h)   ;

*-------------------------------------------
* Creation de la poutre 1 (Poutre Inferieure)
*-------------------------------------------
nh = 3 ;

lA3A = droit A3 A nh;

vect1 = (lo 0. ) ;
POUTRE1 = lA3A trans N vect1 ;
oublier vect1;

SMAI = cote 2 POUTRE1;

POUTRE1 = POUTRE1 et SMAI;

elim 0.001 POUTRE1 ;

*-------------------------------------------
* Creation poutre 2 (poutre superieure)
*-------------------------------------------
*
N1 = enti ( 13*N/20 ) ;
N2 = enti (  7*N/20 ) ;

lB3B  = droit B3 B nh;
vect2 = (dc 0. ) ;

surf1 = lB3B trans N1 vect2 ;
oublier vect2;

lCC3  = cote 3 surf1 ;
li1   = cote 4 surf1 ;
C = LCC3 point init ;
C3 = LCC3 point final; 
vect3 = ( ( -1.*(dc+lo) )    0. ) ;
surf2 = lCC3 trans N2 vect3 ;
oublier vect3;
li2   = cote 2 surf2 ;
lextr = cote 3 surf2 ;

SESC = li1 et  li2 et lextr;

POUTRE2 = surf1 et surf2 et SESC;
elim 0.001 POUTRE2 ;

STRUCT = (poutre1 et poutre2 );
elim 0.001 STRUCT;

opti sauv form 'ssnv505e.mgib' ;
sauv format (STRUCT);

trac cach (STRUCT et (SESC coul vert) et (SMAI coul rouge) );

fin;
