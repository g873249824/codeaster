OPTI DIME 3 ELEM CU27 ECHO 0;
OPTI SAUV FORM 'zzzz166e.mgib' ;
*
* discretisation cote
NEL = 20;
DZ = 0.1;
*
* points de construction
A1 = 0.5 0.5 1.0;
B1 = 2.5 0.5 1.0;
C1 = 0.5 2.5 1.0;
D1 = 2.5 2.5 1.0;

A2 = 3.0 0.5 1.0;
B2 = 5.0 0.5 1.0;
C2 = 3.0 2.5 1.0;
D2 = 5.0 2.5 1.0;

A3 = 5.5 0.5 1.0;
B3 = 7.5 0.5 1.0;
C3 = 5.5 2.5 1.0;
D3 = 7.5 2.5 1.0;

A4 = 0.5 3.0 1.0;
B4 = 2.5 3.0 1.0;
C4 = 0.5 5.0 1.0;
D4 = 2.5 5.0 1.0;

A5 = 3.0 3.0 1.0;
B5 = 5.0 3.0 1.0;
C5 = 3.0 5.0 1.0;
D5 = 5.0 5.0 1.0;

A6 = 5.5 3.0 1.0;
B6 = 7.5 3.0 1.0;
C6 = 5.5 5.0 1.0;
D6 = 7.5 5.0 1.0;

A7 = 0.5 5.5 1.0;
B7 = 2.5 5.5 1.0;
C7 = 0.5 7.5 1.0;
D7 = 2.5 7.5 1.0;

A8 = 3.0 5.5 1.0;
B8 = 5.0 5.5 1.0;
C8 = 3.0 7.5 1.0;
D8 = 5.0 7.5 1.0;

A9 = 5.5 5.5 1.0;
B9 = 7.5 5.5 1.0;
C9 = 5.5 7.5 1.0;
D9 = 7.5 7.5 1.0;

*
* maillage en hexa8
*--------------------
OPTI DIME 3 ELEM CUB8;
AB1 = DROI NEL A1 B1;
CD1 = DROI NEL C1 D1;
SHE8 = AB1 REGL NEL CD1;
VHE8 = SHE8 VOLU 1 TRAN (0. 0. DZ);
*
* maillage en penta6
*--------------------
AB2 = DROI NEL A2 B2;
CD2 = DROI NEL C2 D2;
OPTI ELEM TRI3;
SPE6 = AB2 REGL NEL CD2;
OPTI ELEM PRI6;
VPE6 = SPE6 VOLU 1 TRAN (0. 0. DZ);
*
* maillage en penta15
*--------------------
OPTI DIME 3 ELEM PR15;
AB3 = DROI NEL A3 B3;
CD3 = DROI NEL C3 D3;
OPTI ELEM TRI6;
SPE15 = AB3 REGL NEL CD3;
OPTI ELEM PR15;
VPE15 = SPE15 VOLU 1 TRAN (0. 0. DZ);
*
* maillage en pyram5
* -------------------
A41 = 0.5 3.0 1.0;
B41 = 0.7 3.0 1.0;
C41 = 0.5 3.2 1.0;
D41 = 0.7 3.2 1.0;
A42 = 0.5 3.0 1.1;
B42 = 0.7 3.0 1.1;
C42 = 0.5 3.2 1.1;
D42 = 0.7 3.2 1.1;
E40 = 0.6 3.1 1.05;
VPY51 = MANU PYR5 A41 B41 D41 C41 E40;
VPY52 = MANU PYR5 B41 B42 D42 D41 E40;
VPY53 = MANU PYR5 B42 A42 C42 D42 E40;
VPY54 = MANU PYR5 A42 A41 C41 C42 E40;
VPY55 = MANU PYR5 C41 D41 D42 C42 E40;
VPY56 = MANU PYR5 A42 A41 B41 B42 E40;
VPY50 = VPY51 et VPY52 et VPY53 et VPY54 et VPY55 et VPY56;
IB=0;
DX=0.0;
VPY5 = VPY50 PLUS (0. 0. 0.);
REPE BLPY5X 10;
  DY=0.0;
  REPE BLPY5Y 10;
    SI (IB NEG 0);
      VPY5 = VPY5 ET (VPY50 PLUS (DX DY 0.));
    FINSI;
    IB=1;
    DY=DY+0.2;
  FIN BLPY5Y;
  DX=DX+0.2;
FIN BLPY5X;
ELIM 0.001 VPY5;
ELIM 0.001 VPY5 (A4 ET B4 ET C4 ET D4);
mess (nbel (VPY5 elem PYR5));
trac VPY5;
*
* maillage en tetra4
*--------------------
A5Z = 3.0 3.0 1.1;
B5Z = 5.0 3.0 1.1;
C5Z = 3.0 5.0 1.1;
D5Z = 5.0 5.0 1.1;
AB5  = DROI 10 A5  B5 ;
CD5  = DROI 10 C5  D5 ;
AB5Z = DROI 10 A5Z B5Z;
CD5Z = DROI 10 C5Z D5Z;
BD5  = DROI 10 B5  D5 ;
AC5  = DROI 10 A5  C5 ;
BD5Z = DROI 10 B5Z D5Z;
AC5Z = DROI 10 A5Z C5Z;
OPTI ELEM TRI3;
STE41 = AB5  REGL 10 CD5 ;
STE42 = AB5Z REGL 10 CD5Z;
STE43 = CD5  REGL 1 CD5Z;
STE44 = AB5  REGL 1 AB5Z;
STE45 = BD5  REGL 1 BD5Z;
STE46 = AC5  REGL 1 AC5Z;
ETE4 = STE41 ET STE42 ET STE43 ET STE44
   ET STE45 ET STE46;
ELIM 0.001 ETE4;
OPTI ELEM TET4;
VTE4 = VOLU ETE4;
mess (nbel (VTE4 elem TET4));
trac cach VTE4;
trac VTE4;
*
* maillage en tetra10
*--------------------
VTE10 = VTE4 PLUS (2.5 0. 0.);
VTE10 = CHAN QUADRATIQUE VTE10;
ELIM 0.001 VTE10 (A6 ET B6 ET C6 ET D6);
mess (nbel (VTE10 elem TE10));
trac VTE10;
*
* maillage en pyram13
*--------------------
VPY13 = VPY5 PLUS (0. 2.5 0.);
VPY13 = CHAN QUADRATIQUE VPY13;
ELIM 0.001 VPY13 (A7 ET B7 ET C7 ET D7);
mess (nbel (VPY13 elem PY13));
trac VPY13;
*
* maillage en hexa20
*--------------------
OPTI DIME 3 ELEM CU20;
AB8 = DROI NEL A8 B8;
CD8 = DROI NEL C8 D8;
SHE20 = AB8 REGL NEL CD8;
VHE20 = SHE20 VOLU 1 TRAN (0. 0. DZ);
*
* maillage en hexa27
*--------------------
OPTI DIME 3 ELEM CU20;
AB9 = DROI NEL A9 B9;
CD9 = DROI NEL C9 D9;
SHE27 = AB9 REGL NEL CD9;
VHE27 = SHE27 VOLU 1 TRAN (0. 0. DZ);
VHE27 = CHAN QUAF VHE27;
mess (nbel (VHE27 elem CU27));
trac VHE27;
*
* elimination des noeuds doubles
*--------------------
VTOT = VHE8 et VPE6 et VPY5 et VTE4 
  et VHE20 et VHE27 et VPE15 et VPY13 et VTE10;
elim 0.001 VTOT;
*
* sauvegarde du maillage
*--------------------
SAUV FORM VTOT;
*
* trace de verification
*--------------------
mess 'nbno VTOT : ' (nbno VTOT);
mess 'nbel VTOT : ' (nbel VTOT);
mess 'nbel VHE8 : ' (nbel VHE8) (nbel (VHE8 elem CUB8));
mess 'nbel VPE6 : ' (nbel VPE6) (nbel (VPE6 elem PRI6));
mess 'nbel VPY5 : ' (nbel VPY5) (nbel (VPY5 elem PYR5));
mess 'nbel VTE4 : ' (nbel VTE4) (nbel (VTE4 elem TET4));
mess 'nbel VHE20 : ' (nbel VHE20) (nbel (VHE20 elem CU20));
mess 'nbel VHE27 : ' (nbel VHE27) (nbel (VHE27 elem CU27));
mess 'nbel VPE15 : ' (nbel VPE15) (nbel (VPE15 elem PR15));
mess 'nbel VPY13 : ' (nbel VPY13) (nbel (VPY13 elem PY13));
mess 'nbel VTE10 : ' (nbel VTE10) (nbel (VTE10 elem TE10));
trac cach VTOT;
*
*fin;
