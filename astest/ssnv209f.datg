********************************************************
*                  --- SSNV210E ---
* MAILLAGE 3D D'UNE PLAQUE EN CONTACT AVEC UN BATI
* ELEM QUA8 (QUADRATIQUE)
********************************************************
*
opti dime 3 elem cub8 echo 0 ;
*opti niveau 8 ;
*
* dimensions du bati et de la plaque
* -----------------------------------

COTE  = 0.04;
COTE1 = -0.04*5/21.;
COTE2 = 0.08;
COTE3 = 0.001;
EPSI  = 0.000001;
*
XA = 0.;
XB = 0.00125;
XC = 0.005;
XD = 0.0075;
XE = 0.01125;
*
* discretisation
* ---------------
N1 = 64;
N2 = 10;
N3 = 32;
*
* points, lignes et surface de construction du bati
* --------------------------------------------------
PBA = XA    0.    0.;
PBS = COTE2 0.    0.;
PCA = XA    COTE1 0.; 
PCS = COTE2 COTE1 0.;

*
LBATI  = PBS d N1 PBA;
LENCAV = PCS d (N1) PCA;

SBAT   = LBATI   TRAN 1 (0. 0. COTE3);
SENCAV  = LENCAV  TRAN 1 (0. 0. COTE3);

*
SBATI  = (LBATI regl 3 LENCAV) coul vert;
VBATI  = SBATI VOLU TRAN 1 (0. 0. COTE3);
elim EPSI (VBATI et SENCAV et SBATI et SBAT);
*
* points, lignes et surface de construction de la plaque
* -------------------------------------------------------
PPA = XA     0.   0.;
PPB = XB     0.   0.;
PPC = XC     0.   0.;
PPD = XD     0.   0.;
PPE = XE     0.   0.;
PPS = COTE2  0.   0.;
PHA = XA     COTE 0.;
PHS = COTE2  COTE 0.;
PPI = COTE   0.   0.;
*
LCONTA  = PPA d 1 PPB d 3 PPC d 2 PPD d 3 PPE d (N3-9) PPI d N3 PPS;
PPR     = LCONTA poin N1;
LCONTAR = LCONTA comp PPA PPR;
LBLOCX  = PPS d N2 PHS;
LPRESV  = PHA d N1 PHS;
LPRESH  = PPA d N2 PHA;

SCONTA  = LCONTA  TRAN 1 (0. 0. COTE3);
SCONTAR = LCONTAR TRAN 1 (0. 0. COTE3);
SBLOCX  = LBLOCX  TRAN 1 (0. 0. COTE3);
SPRESV  = LPRESV  TRAN 1 (0. 0. COTE3);
SPRESH  = LPRESH  TRAN 1 (0. 0. COTE3);

*
SPLAQ  = (LCONTA regl N2 LPRESV) coul vert;
elim EPSI SPLAQ LBLOCX;
elim EPSI SPLAQ LPRESH;
VPLAQ1 = SPLAQ VOLU TRAN 1 (0. 0. COTE3);
elim EPSI (VPLAQ1 et SCONTA et SCONTAR et SBLOCX et SPRESV et SPRESH);

PHAP = XA     COTE COTE3;
PHSP = COTE2  COTE COTE3;
VPLAQ = VPLAQ1 et PHAP et PHSP;
elim EPSI VPLAQ;
*
* assemblage, trace de verification et sauvegarde du maillage
* ------------------------------------------------------------
MTOT = VBATI et VPLAQ;

*
trac MTOT;
opti sauv format 'ssnv209f.mgib';

sauv form MTOT;
*
fin;
