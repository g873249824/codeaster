      SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD,
     1 RPL,DDSDDT,DRPLDE,DRPLDT,
     2 STRAN,DSTRAN,TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,CMNAME,
     3 NDI,NSHR,NTENS,NSTATV,PROPS,NPROPS,COORDS,DROT,PNEWDT,
     4 CELENT,DFGRD0,DFGRD1,NOEL,NPT,LAYER,KSPT,KSTEP,KINC)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C      INCLUDE 'ABA_PARAM.INC'
C
C     ROUTINE UMAT EXEMPLE CITEE DANS LA DOCUMENATION ABAQUS
C     COMPORTEMENT VISCOELASTIQUE LINEAIRE     
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C            ARGUMENTS EN ENTREE DE LA ROUTINE UMAT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    NPT     NUMERO DU POINT D'INTEGRATION
C    LAYER   NUMERO DE LA COUCHE POUR LES COQUES (1)
C    KSPT    NUMERO DU SOUS-POINT D'INTEGRATION 
C    NTENS   NTENS = NDI+NSHR
C    NDI     NOMBRE DE COMPOSANTES DE CONTRAINTES (HORS CISAILLEMENT) 
C    NSHR    NOMBRE DE COMPOSANTES DE CONTRAINTES DE CISAILLEMENT 
C    PROPS   TABLEAU DES PARAMETRES MATERIAUX
C    CMNAME  NOM DU MATERIAU = COMPORTEMENT ICI
C            DANS UMAT, TOUJOURS FULL_MECA ?
C            DANS UMAT, PAS DE CRITERES LOCAUX ?
C    CELENT  LONGUEUR CARACTERISTIQUE DE L'ELEMENT.
C    TIME    TEMPS DU PAS ET TEMPS TOTAL AU DEBUT DE L'INCREMENT
C    DTIME   INCREMENT DE TEMPS
C    STRAN   DEFORMATIONS MECANIQUES TOTALES AU DEBUT DE L'INCREMENT. 
C            LES EVENTUELLES DEFORMATIONS THERMIQUES SONT RETIREES.
C    DSTRAN  INCREMENTS DE DEFORMATIONS. 
C            LES  INCREMENTS DE DEFORMATIONS THERMIQUES SONT RETIRES.
C    STRESS  En ENTREE, CONTRAINTES DE CAUCHY AU DEBUT DU PAS DE TEMPS.
C    STATEV  EN ENTREE, VARIABLES INTERNES AU DEBUT DU PAS DE TEMPS. 
C    DROT    MATRICE INCREMENT DE ROTATION.
C    KSTEP   NUMERO DU PAS
C    KINC    NUMERO DE L'INCREMENT
C    NSTATV  NOMBRE DE VARIABLES INTERNES ASSOCIEES AU COMPORTEMENT.
C    TEMP    TEMPERATURE AU DEBUT DE L'INCREMENT
C    DTEMP   INCREMENT DE TEMPERATURE.
C    PREDEF  TABLEAU DES VALEURS INTERPOLEES DES CHAMPS EXTERNES IMPOSES
C    DPRED   TABLEAU DES INCREMENTS DES CHAMPS EXTERNES IMPOSES.
C    NPROPS  NOMBRE DE PARAMETRES MATERIAUX
C    COORDS  TABLEAU CONTENANT LES COORDONNEES DE CE POINT.
C    DFGRD0  GRADIENT DE TRANSFORMATION AU DEBUT DE L'INCREMENT.
C    DFGRD1  GRADIENT DE TRANSFORMATION A LA FIN DE L'INCREMENT.
C    NOEL    NUMERO DE L'ELEMENT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C            ARGUMENTS EN SORTIE DE LA ROUTINE UMAT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    STRESS  TENSEUR DES CONTRAINTES DE CAUCHY A LA FIN DU PAS DE TEMPS.
C    STATEV  VARIABLES INTERNES A LA FIN DU PAS DE TEMPS.
C    DDSDDE  MATRICE TANGENTE DU MODELE DE COMPORTEMENT (NTENS,NTENS)
C    PNEWDT  RAPPORT DU NOUVEAU PAS DE TEMPS SUGGERE SUR LE PAS DE TEMPS
C            UTILISE (DTIME). SI PNEWDT < 1, REDECOUPE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C            ARGUMENTS EN SORTIE DE LA ROUTINE UMAT INUTILSES PAR ASTER
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    SSE     ENERGIE ELASTIQUE
C    SPD     ENERGIE DE DISSIPATION PLASTIQUE
C    SCD     ENERGIE DE FLUAGE.
C    EN THERMO-MECANIQUE COUPLEE UNIQUEMENT :
C    RPL     PRODUCTION DE CHALEUR VOLUMETRIQUE
C    DDSDDT  VARIATION DE L'INCREMENT DES CONTRAINTES VERSUS TEMPERATURE
C    DRPLDE  VARIATION DE LA PRODUCTION DE CHALEUR  VERSUS DEFORMATION
C    DRPLDT  VARIATION DE LA PRODUCTION DE CHALEUR  VERSUS TEMPERATURE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*80 CMNAME
      REAL*8 STRESS(NTENS),STATEV(NSTATV),
     1 DDSDDE(NTENS,NTENS),
     2 DDSDDT(NTENS),DRPLDE(NTENS),
     3 STRAN(NTENS),DSTRAN(NTENS),TIME(2),PREDEF(1),DPRED(1),
     4 PROPS(NPROPS),COORDS(3),DROT(3,3),DFGRD0(3,3),DFGRD1(3,3)
      REAL*8 DSTRES(6),D(3,3)
C
C  EVALUATE NEW STRESS TENSOR
C
      EV = 0.D0
      DEV = 0.D0
      DO 1001 K1=1,NDI
         EV = EV + STRAN(K1)
         DEV = DEV + DSTRAN(K1)
 1001 CONTINUE
C
      TERM1 = .5D0*DTIME + PROPS(5)
      TERM1I = 1.D0/TERM1
      TERM2 = (.5D0*DTIME*PROPS(1)+PROPS(3))*TERM1I*DEV
      TERM3 = (DTIME*PROPS(2)+2.D0*PROPS(4))*TERM1I
C
      DO 1002 K1=1,NDI
         DSTRES(K1) = TERM2+TERM3*DSTRAN(K1)
     1     +DTIME*TERM1I*(PROPS(1)*EV
     2     +2.D0*PROPS(2)*STRAN(K1)-STRESS(K1))
         STRESS(K1) = STRESS(K1) + DSTRES(K1)
 1002 CONTINUE
C
      TERM2 = (.5D0*DTIME*PROPS(2) + PROPS(4))*TERM1I
      I1 = NDI
      DO 1003 K1=1,NSHR
         I1 = I1+1
         DSTRES(I1) = TERM2*DSTRAN(I1)+
     1     DTIME*TERM1I*(PROPS(2)*STRAN(I1)-STRESS(I1))
         STRESS(I1) = STRESS(I1)+DSTRES(I1)
 1003 CONTINUE
C
C  CREATE NEW JACOBIAN
C
      TERM2 = (DTIME*(.5D0*PROPS(1)+PROPS(2))+PROPS(3)+
     1  2.D0*PROPS(4))*TERM1I
      TERM3 = (.5D0*DTIME*PROPS(1)+PROPS(3))*TERM1I
      DO 1004 K1=1,NTENS
         DO 1005 K2=1,NTENS
            DDSDDE(K2,K1) = 0.D0
 1005    CONTINUE
 1004 CONTINUE
C
      DO 1006 K1=1,NDI
         DDSDDE(K1,K1) = TERM2
 1006 CONTINUE
C
      DO 1007 K1=2,NDI
         N2 = K1-1
         DO 1008 K2=1,N2
            DDSDDE(K2,K1) = TERM3
            DDSDDE(K1,K2) = TERM3
 1008    CONTINUE
 1007 CONTINUE
      TERM2 = (.5D0*DTIME*PROPS(2)+PROPS(4))*TERM1I
      I1 = NDI
      DO 1009 K1=1,NSHR
         I1 = I1+1
         DDSDDE(I1,I1) = TERM2
 1009 CONTINUE
C
C  TOTAL CHANGE IN SPECIFIC ENERGY
C
      TDE = 0.D0
      DO 1010 K1=1,NTENS
         TDE = TDE + (STRESS(K1)+.5D0*DSTRES(K1))*DSTRAN(K1)
 1010 CONTINUE
C
C  CHANGE IN SPECIFIC ELASTIC STRAIN ENERGY
C
      TERM1 = PROPS(1) + 2.D0*PROPS(2)
      DO 1011 K1=1,NDI
         D(K1,K1) = TERM1
 1011 CONTINUE
      DO 1012 K1=2,NDI
         N2 = K1-1
         DO 1013 K2=1,N2
            D(K1,K2) = PROPS(1)
            D(K2,K1) = PROPS(1)
 1013    CONTINUE
 1012 CONTINUE
      DEE = 0.D0
      DO 1014 K1=1,NDI
         TERM1 = 0.D0
         TERM2 = 0.D0
         DO 1015 K2=1,NDI
            TERM1 = TERM1 + D(K1,K2)*STRAN(K2)
            TERM2 = TERM2 + D(K1,K2)*DSTRAN(K2)
 1015    CONTINUE
         DEE = DEE + (TERM1+.5D0*TERM2)*DSTRAN(K1)
 1014 CONTINUE
      I1 = NDI
      DO 1016 K1=1,NSHR
         I1 = I1+1
         DEE = DEE + PROPS(2)*(STRAN(I1)+.5D0*DSTRAN(I1))*DSTRAN(I1)
 1016 CONTINUE
      SSE = SSE + DEE
      SCD = SCD + TDE - DEE
      
      END
