'DEBPROC' BARSOUM MESH*'MAILLAGE' PFIS*'POINT' ;
*
*|     ARGUMENTS OBLIGATOIRES :
*|     MESH : MAILLAGE CONCERNE PAR LA TRANSFORMATION
*|     PFIS : POINT CARACTERISANT LA POINTE DE FISSURE
*
&DIME = VALEUR DIME ;
'SI' (&DIME 'NEG' 2) ;
   MESS 'DIMENSION 2 OBLIGATOIRE' ;
   'QUITTER' @BARSOUM;
'FINSI';
*
BARSOUM = MESH 'ELEM' 'APPU' 'LARG' PFIS;
CEXT = 'CONTOUR' BARSOUM ;
PEXT = 'CHANGER' CEXT 'POI1' ;
*
TELEM = EXTRA (BARSOUM ELEM 'TYPE') 1;
'SI' (('NEG' TELEM 'QUA8') 'ET' ('NEG' TELEM 'TRI6')) ;
    SAUT PAGE;
    MESS '  ATTENTION ! CETTE PROCEDURE NE PEUT ETRE UTILISEE ';
    MESS '    QUE POUR DES ELEMENTS QUA8 OU TRI6';
    'QUITTER' @BARSOUM;
'FINSI';
*
SI (EGA TELEM 'QUA8') ;
  II=0 ;
  'REPETER' BOUCLE 5 ;
    II = II + 2 ;
    POINI = PEXT 'POIN' II ;
    VEC = POINI 'MOIN' PFIS ;
    PM = PFIS 'PLUS' (VEC / 2.) ;
    PMIL = BARSOUM 'POIN' 'PROCHE' PM ;
    'DEPL' PMIL 'MOINS' (VEC / 4.) ;
  'FIN' BOUCLE ;
'SINON' ;
NB0 = 'NBNO' PEXT ;
NB0 = NB0 / 2 ;
II = 0 ;
'REPETER' QUART ;
  II = II + 1 ;
  POINI = PEXT 'POIN' II ;
  'SI' ('EGA' POINI PFIS) ;
    'ITERER' QUART ;
  'FINSI' ;
*
  VEC = POINI 'MOIN' PFIS;
  PM = PFIS 'PLUS' (VEC / 2.) ;
  PMIL = BARSOUM 'POIN' 'PROCHE' PM ;
  'DEPL' PMIL 'MOINS' (VEC / 4.) ;
  'SI' (EGA II NB0) ;
     'QUITTER' QUART ;
  'FINSI' ;
'FIN' QUART ;
'FINSI';
'FINPROC' ;

* ******************************************************************
* PROCEDURE DE MAILLAGE D'UNE FISSURE 2D
* --------------------------------------
*
* CISI (AGENCE DE SACLAY)
*
* B. KURTH
*
* VERSION DU 10/05/95
*
* PARAMETRES:
*
* RT        RAYON DU MAILLAGE RAYONNANT
* RT1       COEFFICIENT MULTIPLICATEUR DE RT PERMETTANT DE DEFINIR
*           L'EPAISSEUR DES COURONNES DE DERAFFINEMENT DES SECTEURS
* RT2       COEFFICIENT MULTIPLICATEUR DE RT PERMETTANT DE DEFINIR
*           L'EPAISSEUR DE LA COURONNE DE TRANSITION DU MAILLAGE
*           RAYONNANT AU MAILLAGE DALLE
* NC        NOMBRE DE COURONNES
* NS        NOMBRE DE SECTEURS SUR 90 DEGRES (MULTIPLE DE 2)
* EPS       DEMI-ANGLE D'OUVERTURE DE LA FISSURE (DEGRES)
* NBCOUR    NOMBRE DE COURONNES DE DERAFFINEMENT (0 OU 1 OU 2)
* L_LEV     LONGUEUR DE LA LEVRE DE LA FISSURE
* L_LIG     LONGUEUR DU LIGAMENT
* L_LA      LARGEUR DU DOMAINE
* NLEV      NOMBRE D'ELEMENTS DE PROLONGATION LE LONG DE LA LEVRE
* NLIG      NOMBRE D'ELEMENTS DE PROLONGATION LE LONG DU LIGAMENT
* NLA       NOMBRE D'ELEMENTS DE PROLONGATION LE LONG DE LA LARGEUR
* DX        POSITION SUIVANT X DU CENTRE DU REPERE PAR RAPPORT AU FOND
*           DE FISSURE (DX = 0 : CENTRE DU REPERE AU FOND DE FISSURE)
* LEVRE     POUR LES CAS AXISYMETRIQUES: FISSURE DEBOUCHANTE A
*           L'EXTERIEUR (LEVRE = 'EXT' PAR DEFAUT) OU FISSURE
*           DEBOUCHANTE A L'INTERIEUR (LEVRE = 'INT')
*
* ******************************************************************
*
*$$$$ FISS_2D
*
DEBPROC FISS_2D L_LEV*FLOTTANT L_LIG*FLOTTANT L_LA*FLOTTANT
        EPS*FLOTTANT NC*ENTIER NS*ENTIER RT*FLOTTANT RT1*FLOTTANT
        NBCOUR*ENTIER RT2*FLOTTANT NLEV*ENTIER NLIG*ENTIER
        NLA*ENTIER LEVRE/MOT DX*FLOTTANT ;
*
OPTI DIME 2 ELEM QUA8  ;
*
* CREATION DU MAILLAGE RAYONNANT (MBASE)
* --------------------------------------
*
* CRITERE D'ELIMINATION ET DE RECHERCHE DES NOEUDS
CRIT = (RT / (50. * NC)) ;
*
O = 0. 0. ;
COSEPS = COS EPS ;
SINEPS = SIN EPS ;
P0 = (0.001 * RT * COSEPS) (0.001 * RT * SINEPS) ;
P1 = (RT * COSEPS) (RT * SINEPS) ;
P1_90 = 0 RT ;
P1_180 = (-1 * RT) 0 ;
L11_90 = P1 CERC NS O P1_90 ;
L11_12 = P1_90 CERC NS O P1_180 ;
LEXTBASE = L11_90 ET L11_12 ;
LOP1 = P0 D NC P1 ;
LOP1_180 = P0 D NC P1_180 ;
*
* DEMI BOUDIN INFERIEUR
MBASE = LOP1 ROTA NS (90. - EPS) O ROTA NS 90. O ;
*
* FUSION DES NOEUDS SITUES PRES DU CENTRE DU MAILLAGE EN ETOILE
ELIM MBASE CRIT ;
MBASE = REGE MBASE ;
MBASE = MBASE COUL ROUG ;
*
ELIM (MBASE ET O) CRIT ;
*
SI (((NBCOUR EGA 2) ET (NS < 8)) OU ((NBCOUR EGA 1) ET (NS < 4))) ;
   SAUT LIGNE ;
   MESS '======================================================== ' ;
   MESS '  ATTENTION !!!  LE NOMBRE DE COURONNES DE DERAFFINEMENT ' ;
   MESS '                 EST TROP ELEVE POUR UN NOMBRE DE        ' ;
   MESS '                 DE SECTEURS DE' NS                        ;
   MESS '                                                         ' ;
   MESS '======================================================== ' ;
   SAUT LIGNE ;
FINSI ;
*
* CREATION DES COURONNES DE DERAFFINEMENT (MTRAN)
* -----------------------------------------------
*
SI (NBCOUR NEG 0) ;
*
RTI = 1.4 ;
*
 I = 0 ;
 REPE BOUC1 NBCOUR ;
 I = I + 1 ;
*
   RT1 = RT1 * (I ** RTI) ;
   NSB = NS / (2 * I) ;
*
   P2 = (RT * (1 + RT1) * COSEPS) (RT * (1 + RT1) * SINEPS) ;
   P2_90 = 0 (RT * (1 + RT1)) ;
   P2_180 = (-1 * (RT * (1 + RT1))) 0 ;
   L22_1 = P2 CERC NSB O P2_90 ;
   L2_12_2 = P2_90 CERC NSB O P2_180 ;
   LEXT_RT1 = L22_1 ET L2_12_2 ;
*
   MTRAN1 = COUT LEXTBASE LEXT_RT1 ;
*
   SI (I EGA 1) ;
       MTRAN = MTRAN1 ;
       LP1P2 = P1 D 1 P2 ;
       LP12P22 = P1_180 D 1 P2_180 ;
   SINON ;
       MTRAN = MTRAN ET MTRAN1 ;
       LP1P2 = LP1P2 D 1 P2 ;
       LP12P22 = LP12P22 D 1 P2_180 ;
   FINSI ;
       LEXTBASE = LEXT_RT1 ;
 FIN BOUC1 ;
*
   P3 = P2 PLUS ((RT * RT2) 0) ;
   P4 = P3 PLUS (0 (COOR 1 P3)) ;
   P5 = 0 (COOR 2 P4) ;
   P6 = (-1 * (COOR 1 P4)) (COOR 2 P4) ;
   P7 = (-1 * (COOR 1 P4)) 0 ;
   L2_905 = P2_90 D 1 P5 ;
NSB_P = NSB / 2 ;
NSB_G = NSB - NSB_P ;
   L34 = P3 D NSB_P P4 ;
   L45 = P4 D NSB_G P5 ;
   L56 = P5 D NSB_G P6 ;
   L67 = P6 D NSB_P P7 ;
   L23 = P2 D 1 P3 ;
   L2_27 = P2_180 D 1 P7 ;
*
SINON ;
*
   P3 = P1 PLUS ((RT * RT2) 0) ;
   P4 = P3 PLUS (0 (COOR 1 P3)) ;
   P5 = 0 (COOR 2 P4) ;
   P6 = (-1 * (COOR 1 P4)) (COOR 2 P4) ;
   P7 = (-1 * (COOR 1 P4)) 0 ;
   L22_1 = L11_90 ;
   L2_12_2 = L11_12 ;
   L2_905 = P1_90 D 1 P5 ;
NS_P = NS / 2 ;
NS_G = NS - NS_P ;
   L34 = P3 D NS_P P4 ;
   L45 = P4 D NS_G P5 ;
   L56 = P5 D NS_G P6 ;
   L67 = P6 D NS_P P7 ;
   L23 = P1 D 1 P3 ;
   L2_27 = P1_180 D 1 P7 ;
FINSI ;
*
* PASSAGE DU CERCLE AU RECTANGLE (MCE1 ET MCE2)
* ---------------------------------------------
MCE1 = DALL (L34 ET L45) L2_905 L22_1 L23 ;
MCE2 = DALL (L56 ET L67) L2_27 L2_12_2 L2_905 ;
*
* PROLONGATION LE LONG DE LA LEVRE
* --------------------------------
*
SI ((L_LEV - (COOR 1 P3)) <EG 0) ;
   SAUT LIGNE ;
   MESS '========================================================= ' ;
   MESS '  ATTENTION !!!  LES PARAMETRES RT,RT1 ET RT2 NE SONT PAS ' ;
   MESS '                 CORRECTS.                                ' ;
   MESS '                 REDUISEZ CES PARAMETRES.                 ' ;
   MESS '                                                          ' ;
   MESS '  PROBLEME AU NIVEAU DE LA LEVRE !!!                      ' ;
   MESS '                                                          ' ;
   MESS '  ON TRACE LE MAILLAGE POUR VISUALISER LE PROBLEME        ' ;
   MESS '                                                          ' ;
   MESS '========================================================= ' ;
   SAUT LIGNE ;
FINSI ;
*
MPRO_LE = L34 TRAN NLEV ((L_LEV - (COOR 1 P3)) 0) ;
MPRO_LE = MPRO_LE COUL ROSE ;
*
* PROLONGATION LE LONG DU LIGAMENT	
* --------------------------------
*
SI ((-1 * ((COOR 1 P7) + L_LIG)) >EG 0) ;
   SAUT LIGNE ;
   MESS '========================================================= ' ;
   MESS '  ATTENTION !!!  LES PARAMETRES RT,RT1 ET RT2 NE SONT PAS ' ;
   MESS '                 CORRECTS.                                ' ;
   MESS '                 REDUISEZ CES PARAMETRES.                 ' ;
   MESS '                                                          ' ;
   MESS '  PROBLEME AU NIVEAU DU LIGAMENT !!!                      ' ;
   MESS '                                                          ' ;
   MESS '  ON TRACE LE MAILLAGE POUR VISUALISER LE PROBLEME        ' ;
   MESS '                                                          ' ;
   MESS '========================================================= ' ;
   SAUT LIGNE ;
FINSI ;
*
MPRO_LI = (INVE L67) TRAN NLIG ((-1 * ((COOR 1 P7) + L_LIG)) 0) ;
MPRO_LI = MPRO_LI COUL ROSE ;
*
* CREATION DE L'OBJET TOTAL INTERMEDIARE
* --------------------------------------
SI (NBCOUR NEG 0) ;
   VTOT = MBASE ET MTRAN ET MCE1 ET MCE2 ET MPRO_LE ET MPRO_LI ;
SINON ;
   VTOT = MBASE ET MCE1 ET MCE2 ET MPRO_LE ET MPRO_LI ;
FINSI ;
*
* PROLONGATION DANS LA LARGEUR
* ----------------------------
PSUP = VTOT POIN DROI P4 P6 CRIT ;
VTOT_CON = CONT VTOT ;
LSUP = VTOT_CON ELEM APPUYE STRICTEMENT PSUP ;
MPRO_LA = LSUP TRAN NLA (0 (L_LA - (COOR 2 P5))) ;
MPRO_LA = MPRO_LA COUL ROSE ;
*
* CREATION DE L'OBJET TOTAL
* -------------------------
  VTOT = VTOT ET MPRO_LA ;
*
* RECUPERATION DES LIGNES DE CHARGEMENT ET DE COND. AUX LIMITES
* -------------------------------------------------------------
VTOT_CON = CONT VTOT ;
P8 = L_LEV (COOR 2 P3) ;
P9 = L_LEV L_LA ;
P10 = (-1 * L_LIG) L_LA ;
P11 = (-1 * L_LIG) 0 ;
*
PLP8P9 = VTOT POIN DROI P8 P9 CRIT ;
LP8P9 = VTOT_CON ELEM APPUYE STRICTEMENT PLP8P9 ;
*
PLP9P10 = VTOT POIN DROI P9 P10 CRIT ;
LP9P10 = VTOT_CON ELEM APPUYE STRICTEMENT PLP9P10 ;
*
PLP10P11 = VTOT POIN DROI P10 P11 CRIT ;
LP10P11 = VTOT_CON ELEM APPUYE STRICTEMENT PLP10P11 ;
*
*
* LIGNE DE LA LEVRE
* -----------------
V_CONT1 = CONT (MCE1 ET MPRO_LE) ;
*
 SI (NBCOUR EGA 0) ;
    PLP2P8 = V_CONT1 POIN DROI P1 P8 CRIT ;
    LP2P8 = V_CONT1 ELEM APPUYE STRICTEMENT PLP2P8 ;
    LPOP8 = LOP1 ET LP2P8 ;
 SINON ;
    PLP2P8 = V_CONT1 POIN DROI P2 P8 CRIT ;
    LP2P8 = V_CONT1 ELEM APPUYE STRICTEMENT PLP2P8 ;
    LPOP8 = LOP1 ET LP1P2 ET LP2P8 ;
 FINSI ;
*
* LIGNE DU LIGAMENT
* -----------------
V_CONT2 = CONT (MCE2 ET MPRO_LI) ;
*
 SI (NBCOUR EGA 0) ;
    PLP22P11 = V_CONT2 POIN DROI P1_180 P11 CRIT ;
    LP22P11 = V_CONT2 ELEM APPUYE STRICTEMENT PLP22P11 ;
    LPOP11 = LOP1_180 ET LP22P11 ;
 SINON ;
    PLP22P11 = V_CONT2 POIN DROI P2_180 P11 CRIT ;
    LP22P11 = V_CONT2 ELEM APPUYE STRICTEMENT PLP22P11 ;
    LPOP11 = LOP1_180 ET LP12P22 ET LP22P11 ;
 FINSI ;
*
VSORT = VTOT ET LPOP11 ET LPOP8 ET P8 ET P9 ET P10 ET P11 ;
*
ELIM VSORT CRIT ;
*
* SYMETRIE POUR LES CAS AXISYMETRIQUES (FISSURE INT. OU EXT.)
* -----------------------------------------------------------
SI (EGA LEVRE 'INT') ;
 VSORT = DEPL VSORT SYME DROI O P1_90 ;
FINSI ;
*
* DEPLACEMENT DE L'ORIGINE DU REPERE
* ----------------------------------
VSORT = DEPL VSORT PLUS (DX 0) ;
*
FINPROC VTOT LPOP8 LPOP11 LP9P10 LP8P9 LP10P11 O P8 ;
*



OPTI DIME 2 ELEM QUA8 ;
*
* CREATION DU MAILLAGE D'UNE FISSURE 2D
* -------------------------------------
*
* PARAMETRES:
*
* RT        RAYON DU MAILLAGE RAYONNANT
* RT1       COEFFICIENT MULTIPLICATEUR DE RT PERMETTANT DE DEFINIR
*           L'EPAISSEUR DES COURONNES DE DERAFFINEMENT DES SECTEURS
* RT2       COEFFICIENT MULTIPLICATEUR DE RT PERMETTANT DE DEFINIR
*           L'EPAISSEUR DE LA COURONNE DE TRANSITION DU MAILLAGE
*           RAYONNANT AU MAILLAGE DALLE
* NC        NOMBRE DE COURONNES
* NS        NOMBRE DE SECTEURS SUR 90 DEGRES (MULTIPLE DE 2)
* EPS       DEMI-ANGLE D'OUVERTURE DE LA FISSURE (DEGRES)
* NBCOUR    NOMBRE DE COURONNES DE DERAFFINEMENT (0 OU 1 OU 2)
* L_LEV     LONGUEUR DE LA LEVRE DE LA FISSURE
* L_LIG     LONGUEUR DU LIGAMENT
* L_LA      LARGEUR DU DOMAINE
* NLEV      NOMBRE D'ELEMENTS DE PROLONGATION LE LONG DE LA LEVRE
* NLIG      NOMBRE D'ELEMENTS DE PROLONGATION LE LONG DU LIGAMENT
* NLA       NOMBRE D'ELEMENTS DE PROLONGATION LE LONG DE LA LARGEUR
* DX        POSITION SUIVANT X DU CENTRE DU REPERE PAR RAPPORT AU FOND
*           DE FISSURE (DX = 0 : CENTRE DU REPERE AU FOND DE FISSURE)
* LEVRE     POUR LES CAS AXISYMETRIQUES: FISSURE DEBOUCHANTE A
*           L'EXTERIEUR (LEVRE = 'EXT' PAR DEFAUT) OU FISSURE
*           DEBOUCHANTE A L'INTERIEUR (LEVRE = 'INT')
*
L_LEV     = 2.  ;
L_LIG     = 8.  ;
L_LA = 10. ;
DX     = 2.  ;
RT   = 0.3  ;
RT1   = 1.2   ;
RT2   = 1.2   ;
NC    = 8     ;
NS    = 8     ;
EPS   = 0.   ;
NBCOUR   = 1     ;
NLEV  = 3     ;
NLIG  = 10     ;
NLA  = 10    ;
LEVRE = 'INT' ;

MA1 LP0P8 LP0P11 LP9P10 LP8P9 LP10P11 P0 P8
= FISS_2D L_LEV L_LIG L_LA EPS NC NS RT RT1
        NBCOUR RT2 NLEV NLIG NLA LEVRE DX ;


BARSOUM MA1 P0 ;

TRAC MA1 ;

SYM1 = MA1 SYME DROIT P8 P0 ;
LP10P11S = LP10P11 SYME DROIT P8 P0;
LP9P10S = LP9P10 SYME DROIT P8 P0;
LP8P9S  = LP8P9 SYME DROIT P8 P0;
LP0P11S = LP0P11 SYME DROIT P8 P0 ;
LP0P8S = LP0P8 SYME DROIT P8 P0 ;
SYM1 =  SYM1 ET LP10P11S ET LP9P10S ET LP8P9S ET LP0P11S ET LP0P8S ;
ELIM (SYM1) 1.D-4  ;

ELIM ( LP0P11 ET LP0P11S) 1.D-4  ;

MA1 = MA1 ET SYM1 ;

TRAC  MA1 ;
TRAC (CONT MA1) ;
LIST (NBNO MA1);

fic='/home/galenne/ASTER_DEV/KAXI/CASTEST/sslv134c.mgib';
OPTI SAUV FORMAT fic ;
SAUV FORM MA1 ;

FIN;


