/*!
 * \file   burger.mfront
 * \brief  
 *   resolution explicite Runge KÃ¼tter
 * \author FH
 * \date   13 05 2013
 */

@Parser Implicit;
@Behaviour Burger;
@Algorithm NewtonRaphson_NumericalJacobian;
@Theta 1. ;

@Includes{
#include"TFEL/Material/Lame.hxx"
}

@MaterialProperty stress young;
young.setGlossaryName("YoungModulus");
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");

@MaterialProperty real KRS;
@MaterialProperty real NRS;
@MaterialProperty real KAPPA;
@MaterialProperty real NIS;
@MaterialProperty real KRD;
@MaterialProperty real NRD;
@MaterialProperty real NID;
@MaterialProperty real NFD;
@MaterialProperty real Cini;


@StateVariable real       ESPHR;
@StateVariable real       ESPHI;
@StateVariable real       ELIM;
@StateVariable Stensor    EDEVR;
@StateVariable Stensor    EDEVI;
@StateVariable Stensor    Edess;


@ExternalStateVariable real SECH ,HYDR ;

@LocalVariable real    lambda;
@LocalVariable real    mu;



/* Initialize Lame coefficients */
@InitLocalVariables{
  using namespace tfel::material::lame;
  lambda = computeLambda(young,nu);
  mu = computeMu(young,nu);
} 

@TangentOperator{
  using namespace tfel::material::lame;
  if((smt==ELASTIC)||(smt==SECANTOPERATOR)){
    computeAlteredElasticStiffness<hypothesis,Type>::exe(Dt,lambda,mu);
  } else if(smt==CONSISTENTTANGENTOPERATOR){
    StiffnessTensor Hooke;
    Stensor4 Je;
    computeElasticStiffness<N,Type>::exe(Hooke,lambda,mu);
    getPartialJacobianInvert(Je);
    Dt = Hooke*Je;
  } else {
    return false;
  }
}

@ComputeStress{
    sig = lambda*trace(eel)*Stensor::Id()+2*mu*eel;
} 

@Integrator{
    real stresP=trace(sig)/3.;
    real Vsech =fabs(dSECH);
    Stensor stresD=sig-stresP*Stensor::Id();
    real emax=sqrt(((ESPHI+dESPHI)*Stensor::Id()+(EDEVI+dEDEVI))|((ESPHI+dESPHI)*Stensor::Id()+(EDEVI+dEDEVI)));
    fELIM=ELIM+dELIM-max(ELIM,emax);
    real eexp=exp((ELIM+dELIM)/KAPPA);
    fESPHR=dESPHR-((SECH/Cini*stresP-KRS*(ESPHR+dESPHR))/NRS)*dt;
    fEDEVR=dEDEVR-(SECH/Cini*stresD-KRD*(EDEVR+dEDEVR))/NRD*dt;
    fESPHI=dESPHI-(SECH/Cini*stresP/(NIS*eexp))*dt;
    fEDEVI=dEDEVI-(SECH/Cini*stresD/(NID*eexp))*dt;
    if (NFD>0.)
    {
    fEdess=dEdess-(1./NFD)*Vsech/Cini*sig;
    }
    else
    {
    fEdess=dEdess-0.*Vsech/Cini*sig;
     }
    feel=deel-(deto-dEDEVR-dEDEVI-dEdess-(dESPHR+dESPHI)*Stensor::Id());
}


  
  

